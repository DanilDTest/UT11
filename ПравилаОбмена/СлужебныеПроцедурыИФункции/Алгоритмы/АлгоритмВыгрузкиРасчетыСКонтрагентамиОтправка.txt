Функция АлгоритмВыгрузкиРасчетыСКонтрагентамиОтправка(ДанныеИБ)
	ДанныеШапки = ДанныеИБ.ДополнительныеСвойства.ДанныеШапки;
	ВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();
	
	АлгоритмКонвертации = "";
	Если ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента Тогда 
		АлгоритмКонвертации = "ОплатаОтПокупателя";
	ИначеЕсли ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойОрганизации Тогда
		АлгоритмКонвертации = "ОплатаОтПокупателяИнтеркампани";
	ИначеЕсли ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеОплатыПоПлатежнойКарте Тогда
		АлгоритмКонвертации = "ПрочееПоступление_ПрочиеРасчетыСКонтрагентами";
	ИначеЕсли ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтПоставщика Тогда
		АлгоритмКонвертации = "ВозвратОтПоставщика";
	ИначеЕсли ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтДругойОрганизации Тогда
		АлгоритмКонвертации = "ВозвратОтПоставщикаИнтеркампани";
	ИначеЕсли ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПрочееПоступлениеДенежныхСредств Тогда
		Если Найти(ДанныеШапки.КорреспондирующийСчет, "66") > 0
			Или Найти(ДанныеШапки.КорреспондирующийСчет, "67") > 0 Тогда
			АлгоритмКонвертации = "ПрочееПоступление_РасчетыПоКредитамИЗаймам";
		ИначеЕсли Найти(ДанныеШапки.КорреспондирующийСчет, "60") > 0
			Или Найти(ДанныеШапки.КорреспондирующийСчет, "62") > 0
			Или Найти(ДанныеШапки.КорреспондирующийСчет, "76") > 0 Тогда
			АлгоритмКонвертации = "ПрочееПоступление_ПрочиеРасчетыСКонтрагентами";
		КонецЕсли;
	ИначеЕсли ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.КонвертацияВалюты Тогда
		Если ДанныеИБ.Валюта <> ВалютаРегламентированногоУчета
			И ДанныеИБ.ВалютаКонвертации <> ВалютаРегламентированногоУчета Тогда
			// Конвертацию одной валюты в другую выгружаем с видом операции "Прочие расчеты с контрагентами".
			АлгоритмКонвертации = "КонвертацияВалюты_ПрочиеРасчетыСКонтрагентами";
		КонецЕсли;
	ИначеЕсли ЭтоХозяйственнаяОперацияРасчетыПоКредитамЗаймам(ДанныеИБ.ХозяйственнаяОперация) Тогда
		АлгоритмКонвертации = "Кредиты_РасчетыПоКредитамИЗаймам";
	ИначеЕсли ЭтоХозяйственнаяОперацияРасчетыПоДепозитам(ДанныеИБ.ХозяйственнаяОперация) Тогда
		АлгоритмКонвертации = "Депозиты_ПрочиеРасчетыСКонтрагентами";
	КонецЕсли;
	
	Возврат АлгоритмКонвертации;
КонецФункции
