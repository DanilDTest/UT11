Процедура ОтложеннаяОбработка_ХарактеристикиНоменклатуры(Объект, КомпонентыОбмена)
	#Область ОбработкаВладельца
	
	ВладелецХарактеристики = Неопределено;
	Если ТипЗнч(Объект.Владелец) = Тип("СправочникСсылка.Номенклатура") Тогда
		ИспользованиеХарактеристик = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Владелец, "ИспользованиеХарактеристик");
		Если ИспользованиеХарактеристик = Перечисления.ВариантыИспользованияХарактеристикНоменклатуры.НеИспользовать
			ИЛИ НЕ ЗначениеЗаполнено(ИспользованиеХарактеристик) Тогда
			
			ВладелецХарактеристики = Объект.Владелец.ПолучитьОбъект();
			ВладелецХарактеристики.ИспользованиеХарактеристик = Перечисления.ВариантыИспользованияХарактеристикНоменклатуры.ИндивидуальныеДляНоменклатуры;
			
		КонецЕсли;
	ИначеЕсли ТипЗнч(Объект.Владелец) = Тип("СправочникСсылка.ВидыНоменклатуры")
		И ВерсияФорматаЧислом(КомпонентыОбмена.ВерсияФорматаОбмена) >= ВерсияФорматаЧислом("1.7") Тогда
		
		ДанныеВладельца = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.Владелец,
			"ИспользованиеХарактеристик, ИспользоватьХарактеристики");
		Если НЕ ДанныеВладельца.ИспользоватьХарактеристики
			Или НЕ ЗначениеЗаполнено(ДанныеВладельца.ИспользованиеХарактеристик)
			Или ДанныеВладельца.ИспользованиеХарактеристик = Перечисления.ВариантыИспользованияХарактеристикНоменклатуры.НеИспользовать Тогда
			
			ВладелецХарактеристики = Объект.Владелец.ПолучитьОбъект();
			ВладелецХарактеристики.ИспользоватьХарактеристики = Истина;
			ВладелецХарактеристики.ИспользованиеХарактеристик = Перечисления.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеДляВидаНоменклатуры;
			
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ВладелецХарактеристики = Неопределено Тогда
		ВладелецХарактеристики.ОбменДанными.Загрузка = Истина;
		Попытка
			ВладелецХарактеристики.Записать();
		Исключение
			ЗаписатьОшибкуВЖурналРегистрации(ВладелецХарактеристики.Ссылка,
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()), КомпонентыОбмена);
		КонецПопытки;
	КонецЕсли;
	
	#КонецОбласти
	
	Если НЕ ЗначениеЗаполнено(Объект.Наименование) Тогда
		Объект.Наименование = Объект.НаименованиеПолное;
	КонецЕсли;
КонецПроцедуры
