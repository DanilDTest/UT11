Процедура ОтложеннаяОбработка_ЗаполнитьНоменклатуру(КомпонентыОбмена)
	НоменклатураДляОтложеннойОбработки = КомпонентыОбмена.ПараметрыКонвертации.НоменклатураДляОтложеннойОбработки;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НоменклатураДляОтложеннойОбработки", НоменклатураДляОтложеннойОбработки);
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Номенклатура.Ссылка КАК Номенклатура,
		|	ЕСТЬNULL(Номенклатура.ВидНоменклатуры.ИспользованиеХарактеристик, ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.НеИспользовать)) КАК ИспользованиеХарактеристик
		|ИЗ
		|	Справочник.Номенклатура КАК Номенклатура
		|ГДЕ
		|	Номенклатура.Ссылка В(&НоменклатураДляОтложеннойОбработки)";
		
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НоменклатураОбъект = Выборка.Номенклатура.ПолучитьОбъект();
		НоменклатураОбъект.ИспользованиеХарактеристик = Выборка.ИспользованиеХарактеристик;
		
		НоменклатураОбъект.ОбменДанными.Загрузка = Истина;
		
		Попытка
			НоменклатураОбъект.Записать();
		Исключение
			ЗаписатьОшибкуВЖурналРегистрации(Выборка.Номенклатура,
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()), КомпонентыОбмена);
		КонецПопытки;
	КонецЦикла;
КонецПроцедуры
