Процедура ОтложеннаяОбработка_ОтчетКомитентуИКомиссионера(Объект, КомпонентыОбмена)
	ЗаполнениеПартнераВШапкеДокумента(Объект);
	ЭтоЗакупки = Истина;
	Если ТипЗнч(Объект.Соглашение) = Тип("СправочникСсылка.СоглашенияСКлиентами") Тогда
		ЭтоЗакупки = Ложь;
	КонецЕсли;
	Если ЭтоЗакупки Тогда
		ЗаполнениеСоглашенияСПоставщикомВШапкеДокумента(Объект, КомпонентыОбмена.ПараметрыКонвертации, Перечисления.ХозяйственныеОперации.ПриемНаКомиссию);
	Иначе
		ЗаполнениеСоглашенияСКлиентомВШапкеДокумента(Объект, Перечисления.ХозяйственныеОперации.ПередачаНаКомиссию);
	КонецЕсли;
	ЗаполнитьТипНалогообложенияПоУмолчанию(Объект);
	Если НЕ ЗначениеЗаполнено(Объект.СуммаНДСВознаграждения) Тогда
		ПроцентНДС = УчетНДСУПКлиентСервер.ПолучитьСтавкуНДС(Объект.СтавкаНДСВознаграждения);
		КомиссионнаяТорговляСервер.РассчитатьСуммуНДСВознаграждения(Объект, ПроцентНДС);
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Объект.ПорядокОплаты) Тогда
		Если ЭтоЗакупки Тогда
			ИспользоватьСоглашения = ПолучитьФункциональнуюОпцию("ИспользоватьСоглашенияСПоставщиками");
			ИспользоватьДоговоры = ПолучитьФункциональнуюОпцию("ИспользоватьДоговорыСПоставщиками"); 
		Иначе
			ИспользоватьСоглашения = ПолучитьФункциональнуюОпцию("ИспользоватьСоглашенияСКлиентами");
			ИспользоватьДоговоры = ПолучитьФункциональнуюОпцию("ИспользоватьДоговорыСКлиентами");
		КонецЕсли;
		Если ЗначениеЗаполнено(Объект.Соглашение)
			И ИспользоватьСоглашения Тогда
			Если ЭтоЗакупки Тогда
				УсловияЗакупокПродаж = ЗакупкиСервер.ПолучитьУсловияЗакупок(Объект.Соглашение);
			Иначе
				УсловияЗакупокПродаж = ПродажиСервер.ПолучитьУсловияПродаж(Объект.Соглашение);
			КонецЕсли;
			Если Не ЗначениеЗаполнено(УсловияЗакупокПродаж.ИспользуютсяДоговорыКонтрагентов) 
				Или Не ИспользоватьДоговоры Тогда
				Объект.ПорядокОплаты = УсловияЗакупокПродаж.ПорядокОплаты;
			Иначе
				Объект.ПорядокОплаты = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Договор, "ПорядокОплаты");
			КонецЕсли;
			
		ИначеЕсли ЗначениеЗаполнено(Объект.Договор)
			И ИспользоватьДоговоры Тогда
			Объект.ПорядокОплаты = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Договор, "ПорядокОплаты");
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.ПорядокОплаты) Тогда
		ВалютаОплаты = ДенежныеСредстваСервер.ПолучитьВалютуОплаты(Объект.ФормаОплаты, Объект.БанковскийСчет);
		
		Объект.ПорядокОплаты = Перечисления.ПорядокОплатыПоСоглашениям.ПолучитьПорядокОплатыПоУмолчанию(Объект.Валюта,
			Объект.НалогообложениеНДС,
			ВалютаОплаты);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.ПорядокРасчетов) Тогда
		Объект.ПорядокРасчетов = ВзаиморасчетыСервер.ПорядокРасчетовПоУмолчанию(Объект);
	КонецЕсли;
	
	Если Объект.ЭтапыГрафикаОплаты.Количество() > 0 И Объект.Товары.Итог("СуммаПродажи") <> Объект.ЭтапыГрафикаОплаты.Итог("СуммаПлатежа") Тогда
		Объект.ЭтапыГрафикаОплаты.Очистить();
	КонецЕсли;
	Если Объект.ЭтапыГрафикаОплаты.Количество() = 0 Тогда
		СтрокаОплаты = Объект.ЭтапыГрафикаОплаты.Добавить();
		СтрокаОплаты.ДатаПлатежа    = ?(ЗначениеЗаполнено(Объект.ДатаПлатежа), Объект.ДатаПлатежа, Объект.Дата);
		СтрокаОплаты.ПроцентПлатежа = 100;
		СтрокаОплаты.СуммаПлатежа   = Объект.Товары.Итог("СуммаПродажи");
	КонецЕсли;
КонецПроцедуры
