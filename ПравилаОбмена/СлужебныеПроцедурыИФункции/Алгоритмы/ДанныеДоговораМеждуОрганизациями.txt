Функция ДанныеДоговораМеждуОрганизациями(КомпонентыОбмена, Договор)
	Если ТипЗнч(Договор) = Тип("СправочникОбъект.ДоговорыМеждуОрганизациями") Тогда
		ДанныеДоговораИБ = Договор;
	Иначе
		Запрос = Новый Запрос("ВЫБРАТЬ
			|	ДоговорыМеждуОрганизациями.ПорядокОплаты КАК ПорядокОплаты,
			|	ДоговорыМеждуОрганизациями.Номер КАК Номер,
			|	ДоговорыМеждуОрганизациями.Наименование КАК Наименование,
			|	ДоговорыМеждуОрганизациями.Дата КАК Дата,
			|	ДоговорыМеждуОрганизациями.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
			|	ДоговорыМеждуОрганизациями.ТипДоговора КАК ТипДоговора,
			|	ДоговорыМеждуОрганизациями.ОрганизацияПолучатель КАК ОрганизацияПолучатель,
			|	ДоговорыМеждуОрганизациями.Организация КАК Организация
			|ИЗ
			|	Справочник.ДоговорыМеждуОрганизациями КАК ДоговорыМеждуОрганизациями
			|ГДЕ
			|	ДоговорыМеждуОрганизациями.Ссылка = &Ссылка");
		Запрос.УстановитьПараметр("Ссылка", Договор);
	
		Выборка = Запрос.Выполнить().Выбрать();
		Выборка.Следующий();
		
		ДанныеДоговораИБ = Выборка;
	КонецЕсли;
	
	ДанныеДоговора = Новый Структура();
	
	ДанныеДоговора.Вставить("Номер",                    ДанныеДоговораИБ.Номер);
	ДанныеДоговора.Вставить("Наименование",             ДанныеДоговораИБ.Наименование);
	ДанныеДоговора.Вставить("Дата",                     ДанныеДоговораИБ.Дата);
	ДанныеДоговора.Вставить("ВалютаВзаиморасчетов",     ДанныеДоговораИБ.ВалютаВзаиморасчетов);
	ДанныеДоговора.Вставить("Организация",              ДанныеДоговораИБ.ОрганизацияПолучатель);
	ДанныеДоговора.Вставить("Контрагент",               КонтрагентИзОрганизации(ДанныеДоговораИБ.Организация, КомпонентыОбмена));
	
	ВидДоговора = ?(ДанныеДоговораИБ.ТипДоговора = Перечисления.ТипыДоговоровМеждуОрганизациями.КупляПродажа, "СПоставщиком", "СКомитентом");
	ДанныеДоговора.Вставить("ВидДоговора",               ВидДоговора);
	
	РасчетыВУсловныхЕдиницах = ?(ДанныеДоговораИБ.ПорядокОплаты = Перечисления.ПорядокОплатыПоСоглашениям.РасчетыВВалютеОплатаВРублях, Истина, Ложь);
	ДанныеДоговора.Вставить("РасчетыВУсловныхЕдиницах", РасчетыВУсловныхЕдиницах);
	
	Возврат ДанныеДоговора;
КонецФункции
