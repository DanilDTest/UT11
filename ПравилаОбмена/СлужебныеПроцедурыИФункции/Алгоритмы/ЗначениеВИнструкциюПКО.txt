Функция ЗначениеВИнструкциюПКО(КомпонентыОбмена, Значение, ЭтоПростойТип = Ложь, ПравилоНайдено = Ложь)
	// Параметры:
	// КомпонентыОбмена - структура компонентов обмена в рамках текущего сеанса;
	// Значение - значение, которое необходимо поместить в свойство объекта XDTO;
	// ЭтоПростойТип - исходящий параметр. Указывает, является ли тип переданного значения примитивным (Число, Строка, Булево, Дата);
	// ПравилоНайдено - определяет, было ли найдено правило конвертации. Если правило найдено, но Результат = Неопределено, значит, было найдено более одного ПКО для значения.
	Результат = Неопределено;
	
	Если Не ЗначениеЗаполнено(Значение) Тогда
		Возврат Результат;
	КонецЕсли;
	
	ЭтоОтправка		= (КомпонентыОбмена.НаправлениеОбмена = "Отправка");
	ЭтоПолучение	= (КомпонентыОбмена.НаправлениеОбмена = "Получение");
	
	ТипЗначенияСвойства = ТипЗнч(Значение);
	
	Если ЭтоПолучение
		И ТипЗнч(Значение) = Тип("Структура")
		И Значение.Свойство("Ссылка") Тогда
		
		ТипЗначенияСвойства = Значение.Ссылка.ТипЗначенияXDTO;
	ИначеЕсли ТипЗнч(Значение) = Тип("Структура")
		И Значение.Свойство("ИмяПКО") Тогда
		
		Возврат Новый Структура("Значение, ИмяПКО", Значение, Значение.ИмяПКО);
	КонецЕсли;
	
	Если ТипЗначенияСвойства = Тип("Число") 
		Или ТипЗначенияСвойства = Тип("Строка")
		Или ТипЗначенияСвойства = Тип("Булево")
		Или ТипЗначенияСвойства = Тип("Дата") Тогда
		
		Результат = Значение;
		ЭтоПростойТип = Истина;
	Иначе
		СтрокиПКО = Новый Массив;
		
		Если ЭтоОтправка Тогда	
			СтрокиПКО = КомпонентыОбмена.ПравилаКонвертацииОбъектов.НайтиСтроки(Новый Структура("ТипДанных", ТипЗначенияСвойства));
		ИначеЕсли ЭтоПолучение Тогда
			СтрокиПКО = КомпонентыОбмена.ПравилаКонвертацииОбъектов.НайтиСтроки(Новый Структура("ТипСсылкиXDTO", ТипЗначенияСвойства));
		КонецЕсли;
	
		// Ищем только однозначное соответствие.
		// Если правил будет найдено больше одного, считаем, что конвертация не выполнена.
		ЭтоГруппа = Неопределено;
				
		Для Каждого СтрокаПКО Из СтрокиПКО Цикл
			
			// Обход ошибки до версии ED 1.6
			// тип ссылки "КлючевыеСвойстваОтчетКомитенту" - ОтчетКомиссионера.
			Если ЭтоПолучение И ТипЗнч(Значение) = Тип("Структура") И Значение.Свойство("ТипЗначения")
				И (Значение.ТипЗначения = "ОтчетКомитенту" ИЛИ Значение.ТипЗначения = "ОтчетКомиссионера")
				И СтрНайти(СтрокаПКО.ОбъектФормата, Значение.ТипЗначения) = 0 Тогда
				Продолжить;
			КонецЕсли;
		
			Если ЭтоОтправка Тогда
				
				Если СтрокаПКО.ЭтоСправочник Тогда
					
					Если ЭтоГруппа = Неопределено
						И СтрокаПКО.ОбъектДанных.Иерархический
						И СтрокаПКО.ОбъектДанных.ВидИерархии = Метаданные.СвойстваОбъектов.ВидИерархии.ИерархияГруппИЭлементов Тогда
						ЭтоГруппа = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Значение, "ЭтоГруппа");
					КонецЕсли;
					
					Если ЭтоГруппа = Неопределено 
						Или (ЭтоГруппа = Истина
							И Не СтрокаПКО.ПравилоДляГруппыСправочника)
						Или (Не ЭтоГруппа = Истина
							И СтрокаПКО.ПравилоДляГруппыСправочника) Тогда
						
						Продолжить;					
					
					КонецЕсли;			
					
				КонецЕсли;
	
			КонецЕсли;
			      
			//ЗаписьЖурналаРегистрации("Отладочное сообщение", УровеньЖурналаРегистрации.Предупреждение,,, СтрШаблон("%1", СтрокаПКО.ИмяПКО));		      
			Если ПравилоНайдено Тогда
				Результат = Неопределено;
				Прервать;
			Иначе
				Результат = Новый Структура("Значение, ИмяПКО", Значение, СтрокаПКО.ИмяПКО);
				ПравилоНайдено = Истина;
			КонецЕсли;
					
		КонецЦикла;			
	КонецЕсли;
	
	Возврат Результат;
КонецФункции
