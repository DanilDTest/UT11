Процедура ОбработатьЗагруженныеСтатьиДДСИХозОперации(КомпонентыОбмена)
	СтатьиДДСИХозОперации = КомпонентыОбмена.ПараметрыКонвертации.ЗагруженныеСтатьиДДСИХозОперации;
	СтатьиДДСИХозОперации.Свернуть("СтатьяДДС, ХозОперация");
	СтатьиДДСИХозОперации.Сортировать("СтатьяДДС");
	
	СтатьяДДССсылка = Неопределено;
	МассивХозОпераций = Новый Массив;
	СтатьиДДСКОбработке = Новый Соответствие;
	
	ЗаписыватьСтатьюДДС = Ложь;
	Для Каждого СтрокаТаб Из СтатьиДДСИХозОперации Цикл
		Если СтатьяДДССсылка = Неопределено Тогда
			// Первая строка таблицы.
			СтатьяДДССсылка = СтрокаТаб.СтатьяДДС;
		ИначеЕсли СтатьяДДССсылка <> СтрокаТаб.СтатьяДДС Тогда
			// Обработка перехода к новой статье ДДС.
			Если МассивХозОпераций.Количество() > 0 Тогда
				СтатьиДДСКОбработке.Вставить(СтатьяДДССсылка, МассивХозОпераций);
			КонецЕсли;
			СтатьяДДССсылка = СтрокаТаб.СтатьяДДС;
			МассивХозОпераций = Новый Массив;
		КонецЕсли;
		Если НЕ (ЗначениеЗаполнено(СтатьяДДССсылка) И ОбщегоНазначения.СсылкаСуществует(СтатьяДДССсылка)) Тогда
			Продолжить;
		КонецЕсли;
			
		Если СтатьяДДССсылка.ХозяйственныеОперации.Найти(СтрокаТаб.ХозОперация, "ХозяйственнаяОперация") = Неопределено Тогда
			МассивХозОпераций.Добавить(СтрокаТаб.ХозОперация);
		КонецЕсли;
	КонецЦикла;
	// Обработка последней статьи ДДС.
	Если МассивХозОпераций.Количество() > 0 Тогда
		СтатьиДДСКОбработке.Вставить(СтатьяДДССсылка, МассивХозОпераций);
	КонецЕсли;
	// Изменение статей ДДС.
	Для Каждого ДанныеКОбработке Из СтатьиДДСКОбработке Цикл
		// Необходимо добавить хоз операции в статью ДДС
		ОбъектЗаблокирован = Истина;
		Попытка
			ЗаблокироватьДанныеДляРедактирования(ДанныеКОбработке.Ключ);
		Исключение
			ОбъектЗаблокирован = Ложь;
			ЗаписатьОшибкуВЖурналРегистрации(ДанныеКОбработке.Ключ,
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()), КомпонентыОбмена, НСтр("ru = 'заблокировать объект';
																							|en = 'lock object'"));
		КонецПопытки;
		Если ОбъектЗаблокирован Тогда
			СтатьяОбъект = ДанныеКОбработке.Ключ.ПолучитьОбъект();
			СтатьяОбъект.ОбменДанными.Загрузка = Истина;
			Для Каждого ХозяйственнаяОперация Из ДанныеКОбработке.Значение Цикл
				СтатьяОбъект.ХозяйственныеОперации.Добавить().ХозяйственнаяОперация = ХозяйственнаяОперация;
			КонецЦикла;
			СтатьяОбъект.Записать();
			СтатьяОбъект.Разблокировать();
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры
