Процедура ЗаполнитьДанныеКорректировкиПоступленияТоваров(ДанныеИБ, ДанныеXDTO, КомпонентыОбмена)
	// Данные исходного документа поступления.
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	Док.ЗаказПоставщику КАК ЗаказПоставщику,
		|	Док.Грузоотправитель КАК Грузоотправитель,
		|	ВЫБОР
		|		КОГДА Док.ЗакупкаПодДеятельность = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЭтоЕНВД,
		|	Док.ВернутьМногооборотнуюТару КАК ВернутьМногооборотнуюТару
		|ПОМЕСТИТЬ ДанныеДокументаПоступления
		|ИЗ
		|	Документ.ПриобретениеТоваровУслуг КАК Док
		|ГДЕ
		|	Док.Ссылка = &ДокументОснование
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Док.ЗаказПоставщику КАК ЗаказПоставщику,
		|	Док.Грузоотправитель КАК Грузоотправитель,
		|	Док.ЭтоЕНВД КАК ЭтоЕНВД,
		|	Док.ВернутьМногооборотнуюТару КАК ВернутьМногооборотнуюТару
		|ИЗ
		|	ДанныеДокументаПоступления КАК Док";
	
	// Ссылка на исправляемый документ.
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначения.РазделительПакетаЗапросов() +
		"ВЫБРАТЬ
		|	Док.Ссылка,
		|	Док.МоментВремени,
		|	ЛОЖЬ КАК ЭтоДокументОснование
		|ПОМЕСТИТЬ ПоследовательностьДокументов
		|ИЗ
		|	Документ.КорректировкаПриобретения КАК Док
		|ГДЕ
		|	Док.ДокументОснование = &ДокументОснование
		|	И Док.МоментВремени <= &МоментВремени
		|	И Док.Проведен
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Док.ДокументОснование,
		|	Док.ДокументОснование.МоментВремени,
		|	ИСТИНА
		|ИЗ
		|	Документ.КорректировкаПриобретения КАК Док
		|ГДЕ
		|	Док.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПоследовательностьДокументов.Ссылка,
		|	ПоследовательностьДокументов.МоментВремени,
		|	ПоследовательностьДокументов.ЭтоДокументОснование,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА СчетФактураПолученный.Ссылка ЕСТЬ NULL 
		|				ТОГДА ЛОЖЬ
		|			ИНАЧЕ ИСТИНА
		|		КОНЕЦ) КАК Корректировочный
		|ПОМЕСТИТЬ ПараметрыДокументовПоследовательности
		|ИЗ
		|	ПоследовательностьДокументов КАК ПоследовательностьДокументов
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураПолученный.ДокументыОснования КАК СчетФактураПолученный
		|		ПО ПоследовательностьДокументов.Ссылка = СчетФактураПолученный.ДокументОснование
		|			И (СчетФактураПолученный.Ссылка.Корректировочный)
		|			И (СчетФактураПолученный.Ссылка.Проведен)
		|
		|СГРУППИРОВАТЬ ПО
		|	ПоследовательностьДокументов.Ссылка,
		|	ПоследовательностьДокументов.МоментВремени,
		|	ПоследовательностьДокументов.ЭтоДокументОснование
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ПоследовательностьДокументов.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Параметры1.Ссылка,
		|	Параметры1.Корректировочный,
		|	МИНИМУМ(ВЫБОР
		|			КОГДА Параметры1.Корректировочный
		|					И НЕ Параметры2.Корректировочный
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ ЛОЖЬ
		|		КОНЕЦ) КАК ИсходныйКорректировочный
		|ПОМЕСТИТЬ ПараметрыОтбора
		|ИЗ
		|	ПараметрыДокументовПоследовательности КАК Параметры1
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПараметрыДокументовПоследовательности КАК Параметры2
		|		ПО (Параметры1.Ссылка = &Ссылка)
		|			И (Параметры2.Ссылка <> &Ссылка)
		|
		|СГРУППИРОВАТЬ ПО
		|	Параметры1.Ссылка,
		|	Параметры1.Корректировочный
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Параметры1.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИсправляемыеДокументы.Ссылка
		|ПОМЕСТИТЬ ИсправляемыйДокумент
		|ИЗ
		|	ПараметрыДокументовПоследовательности КАК ИсправляемыеДокументы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПараметрыОтбора КАК ПараметрыОтбора
		|		ПО (ИсправляемыеДокументы.Корректировочный = ПараметрыОтбора.Корректировочный
		|				ИЛИ ПараметрыОтбора.ИсходныйКорректировочный
		|				ИЛИ ИсправляемыеДокументы.ЭтоДокументОснование)
		|			И ИсправляемыеДокументы.Ссылка <> ПараметрыОтбора.Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	ИсправляемыеДокументы.МоментВремени УБЫВ
		|;
		|
		// Собственно исправляемый документ
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ИсправляемыйДокумент.Ссылка КАК Ссылка,
		|	ИсправляемыйДокумент.Ссылка.Дата КАК Дата,
		|	ИсправляемыйДокумент.Ссылка.Номер КАК Номер
		|ИЗ
		|	ИсправляемыйДокумент КАК ИсправляемыйДокумент";
	
	// Ссылка на основание исправления (корректировки) и документ до корректировки.
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначения.РазделительПакетаЗапросов() +
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИсправляемыеДокументы.Ссылка КАК Ссылка,
		|	ИсправляемыеДокументы.Ссылка.Дата КАК Дата,
		|	ИсправляемыеДокументы.Ссылка.Номер КАК Номер
		|ИЗ
		|	ПараметрыДокументовПоследовательности КАК ИсправляемыеДокументы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПараметрыОтбора КАК ПараметрыОтбора
		|		ПО (ИсправляемыеДокументы.Корректировочный = ПараметрыОтбора.Корректировочный
		|				ИЛИ ИсправляемыеДокументы.ЭтоДокументОснование)
		|			И ИсправляемыеДокументы.Ссылка <> ПараметрыОтбора.Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	ИсправляемыеДокументы.ЭтоДокументОснование,
		|	ИсправляемыеДокументы.МоментВремени
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИсправляемыеДокументы.Ссылка
		|ПОМЕСТИТЬ ДокументДоКорректировки
		|ИЗ
		|	ПараметрыДокументовПоследовательности КАК ИсправляемыеДокументы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПараметрыОтбора КАК ПараметрыОтбора
		|		ПО ПараметрыОтбора.Корректировочный И Не ПараметрыОтбора.ИсходныйКорректировочный
		|			И ИсправляемыеДокументы.Корректировочный = Ложь
		|			И ИсправляемыеДокументы.Ссылка <> ПараметрыОтбора.Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	ИсправляемыеДокументы.ЭтоДокументОснование,
		|	ИсправляемыеДокументы.МоментВремени УБЫВ";
	
	// Табличные части Товары и Услуги исправляемого документа.
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначения.РазделительПакетаЗапросов() +
		"ВЫБРАТЬ
		|	Товары.Номенклатура КАК Номенклатура,
		|	Товары.Характеристика КАК Характеристика,
		|	"""" КАК Содержание,
		|	Товары.СтавкаНДС КАК СтавкаНДС,
		|	Товары.НомерГТД КАК НомерГТД,
		|	Товары.Количество КАК Количество,
		|	Товары.Сумма КАК Сумма,
		|	Товары.СуммаНДС КАК СуммаНДС,
		|	Товары.СуммаСНДС КАК СуммаСНДС,
		|	Товары.СтатьяРасходов КАК СтатьяРасходов,
		|	Товары.НомерСтроки КАК НомерСтроки,
		|	Товары.Серия КАК Серия
		|ПОМЕСТИТЬ ТоварыУслугиИсправляемогоДокументаРазвернутые
		|ИЗ
		|	Документ.ПриобретениеТоваровУслуг.Товары КАК Товары
		|ГДЕ
		|	Товары.Ссылка В
		|			(ВЫБРАТЬ
		|				ИсправляемыйДокумент.Ссылка
		|			ИЗ
		|				ИсправляемыйДокумент КАК ИсправляемыйДокумент)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка),
		|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка),
		|	Расходы.Содержание,
		|	Расходы.СтавкаНДС,
		|	ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка),
		|	Расходы.Количество,
		|	Расходы.Сумма,
		|	Расходы.СуммаНДС,
		|	Расходы.СуммаСНДС,
		|	Расходы.СтатьяРасходов,
		|	Расходы.НомерСтроки,
		|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
		|ИЗ
		|	Документ.ПриобретениеУслугПрочихАктивов.Расходы КАК Расходы
		|ГДЕ
		|	Расходы.Ссылка В
		|			(ВЫБРАТЬ
		|				ИсправляемыйДокумент.Ссылка
		|			ИЗ
		|				ИсправляемыйДокумент КАК ИсправляемыйДокумент)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Товары.Номенклатура,
		|	Товары.Характеристика,
		|	Товары.Содержание,
		|	Товары.СтавкаНДС,
		|	Товары.НомерГТД,
		|	Товары.Количество,
		|	Товары.Сумма,
		|	Товары.СуммаНДС,
		|	Товары.СуммаСНДС,
		|	Товары.СтатьяРасходов,
		|	Товары.НомерСтроки,
		|	Товары.Серия
		|ИЗ
		|	Документ.КорректировкаПриобретения.Товары КАК Товары
		|ГДЕ
		|	Товары.Ссылка В
		|			(ВЫБРАТЬ
		|				ИсправляемыйДокумент.Ссылка
		|			ИЗ
		|				ИсправляемыйДокумент КАК ИсправляемыйДокумент)";
	
	// Табличные части Товары и Услуги документа до корректировки.
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначения.РазделительПакетаЗапросов() +
		"ВЫБРАТЬ
		|	Товары.Номенклатура КАК Номенклатура,
		|	Товары.Характеристика КАК Характеристика,
		|	"""" КАК Содержание,
		|	Товары.СтавкаНДС КАК СтавкаНДС,
		|	Товары.НомерГТД КАК НомерГТД,
		|	Товары.Количество КАК Количество,
		|	Товары.Сумма КАК Сумма,
		|	Товары.СуммаНДС КАК СуммаНДС,
		|	Товары.СуммаСНДС КАК СуммаСНДС,
		|	Товары.СтатьяРасходов КАК СтатьяРасходов,
		|	Товары.НомерСтроки КАК НомерСтроки,
		|	Товары.Серия КАК Серия
		|ПОМЕСТИТЬ ТоварыУслугиДокументаДоКорректировкиРазвернутые
		|ИЗ
		|	Документ.ПриобретениеТоваровУслуг.Товары КАК Товары
		|ГДЕ
		|	Товары.Ссылка В
		|			(ВЫБРАТЬ
		|				ДокументДоКорректировки.Ссылка
		|			ИЗ
		|				ДокументДоКорректировки КАК ДокументДоКорректировки)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка),
		|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка),
		|	Расходы.Содержание,
		|	Расходы.СтавкаНДС,
		|	ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка),
		|	Расходы.Количество,
		|	Расходы.Сумма,
		|	Расходы.СуммаНДС,
		|	Расходы.СуммаСНДС,
		|	Расходы.СтатьяРасходов,
		|	Расходы.НомерСтроки,
		|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
		|ИЗ
		|	Документ.ПриобретениеУслугПрочихАктивов.Расходы КАК Расходы
		|ГДЕ
		|	Расходы.Ссылка В
		|			(ВЫБРАТЬ
		|				ДокументДоКорректировки.Ссылка
		|			ИЗ
		|				ДокументДоКорректировки КАК ДокументДоКорректировки)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Товары.Номенклатура,
		|	Товары.Характеристика,
		|	Товары.Содержание,
		|	Товары.СтавкаНДС,
		|	Товары.НомерГТД,
		|	Товары.Количество,
		|	Товары.Сумма,
		|	Товары.СуммаНДС,
		|	Товары.СуммаСНДС,
		|	Товары.СтатьяРасходов,
		|	Товары.НомерСтроки,
		|	Товары.Серия
		|ИЗ
		|	Документ.КорректировкаПриобретения.Товары КАК Товары
		|ГДЕ
		|	Товары.Ссылка В
		|			(ВЫБРАТЬ
		|				ДокументДоКорректировки.Ссылка
		|			ИЗ
		|				ДокументДоКорректировки КАК ДокументДоКорректировки)";
	
	
	// Результирующие табличные части Товары и Услуги.
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначения.РазделительПакетаЗапросов() +
		"ВЫБРАТЬ
		|	Товары.Номенклатура КАК Номенклатура,
		|	Товары.Характеристика КАК Характеристика,
		|	Товары.СтавкаНДС КАК СтавкаНДС,
		|	Товары.НомерГТД КАК НомерГТД,
		|	СУММА(Товары.Количество) КАК Количество,
		|	СУММА(Товары.Сумма) КАК Сумма,
		|	СУММА(Товары.СуммаНДС) КАК СуммаНДС,
		|	Товары.Серия КАК Серия
		|ПОМЕСТИТЬ ТоварыИсправляемогоДокумента
		|ИЗ
		|	ТоварыУслугиИсправляемогоДокументаРазвернутые КАК Товары,
		|	ДанныеДокументаПоступления КАК ДанныеДокументаПоступления
		|ГДЕ
		|	(Товары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар)
		|			ИЛИ Товары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
		|				И НЕ ДанныеДокументаПоступления.ВернутьМногооборотнуюТару)
		|
		|СГРУППИРОВАТЬ ПО
		|	Товары.Номенклатура,
		|	Товары.Характеристика,
		|	Товары.СтавкаНДС,
		|	Товары.НомерГТД,
		|	Товары.Серия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Товары.Номенклатура КАК Номенклатура,
		|	Товары.Характеристика КАК Характеристика,
		|	Товары.СтавкаНДС КАК СтавкаНДС,
		|	Товары.НомерГТД КАК НомерГТД,
		|	СУММА(Товары.Количество) КАК Количество,
		|	СУММА(Товары.Сумма) КАК Сумма,
		|	СУММА(Товары.СуммаНДС) КАК СуммаНДС,
		|	Товары.Серия КАК Серия
		|ПОМЕСТИТЬ ТоварыДокументаДоКорректировки
		|ИЗ
		|	ТоварыУслугиДокументаДоКорректировкиРазвернутые КАК Товары,
		|	ДанныеДокументаПоступления КАК ДанныеДокументаПоступления
		|ГДЕ
		|	(Товары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар)
		|			ИЛИ Товары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
		|				И НЕ ДанныеДокументаПоступления.ВернутьМногооборотнуюТару)
		|
		|СГРУППИРОВАТЬ ПО
		|	Товары.Номенклатура,
		|	Товары.Характеристика,
		|	Товары.СтавкаНДС,
		|	Товары.НомерГТД,
		|	Товары.Серия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Товары.Номенклатура КАК Номенклатура,
		|	Товары.Характеристика КАК Характеристика,
		|	Товары.СтавкаНДС КАК СтавкаНДС,
		|	Товары.НомерГТД КАК НомерГТД,
		|	СУММА(Товары.Количество) КАК Количество,
		|	СУММА(Товары.Сумма) КАК Сумма,
		|	СУММА(Товары.СуммаНДС) КАК СуммаНДС,
		|	СУММА(Товары.СуммаСНДС) КАК СуммаСНДС,
		|	Товары.Серия КАК Серия
		|ПОМЕСТИТЬ ТоварыИсправительногоДокумента
		|ИЗ
		|	Документ.КорректировкаПриобретения.Товары КАК Товары,
		|	ДанныеДокументаПоступления КАК ДанныеДокументаПоступления
		|ГДЕ
		|	Товары.Ссылка = &Ссылка
		|	И (Товары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар)
		|			ИЛИ Товары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
		|				И НЕ ДанныеДокументаПоступления.ВернутьМногооборотнуюТару)
		|
		|СГРУППИРОВАТЬ ПО
		|	Товары.Номенклатура,
		|	Товары.Характеристика,
		|	Товары.СтавкаНДС,
		|	Товары.НомерГТД,
		|	Товары.Серия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Т.Номенклатура КАК Номенклатура,
		|	Т.Характеристика КАК Характеристика,
		|	Т.СтавкаНДС КАК СтавкаНДС,
		|	Т.НомерГТД КАК НомерГТД,
		|	МАКСИМУМ(Т.ТоварыИсправляемогоДокумента_Количество) КАК ТоварыИсправляемогоДокумента_Количество,
		|	МАКСИМУМ(Т.ТоварыИсправляемогоДокумента_Сумма) КАК ТоварыИсправляемогоДокумента_Сумма,
		|	МАКСИМУМ(Т.ТоварыИсправляемогоДокумента_СуммаНДС) КАК ТоварыИсправляемогоДокумента_СуммаНДС,
		|	МАКСИМУМ(Т.ТоварыИсправительногоДокумента_Количество) КАК ТоварыИсправительногоДокумента_Количество,
		|	МАКСИМУМ(Т.ТоварыИсправительногоДокумента_Сумма) КАК ТоварыИсправительногоДокумента_Сумма,
		|	МАКСИМУМ(Т.ТоварыИсправительногоДокумента_СуммаНДС) КАК ТоварыИсправительногоДокумента_СуммаНДС,
		|	МАКСИМУМ(Т.ТоварыИсправительногоДокумента_СуммаСНДС) КАК ТоварыИсправительногоДокумента_СуммаСНДС,
		|	МАКСИМУМ(Т.ТоварыДокументаДоКорректировки_Количество) КАК ТоварыДокументаДоКорректировки_Количество,
		|	МАКСИМУМ(Т.ТоварыДокументаДоКорректировки_Сумма) КАК ТоварыДокументаДоКорректировки_Сумма,
		|	МАКСИМУМ(Т.ТоварыДокументаДоКорректировки_СуммаНДС) КАК ТоварыДокументаДоКорректировки_СуммаНДС,
		|	Т.Серия КАК Серия
		|ПОМЕСТИТЬ РезультатСопоставленияТоваровВрем
		|ИЗ
		|	(ВЫБРАТЬ
		|		ТоварыИсправляемогоДокумента.Номенклатура КАК Номенклатура,
		|		ТоварыИсправляемогоДокумента.Характеристика КАК Характеристика,
		|		ТоварыИсправляемогоДокумента.СтавкаНДС КАК СтавкаНДС,
		|		ТоварыИсправляемогоДокумента.НомерГТД КАК НомерГТД,
		|		ТоварыИсправляемогоДокумента.Количество КАК ТоварыИсправляемогоДокумента_Количество,
		|		ТоварыИсправляемогоДокумента.Сумма КАК ТоварыИсправляемогоДокумента_Сумма,
		|		ТоварыИсправляемогоДокумента.СуммаНДС КАК ТоварыИсправляемогоДокумента_СуммаНДС,
		|		NULL КАК ТоварыИсправительногоДокумента_Количество,
		|		NULL КАК ТоварыИсправительногоДокумента_Сумма,
		|		NULL КАК ТоварыИсправительногоДокумента_СуммаСНДС,
		|		NULL КАК ТоварыИсправительногоДокумента_СуммаНДС,
		|		NULL КАК ТоварыДокументаДоКорректировки_Количество,
		|		NULL КАК ТоварыДокументаДоКорректировки_Сумма,
		|		NULL КАК ТоварыДокументаДоКорректировки_СуммаНДС,
		|		ТоварыИсправляемогоДокумента.Серия КАК Серия
		|	ИЗ
		|		ТоварыИсправляемогоДокумента КАК ТоварыИсправляемогоДокумента
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ТоварыИсправительногоДокумента.Номенклатура,
		|		ТоварыИсправительногоДокумента.Характеристика,
		|		ТоварыИсправительногоДокумента.СтавкаНДС,
		|		ТоварыИсправительногоДокумента.НомерГТД,
		|		NULL,
		|		NULL,
		|		NULL,
		|		ТоварыИсправительногоДокумента.Количество,
		|		ТоварыИсправительногоДокумента.Сумма,
		|		ТоварыИсправительногоДокумента.СуммаСНДС,
		|		ТоварыИсправительногоДокумента.СуммаНДС,
		|		NULL,
		|		NULL,
		|		NULL,
		|		ТоварыИсправительногоДокумента.Серия
		|	ИЗ
		|		ТоварыИсправительногоДокумента КАК ТоварыИсправительногоДокумента
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ТоварыДокументаДоКорректировки.Номенклатура,
		|		ТоварыДокументаДоКорректировки.Характеристика,
		|		ТоварыДокументаДоКорректировки.СтавкаНДС,
		|		ТоварыДокументаДоКорректировки.НомерГТД,
		|		NULL,
		|		NULL,
		|		NULL,
		|		NULL,
		|		NULL,
		|		NULL,
		|		NULL,
		|		ТоварыДокументаДоКорректировки.Количество,
		|		ТоварыДокументаДоКорректировки.Сумма,
		|		ТоварыДокументаДоКорректировки.СуммаНДС,
		|		ТоварыДокументаДоКорректировки.Серия
		|	ИЗ
		|		ТоварыДокументаДоКорректировки КАК ТоварыДокументаДоКорректировки) КАК Т
		|
		|СГРУППИРОВАТЬ ПО
		|	Т.Номенклатура,
		|	Т.Характеристика,
		|	Т.СтавкаНДС,
		|	Т.НомерГТД,
		|	Т.Серия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РезультатСопоставленияТоваровВрем.Номенклатура КАК Номенклатура,
		|	РезультатСопоставленияТоваровВрем.Характеристика КАК Характеристика,
		|	РезультатСопоставленияТоваровВрем.СтавкаНДС КАК СтавкаНДС,
		|	РезультатСопоставленияТоваровВрем.НомерГТД КАК НомерГТД,
		|	РезультатСопоставленияТоваровВрем.ТоварыДокументаДоКорректировки_Количество КАК КоличествоДоКорректировки,
		|	РезультатСопоставленияТоваровВрем.ТоварыДокументаДоКорректировки_Сумма КАК СуммаДоКорректировки,
		|	РезультатСопоставленияТоваровВрем.ТоварыДокументаДоКорректировки_СуммаНДС КАК СуммаНДСДоКорректировки,
		|	РезультатСопоставленияТоваровВрем.ТоварыИсправляемогоДокумента_Количество КАК КоличествоДоИзменения,
		|	РезультатСопоставленияТоваровВрем.ТоварыИсправляемогоДокумента_Сумма КАК СуммаДоИзменения,
		|	РезультатСопоставленияТоваровВрем.ТоварыИсправляемогоДокумента_СуммаНДС КАК СуммаНДСДоИзменения,
		|	РезультатСопоставленияТоваровВрем.ТоварыИсправительногоДокумента_Количество КАК Количество,
		|	РезультатСопоставленияТоваровВрем.ТоварыИсправительногоДокумента_Сумма КАК Сумма,
		|	РезультатСопоставленияТоваровВрем.ТоварыИсправительногоДокумента_СуммаНДС КАК СуммаНДС,
		|	РезультатСопоставленияТоваровВрем.ТоварыИсправительногоДокумента_СуммаСНДС КАК СуммаСНДС,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(РезультатСопоставленияТоваровВрем.ТоварыДокументаДоКорректировки_Сумма, РезультатСопоставленияТоваровВрем.ТоварыИсправляемогоДокумента_Сумма) ЕСТЬ NULL
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК ЕстьВДокументеПоступленияРеализации,
		|	РезультатСопоставленияТоваровВрем.Серия КАК Серия
		|ПОМЕСТИТЬ РезультатСопоставленияТоваров
		|ИЗ
		|	РезультатСопоставленияТоваровВрем КАК РезультатСопоставленияТоваровВрем
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	РезультатСопоставленияТоваровВрем.Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Товары.Номенклатура КАК Номенклатура,
		|	Товары.Содержание КАК Содержание,
		|	МАКСИМУМ(Товары.НомерСтроки) КАК НомерСтроки
		|ПОМЕСТИТЬ НомерСтрок
		|ИЗ
		|	ТоварыУслугиИсправляемогоДокументаРазвернутые КАК Товары
		|
		|СГРУППИРОВАТЬ ПО
		|	Товары.Номенклатура,
		|	Товары.Содержание
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура,
		|	Содержание
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РезультатСопоставления.Номенклатура КАК Номенклатура,
		|	&ТекстЗапросаХарактеристика,
		|	РезультатСопоставления.СтавкаНДС КАК СтавкаНДС,
		|	РезультатСопоставления.СтавкаНДС КАК СтавкаНДСДоИзменения,
		|	РезультатСопоставления.НомерГТД.Код КАК НомерГТД,
		|	РезультатСопоставления.НомерГТД.Код КАК НомерГТДДоИзменения,
		|	РезультатСопоставления.НомерГТД.СтранаПроисхождения КАК СтранаПроисхождения,
		|	РезультатСопоставления.НомерГТД.СтранаПроисхождения КАК СтранаПроисхожденияДоИзменения,
		|	РезультатСопоставления.КоличествоДоКорректировки КАК КоличествоДоКорректировки,
		|	РезультатСопоставления.СуммаДоКорректировки КАК СуммаДоКорректировки,
		|	РезультатСопоставления.СуммаНДСДоКорректировки КАК СуммаНДСДоКорректировки,
		|	РезультатСопоставления.КоличествоДоИзменения КАК КоличествоДоИзменения,
		|	РезультатСопоставления.СуммаДоИзменения КАК СуммаДоИзменения,
		|	РезультатСопоставления.СуммаНДСДоИзменения КАК СуммаНДСДоИзменения,
		|	РезультатСопоставления.Количество КАК Количество,
		|	РезультатСопоставления.Сумма КАК Сумма,
		|	РезультатСопоставления.СуммаНДС КАК СуммаНДС,
		|	РезультатСопоставления.СуммаСНДС КАК СуммаСНДС,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(РезультатСопоставления.КоличествоДоКорректировки, 0) = 0
		|			ТОГДА 0
		|		ИНАЧЕ ВЫБОР
		|				КОГДА РезультатСопоставления.СуммаДоКорректировки / РезультатСопоставления.КоличествоДоКорректировки < 0.01
		|					ТОГДА 0.01
		|				ИНАЧЕ ВЫРАЗИТЬ(РезультатСопоставления.СуммаДоКорректировки / РезультатСопоставления.КоличествоДоКорректировки КАК ЧИСЛО(15, 2))
		|			КОНЕЦ
		|	КОНЕЦ КАК ЦенаДоКорректировки,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(РезультатСопоставления.КоличествоДоИзменения, 0) = 0
		|			ТОГДА 0
		|		ИНАЧЕ ВЫБОР
		|				КОГДА РезультатСопоставления.СуммаДоИзменения / РезультатСопоставления.КоличествоДоИзменения < 0.01
		|					ТОГДА 0.01
		|				ИНАЧЕ ВЫРАЗИТЬ(РезультатСопоставления.СуммаДоИзменения / РезультатСопоставления.КоличествоДоИзменения КАК ЧИСЛО(15, 2))
		|			КОНЕЦ
		|	КОНЕЦ КАК ЦенаДоИзменения,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(РезультатСопоставления.Количество, 0) = 0
		|			ТОГДА 0
		|		ИНАЧЕ ВЫБОР
		|				КОГДА РезультатСопоставления.Сумма / РезультатСопоставления.Количество < 0.01
		|					ТОГДА 0.01
		|				ИНАЧЕ ВЫРАЗИТЬ(РезультатСопоставления.Сумма / РезультатСопоставления.Количество КАК ЧИСЛО(15, 2))
		|			КОНЕЦ
		|	КОНЕЦ КАК Цена,
		|	РезультатСопоставления.ЕстьВДокументеПоступленияРеализации КАК ЕстьВДокументеПоступленияРеализации,
		|	ЕстьNull(НомерСтрок.НомерСтроки, 0) КАК НомерСтрокиДокумента,
		|	&ТекстЗапросаСерия
		|ИЗ
		|	РезультатСопоставленияТоваров КАК РезультатСопоставления
		|		ЛЕВОЕ СОЕДИНЕНИЕ НомерСтрок КАК НомерСтрок
		|		ПО РезультатСопоставления.Номенклатура = НомерСтрок.Номенклатура
		|
		|УПОРЯДОЧИТЬ ПО
		|	РезультатСопоставления.ЕстьВДокументеПоступленияРеализации УБЫВ,
		|	ЕстьNull(НомерСтрок.НомерСтроки, 0)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Услуги.Номенклатура КАК Номенклатура,
		|	Услуги.Содержание КАК Содержание,
		|	Услуги.СтавкаНДС КАК СтавкаНДС,
		|	Услуги.СтатьяРасходов КАК СтатьяРасходов,
		|	СУММА(Услуги.Количество) КАК Количество,
		|	СУММА(Услуги.Сумма) КАК Сумма,
		|	СУММА(Услуги.СуммаНДС) КАК СуммаНДС
		|ПОМЕСТИТЬ УслугиИсправляемогоДокумента
		|ИЗ
		|	ТоварыУслугиИсправляемогоДокументаРазвернутые КАК Услуги
		|ГДЕ
		|	(Услуги.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга)
		|			ИЛИ Услуги.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
		|			ИЛИ &ПоступлениеУслуг)
		|
		|СГРУППИРОВАТЬ ПО
		|	Услуги.Номенклатура,
		|	Услуги.Содержание,
		|	Услуги.СтавкаНДС,
		|	Услуги.СтатьяРасходов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Услуги.Номенклатура КАК Номенклатура,
		|	Услуги.Содержание КАК Содержание,
		|	Услуги.СтавкаНДС КАК СтавкаНДС,
		|	Услуги.СтатьяРасходов КАК СтатьяРасходов,
		|	СУММА(Услуги.Количество) КАК Количество,
		|	СУММА(Услуги.Сумма) КАК Сумма,
		|	СУММА(Услуги.СуммаНДС) КАК СуммаНДС
		|ПОМЕСТИТЬ УслугиДокументаДоКорректировки
		|ИЗ
		|	ТоварыУслугиДокументаДоКорректировкиРазвернутые КАК Услуги
		|ГДЕ
		|	(Услуги.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга)
		|			ИЛИ Услуги.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
		|			ИЛИ &ПоступлениеУслуг)
		|
		|СГРУППИРОВАТЬ ПО
		|	Услуги.Номенклатура,
		|	Услуги.Содержание,
		|	Услуги.СтавкаНДС,
		|	Услуги.СтатьяРасходов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КорректировкаПоступленияТовары.Номенклатура КАК Номенклатура,
		|	КорректировкаПоступленияТовары.Характеристика КАК Характеристика,
		|	КорректировкаПоступленияТовары.Содержание КАК Содержание,
		|	КорректировкаПоступленияТовары.СтавкаНДС КАК СтавкаНДС,
		|	КорректировкаПоступленияТовары.СтатьяРасходов КАК СтатьяРасходов,
		|	СУММА(КорректировкаПоступленияТовары.Количество) КАК Количество,
		|	СУММА(КорректировкаПоступленияТовары.Сумма) КАК Сумма,
		|	СУММА(КорректировкаПоступленияТовары.СуммаНДС) КАК СуммаНДС,
		|	СУММА(КорректировкаПоступленияТовары.СуммаСНДС) КАК СуммаСНДС
		|ПОМЕСТИТЬ УслугиИсправительногоДокумента
		|ИЗ
		|	Документ.КорректировкаПриобретения.Товары КАК КорректировкаПоступленияТовары
		|ГДЕ
		|	КорректировкаПоступленияТовары.Ссылка = &Ссылка
		|	И (КорректировкаПоступленияТовары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга)
		|			ИЛИ КорректировкаПоступленияТовары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
		|			ИЛИ &ПоступлениеУслуг)
		|
		|СГРУППИРОВАТЬ ПО
		|	КорректировкаПоступленияТовары.Номенклатура,
		|	КорректировкаПоступленияТовары.Характеристика,
		|	КорректировкаПоступленияТовары.Содержание,
		|	КорректировкаПоступленияТовары.СтавкаНДС,
		|	КорректировкаПоступленияТовары.СтатьяРасходов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Т.Номенклатура КАК Номенклатура,
		|	Т.Содержание КАК Содержание,
		|	Т.СтавкаНДС КАК СтавкаНДС,
		|	Т.СтатьяРасходов КАК СтатьяРасходов,
		|	МАКСИМУМ(Т.УслугиИсправляемогоДокумента_Количество) КАК УслугиИсправляемогоДокумента_Количество,
		|	МАКСИМУМ(Т.УслугиИсправляемогоДокумента_Сумма) КАК УслугиИсправляемогоДокумента_Сумма,
		|	МАКСИМУМ(Т.УслугиИсправляемогоДокумента_СуммаНДС) КАК УслугиИсправляемогоДокумента_СуммаНДС,
		|	МАКСИМУМ(Т.УслугиИсправительногоДокумента_Количество) КАК УслугиИсправительногоДокумента_Количество,
		|	МАКСИМУМ(Т.УслугиИсправительногоДокумента_Сумма) КАК УслугиИсправительногоДокумента_Сумма,
		|	МАКСИМУМ(Т.УслугиИсправительногоДокумента_СуммаНДС) КАК УслугиИсправительногоДокумента_СуммаНДС,
		|	МАКСИМУМ(Т.УслугиИсправительногоДокумента_СуммаСНДС) КАК УслугиИсправительногоДокумента_СуммаСНДС,
		|	МАКСИМУМ(Т.УслугиДокументаДоКорректировки_Количество) КАК УслугиДокументаДоКорректировки_Количество,
		|	МАКСИМУМ(Т.УслугиДокументаДоКорректировки_Сумма) КАК УслугиДокументаДоКорректировки_Сумма,
		|	МАКСИМУМ(Т.УслугиДокументаДоКорректировки_СуммаНДС) КАК УслугиДокументаДоКорректировки_СуммаНДС
		|ПОМЕСТИТЬ РезультатСопоставленияУслугВрем
		|ИЗ
		|	(ВЫБРАТЬ
		|		УслугиИсправляемогоДокумента.Номенклатура КАК Номенклатура,
		|		УслугиИсправляемогоДокумента.Содержание КАК Содержание,
		|		УслугиИсправляемогоДокумента.СтавкаНДС КАК СтавкаНДС,
		|		УслугиИсправляемогоДокумента.СтатьяРасходов КАК СтатьяРасходов,
		|		УслугиИсправляемогоДокумента.Количество КАК УслугиИсправляемогоДокумента_Количество,
		|		УслугиИсправляемогоДокумента.Сумма КАК УслугиИсправляемогоДокумента_Сумма,
		|		УслугиИсправляемогоДокумента.СуммаНДС КАК УслугиИсправляемогоДокумента_СуммаНДС,
		|		NULL КАК УслугиИсправительногоДокумента_Количество,
		|		NULL КАК УслугиИсправительногоДокумента_Сумма,
		|		NULL КАК УслугиИсправительногоДокумента_СуммаСНДС,
		|		NULL КАК УслугиИсправительногоДокумента_СуммаНДС,
		|		NULL КАК УслугиДокументаДоКорректировки_Количество,
		|		NULL КАК УслугиДокументаДоКорректировки_Сумма,
		|		NULL КАК УслугиДокументаДоКорректировки_СуммаНДС
		|	ИЗ
		|		УслугиИсправляемогоДокумента КАК УслугиИсправляемогоДокумента
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		УслугиИсправительногоДокумента.Номенклатура,
		|		УслугиИсправительногоДокумента.Содержание,
		|		УслугиИсправительногоДокумента.СтавкаНДС,
		|		УслугиИсправительногоДокумента.СтатьяРасходов,
		|		NULL,
		|		NULL,
		|		NULL,
		|		УслугиИсправительногоДокумента.Количество,
		|		УслугиИсправительногоДокумента.Сумма,
		|		УслугиИсправительногоДокумента.СуммаСНДС,
		|		УслугиИсправительногоДокумента.СуммаНДС,
		|		NULL,
		|		NULL,
		|		NULL
		|	ИЗ
		|		УслугиИсправительногоДокумента КАК УслугиИсправительногоДокумента
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		УслугиДокументаДоКорректировки.Номенклатура,
		|		УслугиДокументаДоКорректировки.Содержание,
		|		УслугиДокументаДоКорректировки.СтавкаНДС,
		|		УслугиДокументаДоКорректировки.СтатьяРасходов,
		|		NULL,
		|		NULL,
		|		NULL,
		|		NULL,
		|		NULL,
		|		NULL,
		|		NULL,
		|		УслугиДокументаДоКорректировки.Количество,
		|		УслугиДокументаДоКорректировки.Сумма,
		|		УслугиДокументаДоКорректировки.СуммаНДС
		|	ИЗ
		|		УслугиДокументаДоКорректировки КАК УслугиДокументаДоКорректировки) КАК Т
		|
		|СГРУППИРОВАТЬ ПО
		|	Т.Номенклатура,
		|	Т.Содержание,
		|	Т.СтавкаНДС,
		|	Т.СтатьяРасходов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РезультатСопоставленияУслугВрем.Номенклатура КАК Номенклатура,
		|	РезультатСопоставленияУслугВрем.Содержание КАК Содержание,
		|	РезультатСопоставленияУслугВрем.СтавкаНДС КАК СтавкаНДС,
		|	РезультатСопоставленияУслугВрем.СтатьяРасходов КАК СтатьяРасходов,
		|	РезультатСопоставленияУслугВрем.УслугиДокументаДоКорректировки_Количество КАК КоличествоДоКорректировки,
		|	РезультатСопоставленияУслугВрем.УслугиДокументаДоКорректировки_Сумма КАК СуммаДоКорректировки,
		|	РезультатСопоставленияУслугВрем.УслугиДокументаДоКорректировки_СуммаНДС КАК СуммаНДСДоКорректировки,
		|	РезультатСопоставленияУслугВрем.УслугиИсправляемогоДокумента_Количество КАК КоличествоДоИзменения,
		|	РезультатСопоставленияУслугВрем.УслугиИсправляемогоДокумента_Сумма КАК СуммаДоИзменения,
		|	РезультатСопоставленияУслугВрем.УслугиИсправляемогоДокумента_СуммаНДС КАК СуммаНДСДоИзменения,
		|	РезультатСопоставленияУслугВрем.УслугиИсправительногоДокумента_Количество КАК Количество,
		|	РезультатСопоставленияУслугВрем.УслугиИсправительногоДокумента_Сумма КАК Сумма,
		|	РезультатСопоставленияУслугВрем.УслугиИсправительногоДокумента_СуммаНДС КАК СуммаНДС,
		|	РезультатСопоставленияУслугВрем.УслугиИсправительногоДокумента_СуммаСНДС КАК СуммаСНДС,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(РезультатСопоставленияУслугВрем.УслугиИсправляемогоДокумента_Сумма, РезультатСопоставленияУслугВрем.УслугиДокументаДоКорректировки_Сумма) ЕСТЬ NULL
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК ЕстьВДокументеПоступленияРеализации
		|ПОМЕСТИТЬ РезультатСопоставленияУслуг
		|ИЗ
		|	РезультатСопоставленияУслугВрем КАК РезультатСопоставленияУслугВрем
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РезультатСопоставления.Номенклатура КАК Номенклатура,
		|	РезультатСопоставления.Содержание КАК Содержание,
		|	РезультатСопоставления.Содержание КАК СодержаниеДоИзменения,
		|	РезультатСопоставления.СтавкаНДС КАК СтавкаНДС,
		|	РезультатСопоставления.СтавкаНДС КАК СтавкаНДСДоИзменения,
		|	РезультатСопоставления.КоличествоДоКорректировки КАК КоличествоДоКорректировки,
		|	РезультатСопоставления.СуммаДоКорректировки КАК СуммаДоКорректировки,
		|	РезультатСопоставления.СуммаНДСДоКорректировки КАК СуммаНДСДоКорректировки,
		|	РезультатСопоставления.КоличествоДоИзменения КАК КоличествоДоИзменения,
		|	РезультатСопоставления.СуммаДоИзменения КАК СуммаДоИзменения,
		|	РезультатСопоставления.СуммаНДСДоИзменения КАК СуммаНДСДоИзменения,
		|	РезультатСопоставления.Количество КАК Количество,
		|	РезультатСопоставления.Сумма КАК Сумма,
		|	РезультатСопоставления.СуммаНДС КАК СуммаНДС,
		|	РезультатСопоставления.СуммаСНДС КАК СуммаСНДС,
		|	ВЫБОР
		|		КОГДА НЕ(РезультатСопоставления.Количество ЕСТЬ NULL
		|					ИЛИ РезультатСопоставления.Количество = 0)
		|			ТОГДА ВЫРАЗИТЬ(РезультатСопоставления.Сумма / РезультатСопоставления.Количество КАК ЧИСЛО(15, 2))
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Цена,
		|	ВЫБОР
		|		КОГДА НЕ(РезультатСопоставления.КоличествоДоИзменения ЕСТЬ NULL
		|					ИЛИ РезультатСопоставления.КоличествоДоИзменения = 0)
		|			ТОГДА ВЫРАЗИТЬ(РезультатСопоставления.СуммаДоИзменения / РезультатСопоставления.КоличествоДоИзменения КАК ЧИСЛО(15, 2))
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ЦенаДоИзменения,
		|	ВЫБОР
		|		КОГДА НЕ(РезультатСопоставления.КоличествоДоКорректировки ЕСТЬ NULL
		|					ИЛИ РезультатСопоставления.КоличествоДоКорректировки = 0)
		|			ТОГДА ВЫРАЗИТЬ(РезультатСопоставления.СуммаДоКорректировки / РезультатСопоставления.КоличествоДоКорректировки КАК ЧИСЛО(15, 2))
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ЦенаДоКорректировки,
		|	РезультатСопоставления.СтатьяРасходов КАК СтатьяРасходов,
		|	РезультатСопоставления.СтатьяРасходов.КорреспондирующийСчет КАК СчетЗатрат,
		|	РезультатСопоставления.СтатьяРасходов.КорреспондирующийСчет КАК СчетЗатратНУ,
		|	РезультатСопоставления.ЕстьВДокументеПоступленияРеализации КАК ЕстьВДокументеПоступленияРеализации,
		|	ЕстьNull(НомерСтрок.НомерСтроки, 0) КАК НомерСтрокиДокумента
		|ИЗ
		|	РезультатСопоставленияУслуг КАК РезультатСопоставления
		|		ЛЕВОЕ СОЕДИНЕНИЕ НомерСтрок КАК НомерСтрок
		|		ПО РезультатСопоставления.Номенклатура = НомерСтрок.Номенклатура
		|			И РезультатСопоставления.Содержание = НомерСтрок.Содержание
		|
		|УПОРЯДОЧИТЬ ПО
		|	ЕстьВДокументеПоступленияРеализации УБЫВ,
		|	ЕстьNull(НомерСтрок.НомерСтроки, 0)";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаХарактеристика",
		ПолучитьТекстЗапросаХарактеристики(КомпонентыОбмена, "РезультатСопоставления"));
		
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаСерия",
		ПолучитьТекстЗапросаСерии(КомпонентыОбмена, "РезультатСопоставления"));
	
	Запрос = Новый Запрос(ТекстЗапроса);
	
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеИБ.ДокументОснование);
	Запрос.УстановитьПараметр("МоментВремени",     ДанныеИБ.МоментВремени());
	Запрос.УстановитьПараметр("ПоступлениеУслуг",  ТипЗнч(ДанныеИБ.ДокументОснование) = Тип("ДокументСсылка.ПриобретениеУслугПрочихАктивов"));
	Запрос.УстановитьПараметр("Ссылка",            ДанныеИБ.Ссылка);
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	ЗаказПоставщику = Неопределено;
	ДанныеXDTO.Вставить("Грузоотправитель");
	
	Выборка = РезультатыЗапроса[1].Выбрать();
	Если Выборка.Следующий() Тогда
		ДанныеXDTO.Грузоотправитель = Выборка.Грузоотправитель;
		ЗаказПоставщику             = Выборка.ЗаказПоставщику;
	КонецЕсли;
	
	ВыборкаДанных = РезультатыЗапроса[6].Выбрать();
	Если ВыборкаДанных.Следующий()
		И ЗначениеЗаполнено(ВыборкаДанных.Ссылка) Тогда
			
		ИмяПКО = ИмяПКОДляОбъектаДанных(КомпонентыОбмена, ВыборкаДанных.Ссылка, ВыборкаДанных.Ссылка.Организация);
		Если ИмяПКО <> "" Тогда
			ДанныеXDTO.Вставить("ДокументПоступления", Новый Структура("Значение, ИмяПКО", ВыборкаДанных.Ссылка, ИмяПКО));
		КонецЕсли;
		
	КонецЕсли;
	
	ИсправляемыйДокументЭтоКорректировкаПоступления = Ложь;
	
	ВыборкаДанных = РезультатыЗапроса[7].Выбрать();
	Если ВыборкаДанных.Следующий()
		И ЗначениеЗаполнено(ВыборкаДанных.Ссылка) Тогда
			
		ИсправляемыйДокументЭтоКорректировкаПоступления = (ТипЗнч(ВыборкаДанных.Ссылка) = Тип("ДокументСсылка.КорректировкаПриобретения"));
		
		ИмяПКО = ИмяПКОДляОбъектаДанных(КомпонентыОбмена, ВыборкаДанных.Ссылка, ВыборкаДанных.Ссылка.Организация);
		Если ИмяПКО <> "" Тогда
			ДанныеXDTO.Вставить("ИсправляемыйДокументПоступления", Новый Структура("Значение, ИмяПКО", ВыборкаДанных.Ссылка, ИмяПКО));
		КонецЕсли;
		
	КонецЕсли;
	
	Если ДанныеXDTO.ВидОперации = "СогласованноеИзменение"
		Или ИсправляемыйДокументЭтоКорректировкаПоступления Тогда
		ДанныеXDTO.Вставить("ВосстановитьНДС", Истина);
	Иначе
		ДанныеXDTO.Вставить("ВосстановитьНДС", Ложь);
	КонецЕсли;
	
	// Дополнительные свойства для договора
	Если ЗначениеЗаполнено(ДанныеИБ.Договор) Тогда
		ДанныеXDTO.Вставить("Договор", ДанныеИБ.Договор);
	Иначе
		
		РасчетыВУсловныхЕдиницах = Ложь;
		Если ЗначениеЗаполнено(ДанныеИБ.Соглашение) Тогда
			РасчетыВУсловныхЕдиницах = ВзаиморасчетыСервер.РасчетыВУсловныхЕдиницах(ДанныеИБ.Соглашение);
		Иначе
			РасчетыВУсловныхЕдиницах = (ДанныеИБ.Валюта = Константы.ВалютаРегламентированногоУчета.Получить() И ДанныеИБ.Валюта <> ДанныеИБ.ВалютаВзаиморасчетов);
		КонецЕсли;
	
		ПараметрыПоУмолчанию = Новый Структура(ДоговорФиксированнаяСтруктураКлючей());
		ПараметрыПоУмолчанию.Контрагент               = ДанныеИБ.Контрагент;
		ПараметрыПоУмолчанию.Организация              = ДанныеИБ.Организация;
		ПараметрыПоУмолчанию.ВалютаВзаиморасчетов     = ДанныеИБ.ВалютаВзаиморасчетов;
		ПараметрыПоУмолчанию.ВидДоговора              = "СПоставщиком";
		ПараметрыПоУмолчанию.Дата                     = ДанныеИБ.Дата;
		ПараметрыПоУмолчанию.Номер                    = ДанныеИБ.Номер;
		ПараметрыПоУмолчанию.РасчетыВУсловныхЕдиницах = РасчетыВУсловныхЕдиницах;
		
		Заказ = Новый Структура("Заказ, Соглашение, Сделка", ЗаказПоставщику, ДанныеИБ.Соглашение, ДанныеИБ.Сделка);
		
		Договор = ДоговорИнструкцияКонвертацииПоДаннымВзаиморасчетов(ПараметрыПоУмолчанию, КомпонентыОбмена, Заказ, ДанныеИБ);
		ДанныеXDTO.Вставить("Договор", Договор);
		
	КонецЕсли;
	
	ТаблицаТовары = РезультатыЗапроса[17].Выгрузить();
	ТаблицаУслуги = РезультатыЗапроса[23].Выгрузить();
	
	ЗаполнитьХарактеристикуУпаковкуВТЧ(КомпонентыОбмена, ТаблицаТовары, Истина, Ложь);
	Если ВерсияФорматаЧислом(КомпонентыОбмена.ВерсияФорматаОбмена) >= ВерсияФорматаЧислом("1.6") Тогда
		ВидДеятельностиНДС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеИБ.ДокументОснование, "ЗакупкаПодДеятельность");
		
		ТаблицаТовары.Колонки.Добавить("ЗакупкаПодДеятельность");
		ТаблицаТовары.ЗаполнитьЗначения(ВидДеятельностиНДС, "ЗакупкаПодДеятельность");
	КонецЕсли;
	
	ДанныеXDTO.Вставить("Сумма", ТаблицаТовары.Итог("СуммаСНДС") + ТаблицаУслуги.Итог("СуммаСНДС"));
	
	НДСПоСтавкам2и4 = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеИБ.Контрагент, "НДСпоСтавкам4и2");
	
	Если ТаблицаТовары.Количество() > 0 Тогда
		ОпределитьСтавкуНДС_Отправка(ТаблицаТовары, НДСПоСтавкам2и4);	
		ДанныеXDTO.Вставить("Товары", ТаблицаТовары);
	КонецЕсли;
	
	Если ТаблицаУслуги.Количество() > 0 Тогда
		ОпределитьСтавкуНДС_Отправка(ТаблицаУслуги, НДСПоСтавкам2и4);	
		ДанныеXDTO.Вставить("Услуги", ТаблицаУслуги);
	КонецЕсли;
КонецПроцедуры
