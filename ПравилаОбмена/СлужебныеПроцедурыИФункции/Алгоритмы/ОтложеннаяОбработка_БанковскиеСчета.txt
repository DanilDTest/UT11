Процедура ОтложеннаяОбработка_БанковскиеСчета(Объект, КомпонентыОбмена, ОбъектМодифицирован)
	Если НЕ ЗначениеЗаполнено(Объект.Банк) Тогда
	
		СтруктураПоиска = Новый Структура();
		
		СоответствиеКлючевыхПолей = Новый Соответствие;
		СоответствиеКлючевыхПолей.Вставить("БИКБанка",          "БИК");
		СоответствиеКлючевыхПолей.Вставить("СВИФТБанка",        "СВИФТБИК");
		СоответствиеКлючевыхПолей.Вставить("НаименованиеБанка", "Наименование");
		СоответствиеКлючевыхПолей.Вставить("КоррСчетБанка",     "КоррСчет");
		
		СоответствиеПрочихПолей = Новый Соответствие;
		СоответствиеПрочихПолей.Вставить("ГородБанка",              "Город");
		СоответствиеПрочихПолей.Вставить("АдресБанкаМеждународный", "Адрес");
		СоответствиеПрочихПолей.Вставить("ИностранныйБанк",         "ИностранныйБанк");
		СоответствиеПрочихПолей.Вставить("ТелефоныБанка",           "Телефоны");
		СоответствиеПрочихПолей.Вставить("СтранаБанка",             "Страна");
		
		Для Каждого Соответствие Из СоответствиеКлючевыхПолей Цикл
			Если ЗначениеЗаполнено(Объект[Соответствие.Ключ]) Тогда
				СтруктураПоиска.Вставить(Соответствие.Значение, Объект[Соответствие.Ключ]);
			КонецЕсли;
		КонецЦикла;	
			
		Если ЗначениеЗаполнено(СтруктураПоиска) Тогда
		    Объект.РучноеИзменениеРеквизитовБанка = Истина;
			
			ТаблицаБанки = КомпонентыОбмена.ПараметрыКонвертации.ТаблицаБанки;
			МассивСтрок = ТаблицаБанки.НайтиСтроки(СтруктураПоиска);
			Если МассивСтрок.Количество() = 1 Тогда
				СтрокаБанк = МассивСтрок[0];
				
				Если СтрокаБанк.ИностранныйБанк = Истина Тогда
					СоответствиеПрочихПолей.Вставить("НаименованиеБанкаМеждународное", "Наименование");
					СоответствиеПрочихПолей.Вставить("СчетВБанкеДляРасчетов",          "КоррСчет");
					СоответствиеПрочихПолей.Вставить("ГородБанкаМеждународный",        "Город");
				КонецЕсли;
			
				Для Каждого Соответствие Из СоответствиеПрочихПолей Цикл
					Если ЗначениеЗаполнено(СтрокаБанк[Соответствие.Значение]) Тогда
					    Объект[Соответствие.Ключ] = СтрокаБанк[Соответствие.Значение];
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.БанкДляРасчетов) Тогда
	
		СтруктураПоиска = Новый Структура();
		
		СоответствиеКлючевыхПолей = Новый Соответствие;
		СоответствиеКлючевыхПолей.Вставить("БИКБанкаДляРасчетов",          "БИК");
		СоответствиеКлючевыхПолей.Вставить("СВИФТБанкаДляРасчетов",        "СВИФТБИК");
		СоответствиеКлючевыхПолей.Вставить("НаименованиеБанкаДляРасчетов", "Наименование");
		СоответствиеКлючевыхПолей.Вставить("КоррСчетБанкаДляРасчетов",     "КоррСчет");
		
		СоответствиеПрочихПолей = Новый Соответствие;
		СоответствиеПрочихПолей.Вставить("ГородБанкаДляРасчетов",              "Город");
		СоответствиеПрочихПолей.Вставить("АдресБанкаДляРасчетовМеждународный", "Адрес");
		СоответствиеПрочихПолей.Вставить("ТелефоныБанкаДляРасчетов",           "Телефоны");
		СоответствиеПрочихПолей.Вставить("СтранаБанкаДляРасчетов",             "Страна");
		
		Для Каждого Соответствие Из СоответствиеКлючевыхПолей Цикл
			Если ЗначениеЗаполнено(Объект[Соответствие.Ключ]) Тогда
				СтруктураПоиска.Вставить(Соответствие.Значение, Объект[Соответствие.Ключ]);
			КонецЕсли;
		КонецЦикла;	
			
		Если ЗначениеЗаполнено(СтруктураПоиска) Тогда
		    Объект.РучноеИзменениеРеквизитовБанкаДляРасчетов = Истина;
			
			ТаблицаБанки = КомпонентыОбмена.ПараметрыКонвертации.ТаблицаБанки;
			МассивСтрок = ТаблицаБанки.НайтиСтроки(СтруктураПоиска);
			Если МассивСтрок.Количество() = 1 Тогда
				СтрокаБанк = МассивСтрок[0];
				
				Если НЕ Объект.ВалютаДенежныхСредств = ВалютаРегламентированногоУчета(КомпонентыОбмена) Тогда
					СоответствиеПрочихПолей.Вставить("НаименованиеБанкаДляРасчетовМеждународное", "Наименование");
					СоответствиеПрочихПолей.Вставить("ГородБанкаДляРасчетовМеждународный",        "Город");
				КонецЕсли;
			
				Для Каждого Соответствие Из СоответствиеПрочихПолей Цикл
					Если ЗначениеЗаполнено(СтрокаБанк[Соответствие.Значение]) Тогда
					    Объект[Соответствие.Ключ] = СтрокаБанк[Соответствие.Значение];
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры
