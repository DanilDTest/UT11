Процедура ЗаполнитьКассуККМПоВалютеСкладуИОрганизации(ПолученныеДанные, КомпонентыОбмена, НаименованиеСклада = "")
	Если ЗначениеЗаполнено(НаименованиеСклада) Тогда
		Склад = Справочники.Склады.НайтиПоНаименованию(НаименованиеСклада);
	Иначе
		Склад = ПолученныеДанные.Склад;
	КонецЕсли;
		
	РеквизитыКассыККМ = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПолученныеДанные.КассаККМ, "ВалютаДенежныхСредств, Владелец, Склад");
	
	Если РеквизитыКассыККМ.ВалютаДенежныхСредств <> ПолученныеДанные.Валюта 
		Или РеквизитыКассыККМ.Владелец <> ПолученныеДанные.Организация
		ИЛИ (КомпонентыОбмена.ПараметрыКонвертации.ВыгружатьАналитикуПоСкладам И РеквизитыКассыККМ.Склад <> Склад) Тогда
		
		КассаККМ = Неопределено;
		Валюта = ПолученныеДанные.Валюта;
		Организация = ПолученныеДанные.Организация;
		
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("Валюта", Валюта);
		ПараметрыОтбора.Вставить("Организация", Организация);
		ПараметрыОтбора.Вставить("Склад", Склад);
	
		НеобходимоСоздатьЭлемент = Ложь;
	
		Если ОбщегоНазначения.СсылкаСуществует(Организация) И ОбщегоНазначения.СсылкаСуществует(Валюта) Тогда
			НаименованиеЭлементаДляОбменаПоУмолчанию = СокрЛП(Организация) + " (" + СокрЛП(Валюта) + ")";
		Иначе
			НаименованиеЭлементаДляОбменаПоУмолчанию = НСтр("ru = 'Основная касса ККМ';
															|en = 'Main cash register'");
		КонецЕсли;
		
		СписокСкладов = Новый СписокЗначений();
		СписокСкладов.Добавить(Справочники.Склады.ПустаяСсылка());
		СписокСкладов.Добавить(Склад);
		
		Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 2
			|	Кассы.Ссылка КАК Касса,
			|	ВЫБОР
			|		КОГДА Кассы.Наименование = &НаименованиеЭлементаДляОбменаПоУмолчанию
			|			ТОГДА ИСТИНА
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ КАК ЭтоЭлементПоУмолчаниюДляОбмена,
			|	Кассы.Склад КАК Склад
			|ИЗ
			|	Справочник.КассыККМ КАК Кассы
			|ГДЕ
			|	НЕ Кассы.ПометкаУдаления
			|	И Кассы.Владелец = &Организация
			|	И Кассы.ВалютаДенежныхСредств = &Валюта
			|	И Кассы.Склад В(&СписокСкладов)
	
			|
			|УПОРЯДОЧИТЬ ПО
			|	ЭтоЭлементПоУмолчаниюДляОбмена УБЫВ,
			|	Склад УБЫВ");
	
		Запрос.УстановитьПараметр("НаименованиеЭлементаДляОбменаПоУмолчанию", НаименованиеЭлементаДляОбменаПоУмолчанию);
		Запрос.УстановитьПараметр("Организация", Организация);
		Запрос.УстановитьПараметр("Валюта", Валюта);
		Запрос.УстановитьПараметр("СписокСкладов", СписокСкладов);
	
	
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Если Выборка.Количество() = 1 Или Выборка.ЭтоЭлементПоУмолчаниюДляОбмена Тогда
				КассаККМ = Выборка.Касса;
			Иначе
				НеобходимоСоздатьЭлемент = Истина;
			КонецЕсли;
		Иначе
			НеобходимоСоздатьЭлемент = Истина;
		КонецЕсли;
	
		// Создание элемента для обмена с заданными параметрами
		Если НеобходимоСоздатьЭлемент Тогда
			КассаОбъект = Справочники.КассыККМ.СоздатьЭлемент();
			КассаОбъект.Наименование			= НаименованиеЭлементаДляОбменаПоУмолчанию;
			КассаОбъект.ВалютаДенежныхСредств	= Валюта;
			КассаОбъект.Владелец				= Организация;
			КассаОбъект.Склад					= Склад;
			КассаОбъект.ТипКассы                = Перечисления.ТипыКассККМ.ФискальныйРегистратор;
	
			Попытка
				КассаОбъект.Записать();
				КассаККМ = КассаОбъект.Ссылка;
			Исключение
				КассаККМ = Справочники.КассыККМ.ПустаяСсылка();
				ЗаписатьОшибкуВЖурналРегистрации(КассаККМ, ИнформацияОбОшибке(), КомпонентыОбмена);
			КонецПопытки
		КонецЕсли;
		
		ПолученныеДанные.КассаККМ = КассаККМ;
	КонецЕсли;
КонецПроцедуры
