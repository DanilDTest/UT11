Процедура ЗаполнитьТаблицуОстатковПоРасчетамСКонтрагентами(КомпонентыОбмена, ДанныеИБ, ДанныеXDTO, ВидДоговораПоУмолчанию)
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ВводОстатковРасчетыСПартнерами.Контрагент КАК Контрагент,
		|	ВводОстатковРасчетыСПартнерами.Сумма КАК Сумма,
		|	ВводОстатковРасчетыСПартнерами.СуммаРегл КАК СуммаРегл,
		|	ВЫБОР
		|		КОГДА ВводОстатковРасчетыСПартнерами.ОбъектРасчетов ССЫЛКА Справочник.ДоговорыКонтрагентов
		|			ТОГДА ВводОстатковРасчетыСПартнерами.ОбъектРасчетов
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК ОбъектКакДоговор,
		|	ВЫБОР
		|		КОГДА ВводОстатковРасчетыСПартнерами.ОбъектРасчетов ССЫЛКА Справочник.ДоговорыКонтрагентов
		|			ТОГДА ВводОстатковРасчетыСПартнерами.ОбъектРасчетов.ПорядокОплаты = ЗНАЧЕНИЕ(Перечисление.ПорядокОплатыПоСоглашениям.РасчетыВВалютеОплатаВРублях)
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК РасчетыВУсловныхЕдиницах,
		|	ВводОстатковРасчетыСПартнерами.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
		|	КурсыВалютСрезПоследних.Курс КАК КурсВзаиморасчетов,
		|	КурсыВалютСрезПоследних.Кратность КАК КратностьВзаиморасчетов,
		|	&ТекстЗапросаДокументРасчетов КАК ДокументРасчетов
		|ИЗ
		|	Документ.ВводОстатков.РасчетыСПартнерами КАК ВводОстатковРасчетыСПартнерами
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&ДатаСреза, ) КАК КурсыВалютСрезПоследних
		|		ПО ВводОстатковРасчетыСПартнерами.ВалютаВзаиморасчетов = КурсыВалютСрезПоследних.Валюта
		|ГДЕ
		|	ВводОстатковРасчетыСПартнерами.Ссылка = &Ссылка";
	
	Если ВерсияФорматаЧислом(КомпонентыОбмена.ВерсияФорматаОбмена) >= ВерсияФорматаЧислом("1.7") Тогда
		ТекстЗапросаДокументРасчетов = "ВводОстатковРасчетыСПартнерами.ДокументРасчетов";
	Иначе
		ТекстЗапросаДокументРасчетов = "Неопределено";
	КонецЕсли;
		
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаДокументРасчетов",
		ТекстЗапросаДокументРасчетов);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Ссылка",    ДанныеИБ.Ссылка);
	Запрос.УстановитьПараметр("ДатаСреза", ДанныеИБ.Дата);
	
	ТаблицаРасчеты = Запрос.Выполнить().Выгрузить();
	ТаблицаРасчеты.Колонки.Добавить("Договор");
	
	Для Каждого СтрокаРасчеты Из ТаблицаРасчеты Цикл
	
		Если ЗначениеЗаполнено(СтрокаРасчеты.ОбъектКакДоговор) Тогда
			СтрокаРасчеты.Договор = СтрокаРасчеты.ОбъектКакДоговор;
		Иначе
	
			Если ТипЗнч(СтрокаРасчеты.Контрагент) = Тип("СправочникСсылка.Организации") Тогда	
				СтрокаРасчеты.Контрагент = КонтрагентИзОрганизации(СтрокаРасчеты.Контрагент, КомпонентыОбмена);
			КонецЕсли;
		
			ПараметрыПоУмолчанию = Новый Структура(ДоговорФиксированнаяСтруктураКлючей());
			ПараметрыПоУмолчанию.Организация              = ДанныеИБ.Организация;
			ПараметрыПоУмолчанию.Контрагент               = СтрокаРасчеты.Контрагент;
			ПараметрыПоУмолчанию.РасчетыВУсловныхЕдиницах = Ложь;
			ПараметрыПоУмолчанию.ВалютаВзаиморасчетов     = СтрокаРасчеты.ВалютаВзаиморасчетов;
			ПараметрыПоУмолчанию.ВидДоговора              = ВидДоговораПоУмолчанию;
			
			СтрокаРасчеты.Договор = ДоговорИнструкцияКонвертацииПоДаннымВзаиморасчетов(ПараметрыПоУмолчанию, КомпонентыОбмена);
			
		КонецЕсли;
	КонецЦикла;
	
	ДанныеXDTO.Вставить("Расчеты", ТаблицаРасчеты);
КонецПроцедуры
