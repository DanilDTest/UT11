Процедура ЗаполнитьХарактеристикуУпаковкуВТЧ(КомпонентыОбмена, Таблица, ЗаполнитьХарактеристику = Истина, ЗаполнитьУпаковку = Истина)
	Если ВерсияФорматаЧислом(КомпонентыОбмена.ВерсияФорматаОбмена) < ВерсияФорматаЧислом("1.6") Тогда
		
		Если ЗаполнитьХарактеристику И Не Таблица.Колонки.Найти("Характеристика") = Неопределено Тогда
			Таблица.ЗаполнитьЗначения(Неопределено, "Характеристика");
		КонецЕсли;
		
		Если ЗаполнитьУпаковку И Не Таблица.Колонки.Найти("Упаковка") = Неопределено Тогда
			Таблица.ЗаполнитьЗначения(Неопределено, "Упаковка");
		КонецЕсли;	
		
	Иначе
		
		Если ЗаполнитьХарактеристику Тогда
			
			ХарактеристикаПоДаннымЗапроса = НЕ Таблица.Колонки.Найти("ХарактеристикаСсылка") = Неопределено;
			
			Если ХарактеристикаПоДаннымЗапроса Тогда
				Таблица.Колонки.Добавить("Характеристика");
			Иначе
				Таблица.Колонки.Характеристика.Имя = "ХарактеристикаСсылка";
				Таблица.Колонки.Добавить("Характеристика");
			КонецЕсли;
			
		КонецЕсли;
		
		Если ЗаполнитьУпаковку Тогда
			
			УпаковкаПоДаннымЗапроса = НЕ Таблица.Колонки.Найти("УпаковкаСсылка") = Неопределено;
			
			Если УпаковкаПоДаннымЗапроса Тогда
				Таблица.Колонки.Добавить("Упаковка");
			Иначе
				Таблица.Колонки.Упаковка.Имя = "УпаковкаСсылка";
				Таблица.Колонки.Добавить("Упаковка");
			КонецЕсли;
			
		КонецЕсли;
		
		Для Каждого Строка Из Таблица Цикл
			Если ЗаполнитьХарактеристику
				И ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристикиНоменклатуры")  Тогда
				ЗаполнитьХарактеристикуСтрокиТЧ(КомпонентыОбмена, Строка, ХарактеристикаПоДаннымЗапроса);
			КонецЕсли;
			
			Если ЗаполнитьУпаковку 
				И ПолучитьФункциональнуюОпцию("ИспользоватьУпаковкиНоменклатуры") Тогда
				ЗаполнитьУпаковкуСтрокиТЧ(КомпонентыОбмена, Строка, УпаковкаПоДаннымЗапроса);
			КонецЕсли;
			
		КонецЦикла;
	
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристикиНоменклатуры")
		И Не Таблица.Колонки.Найти("Характеристика") = Неопределено Тогда
		
		Таблица.Колонки.Удалить("Характеристика");
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьУпаковкиНоменклатуры") 
		И Не Таблица.Колонки.Найти("Упаковка") = Неопределено Тогда
		
		Таблица.Колонки.Удалить("Упаковка");
	КонецЕсли;
КонецПроцедуры
