Процедура ОтложеннаяОбработка_ПодобратьПартииТМЦВЭксп(КомпонентыОбмена)
	ТаблицаПартииТМЦ = КомпонентыОбмена.ПараметрыКонвертации.ТаблицаПартииТМЦ;
			
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаПартииТМЦ", ТаблицаПартииТМЦ);
	Запрос.Текст = "
		|ВЫБРАТЬ
		|	ТаблицаПартииТМЦ.ДокументПередачи КАК ДокументПередачи,
		|	ТаблицаПартииТМЦ.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ТаблицаПартииТМЦ.ИнвентарныйНомер КАК ИнвентарныйНомер,
		|	ТаблицаПартииТМЦ.ПрочееОприходованиеТоваров КАК ПрочееОприходованиеТоваров,
		|	ТаблицаПартииТМЦ.НомерСтроки КАК НомерСтроки
		|ПОМЕСТИТЬ втТаблицаПартииТМЦ
		|ИЗ
		|	&ТаблицаПартииТМЦ КАК ТаблицаПартииТМЦ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПартииТМЦВЭксплуатации.Ссылка КАК ПартияТМЦВЭксплуатации,
		|	втТаблицаПартииТМЦ.ПрочееОприходованиеТоваров КАК ПрочееОприходованиеТоваров,
		|	втТаблицаПартииТМЦ.НомерСтроки КАК НомерСтроки
		|ПОМЕСТИТЬ втСопоставленныеПартии
		|ИЗ
		|	втТаблицаПартииТМЦ КАК втТаблицаПартииТМЦ
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПартииТМЦВЭксплуатации КАК ПартииТМЦВЭксплуатации
		|		ПО втТаблицаПартииТМЦ.ДокументПередачи = ПартииТМЦВЭксплуатации.Документ
		|			И втТаблицаПартииТМЦ.ФизическоеЛицо = ПартииТМЦВЭксплуатации.ФизическоеЛицо
		|			И (втТаблицаПартииТМЦ.ИнвентарныйНомер = ПартииТМЦВЭксплуатации.ИнвентарныйНомер
		|				ИЛИ втТаблицаПартииТМЦ.ИнвентарныйНомер = """")
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ втСопоставленныеПартии.ПартияТМЦВЭксплуатации) КАК Количество,
		|	втСопоставленныеПартии.НомерСтроки КАК НомерСтроки,
		|	втСопоставленныеПартии.ПрочееОприходованиеТоваров КАК ПрочееОприходованиеТоваров
		|ПОМЕСТИТЬ втКоличествоСопоставленныхПартий
		|ИЗ
		|	втСопоставленныеПартии КАК втСопоставленныеПартии
		|
		|СГРУППИРОВАТЬ ПО
		|	втСопоставленныеПартии.ПрочееОприходованиеТоваров,
		|	втСопоставленныеПартии.НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втСопоставленныеПартии.ПрочееОприходованиеТоваров КАК ПрочееОприходованиеТоваров,
		|	втСопоставленныеПартии.НомерСтроки КАК НомерСтроки,
		|	втСопоставленныеПартии.ПартияТМЦВЭксплуатации КАК ПартияТМЦВЭксплуатации
		|ИЗ
		|	втСопоставленныеПартии КАК втСопоставленныеПартии
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втКоличествоСопоставленныхПартий КАК втКоличествоСопоставленныхПартий
		|		ПО втСопоставленныеПартии.ПрочееОприходованиеТоваров = втКоличествоСопоставленныхПартий.ПрочееОприходованиеТоваров
		|			И втСопоставленныеПартии.НомерСтроки = втКоличествоСопоставленныхПартий.НомерСтроки
		|			И (втКоличествоСопоставленныхПартий.Количество = 1)
		|ИТОГИ ПО
		|	ПрочееОприходованиеТоваров";
	
	ВыборкаДокументы = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаДокументы.Следующий() Цикл
		ДокументОбъект = ВыборкаДокументы.ПрочееОприходованиеТоваров.ПолучитьОбъект();
		
		Выборка = ВыборкаДокументы.Выбрать();
		Пока Выборка.Следующий() Цикл
			ДокументОбъект.Товары[Выборка.НомерСтроки - 1].ПартияТМЦВЭксплуатации = Выборка.ПартияТМЦВЭксплуатации;
		КонецЦикла;
		
		ДокументОбъект.ОбменДанными.Загрузка = Истина;
		ДокументОбъект.Записать();
		
	КонецЦикла;
КонецПроцедуры
