Процедура ВыгрузитьРасшифровкуПлатежаПрочиеРасходы(ДанныеИБ, ДанныеXDTO)
	ТабРасшифровка = Новый ТаблицаЗначений;
	ТабРасшифровка.Колонки.Добавить("СтатьяДДС");
	ТабРасшифровка.Колонки.Добавить("Сумма");
	ТабРасшифровка.Колонки.Добавить("СтатьяРасходов");
	ТабРасшифровка.Колонки.Добавить("СчетРасходов");
	
	Для Каждого Строка Из ДанныеИБ.РасшифровкаПлатежа Цикл
		
		НоваяСтрока = ТабРасшифровка.Добавить();
		НоваяСтрока.СтатьяДДС      = Строка.СтатьяДвиженияДенежныхСредств;
		НоваяСтрока.Сумма          = Строка.Сумма;
		Если ТипЗнч(Строка.СтатьяРасходов) = Тип("ПланВидовХарактеристикСсылка.СтатьиАктивовПассивов") Тогда
			НоваяСтрока.СтатьяРасходов = ПланыВидовХарактеристик.СтатьиРасходов.ПустаяСсылка();
		Иначе	
			НоваяСтрока.СтатьяРасходов = Строка.СтатьяРасходов;
		КонецЕсли;
		
		Если ЭтоХозяйственнаяОперацияРасчетыПоДепозитам(ДанныеИБ.ХозяйственнаяОперация) Тогда
			НоваяСтрока.СчетРасходов = "55.03";
		ИначеЕсли ЭтоУТ() Тогда
			НоваяСтрока.СчетРасходов = СчетУчетаРасчетовПрочихОперацийДДС(Новый Структура("СтатьяДДС, СтатьяДоходовРасходов",  
				Строка.СтатьяДвиженияДенежныхСредств, Строка.СтатьяРасходов));
		КонецЕсли;
	КонецЦикла;
	
	ДанныеXDTO.Вставить("РасшифровкаПлатежа", ТабРасшифровка);
КонецПроцедуры
