Процедура ОбработатьНоменклатуруКонтрагентаВТЧ(Объект, ИмяТЧ, КомпонентыОбмена)
	Если НЕ ЗначениеЗаполнено(Объект.Партнер) ИЛИ Объект.Партнер = Справочники.Партнеры.НеизвестныйПартнер Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	НоменклатураПоставщиков.Ссылка КАК НоменклатураПоставщика
		|ИЗ
		|	&Источник КАК Таблица
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НоменклатураКонтрагентов КАК НоменклатураПоставщиков
		|		ПО Таблица.НоменклатураПартнера = НоменклатураПоставщиков.Ссылка
		|ГДЕ
		|	Таблица.Ссылка = &Ссылка
		|	И НоменклатураПоставщиков.Владелец = ЗНАЧЕНИЕ(Справочник.Партнеры.НеизвестныйПартнер)";
	
	ИмяОбъекта = Объект.Метаданные().ПолноеИмя();
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Источник", ИмяОбъекта + "." + ИмяТЧ);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НоменклатураОбъект = Выборка.НоменклатураПоставщика.ПолучитьОбъект();
		НоменклатураОбъект.Владелец = Объект.Партнер;
		
		НоменклатураОбъект.ОбменДанными.Загрузка = Истина;
		
		Попытка
			НоменклатураОбъект.Записать();
		Исключение
			ЗаписатьОшибкуВЖурналРегистрации(Выборка.НоменклатураПоставщика,
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()), КомпонентыОбмена);
		КонецПопытки;
	КонецЦикла;
КонецПроцедуры
