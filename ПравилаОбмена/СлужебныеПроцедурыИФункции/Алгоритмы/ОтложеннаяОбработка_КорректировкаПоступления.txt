Процедура ОтложеннаяОбработка_КорректировкаПоступления(Объект, ОбъектМодифицирован, КомпонентыОбмена)
	ЗаполнениеПартнераВШапкеДокумента(Объект);
	Если НЕ ЗначениеЗаполнено(Объект.НалогообложениеНДС) И ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
		Объект.НалогообложениеНДС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ДокументОснование, "НалогообложениеНДС");
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Объект.ВалютаВзаиморасчетов) Тогда
		Если ЗначениеЗаполнено(Объект.Договор) Тогда
			Объект.ВалютаВзаиморасчетов = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Договор, "ВалютаВзаиморасчетов");
		Иначе
			Объект.ВалютаВзаиморасчетов = Объект.Валюта;
		КонецЕсли;
	КонецЕсли;
	// Заполнение колонки КоличествоУпаковок в ТЧ Товары
	Для Каждого Строка Из Объект.Товары Цикл
		Если Строка.Количество = 0 Тогда
			Продолжить;
		КонецЕсли;
		Коэф = Справочники.УпаковкиЕдиницыИзмерения.КоэффициентУпаковки(Строка.Упаковка, Строка.Номенклатура);
		Если Коэф = 0 Тогда
			Строка.КоличествоУпаковок = Строка.КОличество;
		Иначе
			Строка.КоличествоУпаковок = Окр(Строка.КОличество / Коэф, 3);
		КонецЕсли;
	КонецЦикла;
	Если ЗначениеЗаполнено(Объект.ДокументОснование) И ОбщегоНазначения.СсылкаСуществует(Объект.ДокументОснование) Тогда
		ХозОперацияОснования = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ДокументОснование, "ХозяйственнаяОперация");
		ЗаполнениеСоглашенияСПоставщикомВШапкеДокумента(Объект, КомпонентыОбмена.ПараметрыКонвертации, ХозОперацияОснования);
		Объект.Расхождения.Очистить();
		Если ЗначениеЗаполнено(Объект.Договор) Тогда
			ВариантОформленияЗакупок = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Договор, "ВариантОформленияЗакупок");
			ИспользуетсяДокументПоступленияТоваров = (ВариантОформленияЗакупок <> Перечисления.ВариантыОформленияЗакупок.НеРазделять);
		Иначе
			ИспользуетсяДокументПоступленияТоваров = Ложь;
		КонецЕсли;
		Документы.КорректировкаПриобретения.ЗаполнитьРасхождения(Объект, ИспользуетсяДокументПоступленияТоваров);
		// Заполнение варианта отражения расхождений.
		Для Каждого Строка Из Объект.Расхождения Цикл
			Если Строка.КоличествоУпаковок > 0 Тогда
				Строка.ВариантОтражения = Перечисления.ВариантыОтраженияКорректировокПоступлений.УвеличитьЗакупкуУвеличитьСкладскиеОстатки;
			Иначе
				Строка.ВариантОтражения = Перечисления.ВариантыОтраженияКорректировокПоступлений.УменьшитьЗакупкуУменьшитьСкладскиеОстатки;
			КонецЕсли;		
		КонецЦикла;
	Иначе 
		ШаблонПредупреждения = НСтр("ru = 'У документа %1 не заполнен документ-основание. Некоторые реквизиты могут быть заполнены не корректно.';
									|en = 'Base document is not filled in at the document %1. Some attributes may not be filled in correctly.'");
		ТекстПредупреждения = СтрШаблон(ШаблонПредупреждения, Объект.Ссылка);
		ПредупреждениеПриСинхронизации(КомпонентыОбмена, Объект.Ссылка, ТекстПредупреждения);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.ПорядокРасчетов) Тогда
		Объект.ПорядокРасчетов = ВзаиморасчетыСервер.ПорядокРасчетовПоУмолчанию(Объект);
	КонецЕсли;
	
	ЗаполнитьСтатусыУказанияСерий(Объект, КомпонентыОбмена);
КонецПроцедуры
