Процедура ДобавитьДоговорВДопСвойства(ПолученныеДанные, ДанныеXDTO, КомпонентыОбмена, ИмяСвойстваXDTO = "Договор")
	// Если использование договоров выключено, то договор в документе не заполняется.
	Если Не КомпонентыОбмена.ПараметрыКонвертации.ИспользуютсяДоговорыСКлиентами
		И Не КомпонентыОбмена.ПараметрыКонвертации.ИспользуютсяДоговорыСПоставщиками Тогда
		Возврат;
		
	КонецЕсли;
	
	Если ДанныеXDTO.Свойство(ИмяСвойстваXDTO) И ЗначениеЗаполнено(ДанныеXDTO[ИмяСвойстваXDTO]) Тогда
		ЗначениеСвойстваXDTO = ДанныеXDTO[ИмяСвойстваXDTO];
		// Необходимость заполнения договора зависит от типа договора.
		ЗаполнятьДоговор = Истина;
		Если Не КомпонентыОбмена.ПараметрыКонвертации.ИспользуютсяДоговорыСКлиентами
			Или Не КомпонентыОбмена.ПараметрыКонвертации.ИспользуютсяДоговорыСПоставщиками Тогда
			ТипДоговора = Неопределено;
			ВидДоговора = Неопределено;
			ЗначениеСвойстваXDTO.Свойство("ВидДоговора", ВидДоговора);
			Если  ТипЗнч(ВидДоговора) = Тип("Структура")
				И ВидДоговора.Свойство("Значение") Тогда
			    ТипДоговора = ВидДоговора.Значение;
			КонецЕсли;
			Если Не ЗначениеЗаполнено(ТипДоговора)
				Или (Не КомпонентыОбмена.ПараметрыКонвертации.ИспользуютсяДоговорыСКлиентами
				И (ТипДоговора = "СПокупателем"
				Или ТипДоговора = "СКомиссионером"
				Или ТипДоговора = "СКомиссионеромНаЗакупку"))
				Или (Не КомпонентыОбмена.ПараметрыКонвертации.ИспользуютсяДоговорыСПоставщиками
				И (ТипДоговора = "СПоставщиком"
				Или ТипДоговора = "СКомитентом"
				Или ТипДоговора = "СКомитентомНаЗакупку"
				Или ТипДоговора = "Прочее")) Тогда
				
				ЗаполнятьДоговор = Ложь;
			КонецЕсли;
		КонецЕсли;
		Если ЗаполнятьДоговор Тогда
			Если ТипЗнч(ЗначениеСвойстваXDTO) = Тип("Структура")
				И ЗначениеСвойстваXDTO.Свойство("Ссылка") = Ложь Тогда
				ИмяПКО = "Справочник_ДоговорыКонтрагентов_ИзСтруктуры_Получ";
			Иначе
				ИмяПКО = "Справочник_ДоговорыКонтрагентов_Получение";
			КонецЕсли;
			ПолученныеДанные.ДополнительныеСвойства.Вставить("Договор", Новый Структура("Значение, ИмяПКО", ЗначениеСвойстваXDTO, ИмяПКО));
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры
