Процедура ЗаполнитьЗаявкуНаРасходованиеДС(Объект)
	Если Не ТипЗнч(Объект) = Тип("ДокументОбъект.СписаниеБезналичныхДенежныхСредств")
		И Не ТипЗнч(Объект) = Тип("ДокументОбъект.РасходныйКассовыйОрдер") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьЗаявкиНаРасходованиеДенежныхСредств") Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.ЗаявкаНаРасходованиеДенежныхСредств) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Заявка.Ссылка КАК Заявка,
	|	Заявка.СуммаДокумента КАК СуммаДокумента
	|ПОМЕСТИТЬ ВТЗаявкиНаРасходованиеДС
	|ИЗ
	|	Документ.ЗаявкаНаРасходованиеДенежныхСредств КАК Заявка
	|ГДЕ
	|	Заявка.Проведен
	|	И Заявка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаявокНаРасходованиеДенежныхСредств.КОплате)
	|	И Заявка.Дата <= &Дата
	|	И Заявка.ХозяйственнаяОперация = &ХозяйственнаяОперация
	|	И Заявка.СуммаДокумента = &СуммаДокумента
	|	И Заявка.Валюта = &Валюта
	|	И Заявка.Организация = &Организация
	|	И &ДополнительныеУсловия
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Заявка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДенежныеСредстваКВыплатеОстатки.ЗаявкаНаРасходованиеДенежныхСредств КАК Заявка,
	|	ДенежныеСредстваКВыплатеОстатки.СуммаОстаток
	|ПОМЕСТИТЬ ВТОстаткиПоЗаявкам
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваКВыплате.Остатки(
	|			,
	|			ЗаявкаНаРасходованиеДенежныхСредств В
	|				(ВЫБРАТЬ
	|					ВТЗаявкиНаРасходованиеДС.Заявка
	|				ИЗ
	|					ВТЗаявкиНаРасходованиеДС)) КАК ДенежныеСредстваКВыплатеОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТЗаявкиНаРасходованиеДС.Заявка КАК Заявка
	|ИЗ
	|	ВТЗаявкиНаРасходованиеДС КАК ВТЗаявкиНаРасходованиеДС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОстаткиПоЗаявкам КАК ВТОстаткиПоЗаявкам
	|		ПО (ВТОстаткиПоЗаявкам.Заявка = ВТЗаявкиНаРасходованиеДС.Заявка)
	|			И (ВТОстаткиПоЗаявкам.СуммаОстаток = -ВТЗаявкиНаРасходованиеДС.СуммаДокумента)");
	
	ТекстДополнительныеУсловия = "ИСТИНА";
	Если ТипЗнч(Объект) = Тип("ДокументОбъект.СписаниеБезналичныхДенежныхСредств") Тогда
		ТекстДополнительныеУсловия = ТекстДополнительныеУсловия + "
		|	И Заявка.ФормаОплатыБезналичная
		|	И Заявка.ФормаОплатыЗаявки = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Безналичная)";
		
		Если Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОплатаДенежныхСредствВДругуюОрганизацию
			Или Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВнутренняяПередачаДенежныхСредств
			Или Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствВДругуюОрганизацию Тогда
			
			ТекстДополнительныеУсловия = ТекстДополнительныеУсловия + "
			|	И Заявка.БанковскийСчетПолучатель = &БанковскийСчетПолучатель";
			Запрос.УстановитьПараметр("БанковскийСчетПолучатель", Объект.БанковскийСчетПолучатель);
		ИначеЕсли Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыдачаДенежныхСредствПодотчетнику
			И Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыдачаЗаймаСотруднику Тогда
			
			ТекстДополнительныеУсловия = ТекстДополнительныеУсловия + "
			|	И Заявка.ПодотчетноеЛицо = &ПодотчетноеЛицо";
			Запрос.УстановитьПараметр("ПодотчетноеЛицо", Объект.ПодотчетноеЛицо);
		Иначе
			ТекстДополнительныеУсловия = ТекстДополнительныеУсловия + "
			|	И Заявка.Контрагент = &Контрагент
			|	И Заявка.БанковскийСчетКонтрагента = &БанковскийСчетКонтрагента";
			Запрос.УстановитьПараметр("Контрагент", Объект.Контрагент);
			Запрос.УстановитьПараметр("БанковскийСчетКонтрагента", Объект.БанковскийСчетКонтрагента);
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Объект) = Тип("ДокументОбъект.РасходныйКассовыйОрдер") Тогда
		ТекстДополнительныеУсловия = ТекстДополнительныеУсловия + "
		|	И Заявка.ФормаОплатыНаличная
		|	И Заявка.ФормаОплатыЗаявки = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Наличная)";
		
		Если Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОплатаДенежныхСредствВДругуюОрганизацию
			Или Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВнутренняяПередачаДенежныхСредств
			Или Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствВДругуюОрганизацию Тогда
			
			ТекстДополнительныеУсловия = ТекстДополнительныеУсловия + "
			|	И Заявка.КассаПолучатель = &КассаПолучатель";
			Запрос.УстановитьПараметр("КассаПолучатель", Объект.КассаПолучатель);
		ИначеЕсли Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыдачаДенежныхСредствПодотчетнику
			И Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыдачаЗаймаСотруднику Тогда
			
			ТекстДополнительныеУсловия = ТекстДополнительныеУсловия + "
			|	И Заявка.ПодотчетноеЛицо = &ПодотчетноеЛицо";
			Запрос.УстановитьПараметр("ПодотчетноеЛицо", Объект.ПодотчетноеЛицо);
		Иначе
			ТекстДополнительныеУсловия = ТекстДополнительныеУсловия + "
			|	И Заявка.Контрагент = &Контрагент";
			Запрос.УстановитьПараметр("Контрагент", Объект.Контрагент);
		КонецЕсли;
	КонецЕсли;
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ДополнительныеУсловия", ТекстДополнительныеУсловия);
	
	Запрос.УстановитьПараметр("Дата", Объект.Дата);
	Запрос.УстановитьПараметр("ХозяйственнаяОперация", Объект.ХозяйственнаяОперация);
	Запрос.УстановитьПараметр("СуммаДокумента", Объект.СуммаДокумента);
	Запрос.УстановитьПараметр("Валюта", Объект.Валюта);
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Объект.ЗаявкаНаРасходованиеДенежныхСредств = Выборка.Заявка;
		Для Каждого СтрокаРасшифровки Из Объект.РасшифровкаПлатежа Цикл
			СтрокаРасшифровки.ЗаявкаНаРасходованиеДенежныхСредств = Выборка.Заявка;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры
