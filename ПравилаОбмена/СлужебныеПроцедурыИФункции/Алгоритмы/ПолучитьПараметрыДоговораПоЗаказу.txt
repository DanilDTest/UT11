Процедура ПолучитьПараметрыДоговораПоЗаказу(Заказ, ТипРасчетов, ВидДоговора, РасчетыВУсловныхЕдиницах, Инвертировать = Ложь)
	Если ЗначениеЗаполнено(Заказ) Тогда
		ТипЗаказа = ТипЗнч(Заказ);
		
		Если ТипЗаказа = Тип("ДокументСсылка.ОтчетПоКомиссииМеждуОрганизациями") Тогда
			ВидДоговора = ?(ТипРасчетов = Перечисления.ТипыРасчетовСПартнерами.РасчетыСПоставщиком, "СКомитентом", "СКомиссионером");
		КонецЕсли;
		
		Соглашение = Неопределено;
		Если Заказ.Метаданные().Реквизиты.Найти("Соглашение") <> Неопределено Тогда
			Соглашение = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Заказ, "Соглашение");
			
			// Для продолжения анализа операции, необходимо определить вид договора
			Если НЕ ЗначениеЗаполнено(ВидДоговора) Тогда
				ВидДоговора = ВзаиморасчетыСервер.ВидДоговораКонтрагента(Заказ, Соглашение, Неопределено);
			КонецЕсли;
		КонецЕсли;
		
		// Для продолжения анализа операции, необходимо определить вид договора
		Если НЕ ЗначениеЗаполнено(ВидДоговора) Тогда
			ВидДоговора = ?(ТипРасчетов = Перечисления.ТипыРасчетовСПартнерами.РасчетыСПоставщиком, "СПоставщиком", "СПокупателем");
		КонецЕсли;
		
		Если ВидДоговора = "СКомитентом" ИЛИ ВидДоговора = "СКомиссионером" Тогда
			// Договора по комиссии выгружаются без разделения по сделкам и заказам и всегда не в у.е.
			РасчетыВУсловныхЕдиницах = Ложь;
		Иначе
			Если ЗначениеЗаполнено(Соглашение) Тогда
				РасчетыВУсловныхЕдиницах = ВзаиморасчетыСервер.РасчетыВУсловныхЕдиницах(Соглашение);
			ИначеЕсли ТипЗаказа = Тип("ДокументСсылка.РеализацияТоваровУслуг") ИЛИ ТипЗаказа = Тип("ДокументСсылка.ПриобретениеТоваровУслуг") Тогда
				ДанныеЗаказа = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Заказ, "Валюта,ВалютаВзаиморасчетов");
				РасчетыВУсловныхЕдиницах = ДанныеЗаказа.Валюта = Константы.ВалютаРегламентированногоУчета.Получить() И ДанныеЗаказа.Валюта <> ДанныеЗаказа.ВалютаВзаиморасчетов;
			Иначе
				РасчетыВУсловныхЕдиницах = Ложь;
			КонецЕсли;
		КонецЕсли;
	Иначе
		РасчетыВУсловныхЕдиницах = Ложь;
		ВидДоговора = ?(ТипРасчетов = Перечисления.ТипыРасчетовСПартнерами.РасчетыСПоставщиком, "СПоставщиком", "СПокупателем");
	КонецЕсли;
	
	Если Инвертировать Тогда
		Если ВидДоговора = "СПоставщиком" Тогда
			ВидДоговора = "СПокупателем";
		ИначеЕсли ВидДоговора = "СПокупателем" Тогда
			ВидДоговора = "СПоставщиком";
		КонецЕсли;
		
		Если ВидДоговора = "СКомитентом" Тогда
			ВидДоговора = "СКомиссионером";
		ИначеЕсли ВидДоговора = "СКомиссионером" Тогда
			ВидДоговора = "СКомитентом";
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры
