Процедура ПослеМержевания_СБДС(ПолученныеДанные, ДанныеИБ, КомпонентыОбмена)
	ДанныеДляЗаписиВИБ = ?(ДанныеИБ = Неопределено, ПолученныеДанные, ДанныеИБ);
	
	// Тип платежного документа.
	Если Не ЗначениеЗаполнено(ДанныеДляЗаписиВИБ.ТипПлатежногоДокумента) Тогда
		ДанныеДляЗаписиВИБ.ТипПлатежногоДокумента = Перечисления.ТипыПлатежныхДокументов.ПлатежноеПоручение;
	КонецЕсли;
	
	// Очередность платежа.
	Если Не ЗначениеЗаполнено(ДанныеДляЗаписиВИБ.ОчередностьПлатежа) Тогда
		ДанныеДляЗаписиВИБ.ОчередностьПлатежа = 5;
	КонецЕсли;
	
	// Банковский счет контрагента. 
	Если НЕ ЗначениеЗаполнено(ДанныеДляЗаписиВИБ.БанковскийСчетКонтрагента) И ЗначениеЗаполнено(ДанныеДляЗаписиВИБ.Контрагент)
		И ЗначениеЗаполнено(ДанныеДляЗаписиВИБ.Валюта) Тогда
		ДанныеДляЗаписиВИБ.БанковскийСчетКонтрагента = БанковскийСчетКонтрагентаПоУмолчанию(ДанныеДляЗаписиВИБ.Контрагент, ДанныеДляЗаписиВИБ.Валюта);
	КонецЕсли;
	
	// Расшифровка платежа. Статья расходов и подразделение.
	Если ДанныеДляЗаписиВИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПрочаяВыдачаДенежныхСредств
		ИЛИ ДанныеДляЗаписиВИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПеречислениеВБюджет Тогда
		
		ЗаполнитьПодразделениеВРасшифровкеПлатежа(ДанныеДляЗаписиВИБ, КомпонентыОбмена);
		
		Для Каждого СтрокаРП Из ДанныеДляЗаписиВИБ.РасшифровкаПлатежа Цикл
			Если Не ЗначениеЗаполнено(СтрокаРП.СтатьяРасходов) Тогда
				СтрокаРП.СтатьяРасходов = ПланыВидовХарактеристик.СтатьиАктивовПассивов.ПрочиеПассивы;
			КонецЕсли;
		КонецЦикла;
	
	КонецЕсли;
КонецПроцедуры
