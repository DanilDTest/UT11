Процедура ОтложеннаяОбработка_ЗаполнитьКлассификаторТНВЭД(КомпонентыОбмена)
	МассивТНВЭД = КомпонентыОбмена.ПараметрыКонвертации.МассивТНВЭД;
	
	Если МассивТНВЭД.Количество() > 0 Тогда
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	КлассификаторТНВЭД.Ссылка КАК Ссылка,
		|	КлассификаторТНВЭД.Код КАК СсылкаКод
		|ПОМЕСТИТЬ втТНВЭД
		|ИЗ
		|	Справочник.КлассификаторТНВЭД КАК КлассификаторТНВЭД
		|ГДЕ
		|	КлассификаторТНВЭД.ЕдиницаИзмерения = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
		|	И КлассификаторТНВЭД.Ссылка В(&МассивТНВЭД)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Т.Код КАК Код,
		|	Т.ЕдиницаИзмеренияКод КАК ЕдиницаИзмеренияКод
		|ПОМЕСТИТЬ ТаблицаКлассификатора
		|ИЗ
		|	&ТаблицаКлассификатора КАК Т
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Код
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втТНВЭД.Ссылка КАК Ссылка,
		|	Единицы.Ссылка КАК ЕдиницаИзмерения
		|ИЗ
		|	втТНВЭД КАК втТНВЭД
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаКлассификатора КАК ТаблицаКлассификатора
		|		ПО втТНВЭД.СсылкаКод = ТаблицаКлассификатора.Код
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УпаковкиЕдиницыИзмерения КАК Единицы
		|		ПО (ТаблицаКлассификатора.ЕдиницаИзмеренияКод = Единицы.Код)
		|			И (Единицы.Владелец = ЗНАЧЕНИЕ(Справочник.НаборыУпаковок.БазовыеЕдиницыИзмерения))";
			
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("ТаблицаКлассификатора", Справочники.КлассификаторТНВЭД.ТаблицаКлассификатора(0));
		Запрос.УстановитьПараметр("МассивТНВЭД", МассивТНВЭД);
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			СправочникОбъект = Выборка.Ссылка.ПолучитьОбъект();
			Если Не ЗначениеЗаполнено(СправочникОбъект.ЕдиницаИзмерения) И ЗначениеЗаполнено(Выборка.ЕдиницаИзмерения) Тогда
				СправочникОбъект.ЕдиницаИзмерения = Выборка.ЕдиницаИзмерения;
				Попытка
					СправочникОбъект.Записать();
				Исключение
					ЗаписатьОшибкуВЖурналРегистрации(СправочникОбъект,
						ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()), КомпонентыОбмена);
				КонецПопытки
			КонецЕсли;	
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры
