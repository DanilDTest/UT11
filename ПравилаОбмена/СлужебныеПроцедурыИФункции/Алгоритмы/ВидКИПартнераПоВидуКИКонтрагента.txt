Функция ВидКИПартнераПоВидуКИКонтрагента(ВидКИКонтрагента, ИспользоватьПартнеровКакКонтрагентов)
	ВидКиПартнера = Неопределено;
	
	Если ВидКИКонтрагента = Справочники.ВидыКонтактнойИнформации.EmailКонтрагента Тогда
		ВидКиПартнера = Справочники.ВидыКонтактнойИнформации.EmailПартнера;
	ИначеЕсли ВидКИКонтрагента = Справочники.ВидыКонтактнойИнформации.ТелефонКонтрагента Тогда
		ВидКиПартнера = Справочники.ВидыКонтактнойИнформации.ТелефонПартнера;
	ИначеЕсли ВидКИКонтрагента = Справочники.ВидыКонтактнойИнформации.ФактАдресКонтрагента Тогда
		ВидКиПартнера = Справочники.ВидыКонтактнойИнформации.АдресПартнера;
	ИначеЕсли ЗначениеЗаполнено(ВидКИКонтрагента) И ИспользоватьПартнеровКакКонтрагентов Тогда
		Если Не (ОбщегоНазначения.СсылкаСуществует(ВидКИКонтрагента)
			И ТипЗнч(ВидКИКонтрагента) = Тип("СправочникСсылка.ВидыКонтактнойИнформации")) Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		// Поиск или создание прочих видов КИ.
		ЗначенияРеквизитовВидаКИ = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВидКИКонтрагента, "Тип, Наименование");
		
		ВидКИПартнера = ВидПользовательскойКонтактнойИнформации(ЗначенияРеквизитовВидаКИ.Наименование,
			Справочники.ВидыКонтактнойИнформации.СправочникПартнеры,
			ЗначенияРеквизитовВидаКИ.Тип);
		
	КонецЕсли;
	
	Возврат ВидКИПартнера;
КонецФункции
