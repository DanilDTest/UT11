Процедура ЗаполнитьУпаковкуСтрокиТЧ(КомпонентыОбмена, Строка, ПоДаннымЗапроса = Истина)
	Если ПоДаннымЗапроса Тогда
		Если Строка.ВыгружатьУпаковки И ЗначениеЗаполнено(Строка.УпаковкаСсылка) Тогда
			Если Строка.УпаковкаНоменклатуры Тогда
				Строка.Упаковка = Строка.УпаковкаСсылка;
			Иначе
				Упаковка = Новый Структура();
				Упаковка.Вставить("Наименование",     Строка.УпаковкаНаименование);
				Упаковка.Вставить("ЕдиницаИзмерения", Строка.УпаковкаЕдиницаИзмерения);
				Упаковка.Вставить("Высота",           Строка.УпаковкаВысота);
				Упаковка.Вставить("Глубина",          Строка.УпаковкаГлубина);
				Упаковка.Вставить("Объем",            Строка.УпаковкаОбъем);
				Упаковка.Вставить("Ширина",           Строка.УпаковкаШирина);
				Упаковка.Вставить("Знаменатель",      Строка.УпаковкаЗнаменатель);
				Упаковка.Вставить("Числитель",        Строка.УпаковкаЧислитель);
				Упаковка.Вставить("Владелец",         Строка.Номенклатура);
				Упаковка.Вставить("Ссылка",           Строка.УпаковкаСсылка);
				
				// Поиск единицы измерения, если выгружается базовая единица как упаковка.
				Если Не ЗначениеЗаполнено(Строка.УпаковкаЕдиницаИзмерения) И Строка.УпаковкаВладелец = Справочники.НаборыУпаковок.БазовыеЕдиницыИзмерения Тогда
					
					// В интерактивном режиме нельзя указать в упаковке базовую единицу измерения с типом количество штук.
					// Некорректно заполненные упаковки не выгружаем.
					Если Строка.УпаковкаТипИзмеряемойВеличины = Перечисления.ТипыИзмеряемыхВеличин.КоличествоШтук Тогда
						Возврат;
					КонецЕсли;
					
					Если Строка.УпаковкаТипИзмеряемойВеличины = Перечисления.ТипыИзмеряемыхВеличин.Длина Тогда
						НаименованиеБазовойЕдиницыИзмерения = "м";
						КодБазовойЕдиницыИзмерения          = "006";
					ИначеЕсли Строка.УпаковкаТипИзмеряемойВеличины = Перечисления.ТипыИзмеряемыхВеличин.Площадь Тогда
						НаименованиеБазовойЕдиницыИзмерения = "м2";
						КодБазовойЕдиницыИзмерения          = "055";
					ИначеЕсли Строка.УпаковкаТипИзмеряемойВеличины = Перечисления.ТипыИзмеряемыхВеличин.Объем Тогда
						НаименованиеБазовойЕдиницыИзмерения = "м3";
						КодБазовойЕдиницыИзмерения          = "113";
					ИначеЕсли Строка.УпаковкаТипИзмеряемойВеличины = Перечисления.ТипыИзмеряемыхВеличин.Вес Тогда
						НаименованиеБазовойЕдиницыИзмерения = "кг";
						КодБазовойЕдиницыИзмерения          = "166";
					ИначеЕсли Строка.УпаковкаТипИзмеряемойВеличины = Перечисления.ТипыИзмеряемыхВеличин.Энергия Тогда
						НаименованиеБазовойЕдиницыИзмерения = "Вт.ч";
						КодБазовойЕдиницыИзмерения          = "243";
					ИначеЕсли Строка.УпаковкаТипИзмеряемойВеличины = Перечисления.ТипыИзмеряемыхВеличин.Мощность Тогда
						НаименованиеБазовойЕдиницыИзмерения = "Вт";
						КодБазовойЕдиницыИзмерения          = "212";
					ИначеЕсли Строка.УпаковкаТипИзмеряемойВеличины = Перечисления.ТипыИзмеряемыхВеличин.ЭлектрическийЗаряд Тогда
						НаименованиеБазовойЕдиницыИзмерения = "А.ч";
						КодБазовойЕдиницыИзмерения          = "263";
					ИначеЕсли Строка.УпаковкаТипИзмеряемойВеличины = Перечисления.ТипыИзмеряемыхВеличин.Время Тогда
						НаименованиеБазовойЕдиницыИзмерения = "с";
						КодБазовойЕдиницыИзмерения          = "354";
					КонецЕсли;
					
					Запрос = Новый Запрос("ВЫБРАТЬ
						|	УпаковкиЕдиницыИзмерения.Ссылка КАК Ссылка
						|ИЗ
						|	Справочник.УпаковкиЕдиницыИзмерения КАК УпаковкиЕдиницыИзмерения
						|ГДЕ
						|	УпаковкиЕдиницыИзмерения.Владелец = ЗНАЧЕНИЕ(Справочник.НаборыУпаковок.БазовыеЕдиницыИзмерения)
						|	И УпаковкиЕдиницыИзмерения.Код = &Код
						|
						|ОБЪЕДИНИТЬ ВСЕ
						|
						|ВЫБРАТЬ
						|	УпаковкиЕдиницыИзмерения.Ссылка
						|ИЗ
						|	Справочник.УпаковкиЕдиницыИзмерения КАК УпаковкиЕдиницыИзмерения
						|ГДЕ
						|	УпаковкиЕдиницыИзмерения.Владелец = ЗНАЧЕНИЕ(Справочник.НаборыУпаковок.БазовыеЕдиницыИзмерения)
						|	И УпаковкиЕдиницыИзмерения.Наименование = &Наименование");
					Запрос.УстановитьПараметр("Код", КодБазовойЕдиницыИзмерения);
					Запрос.УстановитьПараметр("Наименование", НаименованиеБазовойЕдиницыИзмерения);
					
					Выборка = Запрос.Выполнить().Выбрать();
					Если Выборка.Следующий() И ЗначениеЗаполнено(Выборка.Ссылка) Тогда
						Упаковка.Вставить("ЕдиницаИзмерения", Выборка.Ссылка);
					КонецЕсли;
				КонецЕсли;
				
				Строка.Упаковка = Новый Структура("Значение, ИмяПКО",
					Упаковка, "Справочник_Упаковки_ИзСтруктуры");
					
				ПравилоОбработки = КомпонентыОбмена.ПравилаОбработкиДанных.Найти("Справочник_УпаковкиЕдиницыИзмерения_Отправка", "Имя");
				Если Не ПравилоОбработки = Неопределено Тогда
					RM_ОбменДаннымиXDTOСервер.ВыгрузкаОбъектаВыборки(КомпонентыОбмена, Упаковка, ПравилоОбработки);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	Иначе
		Если ЗначениеЗаполнено(Строка.УпаковкаСсылка) И ЗначениеЗаполнено(Строка.Номенклатура) Тогда
			Упаковка = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
					Строка.УпаковкаСсылка, "Владелец, Наименование, ЕдиницаИзмерения, Высота,
						|Глубина, Объем, Ширина, Знаменатель, Числитель");
			
			Если Упаковка.Владелец = Строка.Номенклатура Тогда
				Строка.Упаковка = Строка.УпаковкаСсылка;
			Иначе
				Упаковка.Вставить("Владелец", Строка.Номенклатура);
				Строка.Упаковка = Новый Структура("Значение, ИмяПКО",
					Упаковка, "Справочник_Упаковки_ИзСтруктуры");
					
				ПравилоОбработки = КомпонентыОбмена.ПравилаОбработкиДанных.Найти("Справочник_УпаковкиЕдиницыИзмерения_Отправка", "Имя");
				Если Не ПравилоОбработки = Неопределено Тогда
					RM_ОбменДаннымиXDTOСервер.ВыгрузкаОбъектаВыборки(КомпонентыОбмена, Упаковка, ПравилоОбработки);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры
