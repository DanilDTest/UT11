Процедура ОтложеннаяОбработка_ЗаполнениеКассовыхСмен(КомпонентыОбмена)
	ДокументыСПС = КомпонентыОбмена.ПараметрыКонвертации.ДокументыСПодарочнымиСертификатами;
	ТаблицаДляОтложенногоПроведения = КомпонентыОбмена.ДокументыДляОтложенногоПроведения;
		
	Если ТипЗнч(ДокументыСПС) = Тип("ТаблицаЗначений") И ДокументыСПС.Количество() > 0 Тогда
		ДокументыСПС.Индексы.Добавить("ОчередьДокумента");
		ДокументыОРП = ДокументыСПС.Скопировать(Новый Структура("ОчередьДокумента", 1));
		Если ДокументыОРП.Количество() > 0 Тогда
		
			ДокументыОРП.Сортировать("ДатаДокумента");
			
			Запрос = Новый Запрос;
			Запрос.Текст =
				"ВЫБРАТЬ
				|	МАКСИМУМ(ЕСТЬNULL(ДокКассоваяСмена.ОкончаниеКассовойСмены, НАЧАЛОПЕРИОДА(ОтчетОРозничныхПродажах.Дата, ДЕНЬ))) КАК НачалоКассовойСмены,
				|	ОтчетОРозничныхПродажах.Ссылка КАК ОРП,
				|	ОтчетОРозничныхПродажах.Валюта КАК Валюта,
				|	ОтчетОРозничныхПродажах.КассаККМ КАК КассаККМ,
				|	ОтчетОРозничныхПродажах.Дата КАК ОкончаниеКассовойСмены,
				|	ОтчетОРозничныхПродажах.Дата КАК Дата,
				|	ОтчетОРозничныхПродажах.Организация КАК Организация,
				|	ОтчетОРозничныхПродажах.Склад КАК Склад,
				|	ОтчетОРозничныхПродажах.ВидЦены КАК ВидЦены,
				|	ОтчетОРозничныхПродажах.НалогообложениеНДС КАК НалогообложениеНДС,
				|	ЗНАЧЕНИЕ(Перечисление.СтатусыКассовойСмены.Закрыта) КАК Статус,
				|	МАКСИМУМ(ВЫБОР
				|			КОГДА ЕСТЬNULL(ДокКассоваяСмена.ОкончаниеКассовойСмены, ДАТАВРЕМЯ(1, 1, 1)) = ОтчетОРозничныхПродажах.Дата
				|					И НЕ ОтчетОРозничныхПродажах.Дата = ДАТАВРЕМЯ(1, 1, 1)
				|				ТОГДА ДокКассоваяСмена.Ссылка
				|			ИНАЧЕ ЗНАЧЕНИЕ(Документ.КассоваяСмена.ПустаяСсылка)
				|		КОНЕЦ) КАК КассоваяСмена
				|ИЗ
				|	Документ.ОтчетОРозничныхПродажах КАК ОтчетОРозничныхПродажах
				|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.КассоваяСмена КАК ДокКассоваяСмена
				|		ПО ОтчетОРозничныхПродажах.КассаККМ = ДокКассоваяСмена.КассаККМ
				|			И ОтчетОРозничныхПродажах.Организация = ДокКассоваяСмена.Организация
				|			И (НЕ ДокКассоваяСмена.ПометкаУдаления)
				|			И (ДокКассоваяСмена.ОкончаниеКассовойСмены = ОтчетОРозничныхПродажах.Дата
				|				ИЛИ НАЧАЛОПЕРИОДА(ОтчетОРозничныхПродажах.Дата, ДЕНЬ) <= ДокКассоваяСмена.НачалоКассовойСмены
				|					И ДокКассоваяСмена.ОкончаниеКассовойСмены < ОтчетОРозничныхПродажах.Дата)
				|ГДЕ
				|	ОтчетОРозничныхПродажах.Ссылка В(&МассивСсылок)
				|
				|СГРУППИРОВАТЬ ПО
				|	ОтчетОРозничныхПродажах.Ссылка,
				|	ОтчетОРозничныхПродажах.Валюта,
				|	ОтчетОРозничныхПродажах.КассаККМ,
				|	ОтчетОРозничныхПродажах.Дата,
				|	ОтчетОРозничныхПродажах.Организация,
				|	ОтчетОРозничныхПродажах.Склад,
				|	ОтчетОРозничныхПродажах.ВидЦены,
				|	ОтчетОРозничныхПродажах.НалогообложениеНДС,
				|	ОтчетОРозничныхПродажах.Дата
				|
				|УПОРЯДОЧИТЬ ПО
				|	Организация,
				|	Склад,
				|	Валюта,
				|	КассаККМ,
				|	ОкончаниеКассовойСмены";
			
			Запрос.УстановитьПараметр("МассивСсылок", ДокументыОРП.ВыгрузитьКолонку("ДокументСсылка"));
			
			Организация = Неопределено;
			Склад = Неопределено;
			Валюта = Неопределено;
			КассаККМ = Неопределено;
	
			Выборка = Запрос.Выполнить().Выбрать();
			Пока Выборка.Следующий() Цикл
				
				ОРПОбъект = Выборка.ОРП.ПолучитьОбъект();
				Если ЗначениеЗаполнено(Выборка.КассоваяСмена) Тогда
					ОРПОбъект.КассоваяСмена = Выборка.КассоваяСмена;
				Иначе
					Если Организация <> Выборка.Организация ИЛИ КассаККМ <> Выборка.КассаККМ
						Или Склад <> Выборка.Склад ИЛИ Валюта <> Выборка.Валюта Тогда
						
						Организация = Выборка.Организация;
						КассаККМ = Выборка.КассаККМ;
						Склад = Выборка.Склад;
						Валюта = Выборка.Валюта;
						НачалоСмены = Выборка.НачалоКассовойСмены;
						
					КонецЕсли;
					
					НовыйДокумент = Документы.КассоваяСмена.СоздатьДокумент();
					НачалоСмены = НачалоСмены + ?(НачалоСмены = НачалоДня(НачалоСмены), 0, 1);
					ЗаполнитьЗначенияСвойств(НовыйДокумент, Выборка);
					НовыйДокумент.Дата = НачалоСмены;
					НовыйДокумент.НачалоКассовойСмены = НачалоСмены;
					НовыйДокумент.СтатусРегламентныхОпераций = Перечисления.СтатусыРегламентныхОперацийКассовойСмены.СозданОтчетЧекиЗаархивированы;
					ПолученныеДанныеСсылка = RM_ОбменДаннымиXDTOСервер.СсылкаОбъектаПоУИДОбъектаXDTO(
						Строка(Выборка.ОРП.УникальныйИдентификатор()),
						Тип("ДокументСсылка.КассоваяСмена"),
						КомпонентыОбмена);
					Если ТипЗнч(ПолученныеДанныеСсылка) = Тип("ДокументОбъект.КассоваяСмена") Тогда
						НовыйДокумент.УстановитьСсылкуНового(ПолученныеДанныеСсылка);
					КонецЕсли;
					НовыйДокумент.ОбменДанными.Загрузка = Истина;
					НовыйДокумент.Записать();
					
					ОРПОбъект.КассоваяСмена = НовыйДокумент.Ссылка;
				КонецЕсли;
				ОРПОбъект.ОбменДанными.Загрузка = Истина;
				ОРПОбъект.Записать();
				НачалоСмены = Выборка.ОкончаниеКассовойСмены;
			КонецЦикла;
		КонецЕсли;
		ДокументыРПС = ДокументыСПС.Скопировать(Новый Структура("ОчередьДокумента", 2));
		Если ДокументыРПС.Количество() > 0 Тогда
			ДокументыРПС.Сортировать("ДатаДокумента");
			Запрос = Новый Запрос;
			Запрос.Текст =
				"ВЫБРАТЬ
				|	ВозвратПодарочныхСертификатов.Ссылка КАК Ссылка,
				|	ВозвратПодарочныхСертификатов.Организация КАК Организация,
				|	ВозвратПодарочныхСертификатов.Валюта КАК Валюта,
				|	ВозвратПодарочныхСертификатов.КассаККМ КАК КассаККМ,
				|	ВозвратПодарочныхСертификатов.Дата КАК Дата
				|ПОМЕСТИТЬ ВТ_РеализацияВозвратПС
				|ИЗ
				|	Документ.ВозвратПодарочныхСертификатов КАК ВозвратПодарочныхСертификатов
				|ГДЕ
				|	ВозвратПодарочныхСертификатов.Ссылка В(&МассивСсылок)
				|
				|ОБЪЕДИНИТЬ ВСЕ
				|
				|ВЫБРАТЬ
				|	РеализацияПодарочныхСертификатов.Ссылка,
				|	РеализацияПодарочныхСертификатов.Организация,
				|	РеализацияПодарочныхСертификатов.Валюта,
				|	РеализацияПодарочныхСертификатов.КассаККМ,
				|	РеализацияПодарочныхСертификатов.Дата
				|ИЗ
				|	Документ.РеализацияПодарочныхСертификатов КАК РеализацияПодарочныхСертификатов
				|ГДЕ
				|	РеализацияПодарочныхСертификатов.Ссылка В(&МассивСсылок)
				|
				|ИНДЕКСИРОВАТЬ ПО
				|	Организация,
				|	Валюта,
				|	КассаККМ
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ
				|	КассоваяСмена.Ссылка КАК КассоваяСменаСсылка,
				|	ВТ_РеализацияВозвратПС.Ссылка КАК Ссылка,
				|	КассоваяСмена.НачалоКассовойСмены КАК НачалоКассовойСмены
				|ИЗ
				|	ВТ_РеализацияВозвратПС КАК ВТ_РеализацияВозвратПС
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КассоваяСмена КАК КассоваяСмена
				|		ПО ВТ_РеализацияВозвратПС.Организация = КассоваяСмена.Организация
				|			И ВТ_РеализацияВозвратПС.КассаККМ = КассоваяСмена.КассаККМ
				|			И ВТ_РеализацияВозвратПС.Дата >= КассоваяСмена.НачалоКассовойСмены
				|			И ВТ_РеализацияВозвратПС.Дата <= КассоваяСмена.ОкончаниеКассовойСмены";
			
			Запрос.УстановитьПараметр("МассивСсылок", ДокументыРПС.ВыгрузитьКолонку("ДокументСсылка"));
			
			Выборка = Запрос.Выполнить().Выбрать();
			Пока Выборка.Следующий() Цикл
				РПСОбъект = Выборка.Ссылка.ПолучитьОбъект();
				РПСОбъект.Дата = Выборка.НачалоКассовойСмены;
				РПСОбъект.КассоваяСмена = Выборка.КассоваяСменаСсылка;
				РПСОбъект.ОбменДанными.Загрузка = Истина;
				РПСОбъект.Записать();
				
				НайденнаяСтрока = ТаблицаДляОтложенногоПроведения.Найти(Выборка.Ссылка, "ДокументСсылка");
				Если НайденнаяСтрока <> Неопределено Тогда
					НайденнаяСтрока.ДатаДокумента  = Выборка.НачалоКассовойСмены;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		ТаблицаВПС = ДокументыСПС.Скопировать(Новый Структура("ОчередьДокумента", 3));
		Если ТаблицаВПС.Количество() > 0 Тогда
			ТаблицаВПС.Сортировать("ДатаДокумента");
			Запрос = Новый Запрос;
			Запрос.Текст = "ВЫБРАТЬ
							|	ТаблицаВПС.ДокументСсылка КАК ВозвратПС,
							|	ТаблицаВПС.ОРП КАК ОРП
							|ПОМЕСТИТЬ втВПС
							|ИЗ
							|	&ТаблицаВПС КАК ТаблицаВПС
							|;
							|
							|////////////////////////////////////////////////////////////////////////////////
							|ВЫБРАТЬ
							|	ВозвратПодарочныхСертификатов.Ссылка КАК ВозвратПС,
							|	ОтчетОРозничныхПродажах.КассоваяСмена КАК КассоваяСмена,
							|	ОтчетОРозничныхПродажах.КассаККМ КАК КассаККМ
							|ИЗ
							|	втВПС КАК втВПС
							|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВозвратПодарочныхСертификатов КАК ВозвратПодарочныхСертификатов
							|		ПО втВПС.ВозвратПС = ВозвратПодарочныхСертификатов.Ссылка
							|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетОРозничныхПродажах КАК ОтчетОРозничныхПродажах
							|		ПО втВПС.ОРП = ОтчетОРозничныхПродажах.Ссылка";
			
			Запрос.УстановитьПараметр("ТаблицаВПС", ТаблицаВПС);
			
			Выборка = Запрос.Выполнить().Выбрать();
			Пока Выборка.Следующий() Цикл
				ВПСОбъект = Выборка.ВозвратПС.ПолучитьОбъект();
				ВПСОбъект.КассоваяСмена = Выборка.КассоваяСмена;
				ВПСОбъект.КассаККМ = Выборка.КассаККМ;
				ВПСОбъект.ОбменДанными.Загрузка = Истина;
				ВПСОбъект.Записать();
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры
