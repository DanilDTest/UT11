Функция КассаПоУмолчанию(ПараметрыОтбора, КомпонентыОбмена)
	Валюта = ПараметрыОтбора.Валюта;
	Организация = ПараметрыОтбора.Организация;
	Подразделение = ПараметрыОтбора.Подразделение;
	
	НеобходимоСоздатьЭлемент = Ложь;
	
	Если ОбщегоНазначения.СсылкаСуществует(Организация) И ОбщегоНазначения.СсылкаСуществует(Валюта) Тогда
		НаименованиеЭлементаДляОбменаПоУмолчанию = СокрЛП(Организация) + " (" + СокрЛП(Валюта) + ")";
	Иначе
		НаименованиеЭлементаДляОбменаПоУмолчанию = НСтр("ru = 'Основная касса';
														|en = 'Main cash account'");
	КонецЕсли;
	
	СписокПодразделений = Новый СписокЗначений();
	СписокПодразделений.Добавить(Справочники.СтруктураПредприятия.ПустаяСсылка());
	СписокПодразделений.Добавить(Подразделение);
	
	Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 2
		|	Кассы.Ссылка КАК Касса,
		|	ВЫБОР
		|		КОГДА Кассы.Наименование = &НаименованиеЭлементаДляОбменаПоУмолчанию
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЭтоЭлементПоУмолчаниюДляОбмена,
		|	Кассы.Подразделение КАК Подразделение
		|ИЗ
		|	Справочник.Кассы КАК Кассы
		|ГДЕ
		|	НЕ Кассы.ПометкаУдаления
		|	И Кассы.Владелец = &Организация
		|	И Кассы.ВалютаДенежныхСредств = &Валюта
		|	И Кассы.Подразделение В(&СписокПодразделений)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ЭтоЭлементПоУмолчаниюДляОбмена УБЫВ,
		|	Подразделение УБЫВ");
	
	Запрос.УстановитьПараметр("НаименованиеЭлементаДляОбменаПоУмолчанию", НаименованиеЭлементаДляОбменаПоУмолчанию);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Валюта", Валюта);
	Запрос.УстановитьПараметр("СписокПодразделений", СписокПодразделений);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Если Выборка.Количество() = 1 Или Выборка.ЭтоЭлементПоУмолчаниюДляОбмена Тогда
			Касса = Выборка.Касса;
		Иначе
			НеобходимоСоздатьЭлемент = Истина;
		КонецЕсли;
	Иначе
		НеобходимоСоздатьЭлемент = Истина;
	КонецЕсли;
	
	// Создание элемента для обмена с заданными параметрами
	Если НеобходимоСоздатьЭлемент Тогда
		КассаОбъект = Справочники.Кассы.СоздатьЭлемент();
		КассаОбъект.Наименование			= НаименованиеЭлементаДляОбменаПоУмолчанию;
		КассаОбъект.ВалютаДенежныхСредств	= Валюта;
		КассаОбъект.Владелец				= Организация;
		КассаОбъект.Подразделение			= Подразделение;
		Попытка
			КассаОбъект.Записать();
			Касса = КассаОбъект.Ссылка;
		Исключение
			Касса = Справочники.Кассы.ПустаяСсылка();
			ЗаписатьОшибкуВЖурналРегистрации(Касса,
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()), КомпонентыОбмена);
		КонецПопытки
	КонецЕсли;
		
	Возврат Касса;
КонецФункции
