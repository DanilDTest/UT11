Процедура ОтложеннаяОбработка_СФПолученныйНалоговыйАгент(Объект, КомпонентыОбмена)
	Если Не ДокументыОснованияСуществуют(Объект) Тогда
		Возврат;
	КонецЕсли;
	
	Объект.ЗаполнитьПараметрыСчетаФактурыПоОснованию();
	
	ЗаполнениеПартнераВШапкеДокумента(Объект);
	
	Если Объект.ДокументыОснования.Количество() > 1 Тогда
		
		ВременныйОбъект = Объект.Скопировать();
		ВременныйОбъект.ЗаполнитьПараметрыСчетаФактурыПоОснованию();
	
		СуммаДокумента    = Объект.ДокументыОснования[0].Сумма;
		СуммаНДСДокумента = Объект.ДокументыОснования[0].СуммаНДС;
		
		РаспределеннаяСумма = Неопределено;	
		Если НЕ СуммаДокумента = ВременныйОбъект.ДокументыОснования.Итог("Сумма") Тогда
			// Распределим сумму документа по суммам документов оснований.
			РаспределеннаяСумма = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(СуммаДокумента,
				ВременныйОбъект.ДокументыОснования.ВыгрузитьКолонку("Сумма"));		
		КонецЕсли;
		
		РаспределеннаяСуммаНДС = Неопределено;
		Если НЕ СуммаНДСДокумента = ВременныйОбъект.ДокументыОснования.Итог("СуммаНДС") Тогда
			// Распределим сумму НДС документа по суммам НДС документов оснований.
			РаспределеннаяСуммаНДС = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(СуммаНДСДокумента,
				ВременныйОбъект.ДокументыОснования.ВыгрузитьКолонку("Сумма"));		
		КонецЕсли;
		
		Для Сч = 0 По ВременныйОбъект.ДокументыОснования.Количество()-1 Цикл
			ВременныйОбъект.ДокументыОснования[Сч].Сумма = ?(РаспределеннаяСумма = Неопределено,
				0, РаспределеннаяСумма[Сч]);
			ВременныйОбъект.ДокументыОснования[Сч].СуммаНДС = ?(РаспределеннаяСуммаНДС = Неопределено,
				0, РаспределеннаяСуммаНДС[Сч]);		
		КонецЦикла;
		
		Объект.ДокументыОснования.Загрузить(ВременныйОбъект.ДокументыОснования.Выгрузить());
		
		ВременныйОбъект = Неопределено;
	
	КонецЕсли;
КонецПроцедуры
