Процедура ВыгрузитьРасшифровкуПлатежаКонвертацияВалюты(КомпонентыОбмена, ДанныеИБ, ДанныеXDTO, ДатаОперации)
	ТабРП = Новый ТаблицаЗначений;
	ТабРП.Колонки.Добавить("СтатьяДДС");
	ТабРП.Колонки.Добавить("Сумма");
	ТабРП.Колонки.Добавить("СуммаВал");
	ТабРП.Колонки.Добавить("ВалютаКонвертации");
	ТабРП.Колонки.Добавить("КурсКонвертации");
	ТабРП.Колонки.Добавить("КратностьКонвертации");
	ТабРП.Колонки.Добавить("КурсЦБНаДатуКонвертации");
	ТабРП.Колонки.Добавить("Договор");
	
	ВалютаРегл          = ВалютаРегламентированногоУчета(КомпонентыОбмена);
	ПараметрыВалютыРег  = Новый Структура("Курс, Кратность, Валюта", 1, 1, ВалютаРегл);
	
	ВалютаОперации = ?(ДанныеИБ.Валюта = ВалютаРегл, ДанныеИБ.ВалютаКонвертации, ДанныеИБ.Валюта);
	ПараметрыКурсаЦБНаДатуОперации = РаботаСКурсамиВалют.ПолучитьКурсВалюты(ВалютаОперации, ДатаОперации);
	
	СтрокаРП = ТабРП.Добавить();
	СтрокаРП.СтатьяДДС            = ДанныеИБ.СтатьяДвиженияДенежныхСредств;
	СтрокаРП.ВалютаКонвертации    = ДанныеИБ.ВалютаКонвертации;
	Если ДанныеИБ.КратностьКурсаКонвертации <> 0 Тогда
		СтрокаРП.КратностьКонвертации = ДанныеИБ.КратностьКурсаКонвертации;
	Иначе
		СтрокаРП.КратностьКонвертации = ПараметрыКурсаЦБНаДатуОперации.Кратность;
	КонецЕсли;
	СтрокаРП.КурсКонвертации      = ДанныеИБ.КурсКонвертации;
	СтрокаРП.Сумма                = ДанныеИБ.СуммаДокумента;
	СтрокаРП.СуммаВал             = ДанныеИБ.СуммаКонвертации;
	
	СтрокаРП.КурсЦБНаДатуКонвертации = ПараметрыКурсаЦБНаДатуОперации.Курс;
	
	// Договор
	ПараметрыПоУмолчанию = Новый Структура(ДоговорФиксированнаяСтруктураКлючей());
	ПараметрыПоУмолчанию.Контрагент  = ДанныеИБ.Контрагент;
	ПараметрыПоУмолчанию.Организация = ДанныеИБ.Организация;
	ПараметрыПоУмолчанию.ВидДоговора = "Прочее";
	ПараметрыПоУмолчанию.Дата        = ДанныеИБ.Дата;
	ПараметрыПоУмолчанию.Номер       = ДанныеИБ.Номер;
	
	ПараметрыПоУмолчанию.ВалютаВзаиморасчетов     = ДанныеИБ.ВалютаКонвертации;
	ПараметрыПоУмолчанию.РасчетыВУсловныхЕдиницах = Ложь;
	
	СтрокаРП.Договор = ДоговорИнструкцияКонвертацииПоДаннымВзаиморасчетов(ПараметрыПоУмолчанию, КомпонентыОбмена);
	
	ДанныеXDTO.Вставить("РасшифровкаПлатежа", ТабРП);
КонецПроцедуры
