Процедура ОтложеннаяОбработка_СФКомитента(Объект, КомпонентыОбмена)
	ДокументОснование = Неопределено;
	Если Объект.Покупатели.Количество() > 0 Тогда
		ДокументОснование = Объект.Покупатели[0].СчетФактураВыданный;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДокументОснование)
		И ОбщегоНазначения.СсылкаСуществует(ДокументОснование) Тогда
		
		РеквизитыСтрокой = "Контрагент, Партнер, Подразделение, НаправлениеДеятельности, Договор";
		Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.СчетФактураВыданный") Тогда
			РеквизитыСтрокой = РеквизитыСтрокой + ", Склад";
		КонецЕсли;
			
		ДанныеОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументОснование,
			РеквизитыСтрокой);
		
		Для Каждого ЭлементСтруктуры Из ДанныеОснования Цикл
			Если ЗначениеЗаполнено(ЭлементСтруктуры.Значение)
				И НЕ Объект[ЭлементСтруктуры.Ключ] = ЭлементСтруктуры.Значение Тогда
				Объект[ЭлементСтруктуры.Ключ] = ЭлементСтруктуры.Значение;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	ЗаполнениеПартнераВШапкеДокумента(Объект);
КонецПроцедуры
