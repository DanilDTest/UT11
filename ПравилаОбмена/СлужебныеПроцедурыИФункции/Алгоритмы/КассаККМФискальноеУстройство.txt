Функция КассаККМФискальноеУстройство(Объект)
	ФискальноеУстройство = Неопределено;
	КассаККМ             = Неопределено;
	
	Если ТипЗнч(Объект) = Тип("СправочникСсылка.КассыККМ") Тогда
		
		КассаККМ = Объект;
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	КассыККМ.ПодключаемоеОборудование КАК ФискальноеУстройство,
		|	ВЫБОР
		|		КОГДА КассыККМ.Ссылка.РабочееМесто = &РабочееМесто
		|			ТОГДА 1
		|		ИНАЧЕ 2
		|	КОНЕЦ КАК Приоритет
		|ИЗ
		|	Справочник.НастройкиРМК.КассыККМ КАК КассыККМ
		|ГДЕ
		|	КассыККМ.КассаККМ = &КассаККМ
		|	И НЕ КассыККМ.ИспользоватьБезПодключенияОборудования
		|
		|УПОРЯДОЧИТЬ ПО
		|	Приоритет");
		
		Запрос.УстановитьПараметр("КассаККМ",     КассаККМ);
		Запрос.УстановитьПараметр("РабочееМесто", МенеджерОборудованияВызовСервера.ПолучитьРабочееМестоКлиента());
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда	
			ФискальноеУстройство = Выборка.ФискальноеУстройство;
		КонецЕсли;
		
	Иначе
		
		ФискальноеУстройство = Объект;
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	КассыККМ.КассаККМ КАК КассаККМ,
		|	ВЫБОР
		|		КОГДА КассыККМ.Ссылка.РабочееМесто = &РабочееМесто
		|			ТОГДА 1
		|		ИНАЧЕ 2
		|	КОНЕЦ КАК Приоритет
		|ИЗ
		|	Справочник.НастройкиРМК.КассыККМ КАК КассыККМ
		|ГДЕ
		|	КассыККМ.ПодключаемоеОборудование = &ФискальноеУстройство
		|
		|УПОРЯДОЧИТЬ ПО
		|	Приоритет");
	
		Запрос.УстановитьПараметр("ФискальноеУстройство", ФискальноеУстройство);
		Запрос.УстановитьПараметр("РабочееМесто",         МенеджерОборудованияВызовСервера.ПолучитьРабочееМестоКлиента());
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда	
			КассаККМ = Выборка.КассаККМ;
		КонецЕсли;
		
	КонецЕсли;
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("ФискальноеУстройство", ФискальноеУстройство);
	ВозвращаемоеЗначение.Вставить("КассаККМ",             КассаККМ);
	
	Возврат ВозвращаемоеЗначение;
КонецФункции
