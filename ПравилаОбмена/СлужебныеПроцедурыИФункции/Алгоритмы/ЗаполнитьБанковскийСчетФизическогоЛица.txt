Процедура ЗаполнитьБанковскийСчетФизическогоЛица(Объект)
	Если НЕ Объект.ДополнительныеСвойства.Свойство("БанковскийСчетКонтрагента_Номер") Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	БанковскиеСчетаКонтрагентов.Ссылка
	|ИЗ
	|	Справочник.БанковскиеСчетаКонтрагентов КАК БанковскиеСчетаКонтрагентов
	|ГДЕ
	|	БанковскиеСчетаКонтрагентов.Владелец = &ФизическоеЛицо
	|	И БанковскиеСчетаКонтрагентов.НомерСчета = &НомерСчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	БанковскиеСчетаКонтрагентов.Ссылка
	|ИЗ
	|	Справочник.БанковскиеСчетаКонтрагентов КАК БанковскиеСчетаКонтрагентов
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(БанковскиеСчетаКонтрагентов.Владелец) = ТИП(Справочник.Контрагенты)
	|	И БанковскиеСчетаКонтрагентов.НомерСчета = &НомерСчета";
	Запрос.УстановитьПараметр("ФизическоеЛицо", Объект.ПодотчетноеЛицо);
	Запрос.УстановитьПараметр("НомерСчета", Объект.ДополнительныеСвойства.БанковскийСчетКонтрагента_Номер);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	Выборка = РезультатЗапроса[0].Выбрать();
	Если Выборка.Следующий() Тогда
		Объект.БанковскийСчетКонтрагента = Выборка.Ссылка;
	Иначе
		
		Выборка = РезультатЗапроса[1].Выбрать();
		Если Выборка.Следующий() Тогда
			
			СчетФизЛица = Справочники.БанковскиеСчетаКонтрагентов.СоздатьЭлемент();
			ЗаполнитьЗначенияСвойств(СчетФизЛица, Выборка.Ссылка,, "Родитель");
			СчетФизЛица.УстановитьНовыйКод();
			СчетФизЛица.Владелец = Объект.ПодотчетноеЛицо;
			СчетФизЛица.ОбменДанными.Загрузка = Истина;
			СчетФизЛица.Записать();
			
			Объект.БанковскийСчетКонтрагента = СчетФизЛица.Ссылка;
			
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры
