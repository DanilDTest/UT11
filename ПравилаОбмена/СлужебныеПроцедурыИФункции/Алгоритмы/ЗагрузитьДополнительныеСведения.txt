Процедура ЗагрузитьДополнительныеСведения(ОбъектСсылка, СтруктураДопСвойства, КомпонентыОбмена)
	Если Не СтруктураДопСвойства.Свойство("ДополнительныеСведения")
		Или СтруктураДопСвойства.ДополнительныеСведения.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если СтандартныеПодсистемыСервер.ЭтоБазоваяВерсияКонфигурации()
		Или Не ПолучитьФункциональнуюОпцию("ИспользоватьДополнительныеРеквизитыИСведения") Тогда
		Возврат;
	КонецЕсли;
	
	ДопСведенияНаборЗаписей = РегистрыСведений.ДополнительныеСведения.СоздатьНаборЗаписей();
	ДопСведенияНаборЗаписей.Отбор.Объект.Установить(ОбъектСсылка);
	
	ДопСведенияНаборЗаписей.Прочитать();
	
	ТаблицаДопСведения = ДопСведенияНаборЗаписей.Выгрузить();
	
	ПКО_ДопРеквизиты = RM_ОбменДаннымиXDTOСервер.ПКОПоИмени(КомпонентыОбмена, "Справочник_ДополнительныеРеквизиты");
	
	Для Каждого СтрокаДопСведения Из СтруктураДопСвойства.ДополнительныеСведения Цикл
		СвойствоСсылка = RM_ОбменДаннымиXDTOСервер.СтруктураОбъектаXDTOВДанныеИБ(КомпонентыОбмена, 
			СтрокаДопСведения.Свойство, 
			ПКО_ДопРеквизиты, 
			"ПолучитьСсылку").Ссылка;
			
		Если ТипЗнч(СтрокаДопСведения.Значение) = Тип("Структура") Тогда
			ПКО_ЗначениеСвойства = RM_ОбменДаннымиXDTOСервер.ПКОПоИмени(КомпонентыОбмена, СтрокаДопСведения.Значение.ИмяПКО);
			
			ЗначениеСвойства = RM_ОбменДаннымиXDTOСервер.СтруктураОбъектаXDTOВДанныеИБ(КомпонентыОбмена, 
				СтрокаДопСведения.Значение.Значение, 
				ПКО_ЗначениеСвойства, 
				"ПолучитьСсылку").Ссылка;
		Иначе
			ЗначениеСвойства = СтрокаДопСведения.Значение;
		КонецЕсли;
		
		НайденныеСтроки = ТаблицаДопСведения.НайтиСтроки(Новый Структура("Объект, Свойство", ОбъектСсылка, СвойствоСсылка));
		Если НайденныеСтроки.Количество() = 1 Тогда
			НовЗапись = НайденныеСтроки[0];	
		Иначе
			НовЗапись = ТаблицаДопСведения.Добавить();
			НовЗапись.Объект   = ОбъектСсылка;
			НовЗапись.Свойство = СвойствоСсылка;	
		КонецЕсли;
		
		НовЗапись.Значение = ЗначениеСвойства;
	КонецЦикла;
	
	ДопСведенияНаборЗаписей.Загрузить(ТаблицаДопСведения);
		
	ДопСведенияНаборЗаписей.ОбменДанными.Загрузка = Истина;
	ДопСведенияНаборЗаписей.Записать();
КонецПроцедуры
