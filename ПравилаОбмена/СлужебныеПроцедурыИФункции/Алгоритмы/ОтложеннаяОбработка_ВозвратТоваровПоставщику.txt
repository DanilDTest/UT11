Процедура ОтложеннаяОбработка_ВозвратТоваровПоставщику(Объект, ОбъектМодифицирован, КомпонентыОбмена)
	ЗаполнениеПартнераВШапкеДокумента(Объект);
	ЗаполнениеСоглашенияСПоставщикомВШапкеДокумента(Объект, КомпонентыОбмена.ПараметрыКонвертации);
	ЗаполнитьПараметрыДоставки(Объект);
	ЗаполнитьТипНалогообложенияПоУмолчанию(Объект);
	
	Если Не ЗначениеЗаполнено(Объект.ПорядокОплаты) Тогда
		Если ЗначениеЗаполнено(Объект.Соглашение)
			И (ПолучитьФункциональнуюОпцию("ИспользоватьСоглашенияСКлиентами")) Тогда
			
			УсловияЗакупок = ЗакупкиСервер.ПолучитьУсловияЗакупок(Объект.Соглашение);
			
			Если Не ЗначениеЗаполнено(УсловияЗакупок.ИспользуютсяДоговорыКонтрагентов) 
				Или Не УсловияЗакупок.ИспользуютсяДоговорыКонтрагентов Тогда
				Объект.ПорядокОплаты = УсловияЗакупок.ПорядокОплаты;
			Иначе
				Объект.ПорядокОплаты = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Договор, "ПорядокОплаты");
			КонецЕсли;
			
		ИначеЕсли ЗначениеЗаполнено(Объект.Договор)
			И (ПолучитьФункциональнуюОпцию("ИспользоватьДоговорыСКлиентами")) Тогда
			Объект.ПорядокОплаты = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Договор, "ПорядокОплаты");
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.ПорядокОплаты) Тогда
		ВалютаОплаты         = ДенежныеСредстваСервер.ПолучитьВалютуОплаты(, Объект.БанковскийСчетОрганизации);
		Объект.ПорядокОплаты = Перечисления.ПорядокОплатыПоСоглашениям.ПолучитьПорядокОплатыПоУмолчанию(Объект.Валюта,
			Объект.НалогообложениеНДС,
			ВалютаОплаты);
	КонецЕсли;
	
	ЗаполнитьСтатусыУказанияСерий(Объект, КомпонентыОбмена);
КонецПроцедуры
