Функция КонтрагентИзОрганизации(Знач ОрганизацияДляСозданияКонтрагента, КомпонентыОбмена)
	// Проверим, есть ли в базе контрагент, созданный по организации, если нет, то создадим его и выгрузим.
	СсылкаНаКонтрагента = Справочники.Контрагенты.ПолучитьСсылку(ОрганизацияДляСозданияКонтрагента.УникальныйИдентификатор());
	Если НЕ ОбщегоНазначения.СсылкаСуществует(СсылкаНаКонтрагента) ИЛИ СсылкаНаКонтрагента.ПолучитьОбъект() = Неопределено Тогда
		
		ЗапросКонтр = Новый Запрос;
		ЗапросКонтр.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1 Ссылка
		|ИЗ Справочник.Контрагенты
		|ГДЕ Наименование = &Наименование 
		|	И ИНН = &ИНН
		|	И КПП = &КПП
		|	И ЮридическоеФизическоеЛицо = &ЮридическоеФизическоеЛицо";
		ЗапросКонтр.УстановитьПараметр("Наименование", ОрганизацияДляСозданияКонтрагента.Наименование);
		ЗапросКонтр.УстановитьПараметр("ИНН", ОрганизацияДляСозданияКонтрагента.ИНН);
		ЗапросКонтр.УстановитьПараметр("КПП", ОрганизацияДляСозданияКонтрагента.КПП);
		ЗапросКонтр.УстановитьПараметр("ЮридическоеФизическоеЛицо", ОрганизацияДляСозданияКонтрагента.ЮридическоеФизическоеЛицо);
		Выборка = ЗапросКонтр.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			СсылкаНаКонтрагента = Выборка.Ссылка;
			Возврат СсылкаНаКонтрагента;
		КонецЕсли;
		
		СоздаваемыйКонтрагент = Справочники.Контрагенты.СоздатьЭлемент();
		ЗаполнитьЗначенияСвойств(СоздаваемыйКонтрагент, ОрганизацияДляСозданияКонтрагента,
			"ПометкаУдаления,ИНН,КодПоОКПО,КПП,НаименованиеПолное,ЮрФизЛицо,Наименование,ОбособленноеПодразделение");
		
		Если СоздаваемыйКонтрагент.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ИндивидуальныйПредприниматель
			Или СоздаваемыйКонтрагент.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо Тогда
			
			СоздаваемыйКонтрагент.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо;
			
		ИначеЕсли СоздаваемыйКонтрагент.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо
			Или СоздаваемыйКонтрагент.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицоНеРезидент Тогда
			
			СоздаваемыйКонтрагент.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо;
			
		КонецЕсли;
		
		Если СоздаваемыйКонтрагент.ОбособленноеПодразделение Тогда
			ОрганизацияДляСозданияКонтрагента = ОрганизацияДляСозданияКонтрагента.ГоловнаяОрганизация;
			ГоловнойКонтрагент = КонтрагентИзОрганизации(ОрганизацияДляСозданияКонтрагента, КомпонентыОбмена);
			СоздаваемыйКонтрагент.ГоловнойКонтрагент = ГоловнойКонтрагент;
		Иначе
			СоздаваемыйКонтрагент.ГоловнойКонтрагент = СсылкаНаКонтрагента;
		КонецЕсли;
		
		ПартнерОбъект = Справочники.Партнеры.СоздатьЭлемент();
		СинхронизироватьДанныеПартнераСДаннымиКонтрагента(ПартнерОбъект, СоздаваемыйКонтрагент, КомпонентыОбмена);
		СоздаваемыйКонтрагент.Партнер = ПартнерОбъект.Ссылка;
		
		СоздаваемыйКонтрагент.УстановитьСсылкуНового(СсылкаНаКонтрагента);
		СоздаваемыйКонтрагент.ОбменДанными.Загрузка = Истина;
		СоздаваемыйКонтрагент.Записать();
		
		КомпонентыОбмена.ПараметрыКонвертации.СозданныеПриВыгрузкеОбъекты.Добавить(СоздаваемыйКонтрагент.Ссылка);
		
		СсылкаНаКонтрагента = СоздаваемыйКонтрагент.Ссылка;
	КонецЕсли;
	Возврат СсылкаНаКонтрагента;
КонецФункции
