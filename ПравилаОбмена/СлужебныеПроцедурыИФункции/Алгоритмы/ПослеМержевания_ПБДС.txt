Процедура ПослеМержевания_ПБДС(ПолученныеДанные, ДанныеИБ, КомпонентыОбмена)
	ДанныеДляЗаписиВИБ = ?(ДанныеИБ = Неопределено, ПолученныеДанные, ДанныеИБ);
	
	// Тип платежного документа.
	Если Не ЗначениеЗаполнено(ДанныеДляЗаписиВИБ.ТипПлатежногоДокумента) Тогда
		ДанныеДляЗаписиВИБ.ТипПлатежногоДокумента = Перечисления.ТипыПлатежныхДокументов.ПлатежноеПоручение;
	КонецЕсли;
	
	// Пересчет суммы конвертации.
	Если ДанныеДляЗаписиВИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.КонвертацияВалюты Тогда
		
		Если ДанныеДляЗаписиВИБ.КурсКонвертации <> 0 И ДанныеДляЗаписиВИБ.СуммаДокумента <> 0 Тогда
			Если ДанныеДляЗаписиВИБ.ВалютаКонвертации = Константы.ВалютаРегламентированногоУчета.Получить() Тогда
				ДанныеДляЗаписиВИБ.СуммаКонвертации = ДанныеДляЗаписиВИБ.СуммаДокумента * ДанныеДляЗаписиВИБ.КурсКонвертации;
			Иначе
				ДанныеДляЗаписиВИБ.СуммаКонвертации = ДанныеДляЗаписиВИБ.СуммаДокумента / ДанныеДляЗаписиВИБ.КурсКонвертации;
			КонецЕсли;
		Иначе
			ДанныеДляЗаписиВИБ.СуммаКонвертации = 0;
		КонецЕсли;
	
	КонецЕсли;
		
	// Расшифровка платежа. Статья доходов и подразделение.
	Если ДанныеДляЗаписиВИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПрочееПоступлениеДенежныхСредств Тогда
		
		ЗаполнитьПодразделениеВРасшифровкеПлатежа(ДанныеДляЗаписиВИБ, КомпонентыОбмена);
		
		Для Каждого СтрокаРП Из ДанныеДляЗаписиВИБ.РасшифровкаПлатежа Цикл
			Если Не ЗначениеЗаполнено(СтрокаРП.СтатьяДоходов) Тогда
				СтрокаРП.СтатьяДоходов = ПланыВидовХарактеристик.СтатьиАктивовПассивов.ПрочиеАктивы;
			КонецЕсли;
		КонецЦикла;
	
	КонецЕсли;
КонецПроцедуры
