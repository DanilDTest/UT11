Процедура ОтложеннаяОбработка_КорректировкаРеализации(Объект, ОбъектМодифицирован, КомпонентыОбмена)
	ЗаполнениеПартнераВШапкеДокумента(Объект);
	Если НЕ ЗначениеЗаполнено(Объект.НалогообложениеНДС) И ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
		Объект.НалогообложениеНДС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ДокументОснование, "НалогообложениеНДС");
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Объект.ВалютаВзаиморасчетов) Тогда
		Если ЗначениеЗаполнено(Объект.Договор) Тогда
			Объект.ВалютаВзаиморасчетов = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Договор, "ВалютаВзаиморасчетов");
		Иначе
			Объект.ВалютаВзаиморасчетов = Объект.Валюта;
		КонецЕсли;
	КонецЕсли;
	// Заполнение колонки КоличествоУпаковок в ТЧ Товары
	Для Каждого Строка Из Объект.Товары Цикл
		Если Строка.Количество = 0 Тогда
			Продолжить;
		КонецЕсли;
		Коэф = Справочники.УпаковкиЕдиницыИзмерения.КоэффициентУпаковки(Строка.Упаковка, Строка.Номенклатура);
		Если Коэф = 0 Тогда
			Строка.КоличествоУпаковок = Строка.КОличество;
		Иначе
			Строка.КоличествоУпаковок = Окр(Строка.КОличество / Коэф, 3);
		КонецЕсли;
	КонецЦикла;
	Если ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
		ХозОперацияОснования = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ДокументОснование, "ХозяйственнаяОперация");
		ЗаполнениеСоглашенияСКлиентомВШапкеДокумента(Объект, ХозОперацияОснования);
		Объект.Расхождения.Очистить();
		Документы.КорректировкаРеализации.ЗаполнитьРасхождения(Объект);
		// Заполнение варианта отражения расхождений.
		Для Каждого Строка Из Объект.Расхождения Цикл
			Если Строка.КоличествоУпаковок > 0 Тогда
				Строка.ВариантОтражения = Перечисления.ВариантыОтраженияКорректировокРеализаций.УвеличитьРеализациюУменьшитьСкладскиеОстатки;
			Иначе
				Строка.ВариантОтражения = Перечисления.ВариантыОтраженияКорректировокРеализаций.УменьшитьРеализациюУвеличитьСкладскиеОстатки;
			КонецЕсли;		
		КонецЦикла;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.ПорядокРасчетов) Тогда
		Объект.ПорядокРасчетов = ВзаиморасчетыСервер.ПорядокРасчетовПоУмолчанию(Объект);
	КонецЕсли;
	
	ЗаполнитьСтатусыУказанияСерий(Объект, КомпонентыОбмена);
КонецПроцедуры
