Процедура ОтложеннаяОбработка_ПоступлениеТоваровУслуг(Объект, ОбъектМодифицирован, КомпонентыОбмена)
	ЗаполнениеПартнераВШапкеДокумента(Объект);
	ЗаполнениеСоглашенияСПоставщикомВШапкеДокумента(Объект, КомпонентыОбмена.ПараметрыКонвертации);
	ЗаполнитьПараметрыДоставки(Объект);
	ЗаполнитьТипНалогообложенияПоУмолчанию(Объект);
	ЗаполнитьВидДеятельностиПоУмолчанию(Объект, "ЗакупкаПодДеятельность");
	
	// Перезаполнение вида договора для операции закупки по импорту.
	Если Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпорту Тогда
	
		ДоговорСсылка = Объект.Договор;
	
		Если ЗначениеЗаполнено(ДоговорСсылка)
			И ОбщегоНазначения.СсылкаСуществует(ДоговорСсылка) Тогда
			
			ДанныеДоговора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДоговорСсылка, "ТипДоговора, ХозяйственнаяОперация");
			
			ТипДоговораИмпортТребуется = Перечисления.ТипыДоговоров.Импорт;
			ХозяйственнаяОперацияТребуется = Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпорту;
			
			Если ДанныеДоговора.ТипДоговора <> ТипДоговораИмпортТребуется
				Или ДанныеДоговора.ХозяйственнаяОперация <> ХозяйственнаяОперацияТребуется Тогда
				
				ДоговорОбъект = ДоговорСсылка.ПолучитьОбъект();
				
				ДоговорОбъект.ТипДоговора           = ТипДоговораИмпортТребуется;
				ДоговорОбъект.ХозяйственнаяОперация = ХозяйственнаяОперацияТребуется;
				
				УзелОбмена = ?(КомпонентыОбмена.ЭтоОбменЧерезПланОбмена, КомпонентыОбмена.УзелКорреспондента, Неопределено);
				ОбменДаннымиСервер.УстановитьОбменДаннымиЗагрузка(ДоговорОбъект, Истина, Ложь, УзелОбмена);
				
				ДоговорОбъект.Записать();
				
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.ПорядокОплаты) Тогда
		Если ЗначениеЗаполнено(Объект.Соглашение)
			И (ПолучитьФункциональнуюОпцию("ИспользоватьСоглашенияСПоставщиками")) Тогда
			
			УсловияЗакупок = ЗакупкиСервер.ПолучитьУсловияЗакупок(Объект.Соглашение, Истина, Истина);
			
			Если Не ЗначениеЗаполнено(УсловияЗакупок.ИспользуютсяДоговорыКонтрагентов) 
				Или Не УсловияЗакупок.ИспользуютсяДоговорыКонтрагентов Тогда
				Объект.ПорядокОплаты = УсловияЗакупок.ПорядокОплаты;
			Иначе
				Объект.ПорядокОплаты = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Договор, "ПорядокОплаты");
			КонецЕсли;
			
		ИначеЕсли ЗначениеЗаполнено(Объект.Договор)
			И (ПолучитьФункциональнуюОпцию("ИспользоватьДоговорыСПоставщиками")) Тогда
			Объект.ПорядокОплаты = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Договор, "ПорядокОплаты");
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.ПорядокОплаты) Тогда
		ВалютаОплаты = ДенежныеСредстваСервер.ПолучитьВалютуОплаты(Объект.ФормаОплаты, Объект.БанковскийСчетОрганизации);
		
		Объект.ПорядокОплаты = Перечисления.ПорядокОплатыПоСоглашениям.ПолучитьПорядокОплатыПоУмолчанию(Объект.ВалютаВзаиморасчетов,
			Объект.НалогообложениеНДС,
			ВалютаОплаты);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.ПорядокРасчетов) Тогда
		Объект.ПорядокРасчетов = ВзаиморасчетыСервер.ПорядокРасчетовПоУмолчанию(Объект);
	КонецЕсли;
	
	// Подразделение в ТЧ Товары для услуг
	Подразделение = ЭлементПоУмолчанию(КомпонентыОбмена, "Подразделение");
	
	// Заполнение пустой статьи расходов и подразделения для услуг
	СтатьяРасходов = Неопределено;
	СтатьяРасходовДопРасходы = Неопределено;
	
	Для Каждого Строка Из Объект.Товары Цикл
		Если (Строка.Номенклатура.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Услуга
				Или Строка.Номенклатура.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Работа) Тогда
			Строка.СписатьНаРасходы = Истина;
			Если ЗначениеЗаполнено(Строка.АналитикаРасходов)
					И ТипЗнч(Строка.АналитикаРасходов) = Тип("ДокументСсылка.ПриобретениеТоваровУслуг")
					И Не ЗначениеЗаполнено(Строка.СтатьяРасходов) Тогда
					Если СтатьяРасходовДопРасходы = Неопределено Тогда
						СтатьяРасходовДопРасходы = ЭлементПоУмолчанию(КомпонентыОбмена, "СтатьяРасходовПоступлениеУслугДопРасходы");
					КонецЕсли;
					Строка.СтатьяРасходов = СтатьяРасходовДопРасходы; 
			ИначеЕсли Не ЗначениеЗаполнено(Строка.СтатьяРасходов) Тогда
				Если СтатьяРасходов = Неопределено Тогда
					СтатьяРасходов = ЭлементПоУмолчанию(КомпонентыОбмена, "СтатьяРасходовПоступлениеУслуг");
				КонецЕсли;
				Строка.СтатьяРасходов = СтатьяРасходов;
			КонецЕсли;
			Если Не ЗначениеЗаполнено(Строка.Подразделение) Тогда
				Строка.Подразделение = Подразделение; 
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	ЗаполнитьТаблицуСтатьиДДСИХозОперации(Объект, КомпонентыОбмена.ПараметрыКонвертации.ЗагруженныеСтатьиДДСИХозОперации);
	
	Если НЕ ЗначениеЗаполнено(Объект.ВариантПриемкиТоваров) Тогда
		Объект.ВариантПриемкиТоваров = ЗакупкиСервер.ПолучитьВариантПриемкиТоваров(Неопределено, Объект.Договор);
	КонецЕсли;
	
	Объект.ЗаполнитьЭтапыГрафикаОплаты();
	ЗаполнитьСтатусыУказанияСерий(Объект, КомпонентыОбмена);
КонецПроцедуры
