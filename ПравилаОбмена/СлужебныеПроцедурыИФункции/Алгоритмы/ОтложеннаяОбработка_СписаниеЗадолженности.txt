Процедура ОтложеннаяОбработка_СписаниеЗадолженности(Объект, КомпонентыОбмена)
	Если Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.СписаниеДебиторскойЗадолженности
		И НЕ ЗначениеЗаполнено(Объект.СтатьяРасходов) Тогда
		Объект.СтатьяРасходов = ЭлементПоУмолчанию(КомпонентыОбмена, "СтатьяРасходовСписаниеЗадолженности");
	ИначеЕсли Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.СписаниеКредиторскойЗадолженности
		И НЕ ЗначениеЗаполнено(Объект.СтатьяДоходов) Тогда
		Объект.СтатьяДоходов = ЭлементПоУмолчанию(КомпонентыОбмена, "СтатьяДоходовСписаниеЗадолженности");
	КонецЕсли;
	
	Партнер = ДенежныеСредстваСервер.ПолучитьПартнераПоКонтрагенту(Объект.Контрагент);	
	Если Не ЗначениеЗаполнено(Партнер) Тогда
		Партнер = Справочники.Партнеры.НеизвестныйПартнер;
	КонецЕсли;
	
	ПервичныеДокументы = КомпонентыОбмена.ПараметрыКонвертации.ПервичныеДокументы;
	Для Каждого СтрокаТЧ из Объект.Задолженность Цикл
		Если НЕ ЗначениеЗаполнено(СтрокаТЧ.Партнер)
			ИЛИ НЕ СтрокаТЧ.Партнер = Партнер Тогда
			СтрокаТЧ.Партнер = Партнер;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаТЧ.Заказ) И НЕ ОбщегоНазначения.СсылкаСуществует(СтрокаТЧ.Заказ) Тогда
			СтрокаТЧ.Заказ = Неопределено;
		ИначеЕсли ЗначениеЗаполнено(СтрокаТЧ.Заказ)
			И ТипЗнч(СтрокаТЧ.Заказ) = Тип("ДокументСсылка.ПервичныйДокумент") Тогда
			
			СтрокаПервичныеДокументы = ПервичныеДокументы.Добавить();
			СтрокаПервичныеДокументы.ПервичныйДокумент = СтрокаТЧ.Заказ;
			Если Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.СписаниеДебиторскойЗадолженности Тогда
			    СтрокаПервичныеДокументы.ТипПервичногоДокумента = Перечисления.ТипыПервичныхДокументов.РеализацияКлиенту;
			Иначе
				СтрокаПервичныеДокументы.ТипПервичногоДокумента = Перечисления.ТипыПервичныхДокументов.ПриобретениеУПоставщика;
			КонецЕсли;
		КонецЕсли;	
	КонецЦикла;
КонецПроцедуры
