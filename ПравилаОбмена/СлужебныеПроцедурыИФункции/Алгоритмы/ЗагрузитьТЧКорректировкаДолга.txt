Процедура ЗагрузитьТЧКорректировкаДолга(КомпонентыОбмена, ДанныеXDTO, ПолученныеДанные, ИмяТЧФормата, ИмяТЧДокумента, ПравилаЗаполнения)
	МассивСтрокЗадолженность = Новый Массив;
	
	Если ДанныеXDTO.Свойство(ИмяТЧФормата)
		И ЗначениеЗаполнено(ДанныеXDTO[ИмяТЧФормата]) Тогда
		Для Каждого Строка Из ДанныеXDTO[ИмяТЧФормата] Цикл
			СтруктураДанныхСтроки = ДанныеКоллекцииВВидеСтруктуры(Строка, ПравилаЗаполнения);
			СтруктураДанныхСтроки.Вставить("Организация", ПолученныеДанные.Организация);
			Если ЗначениеЗаполнено(Строка.ВалютаВзаиморасчетов) Тогда
				Если ТипЗнч(Строка.ВалютаВзаиморасчетов) = Тип("Структура")
					И Не Строка.ВалютаВзаиморасчетов.Свойство("Ссылка") Тогда
					ИмяПКО = "Справочник_Валюты_ИзСтруктуры";
				Иначе
					ИмяПКО = "Справочник_Валюты";
				КонецЕсли;
				СтруктураДанныхСтроки.Вставить("ВалютаВзаиморасчетов", Новый Структура("Значение, ИмяПКО", Строка.ВалютаВзаиморасчетов, ИмяПКО));
			КонецЕсли;
			Если ЗначениеЗаполнено(Строка.ВидРасчетов) Тогда
				ТипРасчетов = Неопределено;
				Если Строка.ВидРасчетов.Значение = "СПокупателем" Тогда
					ТипРасчетов = Перечисления.ТипыРасчетовСПартнерами.РасчетыСКлиентом;
				ИначеЕсли Строка.ВидРасчетов.Значение = "СПоставщиком" Тогда
					ТипРасчетов = Перечисления.ТипыРасчетовСПартнерами.РасчетыСПоставщиком;
				КонецЕсли;
				Если ТипРасчетов <> Неопределено Тогда
					СтруктураДанныхСтроки.Вставить("ТипРасчетов",ТипРасчетов);
				КонецЕсли;
			КонецЕсли; 
			ЗаказЗаполнен = Ложь;
			Если ЗначениеЗаполнено(Строка.ДокументРасчетов) Тогда
				ИнструкцияДокументРасчетов = ЗначениеВИнструкциюПКО(КомпонентыОбмена, Строка.ДокументРасчетов);
				Если ИнструкцияДокументРасчетов <> Неопределено Тогда
					СтруктураДанныхСтроки.Вставить("Заказ",ИнструкцияДокументРасчетов);
					ЗаказЗаполнен = Истина; 
				КонецЕсли;
			КонецЕсли;
			Если ЗаказЗаполнен = Ложь И ЗначениеЗаполнено(Строка.Договор) Тогда
				Если ТипЗнч(Строка.Договор) = Тип("Структура")
					И Строка.Договор.Свойство("Ссылка") = Ложь Тогда
					ИмяПКО = "Справочник_ДоговорыКонтрагентов_ИзСтруктуры_Получ";
				Иначе
					ИмяПКО = "Справочник_ДоговорыКонтрагентов_Получение";
				КонецЕсли;
				СтруктураДанныхСтроки.Вставить("Заказ",Новый Структура("Значение, ИмяПКО", Строка.Договор, ИмяПКО));
			КонецЕсли;
			МассивСтрокЗадолженность.Добавить(СтруктураДанныхСтроки);
		КонецЦикла;
	КонецЕсли;
	Если МассивСтрокЗадолженность.Количество() > 0 Тогда
		ПолученныеДанные.ДополнительныеСвойства.Вставить(ИмяТЧДокумента, МассивСтрокЗадолженность);
	КонецЕсли;
КонецПроцедуры
