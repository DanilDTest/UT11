Процедура ЗаполнениеСтатьиДДСВДокументе(Объект)
	// Если нет хоз операции - невозможно заполнить статью.
	Если НЕ ЗначениеЗаполнено(Объект.ХозяйственнаяОперация) Тогда
		Возврат;
	КонецЕсли;
		
	СтатьяДДСВШапкеЗаполнена = ЗначениеЗаполнено(Объект.СтатьяДвиженияДенежныхСредств);
	СтатьяДДСВРасшифровкеЗаполнена = Объект.РасшифровкаПлатежа.Количество() <> 0;
	Для Каждого СтрокаРасшифровка Из Объект.РасшифровкаПлатежа Цикл
		Если НЕ ЗначениеЗаполнено(СтрокаРасшифровка.СтатьяДвиженияДенежныхСредств) Тогда
			СтатьяДДСВРасшифровкеЗаполнена = Ложь;
		КонецЕсли;
	КонецЦикла;
	// Если статья заполнена в шапке или во всех строках ТЧ - ничего не делаем.
	Если СтатьяДДСВШапкеЗаполнена ИЛИ СтатьяДДСВРасшифровкеЗаполнена Тогда
		Возврат; 
	КонецЕсли;
	
	// Вычисление статьи ДДС по умолчанию.
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 2
	|	ТабХозОперации.Ссылка КАК Ссылка,
	|	ВЫБОР КОГДА ТабХозОперации.Ссылка.Наименование = &НаименованиеСтатьиПоУмолчанию ТОГДА
	|		2 
	|	КОГДА ТабХозОперации.Ссылка.Предопределенный ТОГДА 1
	|	ИНАЧЕ 0 КОНЕЦ КАК ЗначениеУпорядочивания
	|ИЗ Справочник.СтатьиДвиженияДенежныхСредств.ХозяйственныеОперации КАК ТабХозОперации
	|ГДЕ ТабХозОперации.ХозяйственнаяОперация = &ХозяйственнаяОперация
	|Упорядочить ПО ЗначениеУпорядочивания УБЫВ";
	
	ОперацииПоВыплатеЗарплаты = Новый СписокЗначений;
	ОперацииПоВыплатеЗарплаты.Добавить(Перечисления.ХозяйственныеОперации.ВыплатаЗарплаты);
	ОперацииПоВыплатеЗарплаты.Добавить(Перечисления.ХозяйственныеОперации.ВыплатаЗарплатыНаЛицевыеСчета);
	ОперацииПоВыплатеЗарплаты.Добавить(Перечисления.ХозяйственныеОперации.ВыплатаЗарплатыПоЗарплатномуПроекту);
	ОперацииПоВыплатеЗарплаты.Добавить(Перечисления.ХозяйственныеОперации.ВыплатаЗарплатыРаботнику);
	ОперацииПоВыплатеЗарплаты.Добавить(Перечисления.ХозяйственныеОперации.ВыплатаЗарплатыРаздатчиком);
	ОперацииПоВыплатеЗарплаты.Добавить(Перечисления.ХозяйственныеОперации.ВыплатаЗарплатыЧерезКассу);
	
	Если ОперацииПоВыплатеЗарплаты.НайтиПоЗначению(Объект.ХозяйственнаяОперация) = Неопределено Тогда
		ХозОперация = Объект.ХозяйственнаяОперация;
		НаименованиеСтатьиПоУмолчанию = "" + СокрЛП(Объект.ХозяйственнаяОперация) + " "+ НСтр("ru = '(по умолчанию)';
																								|en = '(default)'");
		ВидДвиженияДС = "";
		Если (Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоДепозитам)
			Или (Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоКредитам)
			Или (Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоЗаймамВыданным)
			Или (Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПрочиеДоходы) Тогда
			ВидДвиженияДС = Перечисления.ВидыДвиженийДенежныхСредств.ПрочиеПоступленияПоТекущейДеятельности;
		ИначеЕсли (Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента)
			Или (Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеОплатыОтКлиентаПоПлатежнойКарте) Тогда
			ВидДвиженияДС = Перечисления.ВидыДвиженийДенежныхСредств.СредстваПолученныеОтПокупателейИЗаказчиков;
		ИначеЕсли (Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПрочиеРасходы) Тогда
			ВидДвиженияДС = Перечисления.ВидыДвиженийДенежныхСредств.ПрочиеРасходыПоТекущейДеятельности;
		КонецЕсли;
	Иначе
		ХозОперация = Перечисления.ХозяйственныеОперации.ВыплатаЗарплаты;
		НаименованиеСтатьиПоУмолчанию = "" + СокрЛП(Перечисления.ХозяйственныеОперации.ВыплатаЗарплаты) + " " + НСтр("ru = '(по умолчанию)';
																													|en = '(default)'");
		ВидДвиженияДС = Перечисления.ВидыДвиженийДенежныхСредств.ОплатаТруда;
	КонецЕсли;
	Запрос.УстановитьПараметр("ХозяйственнаяОперация", ХозОперация);
	Запрос.УстановитьПараметр("НаименованиеСтатьиПоУмолчанию", НаименованиеСтатьиПоУмолчанию);
	Выборка = Запрос.Выполнить().Выбрать();
	НеобходимоСоздатьСтатью = Ложь;
	Если Выборка.Следующий() Тогда
		Если Выборка.Количество() = 1 ИЛИ Выборка.ЗначениеУпорядочивания > 0 Тогда
			СтатьяДДСПоУмолчанию = Выборка.Ссылка;
		Иначе
			НеобходимоСоздатьСтатью = Истина;
		КонецЕсли;
	Иначе
		НеобходимоСоздатьСтатью = Истина;
	КонецЕсли;
	Если НеобходимоСоздатьСтатью Тогда
		НовыйЭлемент = Справочники.СтатьиДвиженияДенежныхСредств.СоздатьЭлемент();
		НовыйЭлемент.Наименование = НаименованиеСтатьиПоУмолчанию;
		НовыйЭлемент.ВидДвиженияДенежныхСредств = ВидДвиженияДС;
		
		СтрокаТабличнойЧастиХозяйственныеОперации = НовыйЭлемент.ХозяйственныеОперации.Добавить();
		СтрокаТабличнойЧастиХозяйственныеОперации.ХозяйственнаяОперация = ХозОперация;
		
		НовыйЭлемент.Записать();
		СтатьяДДСПоУмолчанию = НовыйЭлемент.Ссылка;
	КонецЕсли;
	Если НЕ СтатьяДДСВШапкеЗаполнена Тогда
		Объект.СтатьяДвиженияДенежныхСредств = СтатьяДДСПоУмолчанию;
	КонецЕсли;
		
	Для Каждого СтрокаРасшифровка Из Объект.РасшифровкаПлатежа Цикл
		Если НЕ ЗначениеЗаполнено(СтрокаРасшифровка.СтатьяДвиженияДенежныхСредств) Тогда
			СтрокаРасшифровка.СтатьяДвиженияДенежныхСредств = СтатьяДДСПоУмолчанию;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры
