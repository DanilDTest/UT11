Процедура ЗаполнитьХарактеристикуСтрокиТЧ(КомпонентыОбмена, Строка, ПоДаннымЗапроса = Истина)
	Если ПоДаннымЗапроса Тогда
		Если ЗначениеЗаполнено(Строка.ХарактеристикаСсылка) Тогда
			Если Строка.ХарактеристикаСоответствуетФормату Тогда
				Строка.Характеристика = Строка.ХарактеристикаСсылка;
			Иначе
				Характеристика = Новый Структура();
				Характеристика.Вставить("Наименование",       Строка.ХарактеристикаНаименование);
				Характеристика.Вставить("НаименованиеПолное", Строка.ХарактеристикаНаименованиеПолное);
				Характеристика.Вставить("Владелец",           Строка.ХарактеристикаВладелец);
				
				Строка.Характеристика = Новый Структура("Значение, ИмяПКО",
					Характеристика, "Справочник_ХарактеристикиНоменклатуры_ИзСтруктуры");
					
				ПравилоОбработки = КомпонентыОбмена.ПравилаОбработкиДанных.Найти("Справочник_ХарактеристикиНоменклатуры_Отправка", "Имя");
				Если Не ПравилоОбработки = Неопределено Тогда
					RM_ОбменДаннымиXDTOСервер.ВыгрузкаОбъектаВыборки(КомпонентыОбмена, Характеристика, ПравилоОбработки);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	Иначе
		Если ЗначениеЗаполнено(Строка.ХарактеристикаСсылка) И ЗначениеЗаполнено(Строка.Номенклатура) Тогда
			Характеристика = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Строка.ХарактеристикаСсылка,
				"Владелец, Наименование, НаименованиеПолное");
				
			ВидНоменклатуры = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Строка.Номенклатура, "ВидНоменклатуры");
			
			Если Характеристика.Владелец = Строка.Номенклатура
				Или (Характеристика.Владелец = ВидНоменклатуры
				И ВерсияФорматаЧислом(КомпонентыОбмена.ВерсияФорматаОбмена) >= ВерсияФорматаЧислом("1.7")) Тогда
				Строка.Характеристика = Строка.ХарактеристикаСсылка;	
			Иначе	
				Если ВерсияФорматаЧислом(КомпонентыОбмена.ВерсияФорматаОбмена) = ВерсияФорматаЧислом("1.6") Тогда
					Характеристика.Вставить("Владелец", Строка.Номенклатура);
				Иначе
			        Характеристика.Вставить("Владелец", ВидНоменклатуры);
			    КонецЕсли;
			        
				Строка.Характеристика = Новый Структура("Значение, ИмяПКО",
					Характеристика, "Справочник_ХарактеристикиНоменклатуры_ИзСтруктуры");
					
				ПравилоОбработки = КомпонентыОбмена.ПравилаОбработкиДанных.Найти(
					"Справочник_ХарактеристикиНоменклатуры_Отправка", "Имя");
				Если Не ПравилоОбработки = Неопределено Тогда
					RM_ОбменДаннымиXDTOСервер.ВыгрузкаОбъектаВыборки(КомпонентыОбмена, Характеристика, ПравилоОбработки);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры
