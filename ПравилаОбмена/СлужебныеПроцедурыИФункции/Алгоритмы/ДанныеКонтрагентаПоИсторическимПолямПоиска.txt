Функция ДанныеКонтрагентаПоИсторическимПолямПоиска(ДанныеXDTO)
	КлючевыеСвойства = Неопределено;
	Если Не ДанныеXDTO.Свойство("КлючевыеСвойства", КлючевыеСвойства) Тогда
		КлючевыеСвойства = ДанныеXDTO;
	КонецЕсли;
	
	// ИНН+КПП
	Если (КлючевыеСвойства.Свойство("ИНН") И ЗначениеЗаполнено(КлючевыеСвойства.ИНН))
		И (КлючевыеСвойства.Свойство("КПП") И ЗначениеЗаполнено(КлючевыеСвойства.КПП)) Тогда
		Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Контрагенты.КПП КАК КПП
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Контрагенты.ИсторияКПП КАК КонтрагентыИсторияКПП
		|		ПО (КонтрагентыИсторияКПП.Ссылка = Контрагенты.Ссылка)
		|ГДЕ
		|	Контрагенты.ИНН = &ИНН
		|	И КонтрагентыИсторияКПП.КПП = &КПП
		|	И КонтрагентыИсторияКПП.КПП <> Контрагенты.КПП");
		Запрос.УстановитьПараметр("ИНН", КлючевыеСвойства.ИНН);
		Запрос.УстановитьПараметр("КПП", КлючевыеСвойства.КПП);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Результат = Новый Структура("КПП");
			ЗаполнитьЗначенияСвойств(Результат, Выборка);
			
			Возврат Результат;
		КонецЕсли;
	КонецЕсли;
	
	// НаименованиеПолное
	Если КлючевыеСвойства.Свойство("НаименованиеПолное")
		И ЗначениеЗаполнено(КлючевыеСвойства.НаименованиеПолное) Тогда
		Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Контрагенты.НаименованиеПолное КАК НаименованиеПолное
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Контрагенты.ИсторияНаименований КАК КонтрагентыИсторияНаименований
		|		ПО (КонтрагентыИсторияНаименований.Ссылка = Контрагенты.Ссылка)
		|ГДЕ
		|	КонтрагентыИсторияНаименований.НаименованиеПолное = &НаименованиеПолное
		|	И КонтрагентыИсторияНаименований.НаименованиеПолное <> Контрагенты.НаименованиеПолное");
		Запрос.УстановитьПараметр("НаименованиеПолное", КлючевыеСвойства.НаименованиеПолное);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Результат = Новый Структура("НаименованиеПолное");
			ЗаполнитьЗначенияСвойств(Результат, Выборка);
			
			Возврат Результат;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Неопределено;
КонецФункции
