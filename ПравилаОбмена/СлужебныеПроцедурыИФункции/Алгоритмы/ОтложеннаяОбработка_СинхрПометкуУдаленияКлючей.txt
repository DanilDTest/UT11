Процедура ОтложеннаяОбработка_СинхрПометкуУдаленияКлючей(КомпонентыОбмена)
	// Синхронизация пометки удаления объекта и ключей имеет смысл
	// только при получении удаления объекта, т.к. при получении непосредственно
	// объекта, синхронизация пометки осуществляется "ПриЗаписи" объекта без режима загрузки.
	// Однако при получении удаления объекта никакого события не возникает (ПриКонвертацииДанных, ПередЗаписью),
	// поэтому синхронизация пометки осуществляется для объектов метаданных в целом, которыми происходит обмен.
	
	МассивИменСправочниковКлючей = Новый Массив;
	МассивИменСправочниковКлючей.Добавить("Справочник.КлючиАналитикиУчетаНоменклатуры");
	МассивИменСправочниковКлючей.Добавить("Справочник.КлючиАналитикиУчетаПоПартнерам");
	МассивИменСправочниковКлючей.Добавить("Справочник.КлючиАналитикиУчетаПартий");
	МассивИменСправочниковКлючей.Добавить("Справочник.КлючиАналитикиУчетаНаборов");
	МассивИменСправочниковКлючей.Добавить("Справочник.ВидыЗапасов");
	
	Для Каждого ПолноеИмяСправочникаКлючей Из МассивИменСправочниковКлючей Цикл
	
		Запрос = Новый Запрос;
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	Таблица.Ссылка КАК Ссылка,
		|	&ТекстПометкаУдаления КАК ПометкаУдаления
		|ИЗ
		|	&ПолноеИмяСправочника КАК Таблица
		|ГДЕ
		|	НЕ Таблица.ПометкаУдаления = (&ТекстПометкаУдаления)";
		
		МетаданныеСправочникаКлючей = Метаданные.НайтиПоПолномуИмени(ПолноеИмяСправочникаКлючей);
		
		РеквизитыСсылочногоТипа = Новый Массив;
		Для Каждого Реквизит Из МетаданныеСправочникаКлючей.Реквизиты Цикл
			
			Для Каждого ТипРеквизита Из Реквизит.Тип.Типы() Цикл
				
				Если ОбщегоНазначения.ЭтоСсылка(ТипРеквизита)
					И ОбщегоНазначения.ЭтоСправочник(Метаданные.НайтиПоТипу(ТипРеквизита)) Тогда
					РеквизитыСсылочногоТипа.Добавить("Таблица."+ Реквизит.Имя + ".ПометкаУдаления");
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
		КонецЦикла;
		
		ТекстПометкаУдаления = СтрСоединить(РеквизитыСсылочногоТипа, " ИЛИ ");
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПолноеИмяСправочника", ПолноеИмяСправочникаКлючей);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстПометкаУдаления", ТекстПометкаУдаления);
		
		Запрос.Текст = ТекстЗапроса;
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
		
			Объект = Выборка.Ссылка.ПолучитьОбъект();
			ОбменДаннымиСервер.УстановитьОбменДаннымиЗагрузка(Объект);
			Объект.УстановитьПометкуУдаления(Выборка.ПометкаУдаления);
	
		КонецЦикла;
		
	КонецЦикла;
КонецПроцедуры
