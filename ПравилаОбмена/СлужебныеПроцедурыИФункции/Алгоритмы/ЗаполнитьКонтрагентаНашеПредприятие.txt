Процедура ЗаполнитьКонтрагентаНашеПредприятие(ПолученныеДанные, ПараметрыКонвертации, ЗаполнятьБанковскийСчет = Ложь)
	Если ЗначениеЗаполнено(ПолученныеДанные.Контрагент) Тогда
		Возврат;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(ПараметрыКонвертации.КонтрагентНашеПредприятие) Тогда
		Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
		  | Контрагенты.Ссылка КАК КонтрагентНашеПредприятие
		  |ИЗ
		  | Справочник.Контрагенты КАК Контрагенты
		  |ГДЕ
		  | Контрагенты.Партнер = ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)");
		  
		Выборка = Запрос.Выполнить().Выбрать();
		Если НЕ Выборка.Следующий() Тогда
			Возврат;
		КонецЕсли;
		 	
		ПараметрыКонвертации.КонтрагентНашеПредприятие = Выборка.КонтрагентНашеПредприятие;
	КонецЕсли;
	
	ПолученныеДанные.Контрагент = ПараметрыКонвертации.КонтрагентНашеПредприятие;
	
	Если ЗаполнятьБанковскийСчет Тогда
		Если НЕ ЗначениеЗаполнено(ПараметрыКонвертации.КонтрагентНашеПредприятиеБанковскийСчет) Тогда
			Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
		  	| БанкСчета.Ссылка КАК БанковскийСчет
		  	|ИЗ
		  	| Справочник.БанковскиеСчетаКонтрагентов КАК БанкСчета
		  	|ГДЕ
		 	| БанкСчета.Владелец = &НашеПредприятие И БанкСчета.ВалютаДенежныхСредств = &Валюта");
			Запрос.УстановитьПараметр("НашеПредприятие", ПараметрыКонвертации.КонтрагентНашеПредприятие);
			Запрос.УстановитьПараметр("Валюта", ПолученныеДанные.Валюта);
			Выборка = Запрос.Выполнить().Выбрать();
			Если НЕ Выборка.Следующий() Тогда
				Возврат;
			КонецЕсли;
			ПараметрыКонвертации.КонтрагентНашеПредприятиеБанковскийСчет = Выборка.БанковскийСчет;
		КонецЕсли;
		ПолученныеДанные.БанковскийСчетКонтрагента = ПараметрыКонвертации.КонтрагентНашеПредприятиеБанковскийСчет;
	КонецЕсли;
КонецПроцедуры
