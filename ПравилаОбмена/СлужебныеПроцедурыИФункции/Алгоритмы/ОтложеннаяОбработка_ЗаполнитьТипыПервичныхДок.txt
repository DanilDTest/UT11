Процедура ОтложеннаяОбработка_ЗаполнитьТипыПервичныхДок(КомпонентыОбмена)
	ПервичныеДокументы = КомпонентыОбмена.ПараметрыКонвертации.ПервичныеДокументы;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПервичныеДокументы", ПервичныеДокументы);
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПервичныеДокументы.ПервичныйДокумент КАК Ссылка,
		|	ПервичныеДокументы.ТипПервичногоДокумента КАК ТипПервичногоДокумента
		|ПОМЕСТИТЬ втПервичныеДокументы
		|ИЗ
		|	&ПервичныеДокументы КАК ПервичныеДокументы
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПервичныйДокумент.Ссылка КАК ПервичныйДокумент,
		|	втПервичныеДокументы.ТипПервичногоДокумента КАК ТипПервичногоДокумента
		|ИЗ
		|	втПервичныеДокументы КАК втПервичныеДокументы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПервичныйДокумент КАК ПервичныйДокумент
		|		ПО втПервичныеДокументы.Ссылка = ПервичныйДокумент.Ссылка
		|ГДЕ
		|	ПервичныйДокумент.ТипПервичногоДокумента = ЗНАЧЕНИЕ(Перечисление.ТипыПервичныхДокументов.ПустаяСсылка)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ПервичныйДокументОбъект = Выборка.ПервичныйДокумент.ПолучитьОбъект();
		ПервичныйДокументОбъект.ТипПервичногоДокумента = Выборка.ТипПервичногоДокумента;
		
		ПервичныйДокументОбъект.ОбменДанными.Загрузка = Истина;
		
		Попытка
			ПервичныйДокументОбъект.Записать();
		Исключение
			ЗаписатьОшибкуВЖурналРегистрации(Выборка.ПервичныйДокумент,
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()), КомпонентыОбмена);
		КонецПопытки;
	КонецЦикла;
КонецПроцедуры
