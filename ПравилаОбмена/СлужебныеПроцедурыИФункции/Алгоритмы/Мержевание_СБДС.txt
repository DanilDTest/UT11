Процедура Мержевание_СБДС(ПолученныеДанные, ДанныеИБ, КонвертацияСвойств, КомпонентыОбмена)
	Если ДанныеИБ = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеИБ_КонтрагентДоИзменения = ДанныеИБ.Контрагент;
	
	#Область Шапка
	
	// Очередность платежа.
	Если Не ЗначениеЗаполнено(ПолученныеДанные.ОчередностьПлатежа) Тогда
		ПолученныеДанные.ОчередностьПлатежа = ДанныеИБ.ОчередностьПлатежа;
	КонецЕсли;
	
	// Заполнение контрагента, если он не заполнен.
	Если Не ЗначениеЗаполнено(ПолученныеДанные.Контрагент) Тогда
		Если ЗначениеЗаполнено(ДанныеИБ.Контрагент) Тогда
			ПолученныеДанные.Контрагент = ДанныеИБ.Контрагент;
		Иначе
			ЗаполнитьКонтрагентаНашеПредприятие(ПолученныеДанные, КомпонентыОбмена.ПараметрыКонвертации);
		КонецЕсли;
	КонецЕсли;
	
	// При операциях "интеркампани", некоторые реквизиты объекта ИБ имеют больший приоритет.
	Если ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОплатаДенежныхСредствВДругуюОрганизацию Тогда
		ЗаполнитьЗначенияСвойств(ПолученныеДанные, ДанныеИБ,
			"Контрагент,БанковскийСчетКонтрагента,ХозяйственнаяОперация");
	КонецЕсли;
	
	// Заменяем свойства, которые получили из XDTO.
	ЗаполнитьСвойстваШапкиОбъекта(КонвертацияСвойств, ПолученныеДанные, ДанныеИБ);
	
	#КонецОбласти
	
	#Область РасшифровкаПлатежа
	
	// Мержевание расшифровки платежа.
	РасшифровкаПлатежаИсточник = ПолученныеДанные.РасшифровкаПлатежа;
	РасшифровкаПлатежаПриемник = ДанныеИБ.РасшифровкаПлатежа;
	
	Если РасшифровкаПлатежаИсточник.Количество() = РасшифровкаПлатежаПриемник.Количество() Тогда
		
		Для ИндексСтроки = 0 По РасшифровкаПлатежаИсточник.Количество() - 1 Цикл
			
			СтрокаИсточник = РасшифровкаПлатежаИсточник[ИндексСтроки];
			СтрокаПриемник = РасшифровкаПлатежаПриемник[ИндексСтроки];
			
			ЗаполнитьЗначенияСвойств(СтрокаПриемник, СтрокаИсточник,
				"Сумма,
				|СуммаВзаиморасчетов,
				|ВалютаВзаиморасчетов");
			
			Если ЗначениеЗаполнено(СтрокаИсточник.Заказ) Тогда
				СтрокаПриемник.Заказ = СтрокаИсточник.Заказ;
			КонецЕсли;
		
			Если ЗначениеЗаполнено(СтрокаИсточник.СтатьяРасходов) Тогда
				СтрокаПриемник.СтатьяРасходов = СтрокаИсточник.СтатьяРасходов;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(СтрокаИсточник.СтатьяДвиженияДенежныхСредств) Тогда
				СтрокаПриемник.СтатьяДвиженияДенежныхСредств = СтрокаИсточник.СтатьяДвиженияДенежныхСредств;
			КонецЕсли;
			
			// Изменение контрагента обновляет зависимую от него аналитику.
			Если ДанныеИБ.Контрагент <> ДанныеИБ_КонтрагентДоИзменения Тогда
				ЗаполнитьЗначенияСвойств(СтрокаПриемник, СтрокаИсточник, "Партнер, Заказ");
			КонецЕсли;
			
		КонецЦикла;
		
	Иначе
		ДанныеИБ.РасшифровкаПлатежа.Загрузить(ПолученныеДанные.РасшифровкаПлатежа.Выгрузить());
	КонецЕсли;
	
	// Синхронизация суммы в расшифровке и в шапке.
	Если ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПеречислениеДенежныхСредствНаДругойСчет Тогда
		
		Если ДанныеИБ.РасшифровкаПлатежа.Количество() = 1 Тогда
			ДанныеИБ.РасшифровкаПлатежа[0].Сумма = ДанныеИБ.СуммаДокумента;
		КонецЕсли; 
	
	КонецЕсли;
	
	#КонецОбласти
КонецПроцедуры
