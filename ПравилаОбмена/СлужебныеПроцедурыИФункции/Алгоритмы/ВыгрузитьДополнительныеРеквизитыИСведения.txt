Процедура ВыгрузитьДополнительныеРеквизитыИСведения(КомпонентыОбмена, ДанныеИБ, ДанныеXDTO, ЕстьДопРеквизиты = Ложь)
	Если СтандартныеПодсистемыСервер.ЭтоБазоваяВерсияКонфигурации() Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеРеквизиты = Новый ТаблицаЗначений;
	ДополнительныеРеквизиты.Колонки.Добавить("Свойство");
	ДополнительныеРеквизиты.Колонки.Добавить("ЗначениеСвойства");
	
	Если ЕстьДопРеквизиты Тогда
		ВыгрузкаДопРеквизитовИзТаблицыЗначений(ДанныеИБ.ДополнительныеРеквизиты, ДополнительныеРеквизиты);
	КонецЕсли;
	
	Запрос = Новый Запрос("ВЫБРАТЬ
		|	Свойство КАК Свойство,
		|	Значение КАК Значение
		|ИЗ
		|	РегистрСведений.ДополнительныеСведения
		|ГДЕ Объект = &Ссылка");
	
	Запрос.УстановитьПараметр("Ссылка", ДанныеИБ.Ссылка);
	ДополнительныеСведенияИзДанныхИБ = Запрос.Выполнить().Выгрузить();
	
	// Дополнительные сведения типа Булево хранятся в регистре сведений "ДополнительныеСведения"
	// только со значением Истина. В связи с этим значения Ложь необходимо выгружать принудительно.
	Ссылка               = ДанныеИБ.Ссылка;
	МетаданныеОбъекта    = Ссылка.Метаданные();
	ИмяОбъектаМетаданных = МетаданныеОбъекта.Имя;
	ВидОбъектаМетаданных = ОбщегоНазначения.ВидОбъектаПоСсылке(Ссылка);
	
	ВладелецДопСведенийЯвляетсяГруппой = Ложь;
	
	Если ВидОбъектаМетаданных = "Справочник"
		Или ВидОбъектаМетаданных = "ПланВидовХарактеристик" Тогда
		ВладелецДопСведенийЯвляетсяГруппой = ОбщегоНазначения.ОбъектЯвляетсяГруппой(Ссылка);
	КонецЕсли;
	
	ДополнительныеСведенияСЗначениемБулево = ПолучитьТЗДопСведенияБулево(КомпонентыОбмена);
	
	Если Не ВладелецДопСведенийЯвляетсяГруппой Тогда
		
		ИмяЭлемента       = ВидОбъектаМетаданных + "_" + ИмяОбъектаМетаданных;
		СтрокаДополнительногоСвойстваБулево = ДополнительныеСведенияСЗначениемБулево.Найти(ИмяЭлемента, "ИмяПредопределенныхДанных");
		
		Если Не СтрокаДополнительногоСвойстваБулево = Неопределено Тогда
			
			Если ДополнительныеСведенияИзДанныхИБ.Найти(СтрокаДополнительногоСвойстваБулево.Свойство, "Свойство") = Неопределено Тогда
				СтрокаДополнительныхРеквизитов = ДополнительныеСведенияИзДанныхИБ.Добавить();
				СтрокаДополнительныхРеквизитов.Свойство = СтрокаДополнительногоСвойстваБулево.Свойство;
				СтрокаДополнительныхРеквизитов.Значение = Ложь;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ВыгрузкаДопРеквизитовИзТаблицыЗначений(ДополнительныеСведенияИзДанныхИБ, ДополнительныеРеквизиты);
	
	Если ДополнительныеРеквизиты.Количество() > 0 Тогда
		ДанныеXDTO.Вставить("ДополнительныеРеквизиты", ДополнительныеРеквизиты);
	КонецЕсли;
КонецПроцедуры
