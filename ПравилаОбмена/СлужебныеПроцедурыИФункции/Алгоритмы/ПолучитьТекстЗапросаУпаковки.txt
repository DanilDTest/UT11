Функция ПолучитьТекстЗапросаУпаковки(КомпонентыОбмена, НаименованиеИсточника, КоличественныеПоказатели = Истина)
	Если ВерсияФорматаЧислом(КомпонентыОбмена.ВерсияФорматаОбмена) < ВерсияФорматаЧислом("1.6") Тогда
		ТекстЗапроса = 
			"	Ложь КАК ВыгружатьУпаковки,
			|	&Источник.Упаковка КАК УпаковкаСсылка";
			
		ТекстЗапросаКоличественныеПоказатели = "
			|	,&Источник.Количество КАК КоличествоУпаковок";
	Иначе	
		ТекстЗапроса = 
			"	НЕ ЕСТЬNULL(&Источник.Упаковка, ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)) = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) КАК ВыгружатьУпаковки,
			|	&Источник.Упаковка.Владелец КАК УпаковкаВладелец,
			|	&Источник.Упаковка.ТипИзмеряемойВеличины КАК УпаковкаТипИзмеряемойВеличины,
			|	&Источник.Упаковка КАК УпаковкаСсылка,
			|	ЕСТЬNULL(&Источник.Упаковка.Владелец, ЗНАЧЕНИЕ(Справочник.НаборыУпаковок.ПустаяСсылка)) ССЫЛКА Справочник.Номенклатура КАК УпаковкаНоменклатуры,
			|	&Источник.Упаковка.Наименование КАК УпаковкаНаименование,
			|	&Источник.Упаковка.ЕдиницаИзмерения КАК УпаковкаЕдиницаИзмерения,
			|	&Источник.Упаковка.Высота КАК УпаковкаВысота,
			|	&Источник.Упаковка.Глубина КАК УпаковкаГлубина,
			|	&Источник.Упаковка.Объем КАК УпаковкаОбъем,
			|	&Источник.Упаковка.Ширина КАК УпаковкаШирина,
			|	&Источник.Упаковка.Знаменатель КАК УпаковкаЗнаменатель,
			|	&Источник.Упаковка.Числитель КАК УпаковкаЧислитель";
		
		ТекстЗапросаКоличественныеПоказатели = "		
			|	,&Источник.КоличествоУпаковок КАК КоличествоУпаковок";
	КонецЕсли;
	
	Если КоличественныеПоказатели Тогда
		ТекстЗапроса = ТекстЗапроса + ТекстЗапросаКоличественныеПоказатели;
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Источник", НаименованиеИсточника);
	
	Возврат ТекстЗапроса;
КонецФункции
