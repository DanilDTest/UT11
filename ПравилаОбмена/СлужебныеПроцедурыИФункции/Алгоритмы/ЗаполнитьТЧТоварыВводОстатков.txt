Процедура ЗаполнитьТЧТоварыВводОстатков(ПолученныеДанные, ДанныеТЧXDTO, КомпонентыОбмена, ПравилаЗаполнения = Неопределено)
	Если ПравилаЗаполнения = Неопределено Тогда
		ПравилаЗаполнения = Новый Соответствие;
		ПравилаЗаполнения.Вставить("СуммаБезНДС",         "СуммаБезНДС");
		ПравилаЗаполнения.Вставить("СуммаНДС",            "СуммаНДС");
		ПравилаЗаполнения.Вставить("Номенклатура",        "Номенклатура");
		ПравилаЗаполнения.Вставить("Количество",          "Количество");
		ПравилаЗаполнения.Вставить("СтавкаНДС",           "СтавкаНДС");
		ПравилаЗаполнения.Вставить("ИдентификаторСтроки", "ИдентификаторСтроки");
		Если ЗначениеФО("ИспользоватьХарактеристикиНоменклатуры", КомпонентыОбмена) Тогда
			ПравилаЗаполнения.Вставить("Характеристика",     "Характеристика");
		КонецЕсли;
		Если ЗначениеФО("ИспользоватьУпаковкиНоменклатуры", КомпонентыОбмена) Тогда
			ПравилаЗаполнения.Вставить("Упаковка",           "Упаковка");
			ПравилаЗаполнения.Вставить("КоличествоУпаковок", "КоличествоУпаковок");
		КонецЕсли;
		Если ЗначениеФО("ИспользоватьСерииНоменклатуры", КомпонентыОбмена) Тогда
			ПравилаЗаполнения.Вставить("Серия",              "Серия");
		КонецЕсли;
	КонецЕсли;
	
	МассивСтрокТЧ = Новый Массив;
	
	Для Каждого Строка Из ДанныеТЧXDTO Цикл
	
		СтруктураДанныхСтроки = ДанныеКоллекцииВВидеСтруктуры(Строка, ПравилаЗаполнения);
		
		Если ЗначениеЗаполнено(Строка.НомерГТД) Тогда
			СтруктураДанныхСтроки.Вставить("НомерГТД", Новый Структура("Код, СтранаПроисхождения", Строка.НомерГТД, Строка.СтранаПроисхождения));
		КонецЕсли;
		
		//Обход ограничения формата
		Если ЗначениеЗаполнено(Строка.СуммаБезНДС)
			и Строка.СуммаБезНДС = 0.01 * ?(Строка.Количество > 0, -1, 1) Тогда
			Строка.СуммаБезНДС = 0;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Строка.СуммаНДС) Тогда
			Строка.СуммаНДС = 0;
		КонецЕсли;
		
		Если Строка.Количество <> 0 Тогда
			СтруктураДанныхСтроки.Вставить("Цена", Окр(Строка.СуммаБезНДС / Строка.Количество, 2));
		КонецЕсли;
		
		СтруктураДанныхСтроки.Вставить("Сумма",     		 Строка.СуммаБезНДС);
		СтруктураДанныхСтроки.Вставить("СуммаРегл",     	 Строка.СуммаБезНДС);
		СтруктураДанныхСтроки.Вставить("СуммаСНДС",     	 Строка.СуммаБезНДС + Строка.СуммаНДС);
		СтруктураДанныхСтроки.Вставить("НДСРегл",     		 Строка.СуммаНДС);
		Если СтруктураДанныхСтроки.Свойство("КоличествоУпаковок") Тогда
		    Если СтруктураДанныхСтроки.Свойство("Упаковка") И СтруктураДанныхСтроки.Свойство("Сумма") Тогда
				СтруктураДанныхСтроки.Вставить("Цена", СтруктураДанныхСтроки.Сумма / СтруктураДанныхСтроки.КоличествоУпаковок);
			КонецЕсли;
		Иначе
			СтруктураДанныхСтроки.Вставить("КоличествоУпаковок", Строка.Количество);
		КонецЕсли;
		
		МассивСтрокТЧ.Добавить(СтруктураДанныхСтроки);
		
	КонецЦикла;
	
	ПолученныеДанные.ДополнительныеСвойства.Вставить("Товары", МассивСтрокТЧ);
КонецПроцедуры
