Функция ПолучитьДанныеКонтрагентаПоБанкСчетуОрганизации(БанковскийСчетОрганизации, КомпонентыОбмена)
	СтруктураВозврат = Новый Структура("Контрагент, БанковскийСчетКонтрагента");
	
	Если Не ЗначениеЗаполнено(БанковскийСчетОрганизации) Тогда
		Возврат СтруктураВозврат; 
	КонецЕсли;
	
	// Поиск контрагента, соответствующего Организации.
	Контрагент = КонтрагентИзОрганизации(БанковскийСчетОрганизации.Владелец, КомпонентыОбмена);
	
	СтруктураВозврат.Контрагент = Контрагент;
	
	// Заполнение банковского счета.
	Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
		|	БанковскиеСчетаКонтрагентов.Ссылка
		|ИЗ
		|	Справочник.БанковскиеСчетаКонтрагентов КАК БанковскиеСчетаКонтрагентов
		|ГДЕ
		|	БанковскиеСчетаКонтрагентов.Владелец = &Владелец
		|	И БанковскиеСчетаКонтрагентов.НомерСчета = &НомерСчета");
	Запрос.УстановитьПараметр("Владелец",   Контрагент);
	Запрос.УстановитьПараметр("НомерСчета", БанковскийСчетОрганизации.НомерСчета);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		СтруктураВозврат.БанковскийСчетКонтрагента = Выборка.Ссылка;
	Иначе
		
		// Создаем новый банковский счет.
		БанкСчетОбъект = Справочники.БанковскиеСчетаКонтрагентов.СоздатьЭлемент();
		БанкСчетОбъект.Владелец = Контрагент;
		ЗаполнитьЗначенияСвойств(БанкСчетОбъект, БанковскийСчетОрганизации,,"Владелец, Родитель");
		БанкСчетОбъект.Записать();
		
		КомпонентыОбмена.ПараметрыКонвертации.СозданныеПриВыгрузкеОбъекты.Добавить(БанкСчетОбъект.Ссылка);
		
		СтруктураВозврат.БанковскийСчетКонтрагента = БанкСчетОбъект.Ссылка;
		
	КонецЕсли;
	
	Возврат СтруктураВозврат;
КонецФункции
