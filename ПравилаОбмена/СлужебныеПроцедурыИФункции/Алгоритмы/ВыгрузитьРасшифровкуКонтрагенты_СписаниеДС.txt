Процедура ВыгрузитьРасшифровкуКонтрагенты_СписаниеДС(ДанныеИБ, ДанныеXDTO, КонтрагентСсылка, КомпонентыОбмена)
	ВалютаРеглУчета = ВалютаРегламентированногоУчета(КомпонентыОбмена);
	
	Запрос = Новый Запрос("ВЫБРАТЬ
		|	РасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств                       КАК СтатьяДДС,
		|	РасшифровкаПлатежа.Сумма                                               КАК Сумма,
		|	РасшифровкаПлатежа.СуммаНДС                                            КАК СуммаНДС,
		|	РасшифровкаПлатежа.СтавкаНДС                                           КАК СтавкаНДС,
		|	РасшифровкаПлатежа.СуммаВзаиморасчетов                                 КАК СуммаВзаиморасчетов,
		|	РасшифровкаПлатежа.ДоговорКредитаДепозита                              КАК ДоговорКредитаДепозита,
		|	ВЫБОР
		|		КОГДА РасшифровкаПлатежа.Заказ ССЫЛКА Справочник.ДоговорыКонтрагентов
		|			ТОГДА NULL
		|		ИНАЧЕ РасшифровкаПлатежа.Заказ
		|	КОНЕЦ                                                                  КАК Заказ,
		|	РасшифровкаПлатежа.Партнер                                             КАК Партнер,
		|	ВЫРАЗИТЬ(РасшифровкаПлатежа.Заказ КАК Справочник.ДоговорыКонтрагентов) КАК Договор,
		|	ЕСТЬNULL(КурсыВалют.Курс, 1)                                           КАК КурсВзаиморасчетов,
		|	ЕСТЬNULL(КурсыВалют.Кратность, 1)                                      КАК КратностьВзаиморасчетов,
		|	РасшифровкаПлатежа.ВалютаВзаиморасчетов                                КАК ВалютаВзаиморасчетов,
		|	ВЫБОР
		|		КОГДА РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговораКредитовИДепозитов.КредитВБанке)
		|			ИЛИ РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговораКредитовИДепозитов.КредитСотруднику)
		|			ТОГДА ВЫБОР
		|					КОГДА РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипСрочности = ЗНАЧЕНИЕ(Перечисление.ТипыСрочностиКредитовИДепозитов.Краткосрочный)
		|						ТОГДА ВЫБОР
		|							КОГДА РасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств = РасшифровкаПлатежа.ДоговорКредитаДепозита.СтатьяДДСОсновногоДолга
		|								ТОГДА ""КраткосрочныеКредиты""
		|							КОГДА РасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств = РасшифровкаПлатежа.ДоговорКредитаДепозита.СтатьяДДСПроцентов
		|								ИЛИ РасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств = РасшифровкаПлатежа.ДоговорКредитаДепозита.СтатьяДДСКомиссии
		|								ТОГДА ""ПроцентыПоКраткосрочнымКредитам""
		|						КОНЕЦ
		|					КОГДА РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипСрочности = ЗНАЧЕНИЕ(Перечисление.ТипыСрочностиКредитовИДепозитов.Долгосрочный)
		|						ТОГДА ВЫБОР
		|							КОГДА РасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств = РасшифровкаПлатежа.ДоговорКредитаДепозита.СтатьяДДСОсновногоДолга
		|								ТОГДА ""ДолгосрочныеКредиты""
		|							КОГДА РасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств = РасшифровкаПлатежа.ДоговорКредитаДепозита.СтатьяДДСПроцентов
		|								ИЛИ РасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств = РасшифровкаПлатежа.ДоговорКредитаДепозита.СтатьяДДСКомиссии
		|								ТОГДА ""ПроцентыПоДолгосрочнымКредитам""
		|						КОНЕЦ
		|				КОНЕЦ
		|		КОГДА РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговораКредитовИДепозитов.ВнутреннийЗайм)
		|				ИЛИ РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговораКредитовИДепозитов.ВнешнийЗайм)
		|				ИЛИ РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговораКредитовИДепозитов.ВкладСотрудника)
		|			ТОГДА ВЫБОР
		|					КОГДА РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипСрочности = ЗНАЧЕНИЕ(Перечисление.ТипыСрочностиКредитовИДепозитов.Краткосрочный)
		|						ТОГДА ВЫБОР
		|							КОГДА РасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств = РасшифровкаПлатежа.ДоговорКредитаДепозита.СтатьяДДСОсновногоДолга
		|								ТОГДА ""КраткосрочныеЗаймы""
		|							КОГДА РасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств = РасшифровкаПлатежа.ДоговорКредитаДепозита.СтатьяДДСПроцентов
		|								ИЛИ РасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств = РасшифровкаПлатежа.ДоговорКредитаДепозита.СтатьяДДСКомиссии
		|								ТОГДА ""ПроцентыПоКраткосрочнымЗаймам""
		|						КОНЕЦ
		|					КОГДА РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипСрочности = ЗНАЧЕНИЕ(Перечисление.ТипыСрочностиКредитовИДепозитов.Долгосрочный)
		|						ТОГДА ВЫБОР
		|							КОГДА РасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств = РасшифровкаПлатежа.ДоговорКредитаДепозита.СтатьяДДСОсновногоДолга
		|								ТОГДА ""ДолгосрочныеЗаймы""
		|							КОГДА РасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств = РасшифровкаПлатежа.ДоговорКредитаДепозита.СтатьяДДСПроцентов
		|								ИЛИ РасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств = РасшифровкаПлатежа.ДоговорКредитаДепозита.СтатьяДДСКомиссии
		|								ТОГДА ""ПроцентыПоДолгосрочнымЗаймам""
		|						КОНЕЦ
		|				КОНЕЦ
		|		ИНАЧЕ NULL
		|	КОНЕЦ КАК ВидРасчетовРасширенный,
		|	ВЫБОР
		|		КОГДА РасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств.КорреспондирующийСчет <> """"
		|				И &ЭтоУТ
		|			ТОГДА РасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств.КорреспондирующийСчет
		|		ИНАЧЕ ВЫБОР
		|				КОГДА РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговораКредитовИДепозитов.КредитСотруднику)
		|						ТОГДА ""58.03""
		|				КОГДА (РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговораКредитовИДепозитов.ВнутреннийЗайм)
		|					ИЛИ РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговораКредитовИДепозитов.ВнешнийЗайм)
		|					ИЛИ РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговораКредитовИДепозитов.ВкладСотрудника))
		|					И РасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств = РасшифровкаПлатежа.ДоговорКредитаДепозита.СтатьяДДСПоступленияВыдачи
		|						ТОГДА ""58.03""
		|				КОГДА (РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговораКредитовИДепозитов.ВнутреннийЗайм)
		|						ИЛИ РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговораКредитовИДепозитов.ВнешнийЗайм)
		|						ИЛИ РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговораКредитовИДепозитов.ВкладСотрудника))
		|					ТОГДА ВЫБОР
		|							КОГДА РасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств = РасшифровкаПлатежа.ДоговорКредитаДепозита.СтатьяДДСОсновногоДолга
		|								ТОГДА ВЫБОР
		|										КОГДА РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипСрочности = ЗНАЧЕНИЕ(Перечисление.ТипыСрочностиКредитовИДепозитов.Краткосрочный)
		|											ТОГДА ВЫБОР
		|													КОГДА РасшифровкаПлатежа.ДоговорКредитаДепозита.ВалютаВзаиморасчетов = &ВалютаРеглУчета
		|														ТОГДА ""66.03""
		|													ИНАЧЕ ""66.23""
		|												КОНЕЦ
		|										КОГДА РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипСрочности = ЗНАЧЕНИЕ(Перечисление.ТипыСрочностиКредитовИДепозитов.Долгосрочный)
		|											ТОГДА ВЫБОР
		|													КОГДА РасшифровкаПлатежа.ДоговорКредитаДепозита.ВалютаВзаиморасчетов = &ВалютаРеглУчета
		|														ТОГДА ""67.03""
		|													ИНАЧЕ ""67.23""
		|												КОНЕЦ
		|								КОНЕЦ
		|							КОГДА РасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств = РасшифровкаПлатежа.ДоговорКредитаДепозита.СтатьяДДСПроцентов
		|									ИЛИ РасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств = РасшифровкаПлатежа.ДоговорКредитаДепозита.СтатьяДДСКомиссии
		|								ТОГДА ВЫБОР
		|										КОГДА РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипСрочности = ЗНАЧЕНИЕ(Перечисление.ТипыСрочностиКредитовИДепозитов.Краткосрочный)
		|											ТОГДА ВЫБОР
		|													КОГДА РасшифровкаПлатежа.ДоговорКредитаДепозита.ВалютаВзаиморасчетов = &ВалютаРеглУчета
		|														ТОГДА ""66.04""
		|													ИНАЧЕ ""66.43""
		|												КОНЕЦ
		|										КОГДА РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипСрочности = ЗНАЧЕНИЕ(Перечисление.ТипыСрочностиКредитовИДепозитов.Долгосрочный)
		|											ТОГДА ВЫБОР
		|													КОГДА РасшифровкаПлатежа.ДоговорКредитаДепозита.ВалютаВзаиморасчетов = &ВалютаРеглУчета
		|														ТОГДА ""67.04""
		|													ИНАЧЕ ""67.43""
		|												КОНЕЦ
		|								КОНЕЦ
		|					КОНЕЦ
		|				КОГДА РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговораКредитовИДепозитов.КредитВБанке)
		|					ТОГДА ВЫБОР
		|							КОГДА РасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств = РасшифровкаПлатежа.ДоговорКредитаДепозита.СтатьяДДСОсновногоДолга
		|								ТОГДА ВЫБОР
		|										КОГДА РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипСрочности = ЗНАЧЕНИЕ(Перечисление.ТипыСрочностиКредитовИДепозитов.Краткосрочный)
		|											ТОГДА ВЫБОР
		|													КОГДА РасшифровкаПлатежа.ДоговорКредитаДепозита.ВалютаВзаиморасчетов = &ВалютаРеглУчета
		|														ТОГДА ""66.01""
		|													ИНАЧЕ ""66.21""
		|												КОНЕЦ
		|										КОГДА РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипСрочности = ЗНАЧЕНИЕ(Перечисление.ТипыСрочностиКредитовИДепозитов.Долгосрочный)
		|											ТОГДА ВЫБОР
		|													КОГДА РасшифровкаПлатежа.ДоговорКредитаДепозита.ВалютаВзаиморасчетов = &ВалютаРеглУчета
		|														ТОГДА ""67.01""
		|													ИНАЧЕ ""67.21""
		|												КОНЕЦ
		|								КОНЕЦ
		|							КОГДА РасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств = РасшифровкаПлатежа.ДоговорКредитаДепозита.СтатьяДДСПроцентов
		|									ИЛИ РасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств = РасшифровкаПлатежа.ДоговорКредитаДепозита.СтатьяДДСКомиссии
		|								ТОГДА ВЫБОР
		|										КОГДА РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипСрочности = ЗНАЧЕНИЕ(Перечисление.ТипыСрочностиКредитовИДепозитов.Краткосрочный)
		|											ТОГДА ВЫБОР
		|													КОГДА РасшифровкаПлатежа.ДоговорКредитаДепозита.ВалютаВзаиморасчетов = &ВалютаРеглУчета
		|														ТОГДА ""66.02""
		|													ИНАЧЕ ""66.22""
		|												КОНЕЦ
		|										КОГДА РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипСрочности = ЗНАЧЕНИЕ(Перечисление.ТипыСрочностиКредитовИДепозитов.Долгосрочный)
		|											ТОГДА ВЫБОР
		|													КОГДА РасшифровкаПлатежа.ДоговорКредитаДепозита.ВалютаВзаиморасчетов = &ВалютаРеглУчета
		|														ТОГДА ""67.02""
		|													ИНАЧЕ ""67.22""
		|												КОНЕЦ
		|								КОНЕЦ
		|					КОНЕЦ
		|				КОГДА РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговораКредитовИДепозитов.ДепозитВБанке)
		|					ИЛИ РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговораКредитовИДепозитов.ДепозитВБанкеСКапитализацией)
		|					ТОГДА ВЫБОР
		|							КОГДА РасшифровкаПлатежа.ДоговорКредитаДепозита.ВалютаВзаиморасчетов = &ВалютаРеглУчета
		|								ТОГДА ""55.03""
		|							ИНАЧЕ ""55.23""
		|						КОНЕЦ
		|		КОНЕЦ
		|	КОНЕЦ КАК СчетУчетаРасчетовСКонтрагентом
		|ИЗ
		|	Документ.%ИмяДокумента%.РасшифровкаПлатежа КАК РасшифровкаПлатежа
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&Дата, ) КАК КурсыВалют
		|		ПО РасшифровкаПлатежа.ВалютаВзаиморасчетов = КурсыВалют.Валюта
		|ГДЕ
		|	РасшифровкаПлатежа.Ссылка = &Ссылка");
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ИмяДокумента%", ДанныеИБ.Ссылка.Метаданные().Имя);
	
	Запрос.УстановитьПараметр("Ссылка",          ДанныеИБ.Ссылка);
	Запрос.УстановитьПараметр("ЭтоУТ",           ЭтоУТ());
	Запрос.УстановитьПараметр("Дата",            ДанныеИБ.Дата);
	Запрос.УстановитьПараметр("ВалютаРеглУчета", ВалютаРеглУчета);
	
	РасшифровкаПлатежаВИнформационнойБазе = Запрос.Выполнить().Выгрузить();
	
	РасшифровкаПлатежа = Новый ТаблицаЗначений;
	РасшифровкаПлатежа.Колонки.Добавить("Договор");
	РасшифровкаПлатежа.Колонки.Добавить("Заказ");
	РасшифровкаПлатежа.Колонки.Добавить("ДокументРасчетов");
	РасшифровкаПлатежа.Колонки.Добавить("СпособПогашенияЗадолженности");
	РасшифровкаПлатежа.Колонки.Добавить("СтатьяДДС");
	РасшифровкаПлатежа.Колонки.Добавить("Сумма");
	РасшифровкаПлатежа.Колонки.Добавить("СуммаВзаиморасчетов");
	РасшифровкаПлатежа.Колонки.Добавить("СуммаНДС");
	РасшифровкаПлатежа.Колонки.Добавить("КурсВзаиморасчетов");
	РасшифровкаПлатежа.Колонки.Добавить("КратностьВзаиморасчетов");
	РасшифровкаПлатежа.Колонки.Добавить("СтавкаНДС");
	РасшифровкаПлатежа.Колонки.Добавить("ВидРасчетовРасширенный");
	РасшифровкаПлатежа.Колонки.Добавить("СчетУчетаРасчетовСКонтрагентом");
	
	Для Каждого СтрокаРасшифровкиПлатежаИнформационнойБазы Из РасшифровкаПлатежаВИнформационнойБазе Цикл
		
		СтрокаРасшифровкиПлатежа = РасшифровкаПлатежа.Добавить();
		СвойстваЗаполненияСтроки = "СтатьяДДС,Сумма,СуммаВзаиморасчетов,КурсВзаиморасчетов,КратностьВзаиморасчетов";
		
		Если ДанныеXDTO.ВидРасчетов = "СПоставщиком"
			Или ДанныеXDTO.ВидРасчетов = "СПокупателем" Тогда
			СвойстваЗаполненияСтроки = СвойстваЗаполненияСтроки + ",СтавкаНДС,СуммаНДС";
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(СтрокаРасшифровкиПлатежа, СтрокаРасшифровкиПлатежаИнформационнойБазы, СвойстваЗаполненияСтроки);
		
		Если Не ЗначениеЗаполнено(СтрокаРасшифровкиПлатежа.СтатьяДДС) Тогда
			СтрокаРасшифровкиПлатежа.СтатьяДДС = ДанныеИБ.СтатьяДвиженияДенежныхСредств;
		КонецЕсли;
		
		// Заказ.
		ТипЗаказа = Неопределено;
		Если ЗначениеЗаполнено(СтрокаРасшифровкиПлатежаИнформационнойБазы.Заказ) Тогда
			
			ТипЗаказа = ТипЗнч(СтрокаРасшифровкиПлатежаИнформационнойБазы.Заказ);
			ИмяПКО = ИмяПКОДляОбъектаДанных(КомпонентыОбмена, СтрокаРасшифровкиПлатежаИнформационнойБазы.Заказ, ДанныеИБ.Организация);
			ПравилоКонвертации = КомпонентыОбмена.ПравилаКонвертацииОбъектов.Найти(ИмяПКО, "ИмяПКО");
			
			Если ЗначениеЗаполнено(ИмяПКО) И НЕ ПравилоКонвертации = Неопределено Тогда
				Если ТипЗаказа = Тип("ДокументСсылка.ЗаказКлиента")
					ИЛИ ТипЗаказа = Тип("ДокументСсылка.ЗаказПоставщику") Тогда
					ИмяСвойстваЗаказ = "Заказ";
				Иначе
					ИмяСвойстваЗаказ = "ДокументРасчетов";
					СтрокаРасшифровкиПлатежа.СпособПогашенияЗадолженности = "ПоДокументу";
				КонецЕсли;
			
				СтрокаРасшифровкиПлатежа[ИмяСвойстваЗаказ] = Новый Структура("Значение, ИмяПКО", 
					СтрокаРасшифровкиПлатежаИнформационнойБазы.Заказ, ИмяПКО);
			КонецЕсли;
			
		КонецЕсли;
		
		// Договор
		Если ЭтоХозяйственнаяОперацияРасчетыПоКредитамЗаймам(ДанныеИБ.ХозяйственнаяОперация)
			Или ЭтоХозяйственнаяОперацияРасчетыПоДепозитам(ДанныеИБ.ХозяйственнаяОперация) Тогда
			
			СтрокаРасшифровкиПлатежа.Договор = Новый Структура("Значение, ИмяПКО",
				СтрокаРасшифровкиПлатежаИнформационнойБазы.ДоговорКредитаДепозита, "Справочник_ДоговорыКредитовИДепозитов_Отправка");
			
		Иначе
			
			ОперацияИнтеркампани = Ложь;
			
			Если ЗначениеЗаполнено(СтрокаРасшифровкиПлатежаИнформационнойБазы.Заказ) Тогда
				
				ТипДоговора = ТипЗнч(СтрокаРасшифровкиПлатежаИнформационнойБазы.Договор);
				
				ОснованиеИнтеркампани = (ТипЗаказа = Тип("ДокументСсылка.ПередачаТоваровМеждуОрганизациями")
					Или ТипЗаказа = Тип("ДокументСсылка.ВозвратТоваровМеждуОрганизациями")
					Или ТипЗаказа = Тип("ДокументСсылка.ОтчетПоКомиссииМеждуОрганизациями")
					Или ТипЗаказа = Тип("СправочникСсылка.ДоговорыМеждуОрганизациями")
					Или ТипДоговора = Тип("СправочникСсылка.ДоговорыМеждуОрганизациями"));
				
			КонецЕсли;
				
			Если ЗначениеЗаполнено(СтрокаРасшифровкиПлатежаИнформационнойБазы.Договор)
				И Не ОперацияИнтеркампани Тогда
				СтрокаРасшифровкиПлатежа.Договор = СтрокаРасшифровкиПлатежаИнформационнойБазы.Договор;
			Иначе
				
				ДоговорОбъектаРасчетов   = Неопределено;
				ВидДоговора              = "";
				РасчетыВУсловныхЕдиницах = Неопределено;
				
				Если Не ЗначениеЗаполнено(СтрокаРасшифровкиПлатежаИнформационнойБазы.Договор)
					И ЗначениеЗаполнено(СтрокаРасшифровкиПлатежаИнформационнойБазы.Заказ)
					И СтрокаРасшифровкиПлатежаИнформационнойБазы.Заказ.Метаданные().Реквизиты.Найти("Договор") <> Неопределено Тогда
					ДоговорОбъектаРасчетов = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаРасшифровкиПлатежаИнформационнойБазы.Заказ, "Договор");
				КонецЕсли;
				
				Если ЗначениеЗаполнено(ДоговорОбъектаРасчетов) 
					И ТипЗнч(ДоговорОбъектаРасчетов) = Тип("СправочникСсылка.ДоговорыМеждуОрганизациями") Тогда
					ОперацияИнтеркампани = Истина;
				КонецЕсли;
				
				Если ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОплатаЛизингодателю Тогда
					ВидДоговора = "Прочее";
				ИначеЕсли ОперацияИнтеркампани
					Или ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОплатаДенежныхСредствВДругуюОрганизацию
					Или ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствВДругуюОрганизацию Тогда
				
					ДоговорИнтеркампани = Неопределено;
					
					Если ТипЗнч(СтрокаРасшифровкиПлатежаИнформационнойБазы.Договор) = Тип("СправочникСсылка.ДоговорыМеждуОрганизациями") Тогда
						ДоговорИнтеркампани = СтрокаРасшифровкиПлатежаИнформационнойБазы.Договор;
					ИначеЕсли ТипЗаказа = Тип("СправочникСсылка.ДоговорыМеждуОрганизациями") Тогда
						ДоговорИнтеркампани = СтрокаРасшифровкиПлатежаИнформационнойБазы.Заказ;
					ИначеЕсли ЗначениеЗаполнено(ДоговорОбъектаРасчетов) И ТипЗнч(ДоговорОбъектаРасчетов) = Тип("СправочникСсылка.ДоговорыМеждуОрганизациями") Тогда
						ДоговорИнтеркампани = ДоговорОбъектаРасчетов;
					КонецЕсли;
					
					Если ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствВДругуюОрганизацию Тогда
						Если ЗначениеЗаполнено(ДоговорИнтеркампани) Тогда
							ВидДоговора = ВидДоговораПоТипуДоговораИнтеркампани(
								ДанныеИБ.Организация, ДоговорИнтеркампани);
						Иначе
							ВидДоговора = "СПокупателем";
						КонецЕсли;
					Иначе
						Если ЗначениеЗаполнено(СтрокаРасшифровкиПлатежаИнформационнойБазы.Заказ)
							И ТипЗаказа = Тип("ДокументСсылка.ОтчетПоКомиссииМеждуОрганизациями") Тогда
							
							ОрганизацияОбъектаРасчетов = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаРасшифровкиПлатежаИнформационнойБазы.Заказ, "Организация");
							
							ВидДоговора = ?(ДанныеИБ.Организация = ОрганизацияОбъектаРасчетов, "СКомиссионером", "СКомитентом");
							РасчетыВУсловныхЕдиницах = Ложь;
							
						Иначе
							Если ЗначениеЗаполнено(ДоговорИнтеркампани) Тогда
								ВидДоговора = ВидДоговораПоТипуДоговораИнтеркампани(ДанныеИБ.Организация, ДоговорИнтеркампани);
							Иначе
								ВидДоговора = "СПоставщиком";
							КонецЕсли;
						КонецЕсли;
					КонецЕсли;
				Иначе
					
					Если ЗначениеЗаполнено(ДоговорОбъектаРасчетов)
						И Не ОперацияИнтеркампани Тогда
						СтрокаРасшифровкиПлатежа.Договор = ДоговорОбъектаРасчетов;
					Иначе
						
						Если ЗначениеЗаполнено(СтрокаРасшифровкиПлатежаИнформационнойБазы.Заказ)
							И СтрокаРасшифровкиПлатежаИнформационнойБазы.Заказ.Метаданные().Реквизиты.Найти("Соглашение") <> Неопределено Тогда
							Соглашение = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаРасшифровкиПлатежаИнформационнойБазы.Заказ, "Соглашение");
						КонецЕсли;
						
						ВидДоговора = 
							ВзаиморасчетыСервер.ВидДоговораКонтрагента(СтрокаРасшифровкиПлатежаИнформационнойБазы.Заказ, Соглашение, ДанныеИБ.ХозяйственнаяОперация);
						
						Если ВидДоговора = "СКомитентом"
							Или ВидДоговора = "СКомиссионером" Тогда
							РасчетыВУсловныхЕдиницах = Ложь;
						Иначе
							Если ЗначениеЗаполнено(Соглашение) Тогда
								РасчетыВУсловныхЕдиницах = ВзаиморасчетыСервер.РасчетыВУсловныхЕдиницах(Соглашение);
							КонецЕсли;
						КонецЕсли;
						
					КонецЕсли;
					
				КонецЕсли;
				
				Если Не ЗначениеЗаполнено(СтрокаРасшифровкиПлатежа.Договор) Тогда
					
					Если ОперацияИнтеркампани И ЗначениеЗаполнено(ДоговорИнтеркампани) Тогда 
						
						ДанныеДоговора = ДанныеДоговораМеждуОрганизациями(КомпонентыОбмена, ДоговорИнтеркампани);
						СтрокаРасшифровкиПлатежа.Договор = Новый Структура("Значение, ИмяПКО", ДанныеДоговора, "Справочник_ДоговорыКонтрагентов_ИзСтруктуры");
					
					Иначе
					
						Если ДанныеXDTO.ВидРасчетов = "Прочее" Тогда
							ВидДоговора = "Прочее";
						ИначеЕсли Не ЗначениеЗаполнено(ВидДоговора) Тогда
							ВидДоговора = "СПоставщиком";
						КонецЕсли;
						
						Если Не ЗначениеЗаполнено(РасчетыВУсловныхЕдиницах) Тогда
							Если ВидДоговора = "СКомитентом" Или ВидДоговора = "СКомиссионером" Тогда
								РасчетыВУсловныхЕдиницах = Ложь;
							Иначе
								РасчетыВУсловныхЕдиницах = ДанныеИБ.Валюта = Константы.ВалютаРегламентированногоУчета.Получить()
									И ДанныеИБ.Валюта <> СтрокаРасшифровкиПлатежаИнформационнойБазы.ВалютаВзаиморасчетов;
							КонецЕсли;
						КонецЕсли;
						
						ВалютаВзаиморасчетов = СтрокаРасшифровкиПлатежаИнформационнойБазы.ВалютаВзаиморасчетов;
						Если Не ЗначениеЗаполнено(ВалютаВзаиморасчетов) Тогда
							ВалютаВзаиморасчетов = Константы.ВалютаРегламентированногоУчета.Получить();
						КонецЕсли;
						
						ПараметрыПоУмолчанию = Новый Структура(ДоговорФиксированнаяСтруктураКлючей());
						ПараметрыПоУмолчанию.Контрагент               = КонтрагентСсылка;
						ПараметрыПоУмолчанию.Организация              = ДанныеИБ.Организация;
						ПараметрыПоУмолчанию.ВалютаВзаиморасчетов     = ВалютаВзаиморасчетов;
						ПараметрыПоУмолчанию.ВидДоговора              = ВидДоговора;
						ПараметрыПоУмолчанию.Дата                     = ДанныеИБ.Дата;
						ПараметрыПоУмолчанию.Номер                    = ДанныеИБ.Номер;
						ПараметрыПоУмолчанию.РасчетыВУсловныхЕдиницах = РасчетыВУсловныхЕдиницах;
						
						Заказ = Новый Структура("Заказ, Соглашение, Сделка", 
							СтрокаРасшифровкиПлатежаИнформационнойБазы.Заказ, Соглашение, Неопределено);
						СтрокаРасшифровкиПлатежа.Договор = 
							ДоговорИнструкцияКонвертацииПоДаннымВзаиморасчетов(ПараметрыПоУмолчанию, КомпонентыОбмена, Заказ, ДанныеИБ);
					КонецЕсли;
					
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		// ВидРасчетовРасширенный и СчетУчетаРасчетовСКонтрагентом
		СтрокаРасшифровкиПлатежа.ВидРасчетовРасширенный = СтрокаРасшифровкиПлатежаИнформационнойБазы.ВидРасчетовРасширенный;
		СтрокаРасшифровкиПлатежа.СчетУчетаРасчетовСКонтрагентом = СтрокаРасшифровкиПлатежаИнформационнойБазы.СчетУчетаРасчетовСКонтрагентом;
		
	КонецЦикла;
	
	ДанныеXDTO.Вставить("РасшифровкаПлатежа", РасшифровкаПлатежа);
КонецПроцедуры
