Процедура ЗаполнитьВидНоменклатурыУСерийВТЧ(Объект, ПараметрыУказанияСерий, КомпонентыОбмена, ПолноеИмяОбъекта = "")
	Если НЕ ЗначениеЗаполнено(ПолноеИмяОбъекта) Тогда
		ПолноеИмяОбъекта  = Объект.Метаданные().ПолноеИмя();
	КонецЕсли;
	
	Если (ТипЗнч(ПараметрыУказанияСерий) = Тип("ФиксированнаяСтруктура") ИЛИ ТипЗнч(ПараметрыУказанияСерий) = Тип("Структура"))
		И ПараметрыУказанияСерий.Свойство("ИмяТЧТовары") Тогда
		ИмяТЧ = ПараметрыУказанияСерий.ИмяТЧТовары;
	Иначе
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
			"ВЫБРАТЬ
			|	Источник.Серия КАК Серия,
			|	ВЫБОР
			|		КОГДА Источник.Номенклатура.ВидНоменклатуры.ВладелецСерий = ЗНАЧЕНИЕ(Справочник.ВидыНоменклатуры.ПустаяСсылка)
			|			ТОГДА Источник.Номенклатура.ВидНоменклатуры
			|		ИНАЧЕ Источник.Номенклатура.ВидНоменклатуры.ВладелецСерий
			|	КОНЕЦ КАК ВладелецСерий
			|ИЗ
			|	&Источник КАК Источник
			|ГДЕ
			|	Источник.Ссылка = &Ссылка
			|	И Источник.Серия.ВидНоменклатуры = ЗНАЧЕНИЕ(Справочник.ВидыНоменклатуры.ПустаяСсылка)";
	
	
	Если ПараметрыУказанияСерий.ТоварВШапке Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса , "&Источник", ПолноеИмяОбъекта);
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса , "&Источник", ПолноеИмяОбъекта + "." + ИмяТЧ);
	КонецЕсли;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СерияОбъект = Выборка.Серия.ПолучитьОбъект();
		СерияОбъект.ВидНоменклатуры = Выборка.ВладелецСерий;
		
		СерияОбъект.ОбменДанными.Загрузка = Истина;
		
		Попытка
			СерияОбъект.Записать();
		Исключение
			ЗаписатьОшибкуВЖурналРегистрации(Выборка.Серия,
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()), КомпонентыОбмена);
		КонецПопытки;	
	КонецЦикла;
КонецПроцедуры
