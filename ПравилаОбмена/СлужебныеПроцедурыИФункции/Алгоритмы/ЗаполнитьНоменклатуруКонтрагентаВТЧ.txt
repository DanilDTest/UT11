Процедура ЗаполнитьНоменклатуруКонтрагентаВТЧ(КомпонентыОбмена, Таблица, КонтрагентПоУмолчанию)
	Таблица.Колонки.Добавить("SKUКонтрагента");
		
	Для Каждого СтрокаТаблицы Из Таблица Цикл
		Если Не ЗначениеЗаполнено(СтрокаТаблицы.SKUКонтрагентаСсылка) Тогда
			Продолжить;
		КонецЕсли;
		
		НоменклатураКонтрагента = Новый Структура;
		НоменклатураКонтрагента.Вставить("Ссылка", СтрокаТаблицы.SKUКонтрагентаСсылка);
		НоменклатураКонтрагента.Вставить("Владелец", КонтрагентПоУмолчанию);
		НоменклатураКонтрагента.Вставить("Наименование", СтрокаТаблицы.SKUКонтрагентаНаименование);
		НоменклатураКонтрагента.Вставить("Идентификатор", СтрокаТаблицы.SKUКонтрагентаИдентификатор);
		НоменклатураКонтрагента.Вставить("Артикул", СтрокаТаблицы.SKUКонтрагентаАртикул);
		НоменклатураКонтрагента.Вставить("Штрихкод", СтрокаТаблицы.SKUКонтрагентаШтрихкод);
		НоменклатураКонтрагента.Вставить("ИдентификаторНоменклатурыСервиса1СНоменклатура",
			СтрокаТаблицы.SKUКонтрагентаИдентификаторНоменклатурыСервиса);
		НоменклатураКонтрагента.Вставить("ИдентификаторХарактеристикиСервиса1СНоменклатура",
			СтрокаТаблицы.SKUКонтрагентаИдентификаторХарактеристикиСервиса);
		
		Если (ЗначениеЗаполнено(СтрокаТаблицы.SKUКонтрагентаНаименованиеБазовойЕдиницыИзмерения) Или ЗначениеЗаполнено(СтрокаТаблицы.SKUКонтрагентаКодОКЕИБазовойЕдиницыИзмерения)) 
			И (ЗначениеЗаполнено(СтрокаТаблицы.SKUКонтрагентаНаименованиеУпаковки) Или ЗначениеЗаполнено(СтрокаТаблицы.SKUКонтрагентаНаименованиеБазовойЕдиницыИзмерения)) Тогда
	
			ЕдиницаИзмерения = Новый Структура;
			ЕдиницаИзмерения.Вставить("Наименование", СтрокаТаблицы.SKUКонтрагентаНаименованиеБазовойЕдиницыИзмерения);
			ЕдиницаИзмерения.Вставить("Код", СтрокаТаблицы.SKUКонтрагентаКодОКЕИБазовойЕдиницыИзмерения);
	
			Номенклатура = Новый Структура;
			Номенклатура.Вставить("Наименование", НоменклатураКонтрагента.Наименование);
			Номенклатура.Вставить("Артикул", НоменклатураКонтрагента.Артикул);
			Номенклатура.Вставить("ИдентификаторСервиса1СНоменклатура",
				НоменклатураКонтрагента.ИдентификаторНоменклатурыСервиса1СНоменклатура);
	
			Если ЗначениеЗаполнено(СтрокаТаблицы.SKUКонтрагентаНаименованиеУпаковки) Тогда
				Упаковка = Новый Структура;
				Упаковка.Вставить("Наименование", СтрокаТаблицы.SKUКонтрагентаНаименованиеУпаковки);
				Упаковка.Вставить("ЕдиницаИзмерения", ЕдиницаИзмерения);
				Упаковка.Вставить("Номенклатура", Номенклатура);
				Упаковка.Вставить("Коэффициент",?(СтрокаТаблицы.SKUКонтрагентаКоличествоУпаковок = 0,
					СтрокаТаблицы.SKUКонтрагентаКоличествоБазовойЕдиницыИзмерения,
					Окр(СтрокаТаблицы.SKUКонтрагентаКоличествоБазовойЕдиницыИзмерения / СтрокаТаблицы.SKUКонтрагентаКоличествоУпаковок,3)));
			Иначе
				Упаковка = Новый Структура;
				Упаковка.Вставить("Наименование", СтрокаТаблицы.SKUКонтрагентаНаименованиеБазовойЕдиницыИзмерения);
				Упаковка.Вставить("ЕдиницаИзмерения", ЕдиницаИзмерения);
				Упаковка.Вставить("Номенклатура", Номенклатура);
				Упаковка.Вставить("Коэффициент", 1);
			КонецЕсли;
			
			НоменклатураКонтрагента.Вставить("УпаковкаНоменклатуры", Упаковка);
		
		КонецЕсли;
		
		СтрокаТаблицы.SKUКонтрагента = Новый Структура("Значение, ИмяПКО",
				НоменклатураКонтрагента, "Справочник_НоменклатураПоставщиков_ИзСтруктуры");
	КонецЦикла;
КонецПроцедуры
