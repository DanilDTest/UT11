Функция ПолучитьВесообъемныеХарактеристикиТоваров(ТоварыОтбора)
	СтруктураВозврата = Новый Структура;
		СтруктураВозврата.Вставить("Вес", 0);
		СтруктураВозврата.Вставить("Объем", 0); 
		 
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ДанныеНоменклатуры.Номенклатура КАК Номенклатура,
			|	ДанныеНоменклатуры.Упаковка КАК Упаковка,
			|	ДанныеНоменклатуры.Количество КАК Количество
			|ПОМЕСТИТЬ ВТНоменклатура
			|ИЗ
			|	&ДанныеНоменклатуры КАК ДанныеНоменклатуры
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ВТНоменклатура.Номенклатура КАК Номенклатура,
			|	ВТНоменклатура.Упаковка КАК Упаковка,
			|	&ТекстЗапросаВесНоменклатуры * ВТНоменклатура.Количество КАК Вес,
			|	&ТекстЗапросаОбъемНоменклатуры * ВТНоменклатура.Количество КАК Объем
			|ИЗ
			|	ВТНоменклатура КАК ВТНоменклатура
			|ИТОГИ
			|	СУММА(Вес),
			|	СУММА(Объем)
			|ПО
			|	ОБЩИЕ";
		
		Запрос.УстановитьПараметр("ДанныеНоменклатуры", ТоварыОтбора);
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаВесНоменклатуры",
			Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаВесУпаковки(
				"ВТНоменклатура.Упаковка",
				"ВТНоменклатура.Номенклатура"));
			
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаОбъемНоменклатуры",
			Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаОбъемУпаковки(
				"ВТНоменклатура.Упаковка",
				"ВТНоменклатура.Номенклатура"));
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаИтоги = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Если ВыборкаИтоги.Следующий() Тогда
			СтруктураВозврата.Вес = ВыборкаИтоги.Вес;
			СтруктураВозврата.Объем = ВыборкаИтоги.Объем;
		КонецЕсли;      
		
		Возврат СтруктураВозврата;
КонецФункции
