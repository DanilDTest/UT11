Функция КонтрагентИзПартнера(Знач Партнер, КомпонентыОбмена)
	// Проверим, есть ли в базе контрагент, созданный по партнеру, если нет, то создадим его и выгрузим.
	СсылкаНаКонтрагента = Справочники.Контрагенты.ПолучитьСсылку(Партнер.УникальныйИдентификатор());
	Если НЕ ОбщегоНазначения.СсылкаСуществует(СсылкаНаКонтрагента) ИЛИ СсылкаНаКонтрагента.ПолучитьОбъект() = Неопределено Тогда
		
		ЗапросКонтр = Новый Запрос;
		ЗапросКонтр.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1 Ссылка
		|ИЗ Справочник.Контрагенты
		|ГДЕ Наименование = &Наименование 
		|	И ЮридическоеФизическоеЛицо = &ЮридическоеФизическоеЛицо";
		
		ДанныеПартнера = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Партнер, "Наименование, НаименованиеПолное, ЮрФизЛицо");
		ДанныеПартнера.Вставить("ЮридическоеФизическоеЛицо", ?(ДанныеПартнера.ЮрФизЛицо = Перечисления.КомпанияЧастноеЛицо.ЧастноеЛицо,
				Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо,
				Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо));
		
		ЗапросКонтр.УстановитьПараметр("Наименование", ДанныеПартнера.Наименование);
		ЗапросКонтр.УстановитьПараметр("ЮридическоеФизическоеЛицо", ДанныеПартнера.ЮридическоеФизическоеЛицо);
				
		Выборка = ЗапросКонтр.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			СсылкаНаКонтрагента = Выборка.Ссылка;
			Возврат СсылкаНаКонтрагента;
		КонецЕсли;
		
		СоздаваемыйКонтрагент = Справочники.Контрагенты.СоздатьЭлемент();
		ЗаполнитьЗначенияСвойств(СоздаваемыйКонтрагент, ДанныеПартнера,
			"НаименованиеПолное, Наименование, ЮридическоеФизическоеЛицо");
			
		Если СоздаваемыйКонтрагент.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо Тогда
			
			СоздаваемыйКонтрагент.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо;
			
		Иначе
			
			СоздаваемыйКонтрагент.ЮридическоеФизическоеЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо;
			
		КонецЕсли;
		
		СоздаваемыйКонтрагент.Партнер = Партнер;
		
		СоздаваемыйКонтрагент.УстановитьСсылкуНового(СсылкаНаКонтрагента);
		СоздаваемыйКонтрагент.ОбменДанными.Загрузка = Истина;
		СоздаваемыйКонтрагент.Записать();
		
		КомпонентыОбмена.ПараметрыКонвертации.СозданныеПриВыгрузкеОбъекты.Добавить(СоздаваемыйКонтрагент.Ссылка);
		
		СсылкаНаКонтрагента = СоздаваемыйКонтрагент.Ссылка;
	КонецЕсли;
	Возврат СсылкаНаКонтрагента;
КонецФункции
