Функция ПолучитьРеквизитыКассовойСменыНаДату(Объект, ДатаВремя)
	КассаККМФискальноеУстройство = КассаККМФискальноеУстройство(Объект);
	ФискальноеУстройство = КассаККМФискальноеУстройство.ФискальноеУстройство;
	КассаККМ             = КассаККМФискальноеУстройство.КассаККМ;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	КассоваяСмена.Номер КАК НомерКассовойСмены,
	|	КассоваяСмена.Ссылка КАК КассоваяСмена,
	|	КассоваяСмена.Статус КАК СтатусКассовойСмены,
	|	КассоваяСмена.СтатусРегламентныхОпераций КАК СтатусРегламентныхОпераций,
	|	КассоваяСмена.Организация КАК Организация,
	|	КассоваяСмена.КассаККМ КАК КассаККМ,
	|	КассоваяСмена.ФискальноеУстройство КАК ФискальноеУстройство,
	|	ВЫБОР
	|		КОГДА КассоваяСмена.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыКассовойСмены.Открыта)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СменаОткрыта,
	|	ВЫБОР
	|		КОГДА КассоваяСмена.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыКассовойСмены.Открыта)
	|			ТОГДА КассоваяСмена.НачалоКассовойСмены
	|		ИНАЧЕ КассоваяСмена.ОкончаниеКассовойСмены
	|	КОНЕЦ КАК ДатаИзмененияСтатуса
	|ИЗ
	|	Документ.КассоваяСмена КАК КассоваяСмена
	|ГДЕ
	|	КассоваяСмена.Проведен
	|	И (КассоваяСмена.КассаККМ = &КассаККМ
	|				И &КассаККМ <> НЕОПРЕДЕЛЕНО
	|			ИЛИ КассоваяСмена.ФискальноеУстройство = &ФискальноеУстройство
	|				И &ФискальноеУстройство <> НЕОПРЕДЕЛЕНО)
	|	И КассоваяСмена.НачалоКассовойСмены <= &ДатаВремя
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаИзмененияСтатуса УБЫВ";
	
	Запрос.УстановитьПараметр("КассаККМ",             КассаККМ);
	Запрос.УстановитьПараметр("ФискальноеУстройство", ФискальноеУстройство);
	
	Запрос.УстановитьПараметр("ДатаВремя",            ДатаВремя);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	СтруктураРеквизитов = Новый Структура;
	СтруктураРеквизитов.Вставить("НомерКассовойСмены");
	СтруктураРеквизитов.Вставить("КассоваяСмена");
	СтруктураРеквизитов.Вставить("СтатусКассовойСмены");
	СтруктураРеквизитов.Вставить("СтатусРегламентныхОпераций");
	СтруктураРеквизитов.Вставить("Организация");
	СтруктураРеквизитов.Вставить("КассаККМ");
	СтруктураРеквизитов.Вставить("ФискальноеУстройство");
	СтруктураРеквизитов.Вставить("СменаОткрыта", Ложь);
	СтруктураРеквизитов.Вставить("ДатаИзмененияСтатуса");
	
	Если Выборка.Следующий() Тогда
		
		ЗаполнитьЗначенияСвойств(СтруктураРеквизитов, Выборка);
		
	КонецЕсли;
		
	Возврат СтруктураРеквизитов;
КонецФункции
