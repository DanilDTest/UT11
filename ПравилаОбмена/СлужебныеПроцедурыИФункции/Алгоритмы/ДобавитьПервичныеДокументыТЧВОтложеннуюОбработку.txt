Процедура ДобавитьПервичныеДокументыТЧВОтложеннуюОбработку(Объект, КомпонентыОбмена, ИмяТЧ = "РасшифровкаПлатежа", ИмяКолонки = "Заказ")
	СоответствиеХозОперацийТипамДокументов = Новый Соответствие;
	СоответствиеХозОперацийТипамДокументов.Вставить(Перечисления.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента,
		Перечисления.ТипыПервичныхДокументов.РеализацияКлиенту);
	СоответствиеХозОперацийТипамДокументов.Вставить(Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтПоставщика,
		Перечисления.ТипыПервичныхДокументов.ОплатаПоставщику);
	СоответствиеХозОперацийТипамДокументов.Вставить(Перечисления.ХозяйственныеОперации.ОплатаПоставщику,
		Перечисления.ТипыПервичныхДокументов.ПриобретениеУПоставщика);
	СоответствиеХозОперацийТипамДокументов.Вставить(Перечисления.ХозяйственныеОперации.ВозвратОплатыКлиенту,
		Перечисления.ТипыПервичныхДокументов.ОплатаОтКлиента);
	СоответствиеХозОперацийТипамДокументов.Вставить(Перечисления.ТипыОперацийВводаОстатков.ОстаткиПоАвансамКлиентов,
		Перечисления.ТипыПервичныхДокументов.ОплатаОтКлиента);
	СоответствиеХозОперацийТипамДокументов.Вставить(Перечисления.ТипыОперацийВводаОстатков.ОстаткиЗадолженностиКлиентов,
		Перечисления.ТипыПервичныхДокументов.РеализацияКлиенту);
	СоответствиеХозОперацийТипамДокументов.Вставить(Перечисления.ТипыОперацийВводаОстатков.ОстаткиПоАвансамПоставщикам,
		Перечисления.ТипыПервичныхДокументов.ОплатаПоставщику);
	СоответствиеХозОперацийТипамДокументов.Вставить(Перечисления.ТипыОперацийВводаОстатков.ОстаткиЗадолженностиПередПоставщиками,
		Перечисления.ТипыПервичныхДокументов.ПриобретениеУПоставщика);
	
	ПервичныеДокументы = КомпонентыОбмена.ПараметрыКонвертации.ПервичныеДокументы;
	Для Каждого СтрокаДокумента Из Объект[ИмяТЧ] Цикл
		Если ЗначениеЗаполнено(СтрокаДокумента[ИмяКолонки])
			И ТипЗнч(СтрокаДокумента[ИмяКолонки]) = Тип("ДокументСсылка.ПервичныйДокумент") Тогда
			
			Если ТипЗнч(Объект) = Тип("ДокументОбъект.ВводОстатков") Тогда
				ТипПервичногоДокумента = СоответствиеХозОперацийТипамДокументов.Получить(Объект.ТипОперации);
			Иначе
				ТипПервичногоДокумента = СоответствиеХозОперацийТипамДокументов.Получить(Объект.ХозяйственнаяОперация);
			КонецЕсли;
			
			Если НЕ ТипПервичногоДокумента = Неопределено Тогда
				СтрокаПервичныеДокументы = ПервичныеДокументы.Добавить();
				СтрокаПервичныеДокументы.ПервичныйДокумент = СтрокаДокумента[ИмяКолонки];
				СтрокаПервичныеДокументы.ТипПервичногоДокумента = ТипПервичногоДокумента;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры
