Процедура ЗагрузитьШтрихкодыУпаковок(ДанныеXDTO, ПолученныеДанные, КомпонентыОбмена, ИмяТаблицы = "ШтрихкодыУпаковок", ИмяСвойства = "ШтрихкодУпаковки")
	МассивСтрокШтрихкодыУпаковок = Новый Массив;
	Если ДанныеXDTO.Свойство("АкцизныеМарки")
		И ЗначениеЗаполнено(ДанныеXDTO.АкцизныеМарки) Тогда
		
		ПравилаЗаполнения = Новый Соответствие;
		ПравилаЗаполнения.Вставить("Номенклатура",        "Номенклатура");
		ПравилаЗаполнения.Вставить("НомерАкцизнойМарки",  "ЗначениеШтрихкода");
		Если ЗначениеФО("ИспользоватьХарактеристикиНоменклатуры", КомпонентыОбмена) Тогда
			ПравилаЗаполнения.Вставить("Характеристика",  "Характеристика");
		КонецЕсли;
		Если ЗначениеФО("ИспользоватьСерииНоменклатуры", КомпонентыОбмена) Тогда
			ПравилаЗаполнения.Вставить("Серия",           "Серия");
		КонецЕсли;
		Если ЗначениеФО("ИспользоватьУпаковкиНоменклатуры", КомпонентыОбмена) Тогда
			ПравилаЗаполнения.Вставить("Упаковка", "Упаковка");
		КонецЕсли;
		
		Для Каждого Строка Из ДанныеXDTO.АкцизныеМарки Цикл
			СтруктураАкцизнаяМарка = ДанныеКоллекцииВВидеСтруктуры(Строка, ПравилаЗаполнения);
				
			ИнструкцияШтрихкодУпаковки = Новый Структура("Значение, ИмяПКО");
			ИнструкцияШтрихкодУпаковки.Значение = СтруктураАкцизнаяМарка;
			ИнструкцияШтрихкодУпаковки.ИмяПКО = "Справочник_ШтрихкодыУпаковокТоваровИзСтр_Получение";
			
			МассивСтрокШтрихкодыУпаковок.Добавить(Новый Структура(ИмяСвойства, ИнструкцияШтрихкодУпаковки));
		КонецЦикла;
	КонецЕсли;
	
	Если ДанныеXDTO.Свойство("МаркировкиУпаковок")
		И ЗначениеЗаполнено(ДанныеXDTO.МаркировкиУпаковок) Тогда
		
		Для Каждого Строка Из ДанныеXDTO.МаркировкиУпаковок Цикл
				
			ИнструкцияШтрихкодУпаковки = Новый Структура("Значение, ИмяПКО");
			ИнструкцияШтрихкодУпаковки.Значение = Строка.МаркировкаУпаковки;
			ИнструкцияШтрихкодУпаковки.ИмяПКО = "Справочник_ШтрихкодыУпаковокТоваров_Получение";
			
			МассивСтрокШтрихкодыУпаковок.Добавить(Новый Структура(ИмяСвойства, ИнструкцияШтрихкодУпаковки));
		КонецЦикла;
	КонецЕсли;
	
	Если МассивСтрокШтрихкодыУпаковок.Количество() > 0 Тогда
		ПолученныеДанные.ДополнительныеСвойства.Вставить(ИмяТаблицы, МассивСтрокШтрихкодыУпаковок);
	КонецЕсли;
КонецПроцедуры
