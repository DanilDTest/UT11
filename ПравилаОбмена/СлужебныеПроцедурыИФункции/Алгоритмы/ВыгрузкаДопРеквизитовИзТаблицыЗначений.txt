Процедура ВыгрузкаДопРеквизитовИзТаблицыЗначений(ДанныеИБДополнительныеРеквизиты, ТаблицаДопРеквизитов)
	Для Каждого СтрокаТЧ Из ДанныеИБДополнительныеРеквизиты Цикл
		Если НЕ ЗначениеЗаполнено(СтрокаТЧ.Свойство) 
			ИЛИ НЕ ЗначениеЗаполнено(СтрокаТЧ.Свойство.ВерсияДанных) Тогда
			Продолжить;
		КонецЕсли;
		ТипЗначенияСвойства = ТипЗнч(СтрокаТЧ.Значение);
		ИмяПКО = "";
		ИмяПКОГруппа = "";
		Если ТипЗначенияСвойства = Тип("Число") 
			ИЛИ ТипЗначенияСвойства = Тип("Строка")
			ИЛИ ТипЗначенияСвойства = Тип("Булево")
			ИЛИ ТипЗначенияСвойства = Тип("Дата") Тогда
			НовСтрока = ТаблицаДопРеквизитов.Добавить();
			НовСтрока.Свойство = Новый Структура("Значение, ИмяПКО", СтрокаТЧ.Свойство, "Справочник_ДополнительныеРеквизиты");
			НовСтрока.ЗначениеСвойства = СтрокаТЧ.Значение;
			Продолжить;
		ИначеЕсли ТипЗначенияСвойства = Тип("СправочникСсылка.СтруктураПредприятия") Тогда
			СтруктураДанныеПодразделения = Новый Структура(
				"Наименование, РегистрацияПодразделенияВНалоговомОргане", 
				Строка(СтрокаТЧ.Значение), Неопределено);
	
			Инструкция = Новый Структура(
				"Значение, ИмяПКО",
				СтруктураДанныеПодразделения,
				"Справочник_Подразделения_ИзСтруктуры");
			НовСтрока = ТаблицаДопРеквизитов.Добавить();
			НовСтрока.Свойство = Новый Структура("Значение, ИмяПКО", СтрокаТЧ.Свойство, "Справочник_ДополнительныеРеквизиты");
			НовСтрока.ЗначениеСвойства = Инструкция;
			Продолжить;
		КонецЕсли;
		ВыгрузкаДопРеквизитов_ОпределитьИмяПКО(СтрокаТЧ.Значение, ИмяПКО, ИмяПКОГруппа);
		Если ИмяПКО = "" Тогда
			Продолжить;
		КонецЕсли; 	
		Если ИмяПКОГруппа <> "" И СтрокаТЧ.Значение.ЭтоГруппа Тогда
			ТекИмяПКО = ИмяПКОГруппа;
		Иначе
			ТекИмяПКО = ИмяПКО;
		КонецЕсли;
		НовСтрока = ТаблицаДопРеквизитов.Добавить();
		НовСтрока.Свойство = Новый Структура("Значение, ИмяПКО", СтрокаТЧ.Свойство, "Справочник_ДополнительныеРеквизиты");
		НовСтрока.ЗначениеСвойства = Новый Структура("Значение, ИмяПКО", СтрокаТЧ.Значение, ТекИмяПКО);
	КонецЦикла;
КонецПроцедуры
