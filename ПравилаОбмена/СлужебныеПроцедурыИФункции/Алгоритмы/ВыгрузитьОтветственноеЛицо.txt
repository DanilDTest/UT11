Процедура ВыгрузитьОтветственноеЛицо(КомпонентыОбмена,  ДанныеИБ, ДанныеXDTO, ИмяРеквизита, ВыгружатьКакФизЛицо = Ложь)
	ДанныеОтветственногоЛица = Новый Структура("Организация, Дата, " + ИмяРеквизита);
	ЗаполнитьЗначенияСвойств(ДанныеОтветственногоЛица, ДанныеИБ);
	
	ОтветственноеЛицо = Неопределено;
	Если ЗначениеЗаполнено(ДанныеОтветственногоЛица[ИмяРеквизита]) Тогда
		ОтветственноеЛицо = ДанныеОтветственногоЛица[ИмяРеквизита];
	ИначеЕсли ЗначениеЗаполнено(ДанныеОтветственногоЛица.Организация) Тогда
		ОтветственныеЛица = ОтветственныеЛицаСервер.ПолучитьОтветственныеЛицаОрганизации(ДанныеОтветственногоЛица.Организация,
			ДанныеОтветственногоЛица.Дата);
		Если ОтветственныеЛица.Свойство(ИмяРеквизита) И ЗначениеЗаполнено(ОтветственныеЛица[ИмяРеквизита]) Тогда
			ОтветственноеЛицо = ОтветственныеЛица[ИмяРеквизита];
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ОтветственноеЛицо) Тогда
		Возврат;
	КонецЕсли;
	
	Если ВерсияФорматаЧислом(КомпонентыОбмена.ВерсияФорматаОбмена) >= ВерсияФорматаЧислом("1.6")
		ИЛИ ВыгружатьКакФизЛицо Тогда
		
		Если ТипЗнч(ОтветственноеЛицо) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
			ФизическоеЛицо = ОтветственноеЛицо;
		ИначеЕсли ТипЗнч(ОтветственноеЛицо) = Тип("СправочникСсылка.ОтветственныеЛицаОрганизаций") Тогда
			ФизическоеЛицо = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОтветственноеЛицо, "ФизическоеЛицо");
		Иначе
			Возврат;
		КонецЕсли;
		
		ДанныеXDTO.Вставить(ИмяРеквизита, Новый Структура("Значение, ИмяПКО",
			ФизическоеЛицо,
			"Справочник_ФизическиеЛица_Отправка"));
	Иначе
		ДанныеXDTO.Вставить(ИмяРеквизита, ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			ОтветственноеЛицо, "Наименование"));
	КонецЕсли;
КонецПроцедуры
