Процедура ЗаполнитьКассуККМПоВалютеПодразделениюИОрганизации(ПолученныеДанные)
	РеквизитыКассыККМ = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПолученныеДанные.КассаККМ, "ВалютаДенежныхСредств, Владелец");
	
	Если РеквизитыКассыККМ.ВалютаДенежныхСредств <> ПолученныеДанные.Валюта 
		Или РеквизитыКассыККМ.Владелец <> ПолученныеДанные.Организация Тогда
		
		КассаККМ = Неопределено;
		Валюта = ПолученныеДанные.Валюта;
		Организация = ПолученныеДанные.Организация;
		Подразделение = ПолученныеДанные.Подразделение;
		
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("Валюта", Валюта);
		ПараметрыОтбора.Вставить("Организация", Организация);
		ПараметрыОтбора.Вставить("Подразделение", Подразделение);
	
		НеобходимоСоздатьЭлемент = Ложь;
	
		Если ОбщегоНазначения.СсылкаСуществует(Организация) И ОбщегоНазначения.СсылкаСуществует(Валюта) Тогда
			НаименованиеЭлементаДляОбменаПоУмолчанию = СокрЛП(Организация) + " (" + СокрЛП(Валюта) + ")";
		Иначе
			НаименованиеЭлементаДляОбменаПоУмолчанию = НСтр("ru = 'Основная касса ККМ';
															|en = 'Main cash register'");
		КонецЕсли;
		
		СписокПодразделений = Новый СписокЗначений();
		СписокПодразделений.Добавить(Справочники.СтруктураПредприятия.ПустаяСсылка());
		СписокПодразделений.Добавить(Подразделение);
		
		Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 2
			|	Кассы.Ссылка КАК Касса,
			|	ВЫБОР
			|		КОГДА Кассы.Наименование = &НаименованиеЭлементаДляОбменаПоУмолчанию
			|			ТОГДА ИСТИНА
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ КАК ЭтоЭлементПоУмолчаниюДляОбмена,
			|	Кассы.Склад КАК Склад
			|ИЗ
			|	Справочник.КассыККМ КАК Кассы
			|ГДЕ
			|	НЕ Кассы.ПометкаУдаления
			|	И Кассы.Владелец = &Организация
			|	И Кассы.ВалютаДенежныхСредств = &Валюта
			|	И Кассы.Подразделение В(&СписокПодразделений)
			|
			|УПОРЯДОЧИТЬ ПО
			|	ЭтоЭлементПоУмолчаниюДляОбмена УБЫВ,
			|	Склад УБЫВ");
	
		Запрос.УстановитьПараметр("НаименованиеЭлементаДляОбменаПоУмолчанию", НаименованиеЭлементаДляОбменаПоУмолчанию);
		Запрос.УстановитьПараметр("Организация", Организация);
		Запрос.УстановитьПараметр("Валюта", Валюта);
		Запрос.УстановитьПараметр("СписокПодразделений", СписокПодразделений);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Если Выборка.Количество() = 1 Или Выборка.ЭтоЭлементПоУмолчаниюДляОбмена Тогда
				КассаККМ = Выборка.Касса;
			Иначе
				НеобходимоСоздатьЭлемент = Истина;
			КонецЕсли;
		Иначе
			НеобходимоСоздатьЭлемент = Истина;
		КонецЕсли;
		
		// Создание элемента для обмена с заданными параметрами
		Если НеобходимоСоздатьЭлемент Тогда
			КассаОбъект = Справочники.КассыККМ.СоздатьЭлемент();
			КассаОбъект.Наименование			= НаименованиеЭлементаДляОбменаПоУмолчанию;
			КассаОбъект.ВалютаДенежныхСредств	= Валюта;
			КассаОбъект.Владелец				= Организация;
			КассаОбъект.Подразделение			= Подразделение;
			КассаОбъект.ТипКассы                = Перечисления.ТипыКассККМ.ФискальныйРегистратор;
	
			Попытка
				КассаОбъект.Записать();
				КассаККМ = КассаОбъект.Ссылка;
			Исключение
				ЗаписьЖурналаРегистрации(НСтр("ru = 'Обмен данными.Создание элемента КассыККМ';
												|en = 'Data exchange.Create the КассыККМ item'", 
					ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
	       			УровеньЖурналаРегистрации.Ошибка,,,
	       			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
				КассаККМ = Справочники.КассыККМ.ПустаяСсылка();
			КонецПопытки
		КонецЕсли;
		
		ПолученныеДанные.КассаККМ = КассаККМ;
	КонецЕсли;
КонецПроцедуры
