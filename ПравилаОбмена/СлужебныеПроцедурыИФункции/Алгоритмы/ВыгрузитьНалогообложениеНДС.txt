Процедура ВыгрузитьНалогообложениеНДС(КомпонентыОбмена, ДанныеИБ, ДанныеXDTO, НДСПродавца = Истина, ДляВсехВерсий = Истина, ИмяСвойства = "Налогообложение")
	Если ВерсияФорматаЧислом(КомпонентыОбмена.ВерсияФорматаОбмена) >= ВерсияФорматаЧислом("1.6") Тогда
		Если НДСПродавца Тогда
			ИмяПКО = "НалогообложениеНДСПродавца";
		Иначе
			ИмяПКО = "НалогообложениеНДС";
		КонецЕсли;
	ИначеЕсли ДляВсехВерсий Тогда 
		ИмяПКО = "Перечисления_ТипыНалогообложенияНДС";
	Иначе
		Возврат;
	КонецЕсли;
	
	СтруктураДанныеИБ = Новый Структура("НалогообложениеНДС, Организация, Склад, Договор, НаправлениеДеятельности, Дата");
	ЗаполнитьЗначенияСвойств(СтруктураДанныеИБ, ДанныеИБ);
	Если ЗначениеЗаполнено(СтруктураДанныеИБ.НалогообложениеНДС) Тогда
		НалогообложениеНДС = СтруктураДанныеИБ.НалогообложениеНДС;
	Иначе
		ПараметрыУчетаПоОрганизации = УчетНДСУП.ПараметрыУчетаПоОрганизации(
					СтруктураДанныеИБ.Организация,
					СтруктураДанныеИБ.Дата,
					СтруктураДанныеИБ.Склад);
			
		НалогообложениеНДС = ПараметрыУчетаПоОрганизации.ОсновноеНалогообложениеНДСПродажи;
		
	КонецЕсли;
	
	Если НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ЭлектронныеУслуги
		И ВерсияФорматаЧислом(КомпонентыОбмена.ВерсияФорматаОбмена) < ВерсияФорматаЧислом("1.7") Тогда
		Возврат;
	КонецЕсли;		
	
	ДанныеXDTO.Вставить(ИмяСвойства, Новый Структура("Значение, ИмяПКО", НалогообложениеНДС, ИмяПКО));
КонецПроцедуры
