Процедура ОтложеннаяОбработка_СортДокОтложенногоПров(КомпонентыОбмена)
	// В ряде случаев документы с одной датой должны проводиться в определенном порядке.
		
	// Добавим в таблицу "ДокументыДляОтложенногоПроведения" колонку "ОчередьПроведения".
	// Чем меньше значение очереди, тем раньше будет проведен документ в рамках документов одной даты.
	// По умолчанию очередь имеет нулевое (0) значение.
	ДокументыДляОтложенногоПроведения = КомпонентыОбмена.ДокументыДляОтложенногоПроведения;
	ДокументыДляОтложенногоПроведения.Колонки.Добавить("ОчередьПроведения", Новый ОписаниеТипов("Число"));
	
	ТипыСчетФактуры = Новый Массив;
	ТипыСчетФактуры.Добавить(Тип("ДокументСсылка.СчетФактураВыданный"));
	ТипыСчетФактуры.Добавить(Тип("ДокументСсылка.СчетФактураВыданныйАванс"));
	ТипыСчетФактуры.Добавить(Тип("ДокументСсылка.СчетФактураКомиссионеру"));
	ТипыСчетФактуры.Добавить(Тип("ДокументСсылка.СчетФактураКомитента"));
	ТипыСчетФактуры.Добавить(Тип("ДокументСсылка.СчетФактураНалоговыйАгент"));
	ТипыСчетФактуры.Добавить(Тип("ДокументСсылка.СчетФактураНаНеподтвержденнуюРеализацию0"));
	ТипыСчетФактуры.Добавить(Тип("ДокументСсылка.СчетФактураПолученный"));
	ТипыСчетФактуры.Добавить(Тип("ДокументСсылка.СчетФактураПолученныйАванс"));
	ТипыСчетФактуры.Добавить(Тип("ДокументСсылка.СчетФактураПолученныйНалоговыйАгент"));
	
	Для Каждого СтрокаТаблицы Из ДокументыДляОтложенногоПроведения Цикл
		
		Если СтрокаТаблицы.ДокументСсылка.Пустая() Тогда
			Продолжить;
		КонецЕсли;	
	
		Если НЕ ТипыСчетФактуры.Найти(ТипЗнч(СтрокаТаблицы.ДокументСсылка)) = Неопределено Тогда
			СтрокаТаблицы.ОчередьПроведения = 2;
		КонецЕсли;
			
	КонецЦикла;
	
	ДокументыДляОтложенногоПроведения.Сортировать("ДатаДокумента, ОчередьПроведения");
КонецПроцедуры
