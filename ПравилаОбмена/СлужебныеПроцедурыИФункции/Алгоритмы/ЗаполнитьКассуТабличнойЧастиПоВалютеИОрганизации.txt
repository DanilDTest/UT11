Процедура ЗаполнитьКассуТабличнойЧастиПоВалютеИОрганизации(ПолученныеДанные, КомпонентыОбмена, НаименованиеПодразделения = "")
	Если ЗначениеЗаполнено(НаименованиеПодразделения) Тогда
		Подразделение = Справочники.СтруктураПредприятия.НайтиПоНаименованию(НаименованиеПодразделения);
	Иначе
		Подразделение = Справочники.СтруктураПредприятия.ПустаяСсылка();
	КонецЕсли;
	
	Индекс = 0;
	Для Каждого Строка Из ПолученныеДанные.Кассы Цикл
		
		// Получаем валюту из исходной строки из дополнительных свойств. Валюту необходимо конвертировать в объект базы.
		Валюта = ПолученныеДанные.ДополнительныеСвойства.Кассы.Получить(Индекс).Валюта;
		
		РеквизитыКассы = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Строка.Касса, "ВалютаДенежныхСредств, Владелец, Подразделение");
		
		Если РеквизитыКассы.ВалютаДенежныхСредств <> Валюта 
			Или РеквизитыКассы.Владелец <> ПолученныеДанные.Организация
			Или ?(ЗначениеЗаполнено(Подразделение), РеквизитыКассы.Подразделение <> Подразделение, Ложь) Тогда
			
			ПараметрыОтбора = Новый Структура(
				"Валюта,Организация,Подразделение",
				Валюта,
				ПолученныеДанные.Организация,
				Подразделение);
			
			Строка.Касса = КассаПоУмолчанию(ПараметрыОтбора, КомпонентыОбмена);
			
		КонецЕсли;
		
		Индекс = Индекс + 1;
	КонецЦикла;
КонецПроцедуры
