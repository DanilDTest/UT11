Процедура ПКО_Документ_ПУПАЧерезПодотчетноеЛицо_Отправка_ПриОтправкеДанных(ДанныеИБ, ДанныеXDTO, КомпонентыОбмена, СтекВыгрузки)
	Номер = Лев(ДанныеИБ.Номер, 5) + "У" + Сред(ДанныеИБ.Номер, 7);
	ДанныеXDTO.КлючевыеСвойства.Вставить("Номер", Номер);	
	
	Если СтекВыгрузки.Количество() > 1 Тогда
		Возврат;
	КонецЕсли;
	
	ВыгрузитьПодразделение(ДанныеИБ, ДанныеXDTO);
	ВыгрузитьНалогообложениеНДС(КомпонентыОбмена, ДанныеИБ, ДанныеXDTO, , , "НалогообложениеНДСПродавца");
	
	ВалютаРеглУчета = ВалютаРегламентированногоУчета(КомпонентыОбмена);
	
	Если ДанныеИБ.Валюта = ВалютаРеглУчета
		Или ДанныеИБ.ВалютаВзаиморасчетов = ВалютаРеглУчета Тогда
		ДанныеXDTO.Вставить("КурсДокумента",      ДанныеИБ.Курс);
		ДанныеXDTO.Вставить("КратностьДокумента", ДанныеИБ.Кратность);
	Иначе
		ВыгрузитьКурсИКратностьДокумента(ДанныеXDTO, ДанныеИБ.Валюта, ДанныеИБ.Дата);
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Расходы.Ссылка.ДатаВходящегоДокумента 		КАК ДатаВходящегоДокумента,
	|	Расходы.Ссылка.НомерВходящегоДокумента		КАК НомерВходящегоДокумента,
	|	Расходы.Ссылка.Контрагент КАК Поставщик,
	|	ВЫБОР КОГДА СФ.Ссылка is null ТОГДА
	|		ЛОЖЬ
	|	ИНАЧЕ ИСТИНА КОНЕЦ 							КАК ПредъявленСФ,
	|	ВЫБОР КОГДА СФ.Ссылка is null ТОГДА
	|		""""
	|	ИНАЧЕ СФ.Ссылка.Номер КОНЕЦ 				КАК НомерСФ,
	|	ВЫБОР КОГДА СФ.Ссылка is null ТОГДА
	|		null
	|	ИНАЧЕ СФ.Ссылка.ДатаСоставления КОНЕЦ 		КАК ДатаСФ,
	|	Расходы.Содержание 							КАК Содержание,
	|	Расходы.Сумма 								КАК Сумма,
	|	Расходы.СтавкаНДС 							КАК СтавкаНДС,
	|	Расходы.СуммаНДС 							КАК СуммаНДС,
	|	Расходы.Подразделение КАК Подразделение,
	|	Расходы.СтатьяРасходов КАК СтатьяРасходов,
	|	Расходы.СчетУчета КАК СчетУчета
	|ИЗ
	|	Документ.ПриобретениеУслугПрочихАктивов.Расходы КАК Расходы
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	Документ.СчетФактураПолученный.ДокументыОснования КАК СФ
	|ПО СФ.ДокументОснование = Расходы.Ссылка
	|	И СФ.Ссылка.Проведен
	|ГДЕ
	|	Расходы.Ссылка = &Ссылка");
	Запрос.УстановитьПараметр("Ссылка", ДанныеИБ.Ссылка);
		
	ТабПрочее = Запрос.Выполнить().Выгрузить();
	ТабПрочее.Колонки.Добавить("ПодразделениеЗатрат");
	ТабПрочее.Колонки.Добавить("СчетЗатрат");
	ТабПрочее.Колонки.Добавить("СтатьяЗатрат");
	Для Каждого СтрокаПрочее Из ТабПрочее Цикл
		Если ЗначениеЗаполнено(СтрокаПрочее.Подразделение) Тогда
			СтруктураПодразделение = Новый Структура();
			ВыгрузитьПодразделение(ДанныеИБ, СтруктураПодразделение, СтрокаПрочее.Подразделение);
			СтрокаПрочее.ПодразделениеЗатрат = СтруктураПодразделение.Подразделение;
		КонецЕсли;
	
		СчетЗатрат = СчетУчетаПоСтатье(СтрокаПрочее.СтатьяРасходов, ДанныеИБ.Организация, СтрокаПрочее.Подразделение, СтрокаПрочее.СчетУчета);
	
		Если ТипЗнч(СтрокаПрочее.СтатьяРасходов) = Тип("ПланВидовХарактеристикСсылка.СтатьиРасходов") Тогда
			СтрокаПрочее.СтатьяЗатрат = СтрокаПрочее.СтатьяРасходов;
		КонецЕсли;
		
		СтрокаПрочее.СчетЗатрат = СчетЗатрат;
	КонецЦикла;
	
	ЗаполнитьВидДеятельностиНДСВТЧ(КомпонентыОбмена, ДанныеИБ, ТабПрочее, "ЗакупкаПодДеятельность", "ЗакупкаПодДеятельность");
	
	ДанныеXDTO.Вставить("ПрочиеРасходы", ТабПрочее);
	
	ВыгрузитьДополнительныеРеквизитыИСведения(КомпонентыОбмена, ДанныеИБ, ДанныеXDTO);
КонецПроцедуры
