Процедура ПОД_Документ_ВнутреннееПотреблениеТоваров_Отправка_ПриОбработке(ДанныеИБ, ИспользованиеПКО, КомпонентыОбмена)
	УстановитьИспользованиеПКО(ИспользованиеПКО, Ложь);
	
	Если ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.СписаниеТоваровПоТребованию Тогда
		ТекстЗапроса = 
			"ВЫБРАТЬ
			|	Аналитика.Номенклатура КАК Номенклатура,
			|	ВПТ_Товары.Характеристика КАК Характеристика,
			|	ВПТ_Товары.Серия КАК Серия,
			|	ВПТ_Товары.Упаковка КАК Упаковка,
			|	СУММА(ВПТ_Товары.КоличествоУпаковок) КАК КоличествоУпаковок,
			|	Аналитика.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
			|	СУММА(ВПТ_ВидыЗапасов.Количество) КАК Количество,
			|	ВПТ_ВидыЗапасов.НомерГТД.Код КАК НомерГТД,
			|	ВПТ_ВидыЗапасов.НомерГТД.СтранаПроисхождения КАК СтранаПроисхождения,
			|	ВПТ_ВидыЗапасов.СтатьяРасходов КАК СтатьяРасходов,
			|	ВПТ_Шапка.Подразделение КАК Подразделение,
			|	ВЫБОР
			|		КОГДА ВПТ_ВидыЗапасов.ГруппаПродукции = ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
			|			ТОГДА ЕСТЬNULL(Аналитика.Номенклатура.ГруппаАналитическогоУчета, ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка))
			|		ИНАЧЕ ВПТ_ВидыЗапасов.ГруппаПродукции
			|	КОНЕЦ КАК ГруппаАналитическогоУчетаНоменклатуры,
			|	ВПТ_Товары.СчетУчета КАК СчетУчета,
			|	ВЫБОР
			|		КОГДА ВПТ_ВидыЗапасов.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
			|			ТОГДА ВЫРАЗИТЬ(""КомиссионныеТовары"" КАК СТРОКА(18))
			|		ИНАЧЕ ВЫРАЗИТЬ(""СобственныеТовары"" КАК СТРОКА(18))
			|	КОНЕЦ КАК ТипЗапасов
			|ПОМЕСТИТЬ ТоварыВсе
			|ИЗ
			|	Документ.ВнутреннееПотреблениеТоваров КАК ВПТ_Шапка
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВнутреннееПотреблениеТоваров.ВидыЗапасов КАК ВПТ_ВидыЗапасов
			|		ПО (ВПТ_ВидыЗапасов.Ссылка = ВПТ_Шапка.Ссылка)
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
			|		ПО (ВПТ_ВидыЗапасов.АналитикаУчетаНоменклатуры = Аналитика.КлючАналитики)
			|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВнутреннееПотреблениеТоваров.Товары КАК ВПТ_Товары
			|		ПО (ВПТ_Товары.Ссылка = ВПТ_Шапка.Ссылка)
			|			И (ВПТ_Товары.ИдентификаторСтроки = ВПТ_ВидыЗапасов.ИдентификаторСтроки)
			|ГДЕ
			|	ВПТ_Шапка.Ссылка = &Ссылка
			|
			|СГРУППИРОВАТЬ ПО
			|	Аналитика.Номенклатура,
			|	Аналитика.Номенклатура.ЕдиницаИзмерения,
			|	ВПТ_ВидыЗапасов.НомерГТД.Код,
			|	ВПТ_ВидыЗапасов.НомерГТД.СтранаПроисхождения,
			|	ВПТ_ВидыЗапасов.СтатьяРасходов,
			|	ВПТ_Шапка.Подразделение,
			|	ВЫБОР
			|		КОГДА ВПТ_ВидыЗапасов.ГруппаПродукции = ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
			|			ТОГДА ЕСТЬNULL(Аналитика.Номенклатура.ГруппаАналитическогоУчета, ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка))
			|		ИНАЧЕ ВПТ_ВидыЗапасов.ГруппаПродукции
			|	КОНЕЦ,
			|	ВПТ_Товары.СчетУчета,
			|	ВЫБОР
			|		КОГДА ВПТ_ВидыЗапасов.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
			|			ТОГДА ВЫРАЗИТЬ(""КомиссионныеТовары"" КАК СТРОКА(18))
			|		ИНАЧЕ ВЫРАЗИТЬ(""СобственныеТовары"" КАК СТРОКА(18))
			|	КОНЕЦ,
			|	ВПТ_Товары.Характеристика,
			|	ВПТ_Товары.Упаковка,
			|	ВПТ_Товары.Серия
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	ТипЗапасов
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	Товары.Номенклатура КАК Номенклатура,
			|	Товары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
			|	&ТекстЗапросаХарактеристика,
			|	&ТекстЗапросаСерия,
			|	&ТекстЗапросаУпаковка,
			|	Товары.Количество КАК Количество,
			|	Товары.НомерГТД КАК НомерГТД,
			|	Товары.СтранаПроисхождения КАК СтранаПроисхождения,
			|	ВЫРАЗИТЬ("""" КАК СТРОКА(7)) КАК СчетЗатрат,
			|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяЗатрат,
			|	Товары.СтатьяРасходов КАК СтатьяРасходов,
			|	Товары.Подразделение КАК Подразделение,
			|	Товары.ГруппаАналитическогоУчетаНоменклатуры КАК ГруппаАналитическогоУчетаНоменклатуры,
			|	Товары.ТипЗапасов КАК ТипЗапасов,
			|	Товары.СчетУчета КАК СчетУчета
			|ИЗ
			|	ТоварыВсе КАК Товары
			|ГДЕ
			|	Товары.ТипЗапасов = ""СобственныеТовары""
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	Товары.Номенклатура КАК Номенклатура,
			|	Товары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
			|	&ТекстЗапросаХарактеристика,
			|	&ТекстЗапросаСерия,
			|	&ТекстЗапросаУпаковка,
			|	Товары.Количество КАК Количество,
			|	Товары.НомерГТД КАК НомерГТД,
			|	Товары.СтранаПроисхождения КАК СтранаПроисхождения,
			|	Товары.ТипЗапасов КАК ТипЗапасов
			|ИЗ
			|	ТоварыВсе КАК Товары
			|ГДЕ
			|	Товары.ТипЗапасов = ""КомиссионныеТовары""";
			
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаХарактеристика",
			ПолучитьТекстЗапросаХарактеристики(КомпонентыОбмена, "Товары"));
			
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаУпаковка",
			ПолучитьТекстЗапросаУпаковки(КомпонентыОбмена, "Товары"));
			
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаСерия",
			ПолучитьТекстЗапросаСерии(КомпонентыОбмена, "Товары"));
	
			
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("Ссылка", ДанныеИБ.Ссылка);
		Запрос.УстановитьПараметр("ЭтоУТ", ЭтоУТ());
		РезультатЗапроса = Запрос.ВыполнитьПакет();
		
		СобственныеТовары = РезультатЗапроса[1].Выгрузить();
		СобственныеТовары.Колонки.Добавить("ПодразделениеЗатрат");
		ЗаполнитьХарактеристикуУпаковкуВТЧ(КомпонентыОбмена, СобственныеТовары);
		
		КомиссионныеТовары = РезультатЗапроса[2].Выгрузить();
		ЗаполнитьХарактеристикуУпаковкуВТЧ(КомпонентыОбмена, КомиссионныеТовары);
		
		Для Каждого СтрокаСобственныеТовары Из СобственныеТовары Цикл
			Если ЗначениеЗаполнено(СтрокаСобственныеТовары.Подразделение) Тогда
				СтруктураПодразделение = Новый Структура;
				ВыгрузитьПодразделение(ДанныеИБ, СтруктураПодразделение, СтрокаСобственныеТовары.Подразделение);
				СтрокаСобственныеТовары.ПодразделениеЗатрат = СтруктураПодразделение.Подразделение;
			КонецЕсли;
		
			Если ЗначениеЗаполнено(СтрокаСобственныеТовары.ГруппаАналитическогоУчетаНоменклатуры)
				И СвойствоФорматаОбмена(КомпонентыОбмена, "Справочник.ГруппыАналитическогоУчетаНоменклатуры") Тогда
				СтруктураАналитики = Новый Структура;
				СтруктураАналитики.Вставить("Значение", СтрокаСобственныеТовары.ГруппаАналитическогоУчетаНоменклатуры);
				СтруктураАналитики.Вставить("ИмяПКО", ?(СтрокаСобственныеТовары.ГруппаАналитическогоУчетаНоменклатуры.ЭтоГруппа,
					"Спр_ГруппыАналитическогоУчетаНоменклатурыГруппа", "Спр_ГруппыАналитическогоУчетаНоменклатуры"));
				СтрокаСобственныеТовары.ГруппаАналитическогоУчетаНоменклатуры = СтруктураАналитики;
			КонецЕсли;
			
			СтрокаСобственныеТовары.СчетЗатрат = СчетУчетаПоСтатье(СтрокаСобственныеТовары.СтатьяРасходов,
				ДанныеИБ.Организация, ДанныеИБ.Подразделение, СтрокаСобственныеТовары.СчетУчета);
				
			Если ТипЗнч(СтрокаСобственныеТовары.СтатьяРасходов) = Тип("ПланВидовХарактеристикСсылка.СтатьиРасходов") Тогда
				СтрокаСобственныеТовары.СтатьяЗатрат = СтрокаСобственныеТовары.СтатьяРасходов;
			КонецЕсли;
		КонецЦикла;
		
		Если СобственныеТовары.Количество() > 0 Тогда
			ДанныеИБ.ДополнительныеСвойства.Вставить("СобственныеТовары", СобственныеТовары);
			ИспользованиеПКО.Документ_ВнутреннееПотребление_ВПроизводство_Отпр = Истина;
		КонецЕсли;
		
		Если КомиссионныеТовары.Количество() > 0 Тогда    
			ДанныеИБ.ДополнительныеСвойства.Вставить("КомиссионныеТовары", КомиссионныеТовары);
			ИспользованиеПКО.Документ_ВнутреннееПотребление_ВСписаниеЗапас_Отпр = Истина;
		КонецЕсли;
	ИначеЕсли ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаВЭксплуатацию Тогда
		ИспользованиеПКО.Документ_ВнутреннееПотребление_ВЭксплуатацию_Отпра = Истина;
	КонецЕсли;
	
	ПроверкаЗаполненияРеквизитовОбъектаИБ(ДанныеИБ, КомпонентыОбмена, ИспользованиеПКО);
КонецПроцедуры
