Процедура ПОД_Документ_СчетФактураВыданный_Отправка_ПриОбработке(ДанныеИБ, ИспользованиеПКО, КомпонентыОбмена)
	ИспользованиеПКО.Документ_СчетФактураВыданный_Отправка     = Ложь;
	ИспользованиеПКО.Документ_СчетФактураПолученныйИК_Отправка = Ложь;
	
	ОрганизацияОтправитель = ДанныеИБ.Организация;
	
	Если Не ЗначениеЗаполнено(ОрганизацияОтправитель) Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаОснование Из ДанныеИБ.ДокументыОснования Цикл
		
		Если НЕ ЗначениеЗаполнено(СтрокаОснование.ДокументОснование) Тогда
			Продолжить;
		КонецЕсли;
		
		Если НЕ ПроверитьДокументОснование(ДанныеИБ, СтрокаОснование.ДокументОснование, ИспользованиеПКО, КомпонентыОбмена) Тогда
			Возврат;
		КонецЕсли;
		
		ИспользованиеПКО.Документ_СчетФактураВыданный_Отправка = Истина;
		ТипОснования = ТипЗнч(СтрокаОснование.ДокументОснование);
		Если (ТипОснования = Тип("ДокументСсылка.ПередачаТоваровМеждуОрганизациями")
			Или ТипОснования = Тип("ДокументСсылка.ВозвратТоваровМеждуОрганизациями")
			Или ТипОснования = Тип("ДокументСсылка.ОтчетПоКомиссииМеждуОрганизациями"))
			И Не ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаОснование.ДокументОснование, "РасчетыЧерезОтдельногоКонтрагента") Тогда
			
			Если ТипОснования = Тип("ДокументСсылка.ОтчетПоКомиссииМеждуОрганизациями") Тогда
				ОрганизацияПолучатель_ИмяРеквизита = "Организация";
			Иначе
				ОрганизацияПолучатель_ИмяРеквизита = "ОрганизацияПолучатель";
			КонецЕсли;
			
			ОрганизацияПолучатель = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаОснование.ДокументОснование, ОрганизацияПолучатель_ИмяРеквизита);
			ИспользованиеПКО.Документ_СчетФактураПолученныйИК_Отправка = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ИспользованиеПКО.Документ_СчетФактураПолученныйИК_Отправка 
		И КомпонентыОбмена.ПараметрыКонвертации.ИспользоватьОтборПоОрганизациям Тогда
	
		УзелСписокОрганизаций = КомпонентыОбмена.ПараметрыКонвертации.Организации;
		
		ИспользованиеПКО.Документ_СчетФактураВыданный_Отправка     = УзелСписокОрганизаций.НайтиПоЗначению(ОрганизацияОтправитель) <> Неопределено;
		ИспользованиеПКО.Документ_СчетФактураПолученныйИК_Отправка = УзелСписокОрганизаций.НайтиПоЗначению(ОрганизацияПолучатель) <> Неопределено;
	КонецЕсли;
	
	ПроверкаЗаполненияРеквизитовОбъектаИБ(ДанныеИБ, КомпонентыОбмена, ИспользованиеПКО);
КонецПроцедуры
