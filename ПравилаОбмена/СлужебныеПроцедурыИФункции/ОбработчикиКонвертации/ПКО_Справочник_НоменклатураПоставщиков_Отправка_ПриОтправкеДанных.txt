Процедура ПКО_Справочник_НоменклатураПоставщиков_Отправка_ПриОтправкеДанных(ДанныеИБ, ДанныеXDTO, КомпонентыОбмена, СтекВыгрузки)
	ЕдиницаИзмерения = Неопределено;
	Владелец = Неопределено;
	Упаковка = Неопределено;
	
	БазоваяЕдИзм = Справочники.НаборыУпаковок.БазовыеЕдиницыИзмерения;
	Если ЗначениеЗаполнено(ДанныеИБ.Упаковка) И ОбщегоНазначения.СсылкаСуществует(ДанныеИБ.Упаковка)
		И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеИБ.Упаковка, "Владелец") = БазоваяЕдИзм Тогда
		
		ЕдиницаИзмерения = ДанныеИБ.Упаковка;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ЕдиницаИзмерения) И ЗначениеЗаполнено(ДанныеИБ.Номенклатура) Тогда
		ЕдиницаИзмерения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеИБ.Номенклатура, "ЕдиницаИзмерения");
	КонецЕсли;
	
	Если ВерсияФорматаЧислом(КомпонентыОбмена.ВерсияФорматаОбмена) >= ВерсияФорматаЧислом("1.8") Тогда
		Владелец = ПартнерыИКонтрагенты.ПолучитьКонтрагентаПартнераПоУмолчанию(ДанныеИБ.Владелец);
		
		Номенклатура = Новый Структура;
		Номенклатура.Вставить("Наименование", ДанныеИБ.Наименование);
		Номенклатура.Вставить("Артикул", ДанныеИБ.Артикул);
		Номенклатура.Вставить("ИдентификаторСервиса1СНоменклатура", ДанныеИБ.ИдентификаторНоменклатурыСервиса);
		
		Если (ЗначениеЗаполнено(ДанныеИБ.НаименованиеБазовойЕдиницыИзмерения) Или ЗначениеЗаполнено(ДанныеИБ.КодОКЕИБазовойЕдиницыИзмерения)) 
			И (ЗначениеЗаполнено(ДанныеИБ.НаименованиеУпаковки) Или ЗначениеЗаполнено(ДанныеИБ.НаименованиеБазовойЕдиницыИзмерения)) Тогда
				
			ЕдиницаИзмерения = Новый Структура;
			ЕдиницаИзмерения.Вставить("Наименование", ДанныеИБ.НаименованиеБазовойЕдиницыИзмерения);
			ЕдиницаИзмерения.Вставить("Код", ДанныеИБ.КодОКЕИБазовойЕдиницыИзмерения);
	
			Если ЗначениеЗаполнено(ДанныеИБ.НаименованиеУпаковки) Тогда
				Упаковка = Новый Структура;
				Упаковка.Вставить("Наименование", ДанныеИБ.НаименованиеУпаковки);
				Упаковка.Вставить("ЕдиницаИзмерения", ЕдиницаИзмерения);
				Упаковка.Вставить("Номенклатура", Номенклатура);
				Упаковка.Вставить("Коэффициент",?(ДанныеИБ.КоличествоУпаковок = 0,
					ДанныеИБ.КоличествоБазовойЕдиницыИзмерения,
					Окр(ДанныеИБ.КоличествоБазовойЕдиницыИзмерения / ДанныеИБ.КоличествоУпаковок,3)));
			Иначе
				Упаковка = Новый Структура;
				Упаковка.Вставить("Наименование", ДанныеИБ.НаименованиеБазовойЕдиницыИзмерения);
				Упаковка.Вставить("ЕдиницаИзмерения", ЕдиницаИзмерения);
				Упаковка.Вставить("Номенклатура", Номенклатура);
				Упаковка.Вставить("Коэффициент", 1);
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
	Если СтекВыгрузки.Количество() > 1 Тогда
		Если ВерсияФорматаЧислом(КомпонентыОбмена.ВерсияФорматаОбмена) >= ВерсияФорматаЧислом("1.8") Тогда
			ДанныеXDTO.Вставить("УпаковкаНоменклатуры", Упаковка);
			ДанныеXDTO.Вставить("Владелец", Владелец);
		Иначе
			ДанныеXDTO.Вставить("ЕдиницаИзмерения", ЕдиницаИзмерения);
		КонецЕсли;
		
		Возврат;
	КонецЕсли;
	
	Если ВерсияФорматаЧислом(КомпонентыОбмена.ВерсияФорматаОбмена) >= ВерсияФорматаЧислом("1.8") Тогда
		ДанныеXDTO.КлючевыеСвойства.Вставить("УпаковкаНоменклатуры", Упаковка);
		ДанныеXDTO.КлючевыеСвойства.Вставить("Владелец", Владелец);
		
		ДанныеXDTO.Вставить("ЕдиницаИзмерения", ЕдиницаИзмерения);
	Иначе
		ДанныеXDTO.КлючевыеСвойства.Вставить("ЕдиницаИзмерения", ЕдиницаИзмерения);
	КонецЕсли;
КонецПроцедуры
