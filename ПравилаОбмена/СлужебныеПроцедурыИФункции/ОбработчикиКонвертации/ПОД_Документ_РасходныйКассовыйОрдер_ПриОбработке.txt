Процедура ПОД_Документ_РасходныйКассовыйОрдер_ПриОбработке(ДанныеИБ, ИспользованиеПКО, КомпонентыОбмена)
	УстановитьИспользованиеПКО(ИспользованиеПКО, Ложь);
	
	ВидРасчетов = "";
	Если ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВнутренняяПередачаДенежныхСредств Тогда
	
		ИспользованиеПКО.Документ_РКОВнутренняяПередача_Отправка = Истина;
	
	ИначеЕсли ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.КонвертацияВалюты Тогда
	
		ИспользованиеПКО.Документ_РКОКонвертацияВалюты_Отправка = Истина;
	
	ИначеЕсли ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.СдачаДенежныхСредствВБанк
			ИЛИ ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ИнкассацияДенежныхСредствВБанк Тогда
	
		ИспользованиеПКО.Документ_РКОНаБанковскийСчет_Отправка = Истина;
	
	ИначеЕсли ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыплатаЗарплатыЧерезКассу
		ИЛИ ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыплатаЗарплатыРаботнику
		ИЛИ ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыплатаЗарплатыРаздатчиком Тогда
	
		ИспользованиеПКО.Документ_РКОВыплатаЗарплаты_Отправка = Истина;
	
	ИначеЕсли ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыдачаДенежныхСредствПодотчетнику Тогда
	
		ИспользованиеПКО.Документ_РКОРасчетыССотрудниками_Отправка = Истина;
		ВидРасчетов = "Подотчет";
		
	ИначеЕсли ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыдачаЗаймаСотруднику Тогда
		
		ИспользованиеПКО.Документ_РКОРасчетыССотрудниками_Отправка = Истина;
		ВидРасчетов = "Займы";
	
	ИначеЕсли ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПрочаяВыдачаДенежныхСредств
		ИЛИ ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПрочиеРасходы Тогда
		// Хоз. операция ПрочиеРасходы больше не будет использоваться. Оставлено на переходный период.
		// Прочий расход отражается видом операции ПрочаяВыдачаДенежныхСредств.
		ИспользованиеПКО.Документ_РКОПрочаяВыдача_Отправка = Истина;
	
		ВидОперацииПрочее = "ПрочееСписание";
		// Если заполнена статья расходов - то это прочий расход.
		Для Каждого СтрокаТЧ Из ДанныеИБ.РасшифровкаПлатежа Цикл
			Если ЗначениеЗаполнено(СтрокаТЧ.СтатьяРасходов)
				И ТипЗнч(СтрокаТЧ.СтатьяРасходов) = Тип("ПланВидовХарактеристикСсылка.СтатьиРасходов") Тогда
				ИспользованиеПКО.Документ_РКОПрочийРасход_Отправка = Истина;
				ИспользованиеПКО.Документ_РКОПрочаяВыдача_Отправка = Ложь;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	
	ИначеЕсли  ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратОплатыКлиенту
		ИЛИ ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствВДругуюОрганизацию Тогда
	
		ИспользованиеПКО.Документ_РКОРасчетыСКонтрагентами_Отправка = Истина;
		ВидРасчетов = "СПокупателем";
	
	ИначеЕсли ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОплатаПоставщику
		ИЛИ ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОплатаДенежныхСредствВДругуюОрганизацию Тогда
	
		ИспользованиеПКО.Документ_РКОРасчетыСКонтрагентами_Отправка = Истина;
		ВидРасчетов = "СПоставщиком";
	
	ИначеЕсли ЭтоХозяйственнаяОперацияРасчетыПоКредитамЗаймам(ДанныеИБ.ХозяйственнаяОперация) Тогда
	
		ИспользованиеПКО.Документ_РКОРасчетыСКонтрагентами_Отправка = Истина;
		ВидРасчетов = "КредитыИЗаймы";
	
	ИначеЕсли ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыдачаДенежныхСредствВКассуККМ Тогда
	
		Если ВерсияФорматаЧислом(КомпонентыОбмена.ВерсияФорматаОбмена) < ВерсияФорматаЧислом("1.4") Тогда
			ИспользованиеПКО.Удалить("Документ_РКОВыдачаВКассуККМ_Отправка");
		Иначе
			ИспользованиеПКО.Документ_РКОВыдачаВКассуККМ_Отправка = Истина;
		КонецЕсли;
	
	ИначеЕсли ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОплатаЛизингодателю Тогда
	
		ИспользованиеПКО.Документ_РКОРасчетыСКонтрагентами_Отправка = Истина;
		ВидРасчетов = "Прочее";
		
	ИначеЕсли ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыдачаДенежныхСредствВДругуюКассу Тогда
	
		Если ВерсияФорматаЧислом(КомпонентыОбмена.ВерсияФорматаОбмена) < ВерсияФорматаЧислом("1.6") Тогда
			ИспользованиеПКО.Удалить("Документ_РКОВыдачаВДругуюКассу_Отправка");
		Иначе
			ИспользованиеПКО.Документ_РКОВыдачаВДругуюКассу_Отправка = Истина;
		КонецЕсли;
	
	КонецЕсли;
	
	Если ВидРасчетов <> "" Тогда
	
		ДанныеШапки = Новый Структура();
		ДанныеШапки.Вставить("ВидРасчетов", ВидРасчетов);
		ДанныеИБ.ДополнительныеСвойства.Вставить("ДанныеШапки", ДанныеШапки);
	
	КонецЕсли;
	
	ПроверкаЗаполненияРеквизитовОбъектаИБ(ДанныеИБ, КомпонентыОбмена, ИспользованиеПКО);
КонецПроцедуры
