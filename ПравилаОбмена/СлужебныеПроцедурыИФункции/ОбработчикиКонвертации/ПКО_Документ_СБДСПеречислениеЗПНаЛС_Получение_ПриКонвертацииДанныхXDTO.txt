Процедура ПКО_Документ_СБДСПеречислениеЗПНаЛС_Получение_ПриКонвертацииДанныхXDTO(ДанныеXDTO, ПолученныеДанные, КомпонентыОбмена)
	ИспользуетсяНачислениеЗарплаты = Ложь;
	#Область Шапка
	
	ПолученныеДанные.Проведен = Истина;
	Если ДанныеXDTO.Свойство("ДатаВыписки") Тогда
		ПолученныеДанные.ДополнительныеСвойства.Вставить("ДатаВыписки", ДанныеXDTO.ДатаВыписки);
	КонецЕсли;
	ПолученныеДанные.ПроведеноБанком = ЗначениеЗаполнено(ПолученныеДанные.ДатаПроведенияБанком);
	ПолученныеДанные.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыплатаЗарплатыНаЛицевыеСчета;
	
	// СтатьяДвиженияДенежныхСредств
	ПолученныеДанные.ДополнительныеСвойства.Вставить("СтатьяДвиженияДенежныхСредств", ДанныеXDTO.РасшифровкаПлатежа[0].СтатьяДДС);
	ДобавитьВалютуВДопСвойства(ПолученныеДанные, ДанныеXDTO);
	// НомерВедомостиНаВыплатуЗарплаты, ДатаВедомостиНаВыплатуЗарплаты
	Если Не ИспользуетсяНачислениеЗарплаты Тогда
		ПолученныеДанные.ДополнительныеСвойства.Вставить("НомерВедомостиНаВыплатуЗарплаты",
			ДанныеXDTO.РасшифровкаПлатежа[0].НомерВедомостиНаВыплатуЗарплаты);
		ПолученныеДанные.ДополнительныеСвойства.Вставить("ДатаВедомостиНаВыплатуЗарплаты",
			ДанныеXDTO.РасшифровкаПлатежа[0].ДатаВедомостиНаВыплатуЗарплаты);
	КонецЕсли;
	
	#Область БанковскийСчетКонтрагента
	
	// Банковский счет контрагента, содержащийся в объекте формата, загружается в справочник
	// "Банковские счета контрагентов" и привязывается к контрагенту. Для этого документа нужен банковский
	// счет, привязанный к физическому лицу. Создаем и заполняем эти счета после загрузки всех данных.
	// Для этого запоминаем номер счета, чтобы после загрузки всех данных заполнить счет физического лица.
	Если ДанныеXDTO.Свойство("БанковскийСчет") И ЗначениеЗаполнено(ДанныеXDTO.БанковскийСчет) Тогда
		ПолученныеДанные.ДополнительныеСвойства.Вставить("БанковскийСчетКонтрагента_Номер",
			ДанныеXDTO.БанковскийСчет.НомерСчета);
	КонецЕсли;		
	
	#КонецОбласти
	
	#КонецОбласти
	
	Если ДанныеXDTO.Свойство("КодВидаДохода") 
		И ЗначениеЗаполнено(ДанныеXDTO.КодВидаДохода) Тогда
		ПолученныеДанные.ДополнительныеСвойства.Вставить("КодВидаДохода",
			ДанныеXDTO.КодВидаДохода);
	КонецЕсли;
	
	ЗагрузитьДополнительныеРеквизиты(ПолученныеДанные, ДанныеXDTO, КомпонентыОбмена);
КонецПроцедуры
