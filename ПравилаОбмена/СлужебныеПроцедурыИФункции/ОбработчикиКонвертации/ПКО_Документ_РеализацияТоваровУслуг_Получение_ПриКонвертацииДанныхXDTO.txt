Процедура ПКО_Документ_РеализацияТоваровУслуг_Получение_ПриКонвертацииДанныхXDTO(ДанныеXDTO, ПолученныеДанные, КомпонентыОбмена)
	ЗаполнитьНалогообложениеНДСПоПолученнымДанным(КомпонентыОбмена, ПолученныеДанные, ДанныеXDTO, Ложь);
	
	ДобавитьВалютуВДопСвойства(ПолученныеДанные, ДанныеXDTO);
	ДобавитьВалютуВДопСвойства(ПолученныеДанные, ДанныеXDTO, "ВалютаВзаиморасчетов", "ВалютаВзаиморасчетов");
	//ДобавитьДоговорВДопСвойства(ПолученныеДанные, ДанныеXDTO, КомпонентыОбмена);
	
	// Руководитель и главный бухгалтер
	Если ДанныеXDTO.Свойство("Руководитель") И ЗначениеЗаполнено(ДанныеXDTO.Руководитель) Тогда
		
		РуководительСтруктура = Новый Структура;
		РуководительСтруктура.Вставить("Владелец", ПолученныеДанные.Организация);
		РуководительСтруктура.Вставить("ФизическоеЛицо", ДанныеXDTO.Руководитель);
		РуководительСтруктура.Вставить("Должность", НСтр("ru = 'Руководитель';
														|en = 'Manager'"));
		РуководительСтруктура.Вставить("Наименование", НСтр("ru = 'Руководитель';
															|en = 'Manager'"));
		РуководительСтруктура.Вставить("ОтветственноеЛицо", "Руководитель");
		РуководительСтруктура.Вставить("ДатаНачала", Дата(1980,1,1));
		
		ПолученныеДанные.ДополнительныеСвойства.Вставить("Руководитель", РуководительСтруктура);
		
	КонецЕсли;
	
	Если ДанныеXDTO.Свойство("ГлавныйБухгалтер") И ЗначениеЗаполнено(ДанныеXDTO.ГлавныйБухгалтер) Тогда
		
		ГлавныйБухгалтерСтруктура = Новый Структура;
		ГлавныйБухгалтерСтруктура.Вставить("Владелец", ПолученныеДанные.Организация);
		ГлавныйБухгалтерСтруктура.Вставить("ФизическоеЛицо", ДанныеXDTO.ГлавныйБухгалтер);
		ГлавныйБухгалтерСтруктура.Вставить("Должность", НСтр("ru = 'Главный бухгалтер';
															|en = 'Chief Accountant'"));
		ГлавныйБухгалтерСтруктура.Вставить("Наименование", НСтр("ru = 'Главный бухгалтер';
																|en = 'Chief Accountant'"));
		ГлавныйБухгалтерСтруктура.Вставить("ОтветственноеЛицо", "ГлавныйБухгалтер");
		ГлавныйБухгалтерСтруктура.Вставить("ДатаНачала", Дата(1980,1,1));
		
		ПолученныеДанные.ДополнительныеСвойства.Вставить("ГлавныйБухгалтер", ГлавныйБухгалтерСтруктура);
		
	КонецЕсли;
	
	
	ПолученныеДанные.Согласован  = Истина;
	
	ПолученныеДанные.Статус = Перечисления.СтатусыРеализацийТоваровУслуг.Отгружено;
	Если ПолученныеДанные.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности
		И Не ЗначениеЗаполнено(ПолученныеДанные.ДатаПереходаПраваСобственности) Тогда
		ПолученныеДанные.Статус = Перечисления.СтатусыРеализацийТоваровУслуг.ВПути;
	КонецЕсли;
	
	ЗагрузитьДополнительныеРеквизиты(ПолученныеДанные, ДанныеXDTO, КомпонентыОбмена);
	
	#Область Товары
	
	ПравилаЗаполнения = Новый Соответствие;
	ПравилаЗаполнения.Вставить("Номенклатура",       "Номенклатура");
	ПравилаЗаполнения.Вставить("Количество",         "Количество");
	ПравилаЗаполнения.Вставить("Сумма",              "Сумма");       
	ПравилаЗаполнения.Вставить("КлючСвязи",          "КлючСвязи");
	ПравилаЗаполнения.Вставить("Цена",               "Цена");
	ПравилаЗаполнения.Вставить("СтавкаНДС",          "СтавкаНДС");
	ПравилаЗаполнения.Вставить("СуммаНДС",           "СуммаНДС");
	Если ЗначениеФО("ИспользоватьХарактеристикиНоменклатуры", КомпонентыОбмена) Тогда
		ПравилаЗаполнения.Вставить("Характеристика",     "Характеристика");
	КонецЕсли;
	Если ЗначениеФО("ИспользоватьУпаковкиНоменклатуры", КомпонентыОбмена) Тогда
		ПравилаЗаполнения.Вставить("Упаковка",           "Упаковка");
		ПравилаЗаполнения.Вставить("КоличествоУпаковок", "КоличествоУпаковок");
	КонецЕсли;
	Если ЗначениеФО("ИспользоватьСерииНоменклатуры", КомпонентыОбмена) Тогда
		ПравилаЗаполнения.Вставить("Серия",              "Серия");
	КонецЕсли;      
	
	ПравилаЗаполнения.Вставить("КлючСвязи",       "КлючСвязи");
	ПравилаЗаполнения.Вставить("КодСтроки",         "КодСтроки");
	
	
	// Товары 
	МассивСтрокТовары = Новый Массив;
	
	Если ДанныеXDTO.Свойство("Товары")
		И ЗначениеЗаполнено(ДанныеXDTO.Товары) Тогда
		
		Для Каждого Строка Из ДанныеXDTO.Товары Цикл
		
			СтруктураДанныхСтроки = ДанныеКоллекцииВВидеСтруктуры(Строка, ПравилаЗаполнения);
			Если СтруктураДанныхСтроки.Свойство("КоличествоУпаковок") Тогда
			    Если СтруктураДанныхСтроки.Свойство("Упаковка") И СтруктураДанныхСтроки.Свойство("Сумма") Тогда
					СтруктураДанныхСтроки.Вставить("Цена", СтруктураДанныхСтроки.Сумма / СтруктураДанныхСтроки.КоличествоУпаковок);
				КонецЕсли;
			Иначе
				СтруктураДанныхСтроки.Вставить("КоличествоУпаковок", СтруктураДанныхСтроки.Количество);
			КонецЕсли;
			Если ДанныеXDTO.Свойство("Склад") Тогда
				СтруктураДанныхСтроки.Вставить("Склад", ДанныеXDTO.Склад);
			КонецЕсли;
			
			СуммаСНДС = Строка.Сумма;
			Если СтруктураДанныхСтроки.Свойство("СуммаНДС") И НЕ ПолученныеДанные.ЦенаВключаетНДС Тогда
				СуммаСНДС = СуммаСНДС + СтруктураДанныхСтроки.СуммаНДС
			КонецЕсли;
			СтруктураДанныхСтроки.Вставить("СуммаСНДС", СуммаСНДС);
	
			МассивСтрокТовары.Добавить(СтруктураДанныхСтроки);
			
		КонецЦикла;
		
	КонецЕсли;
	
	// Услуги  загружаются в ТЧ Товары 
	Если ДанныеXDTO.Свойство("Услуги")
		И ЗначениеЗаполнено(ДанныеXDTO.Услуги) Тогда
	
		Для Каждого Строка Из ДанныеXDTO.Услуги Цикл
			СтруктураДанныхСтроки = ДанныеКоллекцииВВидеСтруктуры(Строка, ПравилаЗаполнения);
			СтруктураДанныхСтроки.Вставить("КоличествоУпаковок", СтруктураДанныхСтроки.Количество);
			
			СуммаСНДС = Строка.Сумма;
			Если СтруктураДанныхСтроки.Свойство("СуммаНДС") И НЕ ПолученныеДанные.ЦенаВключаетНДС Тогда
				СуммаСНДС = СуммаСНДС + СтруктураДанныхСтроки.СуммаНДС
			КонецЕсли;		
			СтруктураДанныхСтроки.Вставить("СуммаСНДС", СуммаСНДС);
	
			МассивСтрокТовары.Добавить(СтруктураДанныхСтроки);
		КонецЦикла;
		
	КонецЕсли;
	
	Если МассивСтрокТовары.Количество() > 0 Тогда
		ПолученныеДанные.ДополнительныеСвойства.Вставить("Товары", МассивСтрокТовары);
	КонецЕсли;
	
	#КонецОбласти
	
	// Штрихкоды упаковок.    
	МассивСтрокКМ = Новый Массив;
	
	ПравилаЗаполнения = Новый Соответствие;
	ПравилаЗаполнения.Вставить("ШтрихкодУпаковки",       "ШтрихкодУпаковки");                          
	Если ДанныеXDTO.Свойство("АкцизныеМарки")
		И ЗначениеЗаполнено(ДанныеXDTO.АкцизныеМарки) Тогда
	
		Для Каждого Строка Из ДанныеXDTO.АкцизныеМарки Цикл
			СтруктураДанныхСтроки = ДанныеКоллекцииВВидеСтруктуры(Строка, ПравилаЗаполнения);
		
			МассивСтрокКМ.Добавить(СтруктураДанныхСтроки);
		КонецЦикла;
		
	КонецЕсли;
	
	Если МассивСтрокКМ.Количество() > 0 Тогда
		ПолученныеДанные.ДополнительныеСвойства.Вставить("ШтрихкодыУпаковок", МассивСтрокКМ);
	КонецЕсли;
КонецПроцедуры
