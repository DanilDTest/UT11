Процедура ПКО_Документ_ПБДСКонвертацияВалюты_Получение_ПриКонвертацииДанныхXDTO(ДанныеXDTO, ПолученныеДанные, КомпонентыОбмена)
	ПолученныеДанные.Проведен = Истина;
	ЗаполнитьДатуИНомерВходящегоДокумента(ПолученныеДанные);
	ДобавитьВалютуВДопСвойства(ПолученныеДанные, ДанныеXDTO);
	
	ПолученныеДанные.ПроведеноБанком = ЗначениеЗаполнено(ПолученныеДанные.ДатаПроведенияБанком);
	// ХозяйственнаяОперация
	ПолученныеДанные.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.КонвертацияВалюты;
	
	// КурсКонвертации
	ПолученныеДанные.КурсКонвертации           = ДанныеXDTO.РасшифровкаПлатежа[0].КурсКонвертации;
	ПолученныеДанные.КратностьКурсаКонвертации = ДанныеXDTO.РасшифровкаПлатежа[0].КратностьКонвертации;
	
	// СтатьяДвиженияДенежныхСредств
	ПолученныеДанные.ДополнительныеСвойства.Вставить("СтатьяДвиженияДенежныхСредств",
		ДанныеXDTO.РасшифровкаПлатежа[0].СтатьяДДС);
	
	// ВалютаКонвертации
	ВалютаКонвертации = ДанныеXDTO.РасшифровкаПлатежа[0].ВалютаКонвертации;
	Если ТипЗнч(ВалютаКонвертации) = Тип("Структура")
		И ВалютаКонвертации.Свойство("Ссылка") = Ложь Тогда
		ИмяПКО = "Справочник_Валюты_ИзСтруктуры";
	Иначе
		ИмяПКО = "Справочник_Валюты";
	КонецЕсли;
	Инструкция = Новый Структура("Значение, ИмяПКО", ВалютаКонвертации, ИмяПКО);
	ПолученныеДанные.ДополнительныеСвойства.Вставить("ВалютаКонвертации", Инструкция);
	
	ЗагрузитьДополнительныеРеквизиты(ПолученныеДанные, ДанныеXDTO, КомпонентыОбмена);
	
	#Область РасшифровкаПлатежа
	
	МассивСтрокРасшифровка = Новый Массив;
	Для Каждого Строка Из ДанныеXDTO.РасшифровкаПлатежа Цикл
		
		ПравилаЗаполнения = Новый Соответствие;
		ПравилаЗаполнения.Вставить("СуммаВал", "Сумма");
		ПравилаЗаполнения.Вставить("СуммаВзаиморасчетов", "СуммаВзаиморасчетов");
		СтруктураДанныхСтроки = ДанныеКоллекцииВВидеСтруктуры(Строка, ПравилаЗаполнения);
		//ВалютаКонвертации
		ВалютаВзаиморасчетов = Строка.ВалютаКонвертации;
		Если ТипЗнч(ВалютаВзаиморасчетов) = Тип("Структура")
			И ВалютаВзаиморасчетов.Свойство("Ссылка") = Ложь Тогда
			ИмяПКО = "Справочник_Валюты_ИзСтруктуры";
		Иначе
			ИмяПКО = "Справочник_Валюты";
		КонецЕсли;
		Инструкция = Новый Структура("Значение, ИмяПКО", ВалютаВзаиморасчетов, ИмяПКО);
		СтруктураДанныхСтроки.Вставить("ВалютаВзаиморасчетов", Инструкция);
	
		МассивСтрокРасшифровка.Добавить(СтруктураДанныхСтроки);
		
	КонецЦикла;
	ПолученныеДанные.ДополнительныеСвойства.Вставить("РасшифровкаПлатежа", МассивСтрокРасшифровка);
	
	#КонецОбласти
КонецПроцедуры
