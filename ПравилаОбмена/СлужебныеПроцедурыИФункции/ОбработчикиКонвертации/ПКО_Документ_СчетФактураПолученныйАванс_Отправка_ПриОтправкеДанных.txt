Процедура ПКО_Документ_СчетФактураПолученныйАванс_Отправка_ПриОтправкеДанных(ДанныеИБ, ДанныеXDTO, КомпонентыОбмена, СтекВыгрузки)
	Если СтекВыгрузки.Количество()>1 Тогда
		Возврат;
	КонецЕсли;
	
	ВыгрузитьДополнительныеРеквизитыИСведения(КомпонентыОбмена, ДанныеИБ, ДанныеXDTO);
	
	Если ДанныеИБ.ПолученВЭлектронномВиде Тогда
		ДанныеXDTO.Вставить("СпособВыставления", "ВЭлектронномВиде");
	Иначе
		ДанныеXDTO.Вставить("СпособВыставления", "НаБумажномНосителе");
	КонецЕсли;
	
	Если ТипЗнч(ДанныеИБ.Контрагент) = Тип("СправочникСсылка.Организации") Тогда
			
		ОрганизацияКонтрагента = ДанныеИБ.Контрагент;
		СсылкаНаКонтрагента = КонтрагентИзОрганизации(ОрганизацияКонтрагента, КомпонентыОбмена);
			
	ИначеЕсли ТипЗнч(ДанныеИБ.Контрагент) = Тип("СправочникСсылка.Контрагенты") Тогда
		СсылкаНаКонтрагента = ДанныеИБ.Контрагент;
	Иначе
		СсылкаНаКонтрагента = Справочники.Контрагенты.ПустаяСсылка();
	КонецЕсли;
	
	ДанныеXDTO.КлючевыеСвойства.Вставить("Контрагент", СсылкаНаКонтрагента);
	
	Если ВерсияФорматаЧислом(КомпонентыОбмена.ВерсияФорматаОбмена) >= ВерсияФорматаЧислом("1.7") Тогда
		ДанныеXDTO.Вставить("ИННКонтрагента", ДанныеИБ.ИННКонтрагента);
		ДанныеXDTO.Вставить("КППКонтрагента", ДанныеИБ.КППКонтрагента);
	КонецЕсли;
	
	ДанныеXDTO.Вставить("ВидСчетаФактуры", "НаАванс");
	ВалютаРеглУчета = ВалютаРегламентированногоУчета(КомпонентыОбмена);
	
	// Вычисляем валюту документа-основания
	Если ЗначениеЗаполнено(ДанныеИБ.ДокументОснование)
		И Не ТипЗнч(ДанныеИБ.ДокументОснование) = Тип("ДокументСсылка.ВзаимозачетЗадолженности") Тогда
		
		ВалютаДокументаОснования = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеИБ.ДокументОснование, "Валюта");
		
		Если ЗначениеЗаполнено(ВалютаДокументаОснования) Тогда
			ДанныеXDTO.Вставить("Валюта", ВалютаДокументаОснования);
		Иначе
			ДанныеXDTO.Вставить("Валюта", ВалютаРеглУчета);
		КонецЕсли;
		
	Иначе
		ДанныеXDTO.Вставить("Валюта", ВалютаРеглУчета);
	КонецЕсли;
	
	// Получение информации из документа-основания.
	ДокументОснование_ВалютаВзаиморасчетов = ВалютаРеглУчета;
	ДокументОснование_ДоговорСсылка = Неопределено;
	ДокументОснование_РасчетыВУсловныхЕдиницах = Ложь;
	ДокументОснование_ИмяПКО = "";
	
	Заказ = Новый Структура("Заказ, Соглашение, Сделка");
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Ссылка", ДанныеИБ.ДокументОснование);
	Запрос.УстановитьПараметр("Контрагент", СсылкаНаКонтрагента);
	ТипЗначенияДокументаОснования = ТипЗнч(ДанныеИБ.ДокументОснование);
	
	Если ТипЗначенияДокументаОснования = Тип("ДокументСсылка.РасходныйКассовыйОрдер") 
		Или ТипЗначенияДокументаОснования = Тип("ДокументСсылка.СписаниеБезналичныхДенежныхСредств") Тогда
		
		Запрос.Текст = "
		|ВЫБРАТЬ
		|	РасшифровкаПлатежа.Заказ,
		|	РасшифровкаПлатежа.Заказ.Соглашение КАК Соглашение,
		|	РасшифровкаПлатежа.ВалютаВзаиморасчетов
		|ПОМЕСТИТЬ ДанныеДоговора
		|ИЗ
		|	Документ." + ДанныеИБ.ДокументОснование.Метаданные().Имя + ".РасшифровкаПлатежа КАК РасшифровкаПлатежа
		|ГДЕ
		|	РасшифровкаПлатежа.Ссылка = &Ссылка
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеДоговора.Соглашение
		|ИЗ
		|	ДанныеДоговора КАК ДанныеДоговора
		|;
		|
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеДоговора.Заказ
		|ИЗ
		|	ДанныеДоговора КАК ДанныеДоговора
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеДоговора.ВалютаВзаиморасчетов
		|ИЗ
		|	ДанныеДоговора КАК ДанныеДоговора";
		
		ДанныеДоговора = Запрос.ВыполнитьПакет();
		
		СверткаПоСоглашениям = ДанныеДоговора[1].Выгрузить();
		Если СверткаПоСоглашениям.Количество() = 1 Тогда
			Заказ.Соглашение = СверткаПоСоглашениям[0].Соглашение;
		КонецЕсли;
			
		СверткаПоЗаказам = ДанныеДоговора[2].Выгрузить();
		Если СверткаПоЗаказам.Количество() = 1 Тогда
			
			ЗаказПоставщику = СверткаПоЗаказам[0].Заказ;
			Если ТипЗнч(ЗаказПоставщику) = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
				ДокументОснование_ДоговорСсылка = ЗаказПоставщику;
			ИначеЕсли ТипЗнч(ЗаказПоставщику) = Тип("ДокументСсылка.ЗаказПоставщику") Тогда
				Заказ.Заказ = ЗаказПоставщику;
			КонецЕсли;
			
		КонецЕсли;
		
		СверткаПоВалютеВзаиморасчетов = ДанныеДоговора[3].Выгрузить();
		Если СверткаПоВалютеВзаиморасчетов.Количество() = 1 Тогда
			ДокументОснование_ВалютаВзаиморасчетов = СверткаПоВалютеВзаиморасчетов[0].ВалютаВзаиморасчетов;
		КонецЕсли;
		
		Если ТипЗначенияДокументаОснования = Тип("ДокументСсылка.РасходныйКассовыйОрдер") Тогда
			ДокументОснование_ИмяПКО = "Документ_РКОРасчетыСКонтрагентами_Отправка";
		Иначе
			ДокументОснование_ИмяПКО = "Документ_СБДСРасчетыСКонтрагентами_Отправка";
		КонецЕсли;
		
	ИначеЕсли ТипЗначенияДокументаОснования = Тип("ДокументСсылка.ВозвратТоваровПоставщику") Тогда
		Запрос.Текст = "
		|ВЫБРАТЬ
		|	РасшифровкаПлатежа.Ссылка.Соглашение КАК Соглашение,
		|	РасшифровкаПлатежа.Ссылка.Соглашение.Валюта КАК ВалютаСоглашения,
		|	РасшифровкаПлатежа.Заказ,
		|	РасшифровкаПлатежа.ВалютаВзаиморасчетов
		|ПОМЕСТИТЬ ДанныеДоговора
		|ИЗ
		|	Документ.ВозвратТоваровПоставщику.РасшифровкаПлатежа КАК РасшифровкаПлатежа
		|ГДЕ
		|	РасшифровкаПлатежа.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ДанныеДоговора.Соглашение,
		|	ДанныеДоговора.ВалютаВзаиморасчетов,
		|	ДанныеДоговора.ВалютаСоглашения
		|ИЗ
		|	ДанныеДоговора КАК ДанныеДоговора
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеДоговора.Заказ
		|ИЗ
		|	ДанныеДоговора КАК ДанныеДоговора";
		
		ДанныеДоговора = Запрос.ВыполнитьПакет();
		
		ОсновныеРеквизиты = ДанныеДоговора[1].Выгрузить();
		Если ОсновныеРеквизиты.Количество() > 0 Тогда
			Заказ.Соглашение = ОсновныеРеквизиты[0].Соглашение;
			
			Если ЗначениеЗаполнено(Заказ.Соглашение) Тогда
				ДокументОснование_ВалютаВзаиморасчетов = ОсновныеРеквизиты[0].ВалютаСоглашения;
			КонецЕсли;
		КонецЕсли;
		
		СверткаПоЗаказам = ДанныеДоговора[2].Выгрузить();
		Если СверткаПоЗаказам.Количество() = 1 Тогда
			ЗаказПоставщику = СверткаПоЗаказам[0].Заказ;
			Если ТипЗнч(ЗаказПоставщику) = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
				ДокументОснование_ДоговорСсылка = ЗаказПоставщику;
			ИначеЕсли ТипЗнч(ЗаказПоставщику) = Тип("ДокументСсылка.ЗаказПоставщику") Тогда
				Заказ.Заказ = ЗаказПоставщику;
			КонецЕсли;
		КонецЕсли;
		ДокументОснование_ИмяПКО = "Документ_ВозвратТоваровПоставщику_Отправка";
	
	ИначеЕсли ТипЗначенияДокументаОснования = Тип("ДокументСсылка.АвансовыйОтчет") Тогда	
	
		Запрос.Текст = "
		|ВЫБРАТЬ
		|	ОплатаПоставщикам.ВалютаВзаиморасчетов,
		|	ОплатаПоставщикам.Заказ.Соглашение КАК Соглашение,
		|	ОплатаПоставщикам.Заказ КАК Заказ
		|ПОМЕСТИТЬ ДанныеДоговора
		|ИЗ
		|	Документ.АвансовыйОтчет.ОплатаПоставщикам КАК ОплатаПоставщикам
		|ГДЕ
		|	ОплатаПоставщикам.Ссылка = &Ссылка
		|	И ОплатаПоставщикам.Контрагент = &Контрагент
		|;
		|
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеДоговора.Соглашение
		|ИЗ
		|	ДанныеДоговора КАК ДанныеДоговора
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеДоговора.Заказ
		|ИЗ
		|	ДанныеДоговора КАК ДанныеДоговора
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеДоговора.ВалютаВзаиморасчетов
		|ИЗ
		|	ДанныеДоговора КАК ДанныеДоговора";
		
	
		ДанныеДоговора = Запрос.ВыполнитьПакет();
		
		СверткаПоСоглашениям = ДанныеДоговора[1].Выгрузить();
		Если СверткаПоСоглашениям.Количество() = 1 Тогда
			Заказ.Соглашение = СверткаПоСоглашениям[0].Соглашение;
		КонецЕсли;
		
		СверткаПоЗаказам = ДанныеДоговора[2].Выгрузить();
		Если СверткаПоЗаказам.Количество() = 1 Тогда
			
			ЗаказПоставщику = СверткаПоЗаказам[0].Заказ;
			Если ТипЗнч(ЗаказПоставщику) = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
				ДокументОснование_ДоговорСсылка = ЗаказПоставщику;
			ИначеЕсли ТипЗнч(ЗаказПоставщику) = Тип("ДокументСсылка.ЗаказПоставщику") Тогда
				Заказ.Заказ = ЗаказПоставщику;
			КонецЕсли;
			
		КонецЕсли;
		
		СверткаПоВалютеВзаиморасчетов = ДанныеДоговора[3].Выгрузить();
		Если СверткаПоВалютеВзаиморасчетов.Количество() = 1 Тогда
			ДокументОснование_ВалютаВзаиморасчетов = СверткаПоВалютеВзаиморасчетов[0].ВалютаВзаиморасчетов;
		КонецЕсли;
		ДокументОснование_ИмяПКО = "Документ_АвансовыйОтчет_Отправка";
	
	ИначеЕсли ТипЗначенияДокументаОснования = Тип("ДокументСсылка.ВзаимозачетЗадолженности") Тогда
		
		ИмяТЧ = "КредиторскаяЗадолженность";
		ТипРасчетов = "РасчетыСПоставщиком";
		
		Если ДанныеИБ.Организация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеИБ.ДокументОснование, "КонтрагентКредитор") Тогда
			ИмяТЧ = "ДебиторскаяЗадолженность";
			ТипРасчетов = "РасчетыСКлиентом";
		КонецЕсли;
		
		Запрос.Текст = "
		|ВЫБРАТЬ
		|	ТабДок.Заказ,
		|	ТабДок.ВалютаВзаиморасчетов,
		|	ТабДок.Заказ.Соглашение КАК Соглашение
		|ПОМЕСТИТЬ ДанныеДоговора
		|ИЗ
		|	Документ.ВзаимозачетЗадолженности." + ИмяТЧ + " КАК ТабДок
		|ГДЕ
		|	ТабДок.Ссылка = &Ссылка
		|	И ТабДок.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами." + ТипРасчетов + ")
		|;
		|
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеДоговора.Соглашение
		|ИЗ
		|	ДанныеДоговора КАК ДанныеДоговора
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеДоговора.Заказ
		|ИЗ
		|	ДанныеДоговора КАК ДанныеДоговора
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеДоговора.ВалютаВзаиморасчетов
		|ИЗ
		|	ДанныеДоговора КАК ДанныеДоговора";
		
		ДанныеДоговора = Запрос.ВыполнитьПакет();
		
		СверткаПоСоглашениям = ДанныеДоговора[1].Выгрузить();
		Если СверткаПоСоглашениям.Количество() = 1 Тогда
			Заказ.Соглашение = СверткаПоСоглашениям[0].Соглашение;
		КонецЕсли;  
			
		СверткаПоЗаказам = ДанныеДоговора[2].Выгрузить();
		Если СверткаПоЗаказам.Количество() = 1 Тогда
			
			ЗаказПоставщику = СверткаПоЗаказам[0].Заказ;
			Если ТипЗнч(ЗаказПоставщику) = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
				ДокументОснование_ДоговорСсылка = ЗаказПоставщику;
			ИначеЕсли ТипЗнч(ЗаказПоставщику) = Тип("ДокументСсылка.ЗаказПоставщику") Тогда
				Заказ.Заказ = ЗаказПоставщику;
			КонецЕсли;
			
		КонецЕсли;
		
		СверткаПоВалютеВзаиморасчетов = ДанныеДоговора[3].Выгрузить();
		Если СверткаПоВалютеВзаиморасчетов.Количество() = 1 Тогда
			ДокументОснование_ВалютаВзаиморасчетов = СверткаПоВалютеВзаиморасчетов[0].ВалютаВзаиморасчетов;
		КонецЕсли;
		ДокументОснование_ИмяПКО = "Документ_ВЗ_НашаОрганизация";
	ИначеЕсли ТипЗначенияДокументаОснования = Тип("ДокументСсылка.ВозвратТоваровМеждуОрганизациями") Тогда
		
		Запрос.Текст = "
		|ВЫБРАТЬ
		|	Док.Валюта
		|ИЗ
		|	Документ.ВозвратТоваровМеждуОрганизациями КАК Док
		|ГДЕ
		|	Док.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	РасшифровкаПлатежа.ВалютаВзаиморасчетов
		|ИЗ
		|	Документ.ВозвратТоваровМеждуОрганизациями.РасшифровкаПлатежа КАК РасшифровкаПлатежа
		|ГДЕ
		|	РасшифровкаПлатежа.Ссылка = &Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	РасшифровкаПлатежа.НомерСтроки";
		
		РезультатЗапроса = Запрос.ВыполнитьПакет();	
		ДанныеШапки              = РезультатЗапроса[0].Выгрузить()[0];
		ДанныеРасшифровкиПлатежа = РезультатЗапроса[1].Выгрузить();
		
		Если ДанныеРасшифровкиПлатежа.Количество() = 1 Тогда
			ДокументОснование_ВалютаВзаиморасчетов = ДанныеРасшифровкиПлатежа[0].ВалютаВзаиморасчетов;
			ДокументОснование_РасчетыВУсловныхЕдиницах = ДанныеШапки.Валюта = ВалютаРеглУчета
				И ДанныеШапки.Валюта <> ДанныеXDTO.Валюта;
		КонецЕсли;
		ДокументОснование_ИмяПКО = "ВозвратМеждуОрганизациями_Поставщику_Отправка";
	КонецЕсли;	
	Если ЗначениеЗаполнено(ДокументОснование_ДоговорСсылка) Тогда
		ДанныеXDTO.Вставить("Договор", ДокументОснование_ДоговорСсылка);
	Иначе
		Если Не ЗначениеЗаполнено(ДокументОснование_ВалютаВзаиморасчетов) Тогда
			ДокументОснование_ВалютаВзаиморасчетов = ДанныеXDTO.Валюта;
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ДокументОснование_РасчетыВУсловныхЕдиницах) Тогда
			Если ЗначениеЗаполнено(Заказ.Соглашение) Тогда
				ДокументОснование_РасчетыВУсловныхЕдиницах = ВзаиморасчетыСервер.РасчетыВУсловныхЕдиницах(Заказ.Соглашение);
			Иначе
				ДокументОснование_РасчетыВУсловныхЕдиницах = ДанныеXDTO.Валюта = ВалютаРеглУчета
					И ДанныеXDTO.Валюта <> ДокументОснование_ВалютаВзаиморасчетов;
			КонецЕсли;
		КонецЕсли;
		
		ПараметрыПоУмолчанию = Новый Структура(ДоговорФиксированнаяСтруктураКлючей());
		ПараметрыПоУмолчанию.Контрагент = СсылкаНаКонтрагента;
		ПараметрыПоУмолчанию.Организация = ДанныеИБ.Организация;
		ПараметрыПоУмолчанию.ВалютаВзаиморасчетов = ДокументОснование_ВалютаВзаиморасчетов;
		ПараметрыПоУмолчанию.ВидДоговора = "СПоставщиком";
		ПараметрыПоУмолчанию.Дата = ДанныеИБ.Дата;
		ПараметрыПоУмолчанию.Номер = ДанныеИБ.Номер;
		ПараметрыПоУмолчанию.РасчетыВУсловныхЕдиницах = ДокументОснование_РасчетыВУсловныхЕдиницах;
		
		Договор = ДоговорИнструкцияКонвертацииПоДаннымВзаиморасчетов(ПараметрыПоУмолчанию, КомпонентыОбмена, Заказ, ДанныеИБ);
		ДанныеXDTO.Вставить("Договор", Договор);
	КонецЕсли;
	
	ДокументыОснования = Новый ТаблицаЗначений();
	ДокументыОснования.Колонки.Добавить("ДокументОснование");
	СтрокаОснование = ДокументыОснования.Добавить();	
	СтрокаОснование.ДокументОснование = Новый Структура("Значение, ИмяПКО",
		ДанныеИБ.ДокументОснование, ДокументОснование_ИмяПКО);
	ДанныеXDTO.Вставить("ДокументыОснования", ДокументыОснования);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	Сумма,
	|	СтавкаНДС,
	|	СуммаНДС
	|ИЗ Документ.СчетФактураПолученныйАванс.Авансы
	|ГДЕ Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", ДанныеИБ.Ссылка);
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		ДанныеXDTO.Вставить("Авансы", Результат.Выгрузить());
	КонецЕсли;
КонецПроцедуры
