Процедура ПОД_Документ_ОтчетОРозничныхПродажах_ПриОбработке(ДанныеИБ, ИспользованиеПКО, КомпонентыОбмена)
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
		|	ВидыЗапасов.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
		|	ВидыЗапасов.АналитикаУчетаНоменклатуры.Серия КАК Серия,
		|	ВидыЗапасов.Количество КАК Количество,
		|	ВидыЗапасов.СтавкаНДС КАК СтавкаНДС,
		|	ВидыЗапасов.СуммаНДС КАК СуммаНДС,
		|	ВидыЗапасов.НомерГТД.Код КАК НомерГТД,
		|	ВидыЗапасов.НомерГТД.СтранаПроисхождения КАК СтранаПроисхождения,
		|	ВидыЗапасов.ВидЗапасов.Контрагент КАК Поставщик,
		|	ВЫБОР
		|		КОГДА ВЫБОР
		|				КОГДА ВидыЗапасов.Ссылка.ЦенаВключаетНДС
		|					ТОГДА ВидыЗапасов.СуммаСНДС / ВидыЗапасов.Количество
		|				ИНАЧЕ (ВидыЗапасов.СуммаСНДС - ВидыЗапасов.СуммаНДС) / ВидыЗапасов.Количество
		|			КОНЕЦ < 0.01
		|			ТОГДА 0.01
		|		ИНАЧЕ ВЫРАЗИТЬ(ВЫБОР
		|					КОГДА ВидыЗапасов.Ссылка.ЦенаВключаетНДС
		|						ТОГДА ВидыЗапасов.СуммаСНДС / ВидыЗапасов.Количество
		|					ИНАЧЕ (ВидыЗапасов.СуммаСНДС - ВидыЗапасов.СуммаНДС) / ВидыЗапасов.Количество
		|				КОНЕЦ КАК ЧИСЛО(15, 2))
		|	КОНЕЦ КАК Цена,
		|	ВЫБОР
		|		КОГДА ВидыЗапасов.Ссылка.ЦенаВключаетНДС
		|			ТОГДА ВидыЗапасов.СуммаСНДС
		|		ИНАЧЕ ВидыЗапасов.СуммаСНДС - ВидыЗапасов.СуммаНДС
		|	КОНЕЦ КАК Сумма,
		|	ВЫБОР
		|		КОГДА ВидыЗапасов.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
		|			ТОГДА ВЫРАЗИТЬ(""КомиссионныеТовары"" КАК СТРОКА(18))
		|		ИНАЧЕ ВЫРАЗИТЬ(""СобственныеТовары"" КАК СТРОКА(18))
		|	КОНЕЦ КАК ТипЗапасов
		|ПОМЕСТИТЬ вт_ВидыЗапасов
		|ИЗ
		|	Документ.ОтчетОРозничныхПродажах.ВидыЗапасов КАК ВидыЗапасов
		|ГДЕ
		|	ВидыЗапасов.Ссылка = &Ссылка
		|	И ВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга)
		|	И ВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РеализацияПодарочныхСертификатов.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ ВТ_РеализацияПС
		|ИЗ
		|	Документ.РеализацияПодарочныхСертификатов КАК РеализацияПодарочныхСертификатов
		|ГДЕ
		|	РеализацияПодарочныхСертификатов.КассоваяСмена = &КассоваяСмена
		|	И РеализацияПодарочныхСертификатов.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЧековККМ.Пробит)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОтчетОРозничныхПродажахАкцизныеМарки.НомерСтроки КАК НомерСтрокиДокумента,
		|	ОтчетОРозничныхПродажахАкцизныеМарки.АкцизнаяМарка КАК АкцизнаяМарка,
		|	ШтрихкодыУпаковокТоваров.ТипУпаковки КАК ТипУпаковки,
		|	ШтрихкодыУпаковокТоваров.Номенклатура КАК Номенклатура,
		|	ШтрихкодыУпаковокТоваров.Характеристика КАК Характеристика,
		|	ШтрихкодыУпаковокТоваров.Серия КАК Серия,
		|	ШтрихкодыУпаковокТоваров.Упаковка КАК Упаковка,
		|	ШтрихкодыУпаковокТоваров.ЗначениеШтрихкода КАК НомерАкцизнойМарки
		|ПОМЕСТИТЬ втАкцизныеМарки
		|ИЗ
		|	Документ.ОтчетОРозничныхПродажах.АкцизныеМарки КАК ОтчетОРозничныхПродажахАкцизныеМарки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ШтрихкодыУпаковокТоваров КАК ШтрихкодыУпаковокТоваров
		|		ПО ОтчетОРозничныхПродажахАкцизныеМарки.АкцизнаяМарка = ШтрихкодыУпаковокТоваров.Ссылка
		|ГДЕ
		|	ОтчетОРозничныхПродажахАкцизныеМарки.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТабТовары.Номенклатура КАК Номенклатура,
		|	ТабТовары.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	&ТекстЗапросаХарактеристика,
		|	&ТекстЗапросаСерия,
		|	ТабТовары.Цена КАК Цена,
		|	ТабТовары.СтавкаНДС КАК СтавкаНДС,
		|	ТабТовары.НомерГТД КАК НомерГТД,
		|	ТабТовары.СтранаПроисхождения КАК СтранаПроисхождения,
		|	ТабТовары.Поставщик КАК Поставщик,
		|	ТабТовары.ТипЗапасов КАК ТипЗапасов,
		|	ТабТовары.Количество КАК Количество,
		|	ТабТовары.Сумма КАК Сумма,
		|	ТабТовары.СуммаНДС КАК СуммаНДС
		|ИЗ
		|	вт_ВидыЗапасов КАК ТабТовары
		|ГДЕ
		|	НЕ &ВозвратыОтдельно
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТабТовары.Номенклатура КАК Номенклатура,
		|	ТабТовары.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	&ТекстЗапросаХарактеристика,
		|	&ТекстЗапросаСерия,
		|	ТабТовары.Цена КАК Цена,
		|	ТабТовары.СтавкаНДС КАК СтавкаНДС,
		|	ТабТовары.НомерГТД КАК НомерГТД,
		|	ТабТовары.СтранаПроисхождения КАК СтранаПроисхождения,
		|	ТабТовары.Поставщик КАК Поставщик,
		|	ТабТовары.ТипЗапасов КАК ТипЗапасов,
		|	ТабТовары.Количество КАК Количество,
		|	ТабТовары.Сумма КАК Сумма,
		|	ТабТовары.СуммаНДС КАК СуммаНДС
		|ИЗ
		|	вт_ВидыЗапасов КАК ТабТовары
		|ГДЕ
		|	&ВозвратыОтдельно
		|	И ТабТовары.Количество > 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТабТовары.Номенклатура КАК Номенклатура,
		|	ТабТовары.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	&ТекстЗапросаХарактеристика,
		|	&ТекстЗапросаСерия,
		|	ТабТовары.Цена КАК Цена,
		|	ТабТовары.СтавкаНДС КАК СтавкаНДС,
		|	ТабТовары.НомерГТД КАК НомерГТД,
		|	ТабТовары.СтранаПроисхождения КАК СтранаПроисхождения,
		|	ТабТовары.Поставщик КАК Поставщик,
		|	ТабТовары.ТипЗапасов КАК ТипЗапасов,
		|	-ТабТовары.Количество КАК Количество,
		|	-ТабТовары.Сумма КАК Сумма,
		|	-ТабТовары.СуммаНДС КАК СуммаНДС
		|ИЗ
		|	вт_ВидыЗапасов КАК ТабТовары
		|ГДЕ
		|	&ВозвратыОтдельно
		|	И ТабТовары.Количество < 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Товары.Номенклатура КАК Номенклатура,
		|	Товары.Характеристика КАК Характеристика,
		|	Товары.Номенклатура.Наименование КАК Содержание,
		|	Товары.Количество КАК Количество,
		|	Товары.Цена КАК Цена,
		|	Товары.Сумма КАК Сумма,
		|	Товары.СтавкаНДС КАК СтавкаНДС,
		|	Товары.Номенклатура.Принципал КАК ПринципалСсылка,
		|	ВЫБОР
		|		КОГДА Товары.Номенклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ОрганизациейПоАгентскойСхеме)
		|				ИЛИ Товары.Номенклатура.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.Партнером)
		|			ТОГДА ""Агентские""
		|		ИНАЧЕ ""Собственные""
		|	КОНЕЦ КАК ТипУслуги,
		|	Товары.СуммаНДС КАК СуммаНДС
		|ИЗ
		|	Документ.ОтчетОРозничныхПродажах.Товары КАК Товары
		|ГДЕ
		|	Товары.Ссылка = &Ссылка
		|	И (Товары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга)
		|			ИЛИ Товары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОплатаПлатежнымиКартами.Сумма КАК СуммаОплаты,
		|	ОплатаПлатежнымиКартами.НомерПлатежнойКарты КАК НомерКарты,
		|	""Неопределено"" КАК ВидКарты,
		|	ОплатаПлатежнымиКартами.ЭквайринговыйТерминал.БанковскийСчет КАК БанковскийСчетОрганизации,
		|	ОплатаПлатежнымиКартами.ЭквайринговыйТерминал.Код КАК Номер,
		|	ОплатаПлатежнымиКартами.ЭквайринговыйТерминал.Ссылка КАК Ссылка,
		|	ОплатаПлатежнымиКартами.ЭквайринговыйТерминал.Эквайер КАК Эквайрер
		|ИЗ
		|	Документ.ОтчетОРозничныхПродажах.ОплатаПлатежнымиКартами КАК ОплатаПлатежнымиКартами
		|ГДЕ
		|	ОплатаПлатежнымиКартами.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(ПодарочныеСертификаты.Сумма) КАК Сумма,
		|	ПодарочныеСертификаты.ПодарочныйСертификат.Код КАК СерийныйНомер,
		|	ПодарочныеСертификаты.ПодарочныйСертификат.Штрихкод КАК Штрихкод,
		|	ПодарочныеСертификаты.ПодарочныйСертификат.МагнитныйКод КАК МагнитныйКод,
		|	ПодарочныеСертификаты.ПодарочныйСертификат.Владелец КАК ВидПодарочногоСертификатаСсылка
		|ИЗ
		|	Документ.ОтчетОРозничныхПродажах.ПодарочныеСертификаты КАК ПодарочныеСертификаты
		|ГДЕ
		|	ПодарочныеСертификаты.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ПодарочныеСертификаты.ПодарочныйСертификат.Код,
		|	ПодарочныеСертификаты.ПодарочныйСертификат.Штрихкод,
		|	ПодарочныеСертификаты.ПодарочныйСертификат.МагнитныйКод,
		|	ПодарочныеСертификаты.ПодарочныйСертификат.Владелец
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РеализацияПодарочныхСертификатов.Сумма КАК Сумма,
		|	РеализацияПодарочныхСертификатов.ПодарочныйСертификат.Штрихкод КАК Штрихкод,
		|	РеализацияПодарочныхСертификатов.ПодарочныйСертификат.МагнитныйКод КАК МагнитныйКод,
		|	РеализацияПодарочныхСертификатов.ПодарочныйСертификат.Код КАК СерийныйНомер,
		|	ВидыПодарочныхСертификатов.ЧастичнаяОплата КАК РазрешенаЧастичнаяОплата,
		|	ВидыПодарочныхСертификатов.Ссылка КАК ВидПодарочногоСертификата,
		|	МАКСИМУМ(ЕСТЬNULL(ВЫБОР
		|				КОГДА ВидыПодарочныхСертификатов.ПериодДействия = ЗНАЧЕНИЕ(Перечисление.Периодичность.День)
		|					ТОГДА ДОБАВИТЬКДАТЕ(АктивацияПодарочныхСертификатов.Период, ДЕНЬ, ВидыПодарочныхСертификатов.КоличествоПериодовДействия)
		|				КОГДА ВидыПодарочныхСертификатов.ПериодДействия = ЗНАЧЕНИЕ(Перечисление.Периодичность.Неделя)
		|					ТОГДА ДОБАВИТЬКДАТЕ(АктивацияПодарочныхСертификатов.Период, НЕДЕЛЯ, ВидыПодарочныхСертификатов.КоличествоПериодовДействия)
		|				КОГДА ВидыПодарочныхСертификатов.ПериодДействия = ЗНАЧЕНИЕ(Перечисление.Периодичность.Месяц)
		|					ТОГДА ДОБАВИТЬКДАТЕ(АктивацияПодарочныхСертификатов.Период, МЕСЯЦ, ВидыПодарочныхСертификатов.КоличествоПериодовДействия)
		|				КОГДА ВидыПодарочныхСертификатов.ПериодДействия = ЗНАЧЕНИЕ(Перечисление.Периодичность.Квартал)
		|					ТОГДА ДОБАВИТЬКДАТЕ(АктивацияПодарочныхСертификатов.Период, КВАРТАЛ, ВидыПодарочныхСертификатов.КоличествоПериодовДействия)
		|				КОГДА ВидыПодарочныхСертификатов.ПериодДействия = ЗНАЧЕНИЕ(Перечисление.Периодичность.Год)
		|					ТОГДА ДОБАВИТЬКДАТЕ(АктивацияПодарочныхСертификатов.Период, ГОД, ВидыПодарочныхСертификатов.КоличествоПериодовДействия)
		|				КОГДА ВидыПодарочныхСертификатов.ПериодДействия = ЗНАЧЕНИЕ(Перечисление.Периодичность.Декада)
		|					ТОГДА ДОБАВИТЬКДАТЕ(АктивацияПодарочныхСертификатов.Период, ДЕКАДА, ВидыПодарочныхСертификатов.КоличествоПериодовДействия)
		|				КОГДА ВидыПодарочныхСертификатов.ПериодДействия = ЗНАЧЕНИЕ(Перечисление.Периодичность.Полугодие)
		|					ТОГДА ДОБАВИТЬКДАТЕ(АктивацияПодарочныхСертификатов.Период, ПОЛУГОДИЕ, ВидыПодарочныхСертификатов.КоличествоПериодовДействия)
		|				ИНАЧЕ АктивацияПодарочныхСертификатов.Период
		|			КОНЕЦ, ДАТАВРЕМЯ(1, 1, 1))) КАК ДействителенДо
		|ИЗ
		|	Документ.РеализацияПодарочныхСертификатов.ПодарочныеСертификаты КАК РеализацияПодарочныхСертификатов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_РеализацияПС КАК ВТ_РеализацияПС
		|		ПО РеализацияПодарочныхСертификатов.Ссылка = ВТ_РеализацияПС.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыПодарочныхСертификатов КАК ВидыПодарочныхСертификатов
		|		ПО РеализацияПодарочныхСертификатов.ПодарочныйСертификат.Владелец = ВидыПодарочныхСертификатов.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ИсторияПодарочныхСертификатов КАК АктивацияПодарочныхСертификатов
		|		ПО РеализацияПодарочныхСертификатов.ПодарочныйСертификат = АктивацияПодарочныхСертификатов.ПодарочныйСертификат
		|			И (АктивацияПодарочныхСертификатов.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыПодарочныхСертификатов.Активирован))
		|
		|СГРУППИРОВАТЬ ПО
		|	РеализацияПодарочныхСертификатов.Сумма,
		|	РеализацияПодарочныхСертификатов.ПодарочныйСертификат.Штрихкод,
		|	РеализацияПодарочныхСертификатов.ПодарочныйСертификат.МагнитныйКод,
		|	РеализацияПодарочныхСертификатов.ПодарочныйСертификат.Код,
		|	ВидыПодарочныхСертификатов.ЧастичнаяОплата,
		|	ВидыПодарочныхСертификатов.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(ОплатаПлатежнымиКартами.Сумма) КАК СуммаОплаты,
		|	ОплатаПлатежнымиКартами.НомерПлатежнойКарты КАК НомерКарты,
		|	""Неопределено"" КАК ВидКарты,
		|	ЭквайринговыеТерминалы.Эквайер КАК Эквайрер,
		|	ЭквайринговыеТерминалы.БанковскийСчет КАК БанковскийСчетОрганизации,
		|	ЭквайринговыеТерминалы.Код КАК Номер,
		|	ЭквайринговыеТерминалы.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.РеализацияПодарочныхСертификатов.ОплатаПлатежнымиКартами КАК ОплатаПлатежнымиКартами
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЭквайринговыеТерминалы КАК ЭквайринговыеТерминалы
		|		ПО ОплатаПлатежнымиКартами.ЭквайринговыйТерминал = ЭквайринговыеТерминалы.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_РеализацияПС КАК ВТ_РеализацияПС
		|		ПО ОплатаПлатежнымиКартами.Ссылка = ВТ_РеализацияПС.Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ОплатаПлатежнымиКартами.ЭквайринговыйТерминал,
		|	ОплатаПлатежнымиКартами.НомерПлатежнойКарты,
		|	ЭквайринговыеТерминалы.Эквайер,
		|	ЭквайринговыеТерминалы.БанковскийСчет,
		|	ЭквайринговыеТерминалы.Код,
		|	ЭквайринговыеТерминалы.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОтчетОРозничныхПродажахНачислениеБонусныхБаллов.НомерСтроки КАК НомерСтрокиДокумента,
		|	ОтчетОРозничныхПродажахНачислениеБонусныхБаллов.ДатаНачисления КАК ДатаНачисления,
		|	ОтчетОРозничныхПродажахНачислениеБонусныхБаллов.ДатаСписания КАК ДатаСписания,
		|	ОтчетОРозничныхПродажахНачислениеБонусныхБаллов.Партнер КАК Партнер,
		|	ОтчетОРозничныхПродажахНачислениеБонусныхБаллов.БонуснаяПрограммаЛояльности КАК ПрограммаЛояльности,
		|	ОтчетОРозничныхПродажахНачислениеБонусныхБаллов.СуммаБонусныхБаллов КАК СуммаБонусныхБаллов
		|ИЗ
		|	Документ.ОтчетОРозничныхПродажах.НачислениеБонусныхБаллов КАК ОтчетОРозничныхПродажахНачислениеБонусныхБаллов
		|ГДЕ
		|	ОтчетОРозничныхПродажахНачислениеБонусныхБаллов.Ссылка = &Ссылка
		|	И НЕ ОтчетОРозничныхПродажахНачислениеБонусныхБаллов.Партнер = ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОтчетОРозничныхПродажахОплатаБонуснымиБаллами.НомерСтроки КАК НомерСтрокиДокумента,
		|	ОтчетОРозничныхПродажахОплатаБонуснымиБаллами.Партнер КАК Партнер,
		|	ОтчетОРозничныхПродажахОплатаБонуснымиБаллами.БонуснаяПрограммаЛояльности КАК ПрограммаЛояльности,
		|	ОтчетОРозничныхПродажахОплатаБонуснымиБаллами.СуммаБонусныхБаллов КАК СуммаБонусныхБаллов
		|ИЗ
		|	Документ.ОтчетОРозничныхПродажах.ОплатаБонуснымиБаллами КАК ОтчетОРозничныхПродажахОплатаБонуснымиБаллами
		|ГДЕ
		|	ОтчетОРозничныхПродажахОплатаБонуснымиБаллами.Ссылка = &Ссылка
		|	И НЕ ОтчетОРозничныхПродажахОплатаБонуснымиБаллами.Партнер = ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втАкцизныеМарки.НомерСтрокиДокумента КАК НомерСтрокиДокумента,
		|	втАкцизныеМарки.Номенклатура КАК Номенклатура,
		|	&ТекстЗапросаХарактеристикаАкцизныеМарки,
		|	&ТекстЗапросаСерияАкцизныеМарки,
		|	&ТекстЗапросаУпаковкаАкцизныеМарки,
		|	&ТипАкцизнойМарки,
		|	втАкцизныеМарки.НомерАкцизнойМарки КАК НомерАкцизнойМарки
		|ИЗ
		|	втАкцизныеМарки КАК втАкцизныеМарки
		|ГДЕ
		|	втАкцизныеМарки.ТипУпаковки = ЗНАЧЕНИЕ(Перечисление.ТипыУпаковок.МаркированныйТовар)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втАкцизныеМарки.НомерСтрокиДокумента КАК НомерСтрокиДокумента,
		|	втАкцизныеМарки.АкцизнаяМарка КАК МаркировкаУпаковки
		|ИЗ
		|	втАкцизныеМарки КАК втАкцизныеМарки
		|ГДЕ
		|	НЕ втАкцизныеМарки.ТипУпаковки = ЗНАЧЕНИЕ(Перечисление.ТипыУпаковок.МаркированныйТовар)
		|	И НЕ втАкцизныеМарки.ТипУпаковки = ЗНАЧЕНИЕ(Перечисление.ТипыУпаковок.МультитоварнаяУпаковка)";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаХарактеристикаАкцизныеМарки",
		ПолучитьТекстЗапросаХарактеристики(КомпонентыОбмена, "втАкцизныеМарки"));
		
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаСерияАкцизныеМарки",
		ПолучитьТекстЗапросаСерии(КомпонентыОбмена, "втАкцизныеМарки"));
		
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаУпаковкаАкцизныеМарки",
		ПолучитьТекстЗапросаУпаковки(КомпонентыОбмена, "втАкцизныеМарки", Ложь));
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаХарактеристика",
		ПолучитьТекстЗапросаХарактеристики(КомпонентыОбмена, "ТабТовары"));
		
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаУпаковка",
		ПолучитьТекстЗапросаУпаковки(КомпонентыОбмена, "ТабТовары"));
		
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаСерия",
		ПолучитьТекстЗапросаСерии(КомпонентыОбмена, "ТабТовары"));
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТипАкцизнойМарки",
		ПолучитьТекстЗапросаТипАкцизнойМарки(КомпонентыОбмена, "втАкцизныеМарки"));
		
	ВозвратыОтдельно = ВерсияФорматаЧислом(КомпонентыОбмена.ВерсияФорматаОбмена) >= ВерсияФорматаЧислом("1.6");
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Ссылка",           ДанныеИБ.Ссылка);
	Запрос.УстановитьПараметр("КассоваяСмена",    ДанныеИБ.КассоваяСмена);
	Запрос.УстановитьПараметр("ВозвратыОтдельно", ВозвратыОтдельно);
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	ДанныеИБ.ДополнительныеСвойства.Вставить("РезультатыЗапроса", РезультатыЗапроса);
	
	ПроверкаЗаполненияРеквизитовОбъектаИБ(ДанныеИБ, КомпонентыОбмена, ИспользованиеПКО);
КонецПроцедуры
