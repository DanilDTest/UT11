Процедура ПОД_Документ_ПересортицаТоваров_Отправка_ПриОбработке(ДанныеИБ, ИспользованиеПКО, КомпонентыОбмена)
	ИспользованиеПКО.Документ_ПересортицаВОприходование_Отправка = Истина;
	ИспользованиеПКО.Документ_ПересортицаВСписаниеЗапасов_Отправка      = Истина;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка",    ДанныеИБ.Ссылка);
	Запрос.УстановитьПараметр("Дата",      ДанныеИБ.Дата);
	Запрос.УстановитьПараметр("ВидЦен",    ДанныеИБ.ВидЦены);
	Запрос.УстановитьПараметр("РегВалюта", Константы.ВалютаРегламентированногоУчета.Получить());
	Запрос.УстановитьПараметр("УпрВалюта", Константы.ВалютаУправленческогоУчета.Получить());
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Товары.НоменклатураОприходование КАК Номенклатура,
		|	Товары.ХарактеристикаОприходование КАК Характеристика,
		|	Товары.СерияОприходование КАК Серия,
		|	Товары.Номенклатура КАК НоменклатураСписание,
		|	Товары.Характеристика КАК ХарактеристикаСписание,
		|	Товары.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	1 КАК Коэффициент,
		|	Товары.Количество КАК Количество,
		|	Товары.Цена КАК Цена,
		|	Товары.Цена * Товары.Количество КАК Сумма,
		|	Товары.НомерГТД.Код КАК НомерГТД,
		|	Товары.НомерГТД.СтранаПроисхождения КАК СтранаПроисхождения,
		|	Товары.НомерСтроки КАК НомерСтроки
		|ПОМЕСТИТЬ ВременнаяТаблицаТовары
		|ИЗ
		|	Документ.ПересортицаТоваров.Товары КАК Товары
		|ГДЕ
		|	Товары.Ссылка = &Ссылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	НоменклатураСписание,
		|	ХарактеристикаСписание";
	
	Запрос.Выполнить();
	
	Если ДанныеИБ.ПриходоватьТоварыПоСебестоимостиСписания Тогда
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ВременнаяТаблицаТовары.Номенклатура КАК Номенклатура,
			|	ВременнаяТаблицаТовары.Характеристика КАК Характеристика,
			|	Выразить(ЦеныНоменклатурыСрезПоследних.Цена / ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки1, 1) * ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки2, 1) * ВЫБОР
			|		КОГДА &РегВалюта <> ЦеныНоменклатурыСрезПоследних.Валюта
			|			ТОГДА ВЫБОР
			|					КОГДА ЕСТЬNULL(КурсыВалютыЦены.Кратность, 0) > 0
			|							И ЕСТЬNULL(КурсыВалютыЦены.Курс, 0) > 0
			|							И ЕСТЬNULL(КурсыВалюты.Кратность, 0) > 0
			|							И ЕСТЬNULL(КурсыВалюты.Курс, 0) > 0
			|						ТОГДА КурсыВалютыЦены.Курс * КурсыВалюты.Кратность / (КурсыВалюты.Курс * КурсыВалютыЦены.Кратность)
			|					ИНАЧЕ 0
			|				КОНЕЦ
			|		ИНАЧЕ 1
			|	КОНЕЦ КАК Число(15, 2)) КАК Цена,
			|	ВременнаяТаблицаТовары.НомерГТД КАК НомерГТД,
			|	ВременнаяТаблицаТовары.СтранаПроисхождения КАК СтранаПроисхождения,
			|	ВременнаяТаблицаТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
			|	ВременнаяТаблицаТовары.Коэффициент КАК Коэффициент,
			|	ВременнаяТаблицаТовары.Количество КАК Количество,
			|	ВременнаяТаблицаТовары.НомерСтроки КАК НомерСтроки,
			|	ВременнаяТаблицаТовары.Серия КАК Серия
			|ПОМЕСТИТЬ ПервичныеДанные
			|ИЗ
			|	ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(
			|				&Дата,
			|				ВидЦены = &ВидЦен
			|					И (Номенклатура, Характеристика) В
			|						(ВЫБРАТЬ
			|							ВременнаяТаблицаТовары.НоменклатураСписание,
			|							ВременнаяТаблицаТовары.ХарактеристикаСписание
			|						ИЗ
			|							ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары)) КАК ЦеныНоменклатурыСрезПоследних
			|		ПО ВременнаяТаблицаТовары.НоменклатураСписание = ЦеныНоменклатурыСрезПоследних.Номенклатура
			|			И ВременнаяТаблицаТовары.ХарактеристикаСписание = ЦеныНоменклатурыСрезПоследних.Характеристика
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&Дата, ) КАК КурсыВалютыЦены
			|		ПО (ЦеныНоменклатурыСрезПоследних.Валюта = КурсыВалютыЦены.Валюта)
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&Дата, Валюта = &РегВалюта) КАК КурсыВалюты
			|		ПО (ИСТИНА)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ПервичныеДанные.Номенклатура КАК Номенклатура,
			|	&ТекстЗапросаХарактеристика,
			|	&ТекстЗапросаСерия,
			|	ВЫБОР
			|		КОГДА ПервичныеДанные.Цена < 0.01
			|			ТОГДА 0.01
			|		ИНАЧЕ ВЫРАЗИТЬ(ПервичныеДанные.Цена КАК ЧИСЛО(15, 2))
			|	КОНЕЦ КАК Цена,
			|	ВЫРАЗИТЬ(ВЫБОР
			|			КОГДА ПервичныеДанные.Цена < 0.01
			|				ТОГДА 0.01
			|			ИНАЧЕ ВЫРАЗИТЬ(ПервичныеДанные.Цена КАК ЧИСЛО(15, 2))
			|		КОНЕЦ * ПервичныеДанные.Количество КАК ЧИСЛО(15, 2)) КАК Сумма,
			|	ПервичныеДанные.НомерГТД КАК НомерГТД,
			|	ПервичныеДанные.СтранаПроисхождения КАК СтранаПроисхождения,
			|	ПервичныеДанные.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
			|	ПервичныеДанные.Коэффициент КАК Коэффициент,
			|	ПервичныеДанные.Количество КАК Количество,
			|	ПервичныеДанные.НомерСтроки КАК НомерСтроки
			|ИЗ
			|	ПервичныеДанные КАК ПервичныеДанные";
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"&ТекстЗапросаКоэффициентУпаковки1",
			Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"ЦеныНоменклатурыСрезПоследних.Упаковка",
			"ЦеныНоменклатурыСрезПоследних.Номенклатура"));
			
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"&ТекстЗапросаКоэффициентУпаковки2",
			Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"ВременнаяТаблицаТовары.ЕдиницаИзмерения",
			"ВременнаяТаблицаТовары.Номенклатура"));
			
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаХарактеристика",
			ПолучитьТекстЗапросаХарактеристики(КомпонентыОбмена, "ПервичныеДанные"));
			
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаСерия",
			ПолучитьТекстЗапросаСерии(КомпонентыОбмена, "ПервичныеДанные"));
		
		Запрос.УстановитьПараметр("Дата",   ДанныеИБ.Дата);
		Запрос.УстановитьПараметр("ВидЦен", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеИБ.Склад, "УчетныйВидЦены"));
		Запрос.УстановитьПараметр("Валюта", Константы.ВалютаРегламентированногоУчета.Получить());
		
	Иначе
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ВременнаяТаблицаТовары.Номенклатура КАК Номенклатура,
			|	&ТекстЗапросаХарактеристика,
			|	&ТекстЗапросаСерия,
			|	ВременнаяТаблицаТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
			|	ВременнаяТаблицаТовары.Коэффициент КАК Коэффициент,
			|	ВременнаяТаблицаТовары.Количество КАК Количество,
			|	ВЫРАЗИТЬ(ВременнаяТаблицаТовары.Цена * ВЫБОР
			|			КОГДА &РегВалюта <> &УпрВалюта
			|				ТОГДА ВЫБОР
			|						КОГДА ЕСТЬNULL(КурсыВалютыЦены.Кратность, 0) > 0
			|								И ЕСТЬNULL(КурсыВалютыЦены.Курс, 0) > 0
			|								И ЕСТЬNULL(КурсыВалюты.Кратность, 0) > 0
			|								И ЕСТЬNULL(КурсыВалюты.Курс, 0) > 0
			|							ТОГДА КурсыВалютыЦены.Курс * КурсыВалюты.Кратность / (КурсыВалюты.Курс * КурсыВалютыЦены.Кратность)
			|						ИНАЧЕ 0
			|					КОНЕЦ
			|			ИНАЧЕ 1
			|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК Цена,
			|	ВЫРАЗИТЬ(ВременнаяТаблицаТовары.Сумма * ВЫБОР
			|			КОГДА &РегВалюта <> &УпрВалюта
			|				ТОГДА ВЫБОР
			|						КОГДА ЕСТЬNULL(КурсыВалютыЦены.Кратность, 0) > 0
			|								И ЕСТЬNULL(КурсыВалютыЦены.Курс, 0) > 0
			|								И ЕСТЬNULL(КурсыВалюты.Кратность, 0) > 0
			|								И ЕСТЬNULL(КурсыВалюты.Курс, 0) > 0
			|							ТОГДА КурсыВалютыЦены.Курс * КурсыВалюты.Кратность / (КурсыВалюты.Курс * КурсыВалютыЦены.Кратность)
			|						ИНАЧЕ 0
			|					КОНЕЦ
			|			ИНАЧЕ 1
			|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК Сумма,
			|	ВременнаяТаблицаТовары.НомерГТД КАК НомерГТД,
			|	ВременнаяТаблицаТовары.СтранаПроисхождения КАК СтранаПроисхождения
			|ИЗ
			|	ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&Дата, Валюта = &УпрВалюта) КАК КурсыВалютыЦены
			|		ПО (ИСТИНА)
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&Дата, Валюта = &РегВалюта) КАК КурсыВалюты
			|		ПО (ИСТИНА)
			|
			|УПОРЯДОЧИТЬ ПО
			|	ВременнаяТаблицаТовары.НомерСтроки";
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаХарактеристика",
			ПолучитьТекстЗапросаХарактеристики(КомпонентыОбмена, "ВременнаяТаблицаТовары"));
			
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаСерия",
			ПолучитьТекстЗапросаСерии(КомпонентыОбмена, "ВременнаяТаблицаТовары"));
		
	КонецЕсли;
	
	ТаблицаТовары = Запрос.Выполнить().Выгрузить();
	
	ЗаполнитьХарактеристикуУпаковкуВТЧ(КомпонентыОбмена, ТаблицаТовары, Истина, Ложь);
	
	ЕстьПустая = Ложь;
	Для Каждого Строка Из ТаблицаТовары Цикл
		Если Не ЗначениеЗаполнено(Строка.Цена) Или Не ЗначениеЗаполнено(Строка.Сумма) Тогда
			ЕстьПустая = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ЕстьПустая Тогда
		
		ИспользованиеПКО.Документ_ПересортицаВОприходование_Отправка = Ложь;
		ИспользованиеПКО.Документ_ПересортицаВСписаниеЗапасов_Отправка      = Ложь;
		
		ШаблонПредупреждения = НСтр("ru = 'Документ %1 не может быть выгружен,
		|т.к. не для всех приходуемых номенклатурных позиций задана учетная цена.
		|Тип учетной цены задается в параметрах склада как ""Учетный вид цены"".';
		|en = 'The %1 document cannot be exported
		|as an accounting price is not set for all recorded as received product items.
		|The accounting price type is specified in warehouse parameters as ""Accounting price type"".'");
		
		ТекстПредупреждения = СтрШаблон(ШаблонПредупреждения, ДанныеИБ.Ссылка);
		ПредупреждениеПриСинхронизации(КомпонентыОбмена, ДанныеИБ.Ссылка, ТекстПредупреждения);
		
	КонецЕсли;
	
	ДанныеИБ.ДополнительныеСвойства.Вставить("Товары", ТаблицаТовары);
	ПроверкаЗаполненияРеквизитовОбъектаИБ(ДанныеИБ, КомпонентыОбмена, ИспользованиеПКО);
КонецПроцедуры
