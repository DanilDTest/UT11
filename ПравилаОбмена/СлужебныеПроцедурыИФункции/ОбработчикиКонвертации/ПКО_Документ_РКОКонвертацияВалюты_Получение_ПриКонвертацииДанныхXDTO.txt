Процедура ПКО_Документ_РКОКонвертацияВалюты_Получение_ПриКонвертацииДанныхXDTO(ДанныеXDTO, ПолученныеДанные, КомпонентыОбмена)
	ПерваяЗаполненнаяСтатьяДДС = Неопределено;
	Для Каждого Строка Из ДанныеXDTO.РасшифровкаПлатежа Цикл
		Если ПерваяЗаполненнаяСтатьяДДС = Неопределено И ЗначениеЗаполнено(Строка.СтатьяДДС) Тогда
			ПерваяЗаполненнаяСтатьяДДС = RM_ОбменДаннымиXDTOСервер.СтруктураОбъектаXDTOВДанныеИБ(
				КомпонентыОбмена,
				Строка.СтатьяДДС,
				RM_ОбменДаннымиXDTOСервер.ПКОПоИмени(КомпонентыОбмена, "Справочник_СтатьиДДС"),
				"ПолучитьСсылку");
		КонецЕсли;	
	КонецЦикла;
	
	#Область Шапка
	
	ПолученныеДанные.Проведен = Истина;
	ПолученныеДанные.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.КонвертацияВалюты;
	ДобавитьВалютуВДопСвойства(ПолученныеДанные, ДанныеXDTO);
	ЗагрузитьОтветственноеЛицо(ПолученныеДанные, ДанныеXDTO, КомпонентыОбмена, "ГлавныйБухгалтер");
	ЗагрузитьОтветственноеЛицо(ПолученныеДанные, ДанныеXDTO, КомпонентыОбмена, "Руководитель");
	
	// СтатьяДДС
	ПолученныеДанные.СтатьяДвиженияДенежныхСредств = ПерваяЗаполненнаяСтатьяДДС;
	
	#КонецОбласти
	ЗагрузитьДополнительныеРеквизиты(ПолученныеДанные, ДанныеXDTO, КомпонентыОбмена);
	
	#Область РасшифровкаПлатежа
	
	МассивСтрокРасшифровкаПлатежа = Новый Массив;
	Если ЗначениеЗаполнено(ДанныеXDTO.РасшифровкаПлатежа) Тогда
		//ВалютаКонвертации
		ВалютаКонвертации = ДанныеXDTO.РасшифровкаПлатежа[0].ВалютаКонвертации;
		Если ЗначениеЗаполнено(ВалютаКонвертации) Тогда
			Если ТипЗнч(ВалютаКонвертации) = Тип("Структура")
				И Не ВалютаКонвертации.Свойство("Ссылка") Тогда
				ИмяПКО = "Справочник_Валюты_ИзСтруктуры";
			Иначе
				ИмяПКО = "Справочник_Валюты";
			КонецЕсли;
			ПолученныеДанные.ДополнительныеСвойства.Вставить("ВалютаКонвертации", Новый Структура("Значение, ИмяПКО", ВалютаКонвертации, ИмяПКО));
		КонецЕсли;
		
		ПолученныеДанные.КурсКонвертации           = ДанныеXDTO.РасшифровкаПлатежа[0].КурсКонвертации;
		ПолученныеДанные.КратностьКурсаКонвертации = ДанныеXDTO.РасшифровкаПлатежа[0].КратностьКонвертации;
		ПолученныеДанные.СуммаКонвертации          = ДанныеXDTO.РасшифровкаПлатежа[0].СуммаВал;
		
		Т_РП = Новый Соответствие;
		Т_РП.Вставить("Сумма", "Сумма");
		Т_РП.Вставить("СуммаВзаиморасчетов", "Сумма");
	
		Для Каждого СтрокаXDTO Из ДанныеXDTO.РасшифровкаПлатежа Цикл
			СтруктураДанныхСтроки = Новый Структура;
			ЗаполнитьСтруктуруДанныхСтроки(Т_РП, СтрокаXDTO, СтруктураДанныхСтроки);
			
			СтруктураДанныхСтроки.Вставить("ВалютаВзаиморасчетов", ПолученныеДанные.ДополнительныеСвойства.Валюта);
			
			МассивСтрокРасшифровкаПлатежа.Добавить(СтруктураДанныхСтроки);
		КонецЦикла;
		
		ПолученныеДанные.ДополнительныеСвойства.Вставить("РасшифровкаПлатежа", МассивСтрокРасшифровкаПлатежа);
	
	КонецЕсли;
	
	#КонецОбласти
КонецПроцедуры
