Процедура ПОД_Документ_КорректировкаРеализации_ПриОбработке(ДанныеИБ, ИспользованиеПКО, КомпонентыОбмена)
	УстановитьИспользованиеПКО(ИспользованиеПКО, Ложь);
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	КорректировкаРеализации.Валюта КАК Валюта,
		|	КорректировкаРеализации.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
		|	КорректировкаРеализации.Курс КАК КурсВзаиморасчетов,
		|	КорректировкаРеализации.Кратность КАК КратностьВзаиморасчетов,
		|	КорректировкаРеализации.Руководитель.ФизическоеЛицо КАК Руководитель,
		|	КорректировкаРеализации.ГлавныйБухгалтер.ФизическоеЛицо КАК ГлавныйБухгалтер
		|ПОМЕСТИТЬ ДанныеШапкиДокумента
		|ИЗ
		|	Документ.КорректировкаРеализации КАК КорректировкаРеализации
		|ГДЕ
		|	КорректировкаРеализации.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(КурсВалютыДокумента.Курс, 1) КАК КурсВзаиморасчетов,
		|	ЕСТЬNULL(КурсВалютыДокумента.Кратность, 1) КАК КратностьВзаиморасчетов,
		|	ДанныеШапкиДокумента.Руководитель КАК Руководитель,
		|	ДанныеШапкиДокумента.ГлавныйБухгалтер КАК ГлавныйБухгалтер
		|ИЗ
		|	ДанныеШапкиДокумента КАК ДанныеШапкиДокумента
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(
		|				&Дата,
		|				Валюта В
		|					(ВЫБРАТЬ
		|						Док.Валюта
		|					ИЗ
		|						ДанныеШапкиДокумента КАК Док)) КАК КурсВалютыДокумента
		|		ПО ДанныеШапкиДокумента.Валюта = КурсВалютыДокумента.Валюта
		|ГДЕ
		|	НЕ ДанныеШапкиДокумента.Валюта = &ВалютаРеглУчета
		|	И НЕ ДанныеШапкиДокумента.ВалютаВзаиморасчетов = &ВалютаРеглУчета
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДанныеШапкиДокумента.КурсВзаиморасчетов,
		|	ДанныеШапкиДокумента.КратностьВзаиморасчетов,
		|	ДанныеШапкиДокумента.Руководитель,
		|	ДанныеШапкиДокумента.ГлавныйБухгалтер
		|ИЗ
		|	ДанныеШапкиДокумента КАК ДанныеШапкиДокумента
		|ГДЕ
		|	(ДанныеШапкиДокумента.Валюта = &ВалютаРеглУчета
		|			ИЛИ ДанныеШапкиДокумента.ВалютаВзаиморасчетов = &ВалютаРеглУчета)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВложенныйЗапрос.НомерСтроки КАК НомерСтроки,
		|	ВложенныйЗапрос.Номенклатура КАК Номенклатура,
		|	ВложенныйЗапрос.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ВложенныйЗапрос.Характеристика КАК Характеристика,
		|	ВложенныйЗапрос.Серия КАК Серия,
		|	ВложенныйЗапрос.Упаковка КАК Упаковка,
		|	ВложенныйЗапрос.Количество КАК Количество,
		|	ВложенныйЗапрос.КоличествоУпаковок КАК КоличествоУпаковок,
		|	ВложенныйЗапрос.Сумма КАК Сумма,
		|	ВложенныйЗапрос.СтавкаНДС КАК СтавкаНДС,
		|	ВложенныйЗапрос.СуммаНДС КАК СуммаНДС,
		|	ВложенныйЗапрос.ТипЗапасов КАК ТипЗапасов,
		|	ВложенныйЗапрос.НомерГТД КАК НомерГТД,
		|	ВложенныйЗапрос.СтранаПроисхождения КАК СтранаПроисхождения,
		|	ВЫБОР
		|		КОГДА ВложенныйЗапрос.Количество > 0
		|			ТОГДА ВЫРАЗИТЬ(ВложенныйЗапрос.Сумма / ВложенныйЗапрос.Количество КАК ЧИСЛО(15, 2))
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Цена,
		|	ВложенныйЗапрос.НаДоходыРасходы КАК НаДоходыРасходы,
		|	ВЫБОР
		|		КОГДА ВложенныйЗапрос.КоличествоУпаковок > 0
		|			ТОГДА ВЫРАЗИТЬ(ВложенныйЗапрос.Сумма / ВложенныйЗапрос.КоличествоУпаковок КАК ЧИСЛО(15, 2))
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ЦенаЗаУпаковку
		|ПОМЕСТИТЬ втТоварыОприходование
		|ИЗ
		|	(ВЫБРАТЬ
		|		КорректировкаРеализацииВидыЗапасовОприходование.НомерСтроки КАК НомерСтроки,
		|		КорректировкаРеализацииВидыЗапасовОприходование.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
		|		КорректировкаРеализацииВидыЗапасовОприходование.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|		КорректировкаРеализацииВидыЗапасовОприходование.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
		|		КорректировкаРеализацииВидыЗапасовОприходование.АналитикаУчетаНоменклатуры.Серия КАК Серия,
		|		КорректировкаРеализацииВидыЗапасовОприходование.Упаковка КАК Упаковка,
		|		КорректировкаРеализацииВидыЗапасовОприходование.Количество КАК Количество,
		|		КорректировкаРеализацииВидыЗапасовОприходование.КоличествоУпаковок КАК КоличествоУпаковок,
		|		КорректировкаРеализацииВидыЗапасовОприходование.СуммаСНДС - КорректировкаРеализацииВидыЗапасовОприходование.СуммаНДС КАК Сумма,
		|		КорректировкаРеализацииВидыЗапасовОприходование.СтавкаНДС КАК СтавкаНДС,
		|		КорректировкаРеализацииВидыЗапасовОприходование.СуммаНДС КАК СуммаНДС,
		|		ВЫБОР
		|			КОГДА КорректировкаРеализацииВидыЗапасовОприходование.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
		|				ТОГДА ВЫРАЗИТЬ(""КомиссионныеТовары"" КАК СТРОКА(18))
		|			ИНАЧЕ ВЫРАЗИТЬ(""СобственныеТовары"" КАК СТРОКА(18))
		|		КОНЕЦ КАК ТипЗапасов,
		|		КорректировкаРеализацииВидыЗапасовОприходование.НомерГТД.Код КАК НомерГТД,
		|		КорректировкаРеализацииВидыЗапасовОприходование.НомерГТД.СтранаПроисхождения КАК СтранаПроисхождения,
		|		КорректировкаРеализацииВидыЗапасовОприходование.НаДоходыРасходы КАК НаДоходыРасходы
		|	ИЗ
		|		Документ.КорректировкаРеализации.ВидыЗапасовОприходование КАК КорректировкаРеализацииВидыЗапасовОприходование
		|	ГДЕ
		|		КорректировкаРеализацииВидыЗапасовОприходование.Ссылка = &Ссылка
		|		И (КорректировкаРеализацииВидыЗапасовОприходование.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар)
		|				ИЛИ КорректировкаРеализацииВидыЗапасовОприходование.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
		|					И НЕ ЕСТЬNULL(ВЫРАЗИТЬ(КорректировкаРеализацииВидыЗапасовОприходование.Ссылка.ДокументОснование КАК Документ.РеализацияТоваровУслуг).ВернутьМногооборотнуюТару, ЛОЖЬ))) КАК ВложенныйЗапрос
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВложенныйЗапрос.НомерСтроки КАК НомерСтроки,
		|	ВложенныйЗапрос.Номенклатура КАК Номенклатура,
		|	ВложенныйЗапрос.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ВложенныйЗапрос.Характеристика КАК Характеристика,
		|	ВложенныйЗапрос.Упаковка КАК Упаковка,
		|	ВложенныйЗапрос.Серия КАК Серия,
		|	ВложенныйЗапрос.Количество КАК Количество,
		|	ВложенныйЗапрос.КоличествоУпаковок КАК КоличествоУпаковок,
		|	ВложенныйЗапрос.Сумма КАК Сумма,
		|	ВложенныйЗапрос.СтавкаНДС КАК СтавкаНДС,
		|	ВложенныйЗапрос.СуммаНДС КАК СуммаНДС,
		|	ВложенныйЗапрос.ТипЗапасов КАК ТипЗапасов,
		|	ВложенныйЗапрос.НомерГТД КАК НомерГТД,
		|	ВложенныйЗапрос.СтранаПроисхождения КАК СтранаПроисхождения,
		|	ВЫБОР
		|		КОГДА ВложенныйЗапрос.Количество > 0
		|			ТОГДА ВЫРАЗИТЬ(ВложенныйЗапрос.Сумма / ВложенныйЗапрос.Количество КАК ЧИСЛО(15, 2))
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Цена,
		|	ВложенныйЗапрос.НаДоходыРасходы КАК НаДоходыРасходы,
		|	ВЫБОР
		|		КОГДА ВложенныйЗапрос.КоличествоУпаковок > 0
		|			ТОГДА ВЫРАЗИТЬ(ВложенныйЗапрос.Сумма / ВложенныйЗапрос.КоличествоУпаковок КАК ЧИСЛО(15, 2))
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ЦенаЗаУпаковку
		|ПОМЕСТИТЬ втТоварыСписание
		|ИЗ
		|	(ВЫБРАТЬ
		|		КорректировкаРеализацииВидыЗапасовСписание.НомерСтроки КАК НомерСтроки,
		|		КорректировкаРеализацииВидыЗапасовСписание.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
		|		КорректировкаРеализацииВидыЗапасовСписание.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|		КорректировкаРеализацииВидыЗапасовСписание.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
		|		КорректировкаРеализацииВидыЗапасовСписание.Упаковка КАК Упаковка,
		|		КорректировкаРеализацииВидыЗапасовСписание.АналитикаУчетаНоменклатуры.Серия КАК Серия,
		|		КорректировкаРеализацииВидыЗапасовСписание.Количество КАК Количество,
		|		КорректировкаРеализацииВидыЗапасовСписание.КоличествоУпаковок КАК КоличествоУпаковок,
		|		КорректировкаРеализацииВидыЗапасовСписание.СуммаСНДС - КорректировкаРеализацииВидыЗапасовСписание.СуммаНДС КАК Сумма,
		|		КорректировкаРеализацииВидыЗапасовСписание.СтавкаНДС КАК СтавкаНДС,
		|		КорректировкаРеализацииВидыЗапасовСписание.СуммаНДС КАК СуммаНДС,
		|		ВЫБОР
		|			КОГДА КорректировкаРеализацииВидыЗапасовСписание.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
		|				ТОГДА ВЫРАЗИТЬ(""КомиссионныеТовары"" КАК СТРОКА(18))
		|			ИНАЧЕ ВЫРАЗИТЬ(""СобственныеТовары"" КАК СТРОКА(18))
		|		КОНЕЦ КАК ТипЗапасов,
		|		КорректировкаРеализацииВидыЗапасовСписание.НомерГТД.Код КАК НомерГТД,
		|		КорректировкаРеализацииВидыЗапасовСписание.НомерГТД.СтранаПроисхождения КАК СтранаПроисхождения,
		|		КорректировкаРеализацииВидыЗапасовСписание.НаДоходыРасходы КАК НаДоходыРасходы
		|	ИЗ
		|		Документ.КорректировкаРеализации.ВидыЗапасовСписание КАК КорректировкаРеализацииВидыЗапасовСписание
		|	ГДЕ
		|		КорректировкаРеализацииВидыЗапасовСписание.Ссылка = &Ссылка
		|		И (КорректировкаРеализацииВидыЗапасовСписание.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар)
		|				ИЛИ КорректировкаРеализацииВидыЗапасовСписание.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
		|					И НЕ ЕСТЬNULL(ВЫРАЗИТЬ(КорректировкаРеализацииВидыЗапасовСписание.Ссылка.ДокументОснование КАК Документ.РеализацияТоваровУслуг).ВернутьМногооборотнуюТару, ЛОЖЬ))) КАК ВложенныйЗапрос
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Товары.Номенклатура КАК Номенклатура,
		|	&ТекстЗапросаХарактеристика,
		|	&ТекстЗапросаУпаковка,
		|	&ТекстЗапросаСерия,
		|	Товары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	Товары.НомерГТД КАК НомерГТД,
		|	Товары.СтранаПроисхождения КАК СтранаПроисхождения,
		|	Товары.ТипЗапасов КАК ТипЗапасов,
		|	Товары.Количество КАК Количество,
		|	Товары.Сумма КАК Сумма,
		|	0 КАК Цена
		|ИЗ
		|	втТоварыОприходование КАК Товары
		|ГДЕ
		|	Товары.НаДоходыРасходы
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Товары.Номенклатура КАК Номенклатура,
		|	&ТекстЗапросаХарактеристика,
		|	&ТекстЗапросаУпаковка,
		|	&ТекстЗапросаСерия,
		|	Товары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	Товары.НомерГТД КАК НомерГТД,
		|	Товары.СтранаПроисхождения КАК СтранаПроисхождения,
		|	Товары.ТипЗапасов КАК ТипЗапасов,
		|	Товары.Количество КАК Количество,
		|	Товары.Сумма КАК Сумма,
		|	0 КАК Цена
		|ИЗ
		|	втТоварыСписание КАК Товары
		|ГДЕ
		|	Товары.НаДоходыРасходы
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Товары.НомерСтроки КАК НомерСтроки,
		|	Товары.Номенклатура КАК Номенклатура,
		|	Товары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	&ТекстЗапросаХарактеристика,
		|	&ТекстЗапросаУпаковка,
		|	&ТекстЗапросаСерия,
		|	Товары.Количество КАК Количество,
		|	Товары.Сумма КАК Сумма,
		|	Товары.СтавкаНДС КАК СтавкаНДС,
		|	Товары.СуммаНДС КАК СуммаНДС,
		|	Товары.ТипЗапасов КАК ТипЗапасов,
		|	Товары.НомерГТД КАК НомерГТД,
		|	Товары.СтранаПроисхождения КАК СтранаПроисхождения,
		|	Товары.Цена КАК Цена
		|ИЗ
		|	втТоварыОприходование КАК Товары
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Товары.НомерСтроки КАК НомерСтроки,
		|	Товары.Номенклатура КАК Номенклатура,
		|	Товары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	&ТекстЗапросаХарактеристика,
		|	&ТекстЗапросаУпаковка,
		|	&ТекстЗапросаСерия,
		|	Товары.Количество КАК Количество,
		|	Товары.Сумма КАК Сумма,
		|	Товары.СтавкаНДС КАК СтавкаНДС,
		|	Товары.СуммаНДС КАК СуммаНДС,
		|	Товары.ТипЗапасов КАК ТипЗапасов,
		|	Товары.НомерГТД КАК НомерГТД,
		|	Товары.СтранаПроисхождения КАК СтранаПроисхождения,
		|	Товары.Цена КАК Цена
		|ИЗ
		|	втТоварыСписание КАК Товары";
	
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаХарактеристика",
		ПолучитьТекстЗапросаХарактеристики(КомпонентыОбмена, "Товары"));
		
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаУпаковка",
		ПолучитьТекстЗапросаУпаковки(КомпонентыОбмена, "Товары"));
		
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаСерия",
		ПолучитьТекстЗапросаСерии(КомпонентыОбмена, "Товары"));
	
	ВалютаРеглУчета = ВалютаРегламентированногоУчета(КомпонентыОбмена);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Ссылка", ДанныеИБ.Ссылка);
	Запрос.УстановитьПараметр("Дата", ДанныеИБ.Дата);
	Запрос.УстановитьПараметр("ВалютаРеглУчета", ВалютаРеглУчета);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ДанныеОперации = РезультатЗапроса[1].Выбрать();
	Если ДанныеОперации.Следующий() Тогда
		ДанныеИБ.ДополнительныеСвойства.Вставить("КурсВзаиморасчетов",      ДанныеОперации.КурсВзаиморасчетов);
		ДанныеИБ.ДополнительныеСвойства.Вставить("КратностьВзаиморасчетов", ДанныеОперации.КратностьВзаиморасчетов);
		ДанныеИБ.ДополнительныеСвойства.Вставить("Руководитель",            ДанныеОперации.Руководитель);
		ДанныеИБ.ДополнительныеСвойства.Вставить("ГлавныйБухгалтер",        ДанныеОперации.ГлавныйБухгалтер);
	КонецЕсли;
	
	ТоварыКСписаниюВБухгалтерии      = РезультатЗапроса[РезультатЗапроса.Количество() - 4].Выгрузить();
	ТоварыКОприходованиюВБухгалтерии = РезультатЗапроса[РезультатЗапроса.Количество() - 3].Выгрузить();
	ТоварыВозврат                    = РезультатЗапроса[РезультатЗапроса.Количество() - 2].Выгрузить();
	ТоварыРеализация                 = РезультатЗапроса[РезультатЗапроса.Количество() - 1].Выгрузить();
	
	ЗаполнитьХарактеристикуУпаковкуВТЧ(КомпонентыОбмена, ТоварыКСписаниюВБухгалтерии);
	ЗаполнитьХарактеристикуУпаковкуВТЧ(КомпонентыОбмена, ТоварыКОприходованиюВБухгалтерии);
	ЗаполнитьХарактеристикуУпаковкуВТЧ(КомпонентыОбмена, ТоварыВозврат);
	ЗаполнитьХарактеристикуУпаковкуВТЧ(КомпонентыОбмена, ТоварыРеализация);
	
	Если ТоварыКОприходованиюВБухгалтерии.Количество() > 0 Тогда
		ЗаполнитьЦенуСписаниеОприходование(ТоварыКОприходованиюВБухгалтерии, КомпонентыОбмена);
		
		ИспользованиеПКО.Документ_КорректировкаРеализации_Оприходование = Истина;
		ДанныеИБ.ДополнительныеСвойства.Вставить("ТоварыКОприходованиюВБухгалтерии", ТоварыКОприходованиюВБухгалтерии);
	КонецЕсли;
	
	Если ТоварыКСписаниюВБухгалтерии.Количество() > 0 Тогда
		ЗаполнитьЦенуСписаниеОприходование(ТоварыКСписаниюВБухгалтерии, КомпонентыОбмена);
		
		ИспользованиеПКО.Документ_КорректировкаРеализации_СписаниеЗапасов = Истина;
		ДанныеИБ.ДополнительныеСвойства.Вставить("ТоварыКСписаниюВБухгалтерии", ТоварыКСписаниюВБухгалтерии);
	КонецЕсли;
	
	Если ДанныеИБ.ВидКорректировки = Перечисления.ХозяйственныеОперации.РеализацияПерепоставленногоТовара Тогда
		ИспользованиеПКО.Документ_КорректировкаРеализации_Реализация = Истина;
		ДанныеИБ.ДополнительныеСвойства.Вставить("Товары", ТоварыРеализация);
	ИначеЕсли ДанныеИБ.ВидКорректировки = Перечисления.ХозяйственныеОперации.ВозвратНедопоставленногоТовара Тогда
		ИспользованиеПКО.Документ_КорректировкаРеализации_Возврат = Истина;
		ДанныеИБ.ДополнительныеСвойства.Вставить("Товары", ТоварыВозврат);
	Иначе
		ИспользованиеПКО.Документ_КорректировкаРеализации_Отправка = Истина;
	КонецЕсли;
	
	ПроверкаЗаполненияРеквизитовОбъектаИБ(ДанныеИБ, КомпонентыОбмена, ИспользованиеПКО);
КонецПроцедуры
