Процедура ПОД_Справочник_НоменклатураПоставщиков_Отправка_ПриОбработке(ДанныеИБ, ИспользованиеПКО, КомпонентыОбмена)
	Если ВерсияФорматаЧислом(КомпонентыОбмена.ВерсияФорматаОбмена) < ВерсияФорматаЧислом("1.7") Тогда
		ИспользованиеПКО.Очистить();
	КонецЕсли;
	
	Если ВерсияФорматаЧислом(КомпонентыОбмена.ВерсияФорматаОбмена) >= ВерсияФорматаЧислом("1.8") Тогда 
		
		Если Не ЗначениеЗаполнено(ПартнерыИКонтрагенты.ПолучитьКонтрагентаПартнераПоУмолчанию(ДанныеИБ.Владелец)) Тогда
			
			ИспользованиеПКО.Очистить();
			
			ШаблонПредупреждения = НСтр("ru = 'Справочник %1 не может быть выгружен,
			|т.к. не удалось получить контрагента для указанного партнера.';
			|en = 'Cannot export the %1 catalog,
			|because the counterparty for the specified partner could not be retrieved.'");
			
			ТекстПредупреждения = СтрШаблон(ШаблонПредупреждения, ДанныеИБ.Ссылка);
			ПредупреждениеПриСинхронизации(КомпонентыОбмена, ДанныеИБ.Ссылка, ТекстПредупреждения);
			
		КонецЕсли;
		
		ЕдиницаИзмерения = Неопределено;
		
		БазоваяЕдИзм = Справочники.НаборыУпаковок.БазовыеЕдиницыИзмерения;
		Если ЗначениеЗаполнено(ДанныеИБ.Упаковка) И ОбщегоНазначения.СсылкаСуществует(ДанныеИБ.Упаковка)
			И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеИБ.Упаковка, "Владелец") = БазоваяЕдИзм Тогда
			
			ЕдиницаИзмерения = ДанныеИБ.Упаковка;
		КонецЕсли;
	 
		Если Не ЗначениеЗаполнено(ЕдиницаИзмерения) И ЗначениеЗаполнено(ДанныеИБ.Номенклатура) Тогда
			ЕдиницаИзмерения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеИБ.Номенклатура, "ЕдиницаИзмерения");
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ЕдиницаИзмерения) Тогда
			ИспользованиеПКО.Очистить();
			
			ШаблонПредупреждения = НСтр("ru = 'Справочник %1 не может быть выгружен,
			|т.к. не указана единица измерения.';
			|en = 'Cannot export the %1 catalog,
			|because the unit of measure is not specified.'");
			
			ТекстПредупреждения = СтрШаблон(ШаблонПредупреждения, ДанныеИБ.Ссылка);
			ПредупреждениеПриСинхронизации(КомпонентыОбмена, ДанныеИБ.Ссылка, ТекстПредупреждения);
	
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры
