Процедура ПОД_Справочник_ДокументыФизическихЛиц_Получение_ПриОбработке(ДанныеXDTO, ИспользованиеПКО, КомпонентыОбмена)
	СвойстваОтбор = ДанныеXDTO;
	
	Если ДанныеXDTO.Свойство("КлючевыеСвойства") Тогда
		СвойстваОтбор = ДанныеXDTO.КлючевыеСвойства;
	КонецЕсли;
	
	ФизическоеЛицоОтбор = Неопределено;
	ВидДокументаОтбор   = Неопределено;
	ПериодОтбор         = Неопределено;
	
	СвойстваОтбор.Свойство("ФизическоеЛицоОтбор", ФизическоеЛицоОтбор);
	СвойстваОтбор.Свойство("ВидДокументаОтбор",   ВидДокументаОтбор);
	СвойстваОтбор.Свойство("ПериодОтбор",         ПериодОтбор);
	
	ПравилоКонвертацииФизическиеЛица = RM_ОбменДаннымиXDTOСервер.ПКОПоИмени(КомпонентыОбмена, "Справочник_ФизическиеЛица_Получение");
	ПравилоКонвертацииВидыДокументов = RM_ОбменДаннымиXDTOСервер.ПКОПоИмени(КомпонентыОбмена, "Справочник_ВидыДокументовФизическихЛиц");
	
	ДокументыФЛ = РегистрыСведений.ДокументыФизическихЛиц.СоздатьНаборЗаписей();
	ДокументыФЛ.ДополнительныеСвойства.Вставить("ОтключитьМеханизмРегистрацииОбъектов", Истина);
	Если ЗначениеЗаполнено(ФизическоеЛицоОтбор) Тогда
		ДокументыФЛ.Отбор.Физлицо.Установить(RM_ОбменДаннымиXDTOСервер.СтруктураОбъектаXDTOВДанныеИБ(
			КомпонентыОбмена, ФизическоеЛицоОтбор, ПравилоКонвертацииФизическиеЛица, "ПолучитьСсылку").Ссылка);
	КонецЕсли;
	Если ЗначениеЗаполнено(ВидДокументаОтбор) Тогда
		ДокументыФЛ.Отбор.ВидДокумента.Установить(RM_ОбменДаннымиXDTOСервер.СтруктураОбъектаXDTOВДанныеИБ(
			КомпонентыОбмена, ВидДокументаОтбор, ПравилоКонвертацииВидыДокументов, "ПолучитьСсылку").Ссылка);
	КонецЕсли;
	Если ЗначениеЗаполнено(ПериодОтбор) Тогда
		ДокументыФЛ.Отбор.Период.Установить(ПериодОтбор);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДокументыФЛ.Отбор.Физлицо.Значение) Тогда
		Возврат;
	КонецЕсли;
	
	Если ДанныеXDTO.Свойство("Записи")
		И ЗначениеЗаполнено(ДанныеXDTO.Записи) Тогда
	
		Для Каждого СтрокаЗаписи Из ДанныеXDTO.Записи Цикл
			ЗаписьДокументыФЛ = ДокументыФЛ.Добавить();
			
			Если Не ЗначениеЗаполнено(ДокументыФЛ.Отбор.Физлицо.Значение) Тогда
				ЗаписьДокументыФЛ.Физлицо = RM_ОбменДаннымиXDTOСервер.СтруктураОбъектаXDTOВДанныеИБ(
					КомпонентыОбмена, СтрокаЗаписи.ФизическоеЛицо, ПравилоКонвертацииФизическиеЛица, "ПолучитьСсылку").Ссылка;
			Иначе
				ЗаписьДокументыФЛ.Физлицо = ДокументыФЛ.Отбор.Физлицо.Значение;
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(ДокументыФЛ.Отбор.ВидДокумента.Значение) Тогда
				ЗаписьДокументыФЛ.ВидДокумента = RM_ОбменДаннымиXDTOСервер.СтруктураОбъектаXDTOВДанныеИБ(
					КомпонентыОбмена, СтрокаЗаписи.ВидДокумента, ПравилоКонвертацииВидыДокументов, "ПолучитьСсылку").Ссылка;
			Иначе
				ЗаписьДокументыФЛ.ВидДокумента = ДокументыФЛ.Отбор.ВидДокумента.Значение;
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(ДокументыФЛ.Отбор.Период.Значение) Тогда
				ЗаписьДокументыФЛ.Период = СтрокаЗаписи.Период;
			Иначе
				ЗаписьДокументыФЛ.Период = ДокументыФЛ.Отбор.Период.Значение;
			КонецЕсли;
			
			ЗаполнитьЗначенияСвойств(ЗаписьДокументыФЛ, СтрокаЗаписи, "Серия, Номер, ДатаВыдачи, СрокДействия, КемВыдан, КодПодразделения, Представление");
			
			ЗаписьДокументыФЛ.ЯвляетсяДокументомУдостоверяющимЛичность = Истина;
		КонецЦикла;
		
	КонецЕсли;
	
	ДокументыФЛ.ОбменДанными.Загрузка = Истина;
	ДокументыФЛ.Записать(Истина);
КонецПроцедуры
