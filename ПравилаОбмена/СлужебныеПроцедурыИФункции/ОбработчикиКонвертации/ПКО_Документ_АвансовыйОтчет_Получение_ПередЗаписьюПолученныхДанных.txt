Процедура ПКО_Документ_АвансовыйОтчет_Получение_ПередЗаписьюПолученныхДанных(ПолученныеДанные, ДанныеИБ, КонвертацияСвойств, КомпонентыОбмена)
	ЗаполнитьПодразделениеПоУмолчанию(ПолученныеДанные, ДанныеИБ, КомпонентыОбмена);
	
	// Заполним статус документа по умолчанию.
	Если ((ДанныеИБ <> Неопределено И Не ЗначениеЗаполнено(ДанныеИБ.Статус)) Или ДанныеИБ = Неопределено) Тогда
		ПолученныеДанные.Статус = Перечисления.СтатусыАвансовогоОтчета.Утвержден;
	Иначе
		ПолученныеДанные.Статус = ДанныеИБ.Статус;
	КонецЕсли;
	
	Для Каждого Строка Из ПолученныеДанные.ОплатаПоставщикам Цикл
	
		Если ЗначениеЗаполнено(Строка.Поставщик) 
			ИЛИ НЕ ЗначениеЗаполнено(Строка.Контрагент) Тогда
			Продолжить;
		КонецЕсли;
			
		Партнер = ДенежныеСредстваСервер.ПолучитьПартнераПоКонтрагенту(Строка.Контрагент);
		
		Если Не ЗначениеЗаполнено(Партнер) Тогда
			Партнер = Справочники.Партнеры.НеизвестныйПартнер;
		КонецЕсли;
	
		Строка.Поставщик = Партнер;
		
	КонецЦикла;
	
	ЗагрузитьДополнительныеСведения(?(ДанныеИБ = Неопределено, ПолученныеДанные.ПолучитьСсылкуНового(), ДанныеИБ.Ссылка), 
	                            ПолученныеДанные.ДополнительныеСвойства, КомпонентыОбмена);
	
	Если ДанныеИБ = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	// Переносим те свойства, которые указаны в ПКС.
	ЗаполнитьСвойстваШапкиОбъекта(КонвертацияСвойств, ПолученныеДанные, ДанныеИБ);
	
	ДанныеИБ.ОплатаПоставщикам.Загрузить(ПолученныеДанные.ОплатаПоставщикам.Выгрузить());
	
	Если ДанныеИБ.ПрочиеРасходы.Количество() > 0 Тогда
		// Прочее
		МассивКлючевыхПолей = Новый Массив;
		МассивКлючевыхПолей.Добавить("Комментарий");
		МассивКлючевыхПолей.Добавить("Контрагент");
		МассивКлючевыхПолей.Добавить("СтавкаНДС");
		МассивКлючевыхПолей.Добавить("ДатаВходящегоДокумента");
		МассивКлючевыхПолей.Добавить("НомерВходящегоДокумента");
		МассивКлючевыхПолей.Добавить("Сумма");
		МассивКлючевыхПолей.Добавить("СуммаНДС");
		МассивКлючевыхПолей.Добавить("СуммаСНДС");
	
		ЗаполнитьТабличнуюЧастьОбъектаНачальнымиДанными(
			ПолученныеДанные.ПрочиеРасходы,
			ДанныеИБ.ПрочиеРасходы,
			МассивКлючевыхПолей,
			"СтатьяРасходов, АналитикаРасходов, АналитикаАктивовПассивов, Подразделение, СчетУчета, Субконто1, Субконто2, Субконто3");
	КонецЕсли;
	
	ДанныеИБ.ПрочиеРасходы.Загрузить(ПолученныеДанные.ПрочиеРасходы.Выгрузить());
	
	ПолученныеДанные = Неопределено;
КонецПроцедуры
