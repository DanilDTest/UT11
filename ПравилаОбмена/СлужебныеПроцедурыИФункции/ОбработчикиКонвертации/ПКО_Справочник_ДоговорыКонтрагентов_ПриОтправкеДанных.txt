Процедура ПКО_Справочник_ДоговорыКонтрагентов_ПриОтправкеДанных(ДанныеИБ, ДанныеXDTO, КомпонентыОбмена, СтекВыгрузки)
	ДанныеXDTO.КлючевыеСвойства.Вставить("РасчетыВУсловныхЕдиницах", Ложь);
	
	Если ДанныеИБ.ПорядокОплаты = Перечисления.ПорядокОплатыПоСоглашениям.РасчетыВВалютеОплатаВРублях Тогда
		ДанныеXDTO.КлючевыеСвойства.РасчетыВУсловныхЕдиницах = Истина;
	КонецЕсли;
	
	ИнструкцияВидДоговора = Новый Структура;
	ИнструкцияВидДоговора.Вставить("ИмяПКО",   "Перечисление_ТипыДоговоров_Отправка");
	ИнструкцияВидДоговора.Вставить("Значение", ДанныеИБ.ТипДоговора);
	
	Если ДанныеИБ.ТипДоговора = Перечисления.ТипыДоговоров.СПоставщиком Тогда	
		Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ТаможеннаяДекларацияИмпорт.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ТаможеннаяДекларацияИмпорт КАК ТаможеннаяДекларацияИмпорт
		|ГДЕ
		|	ТаможеннаяДекларацияИмпорт.Договор = &Договор
		|	И ТаможеннаяДекларацияИмпорт.Проведен");
		Запрос.УстановитьПараметр("Договор", ДанныеИБ.Ссылка);
	
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			ДанныеXDTO.КлючевыеСвойства.Вставить("ВидДоговора", "Прочее");
		Иначе
			ДанныеXDTO.КлючевыеСвойства.Вставить("ВидДоговора", ИнструкцияВидДоговора);
		КонецЕсли;
	Иначе
		ДанныеXDTO.КлючевыеСвойства.Вставить("ВидДоговора", ИнструкцияВидДоговора);
	КонецЕсли;
	
	Если СтекВыгрузки.Количество() > 1 Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеXDTO.КлючевыеСвойства.Наименование = СокрЛП(ДанныеИБ.Наименование);
	
	ВыгрузитьДополнительныеРеквизитыИСведения(КомпонентыОбмена, ДанныеИБ, ДанныеXDTO, Истина);       
	ВыгрузитьПереводыРеквизитов(ДанныеИБ.Ссылка, ДанныеXDTO);
КонецПроцедуры
