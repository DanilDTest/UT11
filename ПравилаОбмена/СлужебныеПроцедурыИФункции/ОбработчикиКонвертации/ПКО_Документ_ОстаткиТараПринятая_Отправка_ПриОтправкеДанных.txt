Процедура ПКО_Документ_ОстаткиТараПринятая_Отправка_ПриОтправкеДанных(ДанныеИБ, ДанныеXDTO, КомпонентыОбмена, СтекВыгрузки)
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ВводОстатковТовары.Номенклатура КАК Номенклатура,
		|	&ТекстЗапросаХарактеристика,
		|	&ТекстЗапросаУпаковка,
		|	&ТекстЗапросаСерия,
		|	ВводОстатковТовары.Количество КАК Количество,
		|	ВводОстатковТовары.СтавкаНДС КАК СтавкаНДС,
		|	ВводОстатковТовары.СуммаБезНДС КАК СуммаБезНДС,
		|	ВводОстатковТовары.СуммаНДС КАК СуммаНДС,
		|	ВводОстатковТовары.НомерГТД.Код КАК НомерГТД,
		|	ВводОстатковТовары.НомерГТД.СтранаПроисхождения КАК СтранаПроисхождения,
		|	ВводОстатковТовары.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	ЕСТЬNULL(СправочникНоменклатура.ЕдиницаИзмерения, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)) КАК ЕдиницаИзмерения
		|ИЗ
		|	Документ.ВводОстатков.Товары КАК ВводОстатковТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
		|		ПО ВводОстатковТовары.Номенклатура = СправочникНоменклатура.Ссылка
		|ГДЕ
		|	ВводОстатковТовары.Ссылка = &Ссылка";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаХарактеристика",
		ПолучитьТекстЗапросаХарактеристики(КомпонентыОбмена, "ВводОстатковТовары"));
		
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаУпаковка",
		ПолучитьТекстЗапросаУпаковки(КомпонентыОбмена, "ВводОстатковТовары"));
		
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаСерия",
		ПолучитьТекстЗапросаСерии(КомпонентыОбмена, "ВводОстатковТовары"));
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Ссылка", ДанныеИБ.Ссылка);
	Товары = Запрос.Выполнить().Выгрузить();
	ЗаполнитьХарактеристикуУпаковкуВТЧ(КомпонентыОбмена, Товары);
	ДанныеXDTO.Вставить("Тара", Товары);
КонецПроцедуры
