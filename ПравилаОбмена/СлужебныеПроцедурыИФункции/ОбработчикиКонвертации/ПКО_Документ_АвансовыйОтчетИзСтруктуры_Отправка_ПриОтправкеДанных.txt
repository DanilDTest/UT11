Процедура ПКО_Документ_АвансовыйОтчетИзСтруктуры_Отправка_ПриОтправкеДанных(ДанныеИБ, ДанныеXDTO, КомпонентыОбмена, СтекВыгрузки)
	ВыгрузитьПодразделение(ДанныеИБ, ДанныеXDTO);
	ВыгрузитьНалогообложениеНДС(КомпонентыОбмена, ДанныеИБ, ДанныеXDTO, Истина, Ложь, "НалогообложениеНДСПродавца");
	
	Запрос = Новый Запрос;
	
	// Данные шапки документа необходимы для вычисления курса и кратности.
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 1) КАК КурсВзаиморасчетов,
	|	ЕСТЬNULL(КурсыВалютСрезПоследних.Кратность, 1) КАК КратностьВзаиморасчетов
	|ИЗ
	|	(ВЫБРАТЬ
	|		&Валюта КАК Валюта) КАК ВалютаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&Дата, Валюта = &Валюта) КАК КурсыВалютСрезПоследних
	|		ПО (КурсыВалютСрезПоследних.Валюта = ВалютаДокумента.Валюта)";
	
	Запрос.УстановитьПараметр("Дата", ДанныеИБ.Дата);
	Запрос.УстановитьПараметр("Валюта", ДанныеИБ.Валюта);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ДанныеXDTO.Вставить("КурсДокумента", Выборка.КурсВзаиморасчетов);
		ДанныеXDTO.Вставить("КратностьДокумента", Выборка.КратностьВзаиморасчетов);
	КонецЕсли;
	
	Если ДанныеИБ.Свойство("Склад") Тогда
		СкладДляВыгрузки = СкладДляВыгрузки(ДанныеИБ.Склад, КомпонентыОбмена.ПараметрыКонвертации);
		ДанныеXDTO.Вставить("Склад", СкладДляВыгрузки);
	КонецЕсли;
	
	Товары             = ДанныеИБ.Товары;
	ПрочиеРасходы      = ДанныеИБ.ПрочиеРасходы;
	ОплатаКонтрагентам = ДанныеИБ.ОплатаКонтрагентам;
	ВыданныеАвансы     = ДанныеИБ.ВыданныеАвансы;
	
	ДанныеXDTO.Вставить("Товары",				Товары);
	ДанныеXDTO.Вставить("ПрочиеРасходы",		ПрочиеРасходы);
	ДанныеXDTO.Вставить("ОплатаКонтрагентам",	ОплатаКонтрагентам);
	ДанныеXDTO.Вставить("ВыданныеАвансы",		ВыданныеАвансы);
	
	#Область АкцизныеМарки
	
	Если ВерсияФорматаЧислом(КомпонентыОбмена.ВерсияФорматаОбмена) >= ВерсияФорматаЧислом("1.7")
		И ДанныеИБ.Свойство("АкцизныеМарки") Тогда
		ДанныеXDTO.Вставить("АкцизныеМарки", ДанныеИБ.АкцизныеМарки);
	КонецЕсли;
	
	#КонецОбласти
	
	#Область МаркировкиУпаковок
	
	Если ВерсияФорматаЧислом(КомпонентыОбмена.ВерсияФорматаОбмена) >= ВерсияФорматаЧислом("1.7")
		И ДанныеИБ.Свойство("МаркировкиУпаковок") Тогда	
		ДанныеXDTO.Вставить("МаркировкиУпаковок", ДанныеИБ.МаркировкиУпаковок);
	КонецЕсли;
	
	#КонецОбласти
	
	ДанныеXDTO.Вставить("СуммаВключаетНДС", Истина);
	ДанныеXDTO.Вставить("Сумма", Товары.Итог("Сумма") + ПрочиеРасходы.Итог("Сумма") + ОплатаКонтрагентам.Итог("Сумма"));
	
	Если ДанныеИБ.Свойство("Ссылка")
		И Не ДанныеИБ.Свойство("ВыгружатьУдалениеПоСсылке") Тогда
		ДанныеXDTO.КлючевыеСвойства.Вставить("Ссылка", ДанныеИБ.Ссылка);
	КонецЕсли;
КонецПроцедуры
