Процедура ПОД_Документ_ВЗ_КорректировкаДолга_Отправка_ПриОбработке(ДанныеИБ, ИспользованиеПКО, КомпонентыОбмена)
	Если КомпонентыОбмена.ЭтоОбменЧерезПланОбмена Тогда
		ИспользоватьОтборПоОрганизациям = КомпонентыОбмена.ПараметрыКонвертации.ИспользоватьОтборПоОрганизациям;
		ВыгружатьУпрОрганизацию         = КомпонентыОбмена.ПараметрыКонвертации.ВыгружатьУправленческуюОрганизацию;
		УзелСписокОрганизаций           = КомпонентыОбмена.ПараметрыКонвертации.Организации;
	Иначе
		ИспользоватьОтборПоОрганизациям = Ложь;
		ВыгружатьУпрОрганизацию         = Истина;
		УзелСписокОрганизаций           = Новый Массив();
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеИБ.ОрганизацияКредитор)
		И ТипЗнч(ДанныеИБ.ОрганизацияКредитор) = Тип("СправочникСсылка.Организации") Тогда
		
		ДанныеИБ.ДополнительныеСвойства.Вставить("ВзаимозачетСУчастиемДвухОрганизаций", Истина);
	
		ИспользованиеПКО.Документ_ВЗ_НашаОрганизация = Ложь;
		
		Если ИспользоватьОтборПоОрганизациям Тогда
			
			Если УзелСписокОрганизаций.НайтиПоЗначению(ДанныеИБ.Организация) <> Неопределено Тогда
				ИспользованиеПКО.Документ_ВЗ_ОрганизацияДебитор  = Истина;
			Иначе
				ИспользованиеПКО.Документ_ВЗ_ОрганизацияДебитор  = Ложь;
			КонецЕсли;
			
			Если УзелСписокОрганизаций.НайтиПоЗначению(ДанныеИБ.ОрганизацияКредитор) <> Неопределено Тогда
				ИспользованиеПКО.Документ_ВЗ_ОрганизацияКредитор = Истина;
			Иначе
				ИспользованиеПКО.Документ_ВЗ_ОрганизацияКредитор = Ложь;
			КонецЕсли;
			
		Иначе
			
			ИспользованиеПКО.Документ_ВЗ_ОрганизацияДебитор  = ВыгружатьУпрОрганизацию
				Или ДанныеИБ.Организация <> УправленческаяОрганизация();
			ИспользованиеПКО.Документ_ВЗ_ОрганизацияКредитор = ВыгружатьУпрОрганизацию
				Или ДанныеИБ.ОрганизацияКредитор <> УправленческаяОрганизация();
				
		КонецЕсли;
		
	Иначе
		
		Если ИспользоватьОтборПоОрганизациям Тогда
		
			Если УзелСписокОрганизаций.НайтиПоЗначению(ДанныеИБ.Организация) <> Неопределено Тогда
				ИспользованиеПКО.Документ_ВЗ_НашаОрганизация = Истина;
			Иначе
				ИспользованиеПКО.Документ_ВЗ_НашаОрганизация = Ложь;
			КонецЕсли;
			
			Если ТипЗнч(ДанныеИБ.КонтрагентКредитор) = Тип("СправочникСсылка.Организации")
				И УзелСписокОрганизаций.НайтиПоЗначению(ДанныеИБ.КонтрагентКредитор) <> Неопределено Тогда
				ИспользованиеПКО.Документ_ВЗ_ОрганизацияКредитор = Истина;
			Иначе
				ИспользованиеПКО.Документ_ВЗ_ОрганизацияКредитор = Ложь;
			КонецЕсли;
			
			Если ТипЗнч(ДанныеИБ.КонтрагентДебитор) = Тип("СправочникСсылка.Организации")
				И УзелСписокОрганизаций.НайтиПоЗначению(ДанныеИБ.КонтрагентДебитор) <> Неопределено Тогда
				ИспользованиеПКО.Документ_ВЗ_ОрганизацияДебитор = Истина;
			Иначе
				ИспользованиеПКО.Документ_ВЗ_ОрганизацияДебитор = Ложь;
			КонецЕсли;
			
		Иначе
			
			ИспользованиеПКО.Документ_ВЗ_НашаОрганизация = ВыгружатьУпрОрганизацию 
				Или ДанныеИБ.Организация <> УправленческаяОрганизация();
				
			ТипОрганизацияКлиент = Перечисления.ТипыУчастниковВзаимозачета.ОрганизацияКлиент;
				
			ИспользованиеПКО.Документ_ВЗ_ОрганизацияКредитор = (ДанныеИБ.ТипКредитора = ТипОрганизацияКлиент
				Или ДанныеИБ.ТипКредитора = Перечисления.ТипыУчастниковВзаимозачета.ОрганизацияПоставщик)
				И (ВыгружатьУпрОрганизацию Или ДанныеИБ.КонтрагентКредитор <> УправленческаяОрганизация());
				
			ИспользованиеПКО.Документ_ВЗ_ОрганизацияДебитор = (ДанныеИБ.ТипДебитора = ТипОрганизацияКлиент
				Или ДанныеИБ.ТипДебитора = Перечисления.ТипыУчастниковВзаимозачета.ОрганизацияПоставщик)
				И (ВыгружатьУпрОрганизацию Или ДанныеИБ.КонтрагентДебитор <> УправленческаяОрганизация());
				
		КонецЕсли;
	
	КонецЕсли;
	
	ПроверкаЗаполненияРеквизитовОбъектаИБ(ДанныеИБ, КомпонентыОбмена, ИспользованиеПКО);
КонецПроцедуры
