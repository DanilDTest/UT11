Процедура ПКО_Документ_ОтчетКомиссияОрганизСписаниеКомитент_ПриОтправкеДанных(ДанныеИБ, ДанныеXDTO, КомпонентыОбмена, СтекВыгрузки)
	НомерДок = Лев(ДанныеИБ.Номер, 5) + "ИС" + Сред(ДанныеИБ.Номер, 8);
	ДанныеXDTO.КлючевыеСвойства.Вставить("Номер", НомерДок);
	
	Если СтекВыгрузки.Количество() > 1 Тогда
		Возврат;
	КонецЕсли;
	
	ВыгрузитьПодразделение(ДанныеИБ, ДанныеXDTO);
	
	Если ДанныеИБ.РасчетыЧерезОтдельногоКонтрагента Тогда
		КонтрагентСсылка = ДанныеИБ.Контрагент;
	Иначе
		КонтрагентСсылка = КонтрагентИзОрганизации(ДанныеИБ.Организация, КомпонентыОбмена);
	КонецЕсли;
	ДанныеXDTO.Вставить("Контрагент", КонтрагентСсылка);
	
	#Область Договор
	
	ИнструкцияДоговор = Неопределено;
	Если ДанныеИБ.РасчетыЧерезОтдельногоКонтрагента
		И ЗначениеЗаполнено(ДанныеИБ.ДоговорПродажи)
		И ОбщегоНазначения.СсылкаСуществует(ДанныеИБ.ДоговорПродажи) Тогда
		
		ИнструкцияДоговор = Новый Структура;
		ИнструкцияДоговор.Вставить("ИмяПКО",   "Справочник_ДоговорыКонтрагентов");
		ИнструкцияДоговор.Вставить("Значение", ДанныеИБ.ДоговорПродажи);
	
	ИначеЕсли ЗначениеЗаполнено(ДанныеИБ.Договор) Тогда
		
		ДанныеДоговора = ДанныеДоговораМеждуОрганизациями(КомпонентыОбмена, ДанныеИБ.Договор);
		ИнструкцияДоговор = Новый Структура("Значение, ИмяПКО", ДанныеДоговора, "Справочник_ДоговорыКонтрагентов_ИзСтруктуры");
		
	Иначе
		ПараметрыПоУмолчанию = Новый Структура(ДоговорФиксированнаяСтруктураКлючей());
		ПараметрыПоУмолчанию.ВидДоговора              = "СКомитентом";
		ПараметрыПоУмолчанию.Организация              = ДанныеИБ.Комиссионер;
		ПараметрыПоУмолчанию.Контрагент               = КонтрагентСсылка;
		ПараметрыПоУмолчанию.ВалютаВзаиморасчетов     = ДанныеИБ.Валюта;
		ПараметрыПоУмолчанию.РасчетыВУсловныхЕдиницах = Ложь;
		
		ИнструкцияДоговор = ДоговорИнструкцияКонвертацииПоДаннымВзаиморасчетов(ПараметрыПоУмолчанию,
			КомпонентыОбмена,
			ДанныеИБ.Договор);
	КонецЕсли;
	ДанныеXDTO.Вставить("Договор", ИнструкцияДоговор);
	
	#КонецОбласти
	
	ДанныеXDTO.Вставить("СуммаВключаетНДС", Истина);
	ДанныеXDTO.Вставить("СпособРасчетаКомиссионногоВознаграждения","НеРассчитывается");
	Если ДанныеИБ.Дата >= Дата("20190101") Тогда
		ДанныеXDTO.Вставить("СтавкаНДСВознаграждения","НДС20");
	Иначе
		ДанныеXDTO.Вставить("СтавкаНДСВознаграждения","НДС18");
	КонецЕсли;
	
	ВыгрузитьКурсИКратностьВзаиморасчетов(ДанныеXDTO, ДанныеИБ.Валюта, ДанныеИБ.Дата);
	
	//Товары
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	Товары.НомерСтроки КАК НомерСтрокиДокумента,
		|	Товары.Номенклатура КАК Номенклатура,
		|	&ТекстЗапросаХарактеристика,
		|	&ТекстЗапросаУпаковка,
		|	Товары.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	Товары.Количество КАК Количество,
		|	ВЫБОР
		|		КОГДА Товары.СуммаСНДС / Товары.Количество < 0.01
		|			ТОГДА 0.01
		|		ИНАЧЕ ВЫРАЗИТЬ(Товары.СуммаСНДС / Товары.Количество КАК ЧИСЛО(15, 2))
		|	КОНЕЦ КАК ЦенаПоступления,
		|	ВЫБОР
		|		КОГДА Товары.СуммаСНДС / Товары.Количество < 0.01
		|			ТОГДА 0.01
		|		ИНАЧЕ ВЫРАЗИТЬ(Товары.СуммаСНДС / Товары.Количество КАК ЧИСЛО(15, 2))
		|	КОНЕЦ КАК Цена,
		|	Товары.СуммаСНДС КАК СуммаПоступления,
		|	Товары.СуммаСНДС КАК Сумма,
		|	Товары.СтавкаНДС КАК СтавкаНДС,
		|	Товары.СуммаНДС КАК СуммаНДС
		|ИЗ
		|	Документ.ОтчетПоКомиссииМеждуОрганизациямиОСписании.Товары КАК Товары
		|ГДЕ
		|	Товары.Ссылка = &Ссылка";
	
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаХарактеристика",
		ПолучитьТекстЗапросаХарактеристики(КомпонентыОбмена, "Товары"));
		
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаУпаковка",
		ПолучитьТекстЗапросаУпаковки(КомпонентыОбмена, "Товары"));
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Ссылка", ДанныеИБ.Ссылка);
	
	Товары = Запрос.Выполнить().Выгрузить();
	ЗаполнитьХарактеристикуУпаковкуВТЧ(КомпонентыОбмена, Товары);
	
	ДанныеXDTO.Вставить("Товары", Товары);
	
	ВыгрузитьДополнительныеРеквизитыИСведения(КомпонентыОбмена, ДанныеИБ, ДанныеXDTO);
КонецПроцедуры
