Процедура ПКО_Документ_СчетФактураПолученныйНалоговыйАгент_Отп_ПриОтправкеДанных(ДанныеИБ, ДанныеXDTO, КомпонентыОбмена, СтекВыгрузки)
	Если СтекВыгрузки.Количество() > 1 Тогда
		Возврат;
	КонецЕсли;
	
	Если ВерсияФорматаЧислом(КомпонентыОбмена.ВерсияФорматаОбмена) >= ВерсияФорматаЧислом("1.7") Тогда
		ДанныеXDTO.Вставить("ИННКонтрагента", ДанныеИБ.ИННКонтрагента);
		ДанныеXDTO.Вставить("КППКонтрагента", ДанныеИБ.КППКонтрагента);
	КонецЕсли;
	
	// Документ основание
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДокументОснование,
		|	ИсходныйДокумент
		|ИЗ 
		|	Документ.СчетФактураПолученныйНалоговыйАгент.ДокументыОснования
		|ГДЕ 
		|	Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ДанныеИБ.Ссылка);
	ВыборкаТЧ = Запрос.Выполнить().Выбрать();
	
	ДокументыОснования = Новый ТаблицаЗначений();
	ДокументыОснования.Колонки.Добавить("ДокументОснование");
	ДокументыОснования.Колонки.Добавить("ИсходныйДокумент");
	
	Пока ВыборкаТЧ.Следующий() Цикл
		
		ДокументОснование = ВыборкаТЧ.ДокументОснование;
	
		ИмяПКО = ИмяПКОДляОбъектаДанных(КомпонентыОбмена, ДокументОснование, ДанныеИБ.Организация);
		
		Если НЕ (ЗначениеЗаполнено(ДокументОснование) И ЗначениеЗаполнено(ИмяПКО)) Тогда
			Продолжить;
		КонецЕсли;
			
		СтрокаОснование = ДокументыОснования.Добавить();
		СтрокаОснование.ДокументОснование = Новый Структура("Значение, ИмяПКО", ДокументОснование, ИмяПКО);
		Если ЗначениеЗаполнено(ВыборкаТЧ.ИсходныйДокумент) Тогда
			СтрокаОснование.ИсходныйДокумент = ВыборкаТЧ.ИсходныйДокумент;
		КонецЕсли;
	
	КонецЦикла;
	
	ДанныеXDTO.Вставить("ДокументыОснования", ДокументыОснования);
	ДанныеXDTO.Вставить("ВидСчетаФактуры",    "НалоговыйАгент");
	
	ВыгрузитьСпособВыставленияСчетаФактуры(ДанныеXDTO, ДанныеИБ.ПолученВЭлектронномВиде);
	
	ДанныеXDTO.Вставить("Сумма",    ДанныеИБ.ДокументыОснования.Итог("Сумма"));
	ДанныеXDTO.Вставить("СуммаНДС", ДанныеИБ.ДокументыОснования.Итог("СуммаНДС"));
	
	ТабДокументыОснования = ДанныеИБ.ДокументыОснования.Выгрузить();
	ТабДокументыОснования.Свернуть("СтавкаНДС");
	Если ТабДокументыОснования.Количество() = 1 Тогда
		ДанныеXDTO.Вставить("СтавкаНДС", ТабДокументыОснования[0].СтавкаНДС);
	КонецЕсли;
	
	ВыгрузитьДополнительныеРеквизитыИСведения(КомпонентыОбмена, ДанныеИБ, ДанныеXDTO);
КонецПроцедуры
