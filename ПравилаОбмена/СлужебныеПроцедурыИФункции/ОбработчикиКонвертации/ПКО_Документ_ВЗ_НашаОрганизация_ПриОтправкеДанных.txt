Процедура ПКО_Документ_ВЗ_НашаОрганизация_ПриОтправкеДанных(ДанныеИБ, ДанныеXDTO, КомпонентыОбмена, СтекВыгрузки)
	Если СтекВыгрузки.Количество() > 1 Тогда
		ДанныеXDTO.Вставить("Номер", Лев(ДанныеИБ.Номер, 5) + "В" + Сред(ДанныеИБ.Номер, 7));
		Возврат;
	КонецЕсли;	
	
	СсылкаДебитор     = ДанныеИБ.КонтрагентДебитор;
	СсылкаКредитор    = ДанныеИБ.КонтрагентКредитор;
	
	ДанныеXDTO.КлючевыеСвойства.Вставить("Номер", Лев(ДанныеИБ.Номер, 5) + "В" + Сред(ДанныеИБ.Номер, 7));
	
	ДанныеXDTO.Вставить("ВидОперации", "ПроведениеВзаимозачета");
	ДанныеXDTO.Вставить("Валюта",      ВалютаРегламентированногоУчета(КомпонентыОбмена));
	
	ВыгрузитьКурсИКратностьДокумента(ДанныеXDTO, КомпонентыОбмена.ПараметрыКонвертации.ВалютаРегламентированногоУчета, ДанныеИБ.Дата);
	ВыгрузитьПодразделение(ДанныеИБ, ДанныеXDTO);
	
	КонтрагентДебитор = СсылкаДебитор;
	Если ТипЗнч(КонтрагентДебитор) = Тип("СправочникСсылка.Организации") Тогда
		КонтрагентДебитор = КонтрагентИзОрганизации(КонтрагентДебитор, КомпонентыОбмена);
	КонецЕсли;
	
	КонтрагентКредитор = СсылкаКредитор;
	Если ТипЗнч(КонтрагентКредитор) = Тип("СправочникСсылка.Организации") Тогда
		КонтрагентКредитор = КонтрагентИзОрганизации(КонтрагентКредитор, КомпонентыОбмена);
	КонецЕсли;
	
	ДанныеXDTO.Вставить("КонтрагентДебитор",  КонтрагентДебитор);
	ДанныеXDTO.Вставить("КонтрагентКредитор", КонтрагентКредитор);
	
	ТаблицаДЗ = ВыгрузитьТаблицуЗадолженности("ДебиторскаяЗадолженность",
		ДанныеИБ.Ссылка, ДанныеXDTO.КонтрагентДебитор, ДанныеИБ.Организация, , КомпонентыОбмена);
	ДанныеXDTO.Вставить("ДебиторскаяЗадолженность", ТаблицаДЗ);
	
	ТаблицаКЗ = ВыгрузитьТаблицуЗадолженности("КредиторскаяЗадолженность",
		ДанныеИБ.Ссылка, ДанныеXDTO.КонтрагентКредитор, ДанныеИБ.Организация, , КомпонентыОбмена);
	ДанныеXDTO.Вставить("КредиторскаяЗадолженность", ТаблицаКЗ);
	
	ВыгрузитьДополнительныеРеквизитыИСведения(КомпонентыОбмена, ДанныеИБ, ДанныеXDTO);
КонецПроцедуры
