Процедура ПКО_Документ_РКОРасчетыСКонтрагентами_Получение_ПриКонвертацииДанныхXDTO(ДанныеXDTO, ПолученныеДанные, КомпонентыОбмена)
	#Область Шапка
	
	ВидРасчетов = "СПоставщиком";
	Если ДанныеXDTO.Свойство("ВидРасчетов") Тогда
		ВидРасчетов = ДанныеXDTO.ВидРасчетов.Значение;
	КонецЕсли;
	
	Если ВидРасчетов = "СПоставщиком" Тогда
		ПолученныеДанные.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОплатаПоставщику;
	ИначеЕсли ВидРасчетов = "СПокупателем" Тогда
		ПолученныеДанные.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратОплатыКлиенту;
	Иначе
		ПолученныеДанные.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПрочаяВыдачаДенежныхСредств;
	КонецЕсли;
	
	ЗагрузитьОтветственноеЛицо(ПолученныеДанные, ДанныеXDTO, КомпонентыОбмена, "ГлавныйБухгалтер");
	ЗагрузитьОтветственноеЛицо(ПолученныеДанные, ДанныеXDTO, КомпонентыОбмена, "Руководитель");
	
	// СтатьяДвиженияДенежныхСредств
	Если ПолученныеДанные.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПрочаяВыдачаДенежныхСредств Тогда
		ПолученныеДанные.ДополнительныеСвойства.Вставить("СтатьяДвиженияДенежныхСредств",
			ДанныеXDTO.РасшифровкаПлатежа[0].СтатьяДДС);
	КонецЕсли;
		
	ДобавитьВалютуВДопСвойства(ПолученныеДанные, ДанныеXDTO);
	
	ПолученныеДанные.НеКонтролироватьЗаполнениеЗаявки = Истина;
	
	#КонецОбласти
	ЗагрузитьДополнительныеРеквизиты(ПолученныеДанные, ДанныеXDTO, КомпонентыОбмена);
	
	#Область РасшифровкаПлатежа
	ЗаполнятьДоговор = (ВидРасчетов = "СПокупателем" И КомпонентыОбмена.ПараметрыКонвертации.ИспользуютсяДоговорыСКлиентами)
					ИЛИ (ВидРасчетов = "СПоставщиком" И КомпонентыОбмена.ПараметрыКонвертации.ИспользуютсяДоговорыСПоставщиками);
	
	МассивСтрокРасшифровкаПлатежа = Новый Массив;
	Если ЗначениеЗаполнено(ДанныеXDTO.РасшифровкаПлатежа) Тогда
	
		Т_РП = Новый Соответствие;
		Т_РП.Вставить("Сумма", "Сумма");
		Т_РП.Вставить("СуммаВзаиморасчетов", "Сумма");
		Т_РП.Вставить("СтатьяДвиженияДенежныхСредств", "СтатьяДДС");
		Т_РП.Вставить("СтавкаНДС", "СтавкаНДС");
		Т_РП.Вставить("СуммаНДС", "СуммаНДС");
		
		Для Каждого СтрокаXDTO Из ДанныеXDTO.РасшифровкаПлатежа Цикл
			СтруктураДанныхСтроки = Новый Структура;
			ЗаполнитьСтруктуруДанныхСтроки(Т_РП, СтрокаXDTO, СтруктураДанныхСтроки);
			ДобавитьВалютуВРасшифровкуПлатежа(СтруктураДанныхСтроки, СтрокаXDTO);
			
			ИнструкцияЗаказ = Неопределено;
			Если ЗначениеЗаполнено(СтрокаXDTO.ДокументРасчетов) Тогда
				ИнструкцияЗаказ = ЗначениеВИнструкциюПКО(КомпонентыОбмена, СтрокаXDTO.ДокументРасчетов);
			КонецЕсли;
			Если НЕ ЗначениеЗаполнено(ИнструкцияЗаказ) И ЗначениеЗаполнено(СтрокаXDTO.Заказ) Тогда
				ИнструкцияЗаказ = ЗначениеВИнструкциюПКО(КомпонентыОбмена, СтрокаXDTO.Заказ);
			КонецЕсли; 
			Если ЗначениеЗаполнено(ИнструкцияЗаказ) Тогда
			    СтруктураДанныхСтроки.Вставить("Заказ", ИнструкцияЗаказ);	
			Иначе
				//ДобавитьДоговорВРасшифровкуПлатежа(ЗаполнятьДоговор, СтруктураДанныхСтроки, СтрокаXDTO);
			КонецЕсли;
			
			МассивСтрокРасшифровкаПлатежа.Добавить(СтруктураДанныхСтроки);
		КонецЦикла;
		
		ПолученныеДанные.ДополнительныеСвойства.Вставить("РасшифровкаПлатежа", МассивСтрокРасшифровкаПлатежа);
	
	КонецЕсли;
	
	#КонецОбласти
КонецПроцедуры
