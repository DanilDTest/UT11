Процедура ПКО_Документ_СБДСПрочееСписание_Получение_ПриКонвертацииДанныхXDTO(ДанныеXDTO, ПолученныеДанные, КомпонентыОбмена)
	ПолученныеДанные.Проведен = Истина;
	ПолученныеДанные.ПроведеноБанком = ЗначениеЗаполнено(ПолученныеДанные.ДатаПроведенияБанком);
	ДобавитьВалютуВДопСвойства(ПолученныеДанные, ДанныеXDTO);
	// Уточнение вида операции
	
	СчетУчетаРасчетов = Неопределено;
	Если ДанныеXDTO.РасшифровкаПлатежа.Колонки.Найти("СчетУчетаРасчетов") <> Неопределено Тогда
		СчетУчетаРасчетов = ДанныеXDTO.РасшифровкаПлатежа[0].СчетУчетаРасчетов;
	КонецЕсли;
	Если ЗначениеЗаполнено(СчетУчетаРасчетов) Тогда
		Если Найти(СчетУчетаРасчетов, "57.01") > 0 Тогда
			ПолученныеДанные.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.СнятиеНаличныхДенежныхСредств;
		КонецЕсли;
	КонецЕсли;
	
	// СтатьяДДС
	Если  ДанныеXDTO.РасшифровкаПлатежа.Количество() > 0 Тогда
		ПравилаЗаполнения = Новый Соответствие;
		ПравилаЗаполнения.Вставить("Сумма", "Сумма");
		ПравилаЗаполнения.Вставить("СтатьяДДС", "СтатьяДвиженияДенежныхСредств");
		МассивСтрокРасшифровка = Новый Массив;
		Для Каждого Строка Из ДанныеXDTO.РасшифровкаПлатежа Цикл
			СтруктураДанныхСтроки = ДанныеКоллекцииВВидеСтруктуры(Строка, ПравилаЗаполнения);
			МассивСтрокРасшифровка.Добавить(СтруктураДанныхСтроки);
		КонецЦикла;
		ПолученныеДанные.ДополнительныеСвойства.Вставить("РасшифровкаПлатежа", МассивСтрокРасшифровка);
		
		Если ДанныеXDTO.РасшифровкаПлатежа.Количество() = 1 Тогда
			ПолученныеДанные.ДополнительныеСвойства.Вставить("СтатьяДвиженияДенежныхСредств",
				ДанныеXDTO.РасшифровкаПлатежа[0].СтатьяДДС);
		КонецЕсли;
	КонецЕсли;
	
	Если ДанныеXDTO.Свойство("КодВидаДохода") 
		И ЗначениеЗаполнено(ДанныеXDTO.КодВидаДохода) Тогда
		ПолученныеДанные.ДополнительныеСвойства.Вставить("КодВидаДохода",
			ДанныеXDTO.КодВидаДохода);
	КонецЕсли;
	
	ЗагрузитьДополнительныеРеквизиты(ПолученныеДанные, ДанныеXDTO, КомпонентыОбмена);
КонецПроцедуры
