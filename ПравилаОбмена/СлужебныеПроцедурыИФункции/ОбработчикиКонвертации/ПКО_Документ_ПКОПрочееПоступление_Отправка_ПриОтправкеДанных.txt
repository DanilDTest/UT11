Процедура ПКО_Документ_ПКОПрочееПоступление_Отправка_ПриОтправкеДанных(ДанныеИБ, ДанныеXDTO, КомпонентыОбмена, СтекВыгрузки)
	Если СтекВыгрузки.Количество() > 1 Тогда
		Возврат;
	КонецЕсли;
	
	ВыгрузитьПодразделениеИзРеквизитаДокумента(КомпонентыОбмена, ДанныеИБ, ДанныеXDTO, "Касса");
	ВыгрузитьОтветственноеЛицо(КомпонентыОбмена, ДанныеИБ, ДанныеXDTO, "ГлавныйБухгалтер");
	ВыгрузитьДополнительныеРеквизитыИСведения(КомпонентыОбмена, ДанныеИБ, ДанныеXDTO);
	
	ТабРасшифровка = Новый ТаблицаЗначений;
	ТабРасшифровка.Колонки.Добавить("СтатьяДДС");
	ТабРасшифровка.Колонки.Добавить("Сумма");
	ТабРасшифровка.Колонки.Добавить("СчетУчетаРасчетов");
	
	Если ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ИнкассацияДенежныхСредствИзБанка Тогда
		// Операция конвертируется по этому правилу только при использовании формата "1.0.beta" и "1.0".
		НоваяСтрока = ТабРасшифровка.Добавить();
		НоваяСтрока.СтатьяДДС         = ДанныеИБ.СтатьяДвиженияДенежныхСредств;
		НоваяСтрока.Сумма             = ДанныеИБ.СуммаДокумента;
		НоваяСтрока.СчетУчетаРасчетов = "57.01";
	Иначе
		// Выгружается документ с хоз. операцией "Прочее поступление денежных средств", 
		// в табличной части которого статьи доходов либо не заполнены,
		// либо в ней указаны только статьи активов/пассивов.
		Если ДанныеИБ.РасшифровкаПлатежа.Количество() > 0 Тогда
			Для Каждого Строка Из ДанныеИБ.РасшифровкаПлатежа Цикл
				Если ЭтоУТ() Тогда
					СчетРасчетов = СчетУчетаРасчетовПрочихОперацийДДС(Новый Структура("СтатьяДДС",Строка.СтатьяДвиженияДенежныхСредств));
				КонецЕсли;
	
				НоваяСтрока = ТабРасшифровка.Добавить();
				НоваяСтрока.СтатьяДДС         = Строка.СтатьяДвиженияДенежныхСредств;
				НоваяСтрока.Сумма             = Строка.Сумма;
				НоваяСтрока.СчетУчетаРасчетов = СчетРасчетов;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	ДанныеXDTO.Вставить("РасшифровкаПлатежа", ТабРасшифровка);
КонецПроцедуры
