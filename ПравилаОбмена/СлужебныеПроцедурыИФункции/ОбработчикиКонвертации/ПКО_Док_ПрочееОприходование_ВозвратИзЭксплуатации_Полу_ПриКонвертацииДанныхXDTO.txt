Процедура ПКО_Док_ПрочееОприходование_ВозвратИзЭксплуатации_Полу_ПриКонвертацииДанныхXDTO(ДанныеXDTO, ПолученныеДанные, КомпонентыОбмена)
	ПолученныеДанные.Проведен = Истина;
	ЗагрузитьДополнительныеРеквизиты(ПолученныеДанные, ДанныеXDTO, КомпонентыОбмена);
	ПолученныеДанные.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратИзЭксплуатации; 
	
	#Область Товары
	
	ПравилаЗаполнения = Новый Соответствие;
	ПравилаЗаполнения.Вставить("Номенклатура",        "Номенклатура");
	ПравилаЗаполнения.Вставить("Количество",          "Количество");
	ПравилаЗаполнения.Вставить("Цена",                "Цена");
	ПравилаЗаполнения.Вставить("Сумма",               "Сумма");
	Если ЗначениеФО("ИспользоватьХарактеристикиНоменклатуры", КомпонентыОбмена) Тогда
		ПравилаЗаполнения.Вставить("Характеристика",     "Характеристика");
	КонецЕсли;
	Если ЗначениеФО("ИспользоватьУпаковкиНоменклатуры", КомпонентыОбмена) Тогда
		ПравилаЗаполнения.Вставить("Упаковка",           "Упаковка");
		ПравилаЗаполнения.Вставить("КоличествоУпаковок", "КоличествоУпаковок");
	КонецЕсли;
	Если ЗначениеФО("ИспользоватьСерииНоменклатуры", КомпонентыОбмена) Тогда
		ПравилаЗаполнения.Вставить("Серия",              "Серия");
	КонецЕсли;
	
	ПравилаЗаполнения.Вставить("ДокументПередачи",    "ДокументПередачи");
	ПравилаЗаполнения.Вставить("ФизическоеЛицо",      "ФизическоеЛицо");
	ПравилаЗаполнения.Вставить("ИнвентарныйНомер",    "ИнвентарныйНомер");
	
	ПравилаЗаполнения.Вставить("СтатьяДоходов",       "СтатьяДоходов");
	ПравилаЗаполнения.Вставить("СтатьяЗатрат",        "СтатьяЗатрат");
	
	МассивСтрокТовары = Новый Массив;
	Для Каждого Строка Из ДанныеXDTO.Товары Цикл
		
		СтруктураДанныхСтроки = ДанныеКоллекцииВВидеСтруктуры(Строка, ПравилаЗаполнения);
		
		Если СтруктураДанныхСтроки.Свойство("КоличествоУпаковок") Тогда
		    Если СтруктураДанныхСтроки.Свойство("Упаковка") И СтруктураДанныхСтроки.Свойство("Сумма") Тогда
				СтруктураДанныхСтроки.Вставить("Цена", СтруктураДанныхСтроки.Сумма / СтруктураДанныхСтроки.КоличествоУпаковок);
			КонецЕсли;
		Иначе
			СтруктураДанныхСтроки.Вставить("КоличествоУпаковок", СтруктураДанныхСтроки.Количество);
		КонецЕсли;	
		
		Если СтруктураДанныхСтроки.Свойство("СтатьяДоходов") Тогда
			СтруктураДанныхСтроки.Вставить("СтатьяРасходовДоходов", Новый Структура("Значение, ИмяПКО",
				СтруктураДанныхСтроки.СтатьяДоходов, "Справочник_СтатьиДоходов"));
		ИначеЕсли СтруктураДанныхСтроки.Свойство("СтатьяЗатрат") Тогда
			СтруктураДанныхСтроки.Вставить("СтатьяРасходовДоходов", Новый Структура("Значение, ИмяПКО",
				СтруктураДанныхСтроки.СтатьяЗатрат, "Справочник_СтатьиРасходов"));
		КонецЕсли;
		
		МассивСтрокТовары.Добавить(СтруктураДанныхСтроки);
		
	КонецЦикла;
	
	ПолученныеДанные.ДополнительныеСвойства.Вставить("Товары", МассивСтрокТовары);
	
	#КонецОбласти
КонецПроцедуры
