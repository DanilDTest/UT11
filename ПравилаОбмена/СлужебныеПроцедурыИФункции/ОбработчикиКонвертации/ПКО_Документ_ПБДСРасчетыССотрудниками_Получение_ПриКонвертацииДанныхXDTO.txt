Процедура ПКО_Документ_ПБДСРасчетыССотрудниками_Получение_ПриКонвертацииДанныхXDTO(ДанныеXDTO, ПолученныеДанные, КомпонентыОбмена)
	ПолученныеДанные.Проведен = Истина;
	ЗаполнитьДатуИНомерВходящегоДокумента(ПолученныеДанные);
	
	ПолученныеДанные.ПроведеноБанком = ЗначениеЗаполнено(ПолученныеДанные.ДатаПроведенияБанком);
	ДобавитьВалютуВДопСвойства(ПолученныеДанные, ДанныеXDTO);
	// ХозяйственнаяОперация
	ВидРасчетов = ДанныеXDTO.ВидРасчетов.Значение;
	Если ВидРасчетов = "Подотчет" Тогда
		ПолученныеДанные.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтПодотчетника;	
	Иначе
		// В том числе, если "Займы"
		ПолученныеДанные.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПогашениеЗаймаСотрудником;
	КонецЕсли;
	
	#Область РасшифровкаПлатежа
	
	МассивСтрокРасшифровка = Новый Массив;
	Для Каждого Строка Из ДанныеXDTO.РасшифровкаПлатежа Цикл
		
		ПравилаЗаполнения = Новый Соответствие;
		ПравилаЗаполнения.Вставить("Сумма", "Сумма");
		ПравилаЗаполнения.Вставить("СтатьяДДС", "СтатьяДвиженияДенежныхСредств");
		СтруктураДанныхСтроки = ДанныеКоллекцииВВидеСтруктуры(Строка, ПравилаЗаполнения);
		
		МассивСтрокРасшифровка.Добавить(СтруктураДанныхСтроки);
	КонецЦикла;
	ПолученныеДанные.ДополнительныеСвойства.Вставить("РасшифровкаПлатежа", МассивСтрокРасшифровка);
	
	#КонецОбласти
	
	// СтатьяДДС
	ПолученныеДанные.ДополнительныеСвойства.Вставить("СтатьяДвиженияДенежныхСредств",
		ДанныеXDTO.РасшифровкаПлатежа[0].СтатьяДДС);
	
	ЗагрузитьДополнительныеРеквизиты(ПолученныеДанные, ДанныеXDTO, КомпонентыОбмена);
КонецПроцедуры
