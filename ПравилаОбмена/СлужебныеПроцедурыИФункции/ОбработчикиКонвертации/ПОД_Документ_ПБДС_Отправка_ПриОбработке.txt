Процедура ПОД_Документ_ПБДС_Отправка_ПриОбработке(ДанныеИБ, ИспользованиеПКО, КомпонентыОбмена)
	УстановитьИспользованиеПКО(ИспользованиеПКО, Ложь);
	
	Если ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзКассыНаРасчетныйСчет Тогда
		Если ВерсияФорматаЧислом(КомпонентыОбмена.ВерсияФорматаОбмена) < ВерсияФорматаЧислом("1.6") Тогда
			ИспользованиеПКО.Документ_ПБДСПрочееПоступление_Отправка = Истина;
		Иначе
			ИспользованиеПКО.Документ_ПБДСВзносНаличными_Отправка = Истина;
			Возврат;
		КонецЕсли;
	ИначеЕсли ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратНеперечисленнойЗарплатыПоЗарплатномуПроекту Тогда
		Если ВерсияФорматаЧислом(КомпонентыОбмена.ВерсияФорматаОбмена) < ВерсияФорматаЧислом("1.6") ИЛИ ЭтоУТ() Тогда
			ОчиститьИспользованиеПКО(ИспользованиеПКО);
		Иначе
			ИспользованиеПКО.Документ_ПБДСВозвратЗарплатыПоЗарпПроекту_Отправка = Истина;
		КонецЕсли;
		
		Возврат;
			
	ИначеЕсли ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствСДругогоСчета Тогда
		Если ВерсияФорматаЧислом(КомпонентыОбмена.ВерсияФорматаОбмена) < ВерсияФорматаЧислом("1.6") Тогда
			ИспользованиеПКО.Документ_ПБДСПрочееПоступление_Отправка = Истина;
		Иначе
			ИспользованиеПКО.Документ_ПБДСПереводНаДругойСчет_Отправка = Истина;
			Возврат;
		КонецЕсли;	
	КонецЕсли;
		
	ВалютаРеглУчета = ВалютаРегламентированногоУчета(КомпонентыОбмена);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЕСТЬNULL(Шапка.СтатьяДвиженияДенежныхСредств.КорреспондирующийСчет, """") КАК КорреспондирующийСчет,
	|	ЕСТЬNULL(Шапка.ДокументВыдачи.Дата, Шапка.Дата) КАК СБДС_Дата,
	|	Шапка.БанковскийСчет.ВалютаДенежныхСредств КАК ВалютаБанковскогоСчета,
	|	Шапка.Валюта КАК Валюта,
	|	Шапка.ВалютаКонвертации
	|ПОМЕСТИТЬ Шапка
	|ИЗ
	|	Документ.ПоступлениеБезналичныхДенежныхСредств КАК Шапка
	|ГДЕ
	|	Шапка.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДанныеДокумента.СтатьяДоходов.КорреспондирующийСчет КАК КорреспондирующийСчет,
	|	ДанныеДокумента.СтатьяДоходов КАК СтатьяДоходов
	|ПОМЕСТИТЬ СтрокаСоСтатьейДоходовСКорСчетом
	|ИЗ
	|	Документ.ПоступлениеБезналичныхДенежныхСредств.РасшифровкаПлатежа КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И (ДанныеДокумента.Ссылка.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрочиеДоходы)
	|		ИЛИ ДанныеДокумента.Ссылка.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрочееПоступлениеДенежныхСредств))
	|	И ДанныеДокумента.СтатьяДоходов.КорреспондирующийСчет is not null
	|	И ДанныеДокумента.СтатьяДоходов.КорреспондирующийСчет <> """"
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДанныеДокумента.НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПерваяСтрокаРасшифровки.ДоговорКредитаДепозита.СтатьяДДСКомиссии КАК ПерваяСтрРП_ДоговорКредитаДепозита_СДДСКомиссии,
	|	ПерваяСтрокаРасшифровки.ДоговорКредитаДепозита.СтатьяДДСПроцентов КАК ПерваяСтрРП_ДоговорКредитаДепозита_СДДСПроцентов,
	|	ПерваяСтрокаРасшифровки.СтатьяДвиженияДенежныхСредств КАК ПерваяСтрРП_СДДС
	|ПОМЕСТИТЬ ПерваяСтрокаРасшифровки
	|ИЗ
	|	Документ.ПоступлениеБезналичныхДенежныхСредств.РасшифровкаПлатежа КАК ПерваяСтрокаРасшифровки
	|ГДЕ
	|	ПерваяСтрокаРасшифровки.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПерваяСтрокаРасшифровки.НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(СтрокаСоСтатьейДоходовСКорСчетом.КорреспондирующийСчет, Шапка.КорреспондирующийСчет) КАК КорреспондирующийСчет,
	|	ЕСТЬNULL(СтрокаСоСтатьейДоходовСКорСчетом.СтатьяДоходов, Неопределено) КАК СтатьяДоходов,
	|	Шапка.СБДС_Дата,
	|	Шапка.ВалютаБанковскогоСчета,
	|	ЕСТЬNULL(ПерваяСтрокаРасшифровки.ПерваяСтрРП_ДоговорКредитаДепозита_СДДСКомиссии, Неопределено) КАК ПерваяСтрРП_ДоговорКредитаДепозита_СДДСКомиссии,
	|	ЕСТЬNULL(ПерваяСтрокаРасшифровки.ПерваяСтрРП_ДоговорКредитаДепозита_СДДСПроцентов, Неопределено) КАК ПерваяСтрРП_ДоговорКредитаДепозита_СДДСПроцентов,
	|	ЕСТЬNULL(ПерваяСтрокаРасшифровки.ПерваяСтрРП_СДДС, Неопределено) КАК ПерваяСтрРП_СДДС,
	|	ЕСТЬNULL(КурсыВалют.Курс, 1) КАК Валюта_Курс,
	|	ЕСТЬNULL(КурсыВалют.Кратность, 1) КАК Валюта_Кратность
	|ИЗ
	|	Шапка КАК Шапка
	|		ЛЕВОЕ СОЕДИНЕНИЕ СтрокаСоСтатьейДоходовСКорСчетом КАК СтрокаСоСтатьейДоходовСКорСчетом
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПерваяСтрокаРасшифровки
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&Дата, ) КАК КурсыВалют
	|		ПО Шапка.Валюта = КурсыВалют.Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств                       КАК СтатьяДДС,
	|	РасшифровкаПлатежа.Сумма                                               КАК Сумма,
	|	РасшифровкаПлатежа.СуммаНДС                                            КАК СуммаНДС,
	|	РасшифровкаПлатежа.СтавкаНДС                                           КАК СтавкаНДС,
	|	РасшифровкаПлатежа.СуммаВзаиморасчетов                                 КАК СуммаВзаиморасчетов,
	|	РасшифровкаПлатежа.Заказ                                               КАК Заказ,
	|	РасшифровкаПлатежа.ОснованиеПлатежа                                    КАК ОснованиеПлатежа,
	|	РасшифровкаПлатежа.Партнер                                             КАК Партнер,
	|	ВЫРАЗИТЬ(РасшифровкаПлатежа.Заказ КАК Справочник.ДоговорыКонтрагентов) КАК Договор,
	|	РасшифровкаПлатежа.ДоговорКредитаДепозита 							   КАК ДоговорКредитаДепозита,
	|	ЕСТЬNULL(КурсыВалют.Курс, 1)                                           КАК КурсВзаиморасчетов,
	|	ЕСТЬNULL(КурсыВалют.Кратность, 1)                                      КАК КратностьВзаиморасчетов,
	|	РасшифровкаПлатежа.ВалютаВзаиморасчетов                                КАК ВалютаВзаиморасчетов,
	|	ВЫБОР
	|		КОГДА РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговораКредитовИДепозитов.КредитВБанке)
	|			ИЛИ РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговораКредитовИДепозитов.КредитСотруднику)
	|			ТОГДА ВЫБОР
	|					КОГДА РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипСрочности = ЗНАЧЕНИЕ(Перечисление.ТипыСрочностиКредитовИДепозитов.Краткосрочный)
	|						ТОГДА ""КраткосрочныеКредиты""
	|					КОГДА РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипСрочности = ЗНАЧЕНИЕ(Перечисление.ТипыСрочностиКредитовИДепозитов.Долгосрочный)
	|						ТОГДА ""ДолгосрочныеКредиты""
	|				КОНЕЦ
	|		КОГДА РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговораКредитовИДепозитов.ВнутреннийЗайм)
	|				ИЛИ РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговораКредитовИДепозитов.ВнешнийЗайм)
	|				ИЛИ РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговораКредитовИДепозитов.ВкладСотрудника)
	|			ТОГДА ВЫБОР
	|					КОГДА РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипСрочности = ЗНАЧЕНИЕ(Перечисление.ТипыСрочностиКредитовИДепозитов.Краткосрочный)
	|						ТОГДА ВЫБОР
	|							КОГДА РасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств = РасшифровкаПлатежа.ДоговорКредитаДепозита.СтатьяДДСПоступленияВыдачи
	|								ТОГДА ""КраткосрочныеЗаймы""
	|						КОНЕЦ
	|					КОГДА РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипСрочности = ЗНАЧЕНИЕ(Перечисление.ТипыСрочностиКредитовИДепозитов.Долгосрочный)
	|						ТОГДА ВЫБОР
	|							КОГДА РасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств = РасшифровкаПлатежа.ДоговорКредитаДепозита.СтатьяДДСПоступленияВыдачи
	|								ТОГДА ""ДолгосрочныеЗаймы""
	|						КОНЕЦ
	|				КОНЕЦ
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК ВидРасчетовРасширенный,
	|	ВЫБОР
	|		КОГДА РасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств.КорреспондирующийСчет <> """" И &ЭтоУТ
	|			ТОГДА РасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств.КорреспондирующийСчет
	|		ИНАЧЕ ВЫБОР
	|				КОГДА РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговораКредитовИДепозитов.КредитСотруднику)
	|					ТОГДА ""58.03""
	|				КОГДА (РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговораКредитовИДепозитов.ВнутреннийЗайм)
	|						ИЛИ РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговораКредитовИДепозитов.ВнешнийЗайм))
	|					И РасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств <> РасшифровкаПлатежа.ДоговорКредитаДепозита.СтатьяДДСПоступленияВыдачи
	|					ТОГДА ""58.03""
	|				КОГДА (РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговораКредитовИДепозитов.ВнутреннийЗайм)
	|						ИЛИ РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговораКредитовИДепозитов.ВнешнийЗайм)
	|						ИЛИ РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговораКредитовИДепозитов.ВкладСотрудника))
	|					И РасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств = РасшифровкаПлатежа.ДоговорКредитаДепозита.СтатьяДДСПоступленияВыдачи
	|					ТОГДА ВЫБОР
	|							КОГДА РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипСрочности = ЗНАЧЕНИЕ(Перечисление.ТипыСрочностиКредитовИДепозитов.Краткосрочный)
	|								ТОГДА ВЫБОР
	|										КОГДА РасшифровкаПлатежа.ДоговорКредитаДепозита.ВалютаВзаиморасчетов = &ВалютаРегУчета
	|											ТОГДА ""66.03""
	|										ИНАЧЕ ""66.23""
	|									КОНЕЦ
	|							КОГДА РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипСрочности = ЗНАЧЕНИЕ(Перечисление.ТипыСрочностиКредитовИДепозитов.Долгосрочный)
	|								ТОГДА ВЫБОР
	|										КОГДА РасшифровкаПлатежа.ДоговорКредитаДепозита.ВалютаВзаиморасчетов = &ВалютаРегУчета
	|											ТОГДА ""67.03""
	|										ИНАЧЕ ""67.23""
	|									КОНЕЦ
	|						КОНЕЦ
	|				КОГДА РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговораКредитовИДепозитов.КредитВБанке)
	|					ТОГДА ВЫБОР
	|							КОГДА РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипСрочности = ЗНАЧЕНИЕ(Перечисление.ТипыСрочностиКредитовИДепозитов.Краткосрочный)
	|								ТОГДА ВЫБОР
	|										КОГДА РасшифровкаПлатежа.ДоговорКредитаДепозита.ВалютаВзаиморасчетов = &ВалютаРегУчета
	|											ТОГДА ""66.01""
	|										ИНАЧЕ ""66.21""
	|									КОНЕЦ
	|							КОГДА РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипСрочности = ЗНАЧЕНИЕ(Перечисление.ТипыСрочностиКредитовИДепозитов.Долгосрочный)
	|								ТОГДА ВЫБОР
	|										КОГДА РасшифровкаПлатежа.ДоговорКредитаДепозита.ВалютаВзаиморасчетов = &ВалютаРегУчета
	|											ТОГДА ""67.01""
	|										ИНАЧЕ ""67.21""
	|									КОНЕЦ
	|					КОНЕЦ
	|				КОГДА РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговораКредитовИДепозитов.ДепозитВБанке)
	|					ИЛИ РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговораКредитовИДепозитов.ДепозитВБанкеСКапитализацией)
	|					ТОГДА ВЫБОР
	|							КОГДА РасшифровкаПлатежа.ДоговорКредитаДепозита.ВалютаВзаиморасчетов = &ВалютаРегУчета
	|								ТОГДА ""55.03""
	|							ИНАЧЕ ""55.23""
	|						КОНЕЦ
	|		КОНЕЦ
	|	КОНЕЦ КАК СчетУчетаРасчетовСКонтрагентом
	|ИЗ
	|	Документ.ПоступлениеБезналичныхДенежныхСредств.РасшифровкаПлатежа КАК РасшифровкаПлатежа
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&Дата, ) КАК КурсыВалют
	|		ПО РасшифровкаПлатежа.ВалютаВзаиморасчетов = КурсыВалют.Валюта
	|ГДЕ
	|	РасшифровкаПлатежа.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДанныеДокумента.СтатьяДоходов КАК СтатьяДоходов
	|ИЗ
	|	Документ.ПоступлениеБезналичныхДенежныхСредств.РасшифровкаПлатежа КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И (ДанныеДокумента.Ссылка.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрочиеДоходы)
	|		ИЛИ ДанныеДокумента.Ссылка.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрочееПоступлениеДенежныхСредств))
	|	И ДанныеДокумента.СтатьяДоходов ССЫЛКА ПланВидовХарактеристик.СтатьиДоходов
	|   И ДанныеДокумента.СтатьяДоходов <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.ПустаяСсылка)
	|УПОРЯДОЧИТЬ ПО
	|	ДанныеДокумента.НомерСтроки
	|";
	Запрос.УстановитьПараметр("Ссылка", ДанныеИБ.Ссылка);
	Запрос.УстановитьПараметр("ЭтоУТ", ЭтоУТ());
	Запрос.УстановитьПараметр("Дата", ДанныеИБ.Дата);
	Запрос.УстановитьПараметр("ВалютаРегУчета", ВалютаРеглУчета);
	
	ДополнительныеДанные = Запрос.ВыполнитьПакет();
	
	ДанныеШапки = ДополнительныеДанные[3].Выгрузить()[0];
	ДанныеИБ.ДополнительныеСвойства.Вставить("ДанныеШапки", ДанныеШапки);
	ДанныеИБ.ДополнительныеСвойства.Вставить("РасшифровкаПлатежаРасширенная", ДополнительныеДанные[4]);
	
	Если ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.КонвертацияВалюты Тогда
		
		Если ДанныеИБ.ВалютаКонвертации = ВалютаРеглУчета
			Или ДанныеИБ.Валюта = ВалютаРеглУчета Тогда
			
			ИспользованиеПКО.Документ_ПБДСКонвертацияВалюты_Отправка = Истина;
			
		ИначеЕсли ДанныеИБ.Валюта <> ВалютаРеглУчета
			И ДанныеИБ.ВалютаКонвертации <> ВалютаРеглУчета Тогда
			
			ИспользованиеПКО.Документ_ПБДСРасчетыСКонтрагентами_Отправка = Истина;
			
		КонецЕсли;
		
	ИначеЕсли ЭтоХозяйственнаяОперацияРасчетыПоКредитамЗаймам(ДанныеИБ.ХозяйственнаяОперация) Тогда
		
		ИспользованиеПКО.Документ_ПБДСРасчетыСКонтрагентами_Отправка = Истина;
		
	ИначеЕсли ЭтоХозяйственнаяОперацияРасчетыПоДепозитам(ДанныеИБ.ХозяйственнаяОперация) Тогда
		
		ИспользованиеПКО.Документ_ПБДСПрочееПоступление_Отправка = Истина;
		
	ИначеЕсли ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента
		Или ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтПоставщика
		Или ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойОрганизации
		Или ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтДругойОрганизации Тогда
		
		ИспользованиеПКО.Документ_ПБДСРасчетыСКонтрагентами_Отправка = Истина;
		
	ИначеЕсли ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПрочиеДоходы
		Или ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПрочееПоступлениеДенежныхСредств Тогда
		
		// Хоз операция ПрочиеДоходы устарела. Оставлено на переходный период.
		// Прочий доход отражается видом операции ПрочееПоступлениеДенежныхСредств.
		Для Каждого СтрокаТЧ Из ДанныеИБ.РасшифровкаПлатежа Цикл
			
			// Если заполнена статья доходов - то это ПрочиеДоходы.
			Если ЗначениеЗаполнено(СтрокаТЧ.СтатьяДоходов)
				И ТипЗнч(СтрокаТЧ.СтатьяДоходов) = Тип("ПланВидовХарактеристикСсылка.СтатьиДоходов") Тогда
				
				ИспользованиеПКО.Документ_ПБДСПрочиеДоходы_Отправка = Истина;
				Прервать;
				
			КонецЕсли;
			
		КонецЦикла;
		
		ИспользованиеПКО.Документ_ПБДСПрочееПоступление_Отправка = Не ИспользованиеПКО.Документ_ПБДСПрочиеДоходы_Отправка;
		
	ИначеЕсли ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтПодотчетника
		Или ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПогашениеЗаймаСотрудником Тогда
		
		ИспользованиеПКО.Документ_ПБДСРасчетыССотрудниками_Отправка = Истина;
		
	ИначеЕсли ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеОплатыПоПлатежнойКарте Тогда
		
		Если СвойствоФорматаОбмена(КомпонентыОбмена, "Документ.ПБДСПоПлатежнымКартам") Тогда
			ИспользованиеПКО.Документ_ПБДСПоПлатежнымКартам_Отправка = Истина;
		Иначе
			ИспользованиеПКО.Документ_ПБДСРасчетыСКонтрагентами_Отправка = Истина;
		КонецЕсли;
		
	ИначеЕсли ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВнутренняяПередачаДенежныхСредств
		Или ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратНеперечисленныхДС Тогда
		
		ИспользованиеПКО.Документ_ПБДСПрочееПоступление_Отправка = Истина;
		
	КонецЕсли;
	
	ПроверкаЗаполненияРеквизитовОбъектаИБ(ДанныеИБ, КомпонентыОбмена, ИспользованиеПКО);
КонецПроцедуры
