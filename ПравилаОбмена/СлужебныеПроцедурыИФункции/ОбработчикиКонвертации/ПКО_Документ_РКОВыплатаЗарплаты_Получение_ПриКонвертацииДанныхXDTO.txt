Процедура ПКО_Документ_РКОВыплатаЗарплаты_Получение_ПриКонвертацииДанныхXDTO(ДанныеXDTO, ПолученныеДанные, КомпонентыОбмена)
	ДобавитьВалютуВДопСвойства(ПолученныеДанные, ДанныеXDTO);
	ЗагрузитьОтветственноеЛицо(ПолученныеДанные, ДанныеXDTO, КомпонентыОбмена, "ГлавныйБухгалтер");
	ЗагрузитьОтветственноеЛицо(ПолученныеДанные, ДанныеXDTO, КомпонентыОбмена, "Руководитель");
	ЗагрузитьДополнительныеРеквизиты(ПолученныеДанные, ДанныеXDTO, КомпонентыОбмена);
	
	МассивСтрокВыплаты         = Новый Массив;
	МассивСтрокРасшифровки     = Новый Массив;
	ПерваяЗаполненнаяСтатьяДДС = Неопределено;
	Для Каждого Строка Из ДанныеXDTO.РасшифровкаПлатежа Цикл
		Если ЗначениеЗаполнено(Строка.Сотрудник) Тогда
			СтруктураДанныхСтроки = Новый Структура("ФизическоеЛицо,Сумма");
			СтруктураДанныхСтроки.ФизическоеЛицо = Строка.Сотрудник;
			СтруктураДанныхСтроки.Сумма = Строка.Сумма;
			
			МассивСтрокВыплаты.Добавить(СтруктураДанныхСтроки);
		КонецЕсли;
		
		СтруктураРасшифровки = Новый Структура("Сумма,СтатьяДвиженияДенежныхСредств,СуммаВзаиморасчетов");
		СтруктураРасшифровки.СтатьяДвиженияДенежныхСредств = Строка.СтатьяДДС;
		СтруктураРасшифровки.Сумма = Строка.Сумма;
		СтруктураРасшифровки.СуммаВзаиморасчетов = Строка.Сумма;
		МассивСтрокРасшифровки.Добавить(СтруктураРасшифровки);
		
		Если ПерваяЗаполненнаяСтатьяДДС = Неопределено И ЗначениеЗаполнено(Строка.СтатьяДДС) Тогда
			ПерваяЗаполненнаяСтатьяДДС = RM_ОбменДаннымиXDTOСервер.СтруктураОбъектаXDTOВДанныеИБ(
				КомпонентыОбмена,
				Строка.СтатьяДДС,
				RM_ОбменДаннымиXDTOСервер.ПКОПоИмени(КомпонентыОбмена, "Справочник_СтатьиДДС"),
				"ПолучитьСсылку");
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Строка.НомерВедомостиНаВыплатуЗарплаты) И Не ЗначениеЗаполнено(ПолученныеДанные.НомерВедомостиНаВыплатуЗарплаты) Тогда
			ПолученныеДанные.НомерВедомостиНаВыплатуЗарплаты = Строка.НомерВедомостиНаВыплатуЗарплаты;
		КонецЕсли;
		Если ЗначениеЗаполнено(Строка.ДатаВедомостиНаВыплатуЗарплаты) И Не ЗначениеЗаполнено(ПолученныеДанные.ДатаВедомостиНаВыплатуЗарплаты) Тогда
		    ПолученныеДанные.ДатаВедомостиНаВыплатуЗарплаты = Строка.ДатаВедомостиНаВыплатуЗарплаты;
		КонецЕсли;
			
	КонецЦикла;
	
	
	#Область Шапка
	
	ПолученныеДанные.Проведен = Истина;
	
	// ХозяйственнаяОперация
	Если МассивСтрокВыплаты.Количество() = 1 Тогда
	
		ПолученныеДанные.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыплатаЗарплатыРаботнику;
		ПолученныеДанные.ПодотчетноеЛицо = RM_ОбменДаннымиXDTOСервер.СтруктураОбъектаXDTOВДанныеИБ(
					КомпонентыОбмена,
					МассивСтрокВыплаты[0].ФизическоеЛицо,
					RM_ОбменДаннымиXDTOСервер.ПКОПоИмени(КомпонентыОбмена, "Справочник_ФизическиеЛица_Получение"),
					"ПолучитьСсылку");
		
	Иначе
		ПолученныеДанные.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыплатаЗарплатыЧерезКассу;
	КонецЕсли;
	
	// СтатьяДДС
	Если ЗначениеЗаполнено(ПерваяЗаполненнаяСтатьяДДС) Тогда
		ПолученныеДанные.СтатьяДвиженияДенежныхСредств = ПерваяЗаполненнаяСтатьяДДС;
	КонецЕсли;	
	
	#КонецОбласти
КонецПроцедуры
