Процедура ПКО_Документ_СБДСПлатежиВБюджет_Отправка_ПриОтправкеДанных(ДанныеИБ, ДанныеXDTO, КомпонентыОбмена, СтекВыгрузки)
	Если СтекВыгрузки.Количество() > 1 Тогда
		Возврат;
	КонецЕсли;
	
	ВыгрузитьПодразделениеИзРеквизитаДокумента(КомпонентыОбмена, ДанныеИБ, ДанныеXDTO, "БанковскийСчет");
	
	ЗаполнитьДанныеВходящегоДокументаСБДС(ДанныеИБ, ДанныеXDTO);
	ДанныеШапки = ДанныеИБ.ДополнительныеСвойства.ДанныеШапки;
	ДанныеXDTO.Вставить("ВидОперации", "ПрочееСписание");
	
	ПоказательДаты = ДанныеИБ.ПоказательДаты;
	Если ЗначениеЗаполнено(ПоказательДаты) И ПоказательДаты <> "0" Тогда
		ПоказательДаты = Формат(СтроковыеФункцииКлиентСервер.СтрокаВДату(ПоказательДаты), "ДЛФ=D");
	КонецЕсли;
	ДанныеXDTO.Вставить("ПоказательДаты", ПоказательДаты);
	
	Если ВерсияФорматаЧислом(КомпонентыОбмена.ВерсияФорматаОбмена) >= ВерсияФорматаЧислом("1.7")
		И ЗначениеЗаполнено(ДанныеИБ.ЗаявкаНаРасходованиеДенежныхСредств) Тогда
		ДанныеXDTO.Вставить("ЗаявкаНаРасходДенежныхСредств", ДанныеИБ.ЗаявкаНаРасходованиеДенежныхСредств);
	КонецЕсли;
	
	ТабРасшифровка = Новый ТаблицаЗначений;
	ТабРасшифровка.Колонки.Добавить("СтатьяДДС");
	ТабРасшифровка.Колонки.Добавить("Сумма");
	Если ДанныеИБ.РасшифровкаПлатежа.Количество() > 0 Тогда
		Для Каждого Строка Из ДанныеИБ.РасшифровкаПлатежа Цикл
			НоваяСтрока = ТабРасшифровка.Добавить();
			НоваяСтрока.СтатьяДДС = Строка.СтатьяДвиженияДенежныхСредств;
			НоваяСтрока.Сумма = Строка.Сумма;
		КонецЦикла;
	Иначе
		НоваяСтрока = ТабРасшифровка.Добавить();
		НоваяСтрока.СтатьяДДС = ДанныеИБ.СтатьяДвиженияДенежныхСредств;
		НоваяСтрока.Сумма = ДанныеИБ.СуммаДокумента;
	КонецЕсли;
	ДанныеXDTO.Вставить("РасшифровкаПлатежа", ТабРасшифровка);
КонецПроцедуры
