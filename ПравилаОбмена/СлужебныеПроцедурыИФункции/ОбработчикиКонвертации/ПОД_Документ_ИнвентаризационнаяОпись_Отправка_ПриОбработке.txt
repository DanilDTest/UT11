Процедура ПОД_Документ_ИнвентаризационнаяОпись_Отправка_ПриОбработке(ДанныеИБ, ИспользованиеПКО, КомпонентыОбмена)
	ИспользованиеПКО.Документ_ИнвентаризационнаяОпись_Отправка = Ложь;	
	
	ДанныеДокумента = Документы.ИнвентаризационнаяОпись.ДанныеИнвентаризации(ДанныеИБ.Ссылка);
	
	Если ДанныеДокумента = Неопределено Тогда
		ИспользованиеПКО.Документ_ИнвентаризационнаяОпись_Отправка = Ложь;
	Иначе
		ТаблицаТоваров = ДанныеДокумента.РезультатПоТабличнойЧасти.Выгрузить();
		ТаблицаТоваров.Колонки.КоличествоПоУчету.Имя = "КоличествоУчет";
		ТаблицаТоваров.Колонки.КоличествоФактическое.Имя = "Количество";
		ТаблицаТоваров.Колонки.СуммаПоУчету.Имя = "СуммаУчет";
		ТаблицаТоваров.Колонки.СуммаФактическая.Имя = "Сумма";
		Если ВерсияФорматаЧислом(КомпонентыОбмена.ВерсияФорматаОбмена) < ВерсияФорматаЧислом("1.6") Тогда
			ТаблицаТоваров.Колонки.Серия.Имя = "СерияСсылка";
		КонецЕсли;	
		ТаблицаТоваров.Колонки.Добавить("Штрихкод");
		ТаблицаТоваров.Колонки.Добавить("ЦенаВРознице");
		ТаблицаТоваров.Колонки.Добавить("ЦенаЗаУпаковку");
		ТаблицаТоваров.Колонки.Добавить("КоличествоУпаковок");
		ТаблицаТоваров.Колонки.Добавить("КоличествоУпаковокУчет");
		ТаблицаТоваров.Колонки.Добавить("ТипЗапасов");
		ТаблицаТоваров.Колонки.Добавить("СчетУчета");
		ТаблицаТоваров.Колонки.Добавить("НомерСтрокиДокумента");
		ТаблицаТоваров.Колонки.Добавить("ЕдиницаИзмерения");
	
		Если ДанныеДокумента.РезультатКурсВалюты <> Неопределено Тогда
			КурсВалюты = ДанныеДокумента.РезультатКурсВалюты.Выбрать();
			Если КурсВалюты.Следующий() Тогда
				КоэффициентПересчета = КурсВалюты.КоэффициентПересчета;
			Иначе
				КоэффициентПересчета = 1;
			КонецЕсли;
		Иначе
			КоэффициентПересчета = 1;
		КонецЕсли;
		
		ЗаполнитьХарактеристикуУпаковкуВТЧ(КомпонентыОбмена, ТаблицаТоваров);
	
		НомерСтроки = 0;
		ЕдиницыИзмерения = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(ТаблицаТоваров.ВыгрузитьКолонку("Номенклатура"), "ЕдиницаИзмерения");
		Для Каждого СтрокаТаблицы Из ТаблицаТоваров Цикл
			НомерСтроки = НомерСтроки + 1;
			СтрокаТаблицы.НомерСтрокиДокумента = НомерСтроки;
			СтрокаТаблицы.ЕдиницаИзмерения     = ЕдиницыИзмерения[СтрокаТаблицы.Номенклатура];
			СтрокаТаблицы.Цена                 = Окр(СтрокаТаблицы.Цена * КоэффициентПересчета, 2);
			СтрокаТаблицы.СуммаУчет            = Окр(СтрокаТаблицы.СуммаУчет * КоэффициентПересчета, 2);
			СтрокаТаблицы.Сумма                = Окр(СтрокаТаблицы.Сумма * КоэффициентПересчета, 2);
			СтрокаТаблицы.ТипЗапасов           = Перечисления.ТипыЗапасов.Товар;	
		КонецЦикла;
		
		ДанныеИБ.ДополнительныеСвойства.Вставить("Товары", ТаблицаТоваров);
		
		ПроверкаЗаполненияРеквизитовОбъектаИБ(ДанныеИБ, КомпонентыОбмена, ИспользованиеПКО);
	КонецЕсли;
КонецПроцедуры
