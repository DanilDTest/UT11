Процедура ПКО_Документ_ПриходныйОрдерНаТовары_Отправка_ПриОтправкеДанных(ДанныеИБ, ДанныеXDTO, КомпонентыОбмена, СтекВыгрузки)
	Если СтекВыгрузки.Количество() > 1 Тогда
		Возврат;
	КонецЕсли;   
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ПриходныйОрдерНаТоварыТовары.Номенклатура КАК Номенклатура,
	|	ПриходныйОрдерНаТоварыТовары.НомерСтроки КАК НомерСтроки,
	|	ПриходныйОрдерНаТоварыТовары.Характеристика КАК Характеристика, 
	|	ВЫБОР
	|		КОГДА ПриходныйОрдерНаТоварыТовары.Упаковка.Числитель > 1
	|			ТОГДА ПриходныйОрдерНаТоварыТовары.Количество
	|		ИНАЧЕ ПриходныйОрдерНаТоварыТовары.КоличествоУпаковок
	|	КОНЕЦ КАК КоличествоУпаковок,
	|	ВЫБОР
	|		КОГДА ПриходныйОрдерНаТоварыТовары.Упаковка.Числитель > 1
	|			ТОГДА ПриходныйОрдерНаТоварыТовары.Упаковка.Родитель
	|		ИНАЧЕ ПриходныйОрдерНаТоварыТовары.Упаковка
	|	КОНЕЦ КАК Упаковка,
	|	ПриходныйОрдерНаТоварыТовары.Количество КАК Количество,
	|	ПриходныйОрдерНаТоварыТовары.СтатусУказанияСерий КАК СтатусУказанияСерий,
	|	ПриходныйОрдерНаТоварыТовары.RM_Факт КАК RM_Факт
	|ИЗ
	|	Документ.ПриходныйОрдерНаТовары.Товары КАК ПриходныйОрдерНаТоварыТовары
	|ГДЕ
	|	ПриходныйОрдерНаТоварыТовары.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПриходныйОрдерНаТоварыRM_ШтрихкодыУпаковок.НомерСтроки КАК НомерСтрокиДокумента,
	|	ПриходныйОрдерНаТоварыRM_ШтрихкодыУпаковок.ШтрихкодУпаковки КАК ШтрихкодУпаковки,
	|	ЕСТЬNULL(ПриходныйОрдерНаТоварыRM_ШтрихкодыУпаковок.ШтрихкодУпаковки.ЗначениеШтрихкода, """") КАК НомерАкцизнойМарки
	|ИЗ
	|	Документ.ПриходныйОрдерНаТовары.RM_ШтрихкодыУпаковок КАК ПриходныйОрдерНаТоварыRM_ШтрихкодыУпаковок
	|ГДЕ
	|	ПриходныйОрдерНаТоварыRM_ШтрихкодыУпаковок.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтрокиДокумента
	|";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.Параметры.Вставить("Ссылка", ДанныеИБ.Ссылка);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	Товары = РезультатЗапроса[РезультатЗапроса.ВГраница() - 1].Выгрузить();
	ШтрихкодыУпаковок = РезультатЗапроса[РезультатЗапроса.ВГраница()].Выгрузить();
	
	ДанныеXDTO.Вставить("Товары", Товары);
	ДанныеXDTO.Вставить("АкцизныеМарки", ШтрихкодыУпаковок);    
	
	Если ТипЗнч(ДанныеИБ.Отправитель) = Тип("СправочникСсылка.Склады") Тогда
		ДанныеXDTO.Вставить("СкладОтправитель", Новый Структура("Значение, ИмяПКО ", ДанныеИБ.Отправитель, "Справочник_Склады_Отправка"));
	КонецЕсли;
КонецПроцедуры
