Процедура ПКО_Документ_ВозвратТоваровОтКлиента_Отправка_ПриОтправкеДанных(ДанныеИБ, ДанныеXDTO, КомпонентыОбмена, СтекВыгрузки)
	Если СтекВыгрузки.Количество() > 1 Тогда
		Возврат;
	КонецЕсли;
	
	СкладДляВыгрузки = СкладДляВыгрузки(ДанныеИБ.Склад, КомпонентыОбмена.ПараметрыКонвертации);
	ДанныеXDTO.Вставить("Склад", СкладДляВыгрузки);
	
	// Определим контрагента документа. Для возврата от розничного покупателя, контрагент в документе не заполняется.
	// Заполняется только партнер - Розничный покупатель.
	КонтрагентДокумента = ДанныеИБ.Контрагент;
	Если Не ЗначениеЗаполнено(КонтрагентДокумента)
		И ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратОтРозничногоПокупателя Тогда
		КонтрагентДокумента = Справочники.Контрагенты.РозничныйПокупатель;
	КонецЕсли;
	
	ДанныеXDTO.Вставить("Контрагент", КонтрагентДокумента);
	
	// ВалютаВзаиморасчетов нужна для расчета курса и кратности.
	// РасчетыВУсловныхЕдиницах вычисляются параллельно, т.к. могут пригодиться если договор пустой.
	РасчетыВУсловныхЕдиницах = Ложь;
	ВалютаВзаиморасчетов     = ДанныеИБ.Валюта;
	
	Если Не (ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратОтКомиссионера
		Или ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратОтРозничногоПокупателя) Тогда
		
		Если ЗначениеЗаполнено(ДанныеИБ.Соглашение) Тогда
			ВалютаВзаиморасчетов = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеИБ.Соглашение, "Валюта");
			РасчетыВУсловныхЕдиницах = ВзаиморасчетыСервер.РасчетыВУсловныхЕдиницах(ДанныеИБ.Соглашение);
		Иначе
			Если ДанныеИБ.РасшифровкаПлатежа.Количество() > 0 Тогда
				ВалютаВзаиморасчетов = ДанныеИБ.РасшифровкаПлатежа[0].ВалютаВзаиморасчетов;
				РасчетыВУсловныхЕдиницах = (ДанныеИБ.Валюта = Константы.ВалютаРегламентированногоУчета.Получить() И ДанныеИБ.Валюта <> ВалютаВзаиморасчетов);
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	ВыгрузитьПодразделениеИзРеквизитаДокумента(КомпонентыОбмена, ДанныеИБ, ДанныеXDTO, "Склад");
	
	ВыгрузитьНалогообложениеНДС(КомпонентыОбмена, ДанныеИБ, ДанныеXDTO, Истина);
	
	Если Не ЗначениеЗаполнено(ДанныеИБ.ХозяйственнаяОперация) Тогда
		ДанныеXDTO.Вставить("ВидОперации", "ВозвратОтКлиента");
	КонецЕсли;
	
	ВыгрузитьКурсИКратностьВзаиморасчетов(ДанныеXDTO, ВалютаВзаиморасчетов, ДанныеИБ.Дата);
	
	// Документ реализации
	ДокументРеализации = Неопределено;
	Если ЗначениеЗаполнено(ДанныеИБ.ДокументРеализации) Тогда
		
		ДокументРеализации = ДанныеИБ.ДокументРеализации;
		
	ИначеЕсли Не ДанныеИБ.ВозвратПорчи Тогда
	
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
			|Товары.ДокументРеализации.Ссылка КАК Ссылка
			|ИЗ Документ.ВозвратТоваровОтКлиента.Товары КАК Товары
			|ГДЕ 
			|	Товары.Ссылка = &Ссылка";
	
		Запрос.УстановитьПараметр("Ссылка", ДанныеИБ.Ссылка);
	
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Количество() = 1 Тогда
			Выборка.Следующий();
			ДокументРеализации = Выборка.Ссылка;
		КонецЕсли;
		
	КонецЕсли;
	
	ДанныеXDTO.Вставить("ДокументРеализации");
	
	Если ДокументРеализации <> Неопределено Тогда
		
		ИмяПКО = "Документ_РеализацияТоваровУслуг_Отправка";
		
		Если ТипЗнч(ДокументРеализации) = Тип("ДокументСсылка.ОтчетОРозничныхПродажах") Тогда
			ИмяПКО = "Документ_ОтчетОРозничныхПродажах_Отправка";
		КонецЕсли;
		
		ДанныеXDTO.Вставить("ДокументРеализации", Новый Структура("Значение, ИмяПКО", ДокументРеализации, ИмяПКО));
		
	КонецЕсли;
	
	Если ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратОтРозничногоПокупателя Тогда
		ДанныеXDTO.Вставить("ПокупателемВыставляетсяСчетФактураНаВозврат", Ложь);
	Иначе	
		РеквизитыСчетФактуры = Неопределено;
		Документы.СчетФактураПолученный.СчетаФактурыПоОснованию(ДанныеИБ.Ссылка, ДанныеИБ.Организация, РеквизитыСчетФактуры);
		ДанныеXDTO.Вставить("ПокупателемВыставляетсяСчетФактураНаВозврат", ЗначениеЗаполнено(РеквизитыСчетФактуры.Ссылка));
	КонецЕсли;
	
	// Товары
	Если ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратОтКомиссионера Тогда
		
		ТекстЗапроса = 
			"ВЫБРАТЬ
			|	ВЫБОР
			|		КОГДА ВидыЗапасов.НоменклатураОприходование = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
			|			ТОГДА ВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура
			|		ИНАЧЕ ВидыЗапасов.НоменклатураОприходование
			|	КОНЕЦ КАК Номенклатура,
			|	ВЫБОР
			|		КОГДА ВидыЗапасов.ХарактеристикаОприходование = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
			|			ТОГДА ВидыЗапасов.АналитикаУчетаНоменклатуры.Характеристика
			|		ИНАЧЕ ВидыЗапасов.ХарактеристикаОприходование
			|	КОНЕЦ КАК Характеристика,
			|	ВидыЗапасов.Количество КАК Количество,
			|	ВидыЗапасов.СуммаСНДС КАК СуммаСНДС,
			|	ВидыЗапасов.СтавкаНДС КАК СтавкаНДС,
			|	ВидыЗапасов.СуммаНДС КАК СуммаНДС,
			|	ВЫБОР
			|		КОГДА ВидыЗапасов.СпособОпределенияСебестоимости = ЗНАЧЕНИЕ(Перечисление.СпособыОпределенияСебестоимости.ИзТекущегоДокумента)
			|			ТОГДА ВЫБОР
			|					КОГДА ВидыЗапасов.Ссылка.ЦенаВключаетНДС
			|						ТОГДА ВидыЗапасов.СуммаСНДС
			|					ИНАЧЕ ВидыЗапасов.СуммаСНДС - ВидыЗапасов.СуммаНДС
			|				КОНЕЦ
			|		КОГДА ВидыЗапасов.СпособОпределенияСебестоимости = ЗНАЧЕНИЕ(Перечисление.СпособыОпределенияСебестоимости.Вручную)
			|			ТОГДА ВЫБОР
			|					КОГДА ЕСТЬNULL(&Курс, 0) > 0
			|							И ЕСТЬNULL(&Кратность, 0) > 0
			|						ТОГДА ВЫРАЗИТЬ(ВидыЗапасов.СебестоимостьРегл / &Курс / &Кратность КАК ЧИСЛО(15, 2))
			|					ИНАЧЕ 0
			|				КОНЕЦ
			|		ИНАЧЕ 0
			|	КОНЕЦ КАК Себестоимость,
			|	ВидыЗапасов.Ссылка.ЦенаВключаетНДС КАК ЦенаВключаетНДС,
			|	ВидыЗапасов.НомерГТД.Код КАК НомерГТД,
			|	ВидыЗапасов.НомерГТД.СтранаПроисхождения КАК СтранаПроисхождения,
			|	ВидыЗапасов.Ссылка.ВозвратПереданнойМногооборотнойТары КАК ВозвратПереданнойМногооборотнойТары,
			|	ВЫБОР
			|		КОГДА ВидыЗапасов.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
			|			ТОГДА ВЫРАЗИТЬ(""КомиссионныеТовары"" КАК СТРОКА(18))
			|		ИНАЧЕ ВЫРАЗИТЬ(""СобственныеТовары"" КАК СТРОКА(18))
			|	КОНЕЦ КАК ТипЗапасов,
			|	ВидыЗапасов.АналитикаУчетаНоменклатуры.Серия КАК Серия
			|ПОМЕСТИТЬ ВидыЗапасов
			|ИЗ
			|	Документ.ВозвратТоваровОтКлиента.ВидыЗапасов КАК ВидыЗапасов
			|ГДЕ
			|	ВидыЗапасов.Ссылка = &Ссылка
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Номенклатура
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ВидыЗапасов.Номенклатура КАК Номенклатура,
			|	ВидыЗапасов.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
			|	&ТекстЗапросаХарактеристика,
			|	&ТекстЗапросаСерия,
			|	ВидыЗапасов.Количество КАК Количество,
			|	ВидыЗапасов.НомерГТД КАК НомерГТД,
			|	ВидыЗапасов.СтранаПроисхождения КАК СтранаПроисхождения,
			|	ВидыЗапасов.СтавкаНДС КАК СтавкаНДС,
			|	ВидыЗапасов.СуммаНДС КАК СуммаНДС,
			|	ВЫРАЗИТЬ(ВЫБОР
			|			КОГДА ВидыЗапасов.ЦенаВключаетНДС
			|				ТОГДА ВидыЗапасов.СуммаСНДС / ВидыЗапасов.Количество
			|			ИНАЧЕ (ВидыЗапасов.СуммаСНДС - ВидыЗапасов.СуммаНДС) / ВидыЗапасов.Количество
			|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК Цена,
			|	ВЫБОР
			|		КОГДА ВидыЗапасов.ЦенаВключаетНДС
			|			ТОГДА ВидыЗапасов.СуммаСНДС
			|		ИНАЧЕ ВидыЗапасов.СуммаСНДС - ВидыЗапасов.СуммаНДС
			|	КОНЕЦ КАК Сумма,
			|	ВидыЗапасов.Себестоимость КАК Себестоимость,
			|	ВЫБОР
			|		КОГДА ВидыЗапасов.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
			|				И ВидыЗапасов.ВозвратПереданнойМногооборотнойТары
			|			ТОГДА ""ВозвратнаяТара""
			|		КОГДА ВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
			|			ТОГДА ВЫРАЗИТЬ(""КомиссионныеТовары"" КАК СТРОКА(18))
			|		ИНАЧЕ ВЫРАЗИТЬ(""СобственныеТовары"" КАК СТРОКА(18))
			|	КОНЕЦ КАК ТипЗапасов
			|ИЗ
			|	ВидыЗапасов КАК ВидыЗапасов";
			
	Иначе
			
		ТекстЗапроса = 
			"ВЫБРАТЬ
			|	ВЫБОР
			|		КОГДА ВидыЗапасов.НоменклатураОприходование = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
			|			ТОГДА ВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура
			|		ИНАЧЕ ВидыЗапасов.НоменклатураОприходование
			|	КОНЕЦ КАК Номенклатура,
			|	ВЫБОР
			|		КОГДА ВидыЗапасов.ХарактеристикаОприходование = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
			|			ТОГДА ВидыЗапасов.АналитикаУчетаНоменклатуры.Характеристика
			|		ИНАЧЕ ВидыЗапасов.ХарактеристикаОприходование
			|	КОНЕЦ КАК Характеристика,
			|	ВидыЗапасов.Количество КАК Количество,
			|	ВидыЗапасов.Ссылка.ДокументРеализации КАК ДокументРеализации,
			|	ВидыЗапасов.Ссылка.ВозвратПорчи КАК ВозвратПорчи,
			|	ВидыЗапасов.Ссылка.ЦенаВключаетНДС КАК ЦенаВключаетНДС,
			|	ВидыЗапасов.СуммаСНДС КАК СуммаСНДС,
			|	ВидыЗапасов.СуммаНДС КАК СуммаНДС,
			|	ВидыЗапасов.СтавкаНДС КАК СтавкаНДС,
			|	ВЫБОР
			|		КОГДА ВидыЗапасов.СпособОпределенияСебестоимости = ЗНАЧЕНИЕ(Перечисление.СпособыОпределенияСебестоимости.ИзТекущегоДокумента)
			|			ТОГДА ВЫБОР
			|					КОГДА ВидыЗапасов.Ссылка.ЦенаВключаетНДС
			|						ТОГДА ВидыЗапасов.СуммаСНДС
			|					ИНАЧЕ ВидыЗапасов.СуммаСНДС - ВидыЗапасов.СуммаНДС
			|				КОНЕЦ
			|		КОГДА ВидыЗапасов.СпособОпределенияСебестоимости = ЗНАЧЕНИЕ(Перечисление.СпособыОпределенияСебестоимости.Вручную)
			|			ТОГДА ВЫБОР
			|					КОГДА ЕСТЬNULL(&Курс, 0) > 0
			|							И ЕСТЬNULL(&Кратность, 0) > 0
			|						ТОГДА ВЫРАЗИТЬ(ВидыЗапасов.СебестоимостьРегл / &Курс / &Кратность КАК ЧИСЛО(15, 2))
			|					ИНАЧЕ 0
			|				КОНЕЦ
			|		ИНАЧЕ 0
			|	КОНЕЦ КАК Себестоимость,
			|	ВидыЗапасов.НомерГТД.Код КАК НомерГТД,
			|	ВидыЗапасов.НомерГТД.СтранаПроисхождения КАК СтранаПроисхождения,
			|	ВЫБОР
			|		КОГДА ВидыЗапасов.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
			|			ТОГДА ВЫРАЗИТЬ(""КомиссионныеТовары"" КАК СТРОКА(18))
			|		ИНАЧЕ ВЫРАЗИТЬ(""СобственныеТовары"" КАК СТРОКА(18))
			|	КОНЕЦ КАК ТипЗапасов,
			|	ВидыЗапасов.ВидЗапасовОтгрузки КАК ВидЗапасовОтгрузки,
			|	ВидыЗапасов.Ссылка.ВозвратПереданнойМногооборотнойТары КАК ВозвратПереданнойМногооборотнойТары,
			|	ВидыЗапасов.АналитикаУчетаНоменклатуры.Серия КАК Серия
			|ПОМЕСТИТЬ ВидыЗапасов
			|ИЗ
			|	Документ.ВозвратТоваровОтКлиента.ВидыЗапасов КАК ВидыЗапасов
			|ГДЕ
			|	ВидыЗапасов.Ссылка = &Ссылка
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Номенклатура
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ВидыЗапасовРеализация.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
			|	ВидыЗапасовРеализация.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
			|	ВидыЗапасовРеализация.ВидЗапасов КАК ВидЗапасов,
			|	МАКСИМУМ(ВЫРАЗИТЬ(ВЫБОР
			|		КОГДА ВидыЗапасовРеализация.СуммаСНДС = 0 Или ВидыЗапасовРеализация.Количество = 0
			|			ТОГДА NULL
			|		КОГДА ВидыЗапасовРеализация.Ссылка.ЦенаВключаетНДС
			|			ТОГДА ВидыЗапасовРеализация.СуммаСНДС / ВидыЗапасовРеализация.Количество
			|		ИНАЧЕ (ВидыЗапасовРеализация.СуммаСНДС - ВидыЗапасовРеализация.СуммаНДС) / ВидыЗапасовРеализация.Количество
			|	КОНЕЦ КАК ЧИСЛО(15, 2))) КАК Цена,
			|	СУММА(1) КАК КоличествоСтрок
			|ПОМЕСТИТЬ ВидыЗапасовРеализация
			|ИЗ
			|	Документ.РеализацияТоваровУслуг.ВидыЗапасов КАК ВидыЗапасовРеализация
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВидыЗапасов КАК ВидыЗапасов
			|		ПО ВидыЗапасовРеализация.Ссылка = ВидыЗапасов.ДокументРеализации
			|
			|СГРУППИРОВАТЬ ПО
			|	ВидыЗапасовРеализация.АналитикаУчетаНоменклатуры.Характеристика,
			|	ВидыЗапасовРеализация.ВидЗапасов,
			|	ВидыЗапасовРеализация.АналитикаУчетаНоменклатуры.Номенклатура
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ВидыЗапасов.Номенклатура КАК Номенклатура,
			|	ВидыЗапасов.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
			|	&ТекстЗапросаХарактеристика,
			|	&ТекстЗапросаСерия,
			|	ВидыЗапасов.Количество КАК Количество,
			|	ВидыЗапасов.СтавкаНДС КАК СтавкаНДС,
			|	ВидыЗапасов.СуммаНДС КАК СуммаНДС,
			|	ВидыЗапасов.НомерГТД КАК НомерГТД,
			|	ВидыЗапасов.СтранаПроисхождения КАК СтранаПроисхождения,
			|	ЕСТЬNULL(ВидыЗапасовРеализация.Цена, ВЫРАЗИТЬ(ВЫБОР
			|			КОГДА ВидыЗапасов.ЦенаВключаетНДС
			|				ТОГДА ВидыЗапасов.СуммаСНДС / ВидыЗапасов.Количество
			|			ИНАЧЕ (ВидыЗапасов.СуммаСНДС - ВидыЗапасов.СуммаНДС) / ВидыЗапасов.Количество
			|		КОНЕЦ КАК ЧИСЛО(15, 2))) КАК Цена,
			|	ВЫБОР
			|		КОГДА ВидыЗапасов.ЦенаВключаетНДС
			|			ТОГДА ВидыЗапасов.СуммаСНДС
			|		ИНАЧЕ ВидыЗапасов.СуммаСНДС - ВидыЗапасов.СуммаНДС
			|	КОНЕЦ КАК Сумма,
			|	ВидыЗапасов.Себестоимость КАК Себестоимость,
			|	ВЫБОР
			|		КОГДА ВидыЗапасов.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
			|				И ВидыЗапасов.ВозвратПереданнойМногооборотнойТары
			|			ТОГДА ""ВозвратнаяТара""
			|		ИНАЧЕ ВидыЗапасов.ТипЗапасов
			|	КОНЕЦ КАК ТипЗапасов
			|ИЗ
			|	ВидыЗапасов КАК ВидыЗапасов
			|	ЛЕВОЕ СОЕДИНЕНИЕ ВидыЗапасовРеализация КАК ВидыЗапасовРеализация
			|ПО ВидыЗапасов.Номенклатура = ВидыЗапасовРеализация.Номенклатура
			|	И ВидыЗапасов.Характеристика = ВидыЗапасовРеализация.Характеристика
			|	И ВидыЗапасов.ВидЗапасовОтгрузки = ВидыЗапасовРеализация.ВидЗапасов
			|	И (ВидыЗапасовРеализация.КоличествоСтрок = 1)";
	
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаХарактеристика",
		ПолучитьТекстЗапросаХарактеристики(КомпонентыОбмена, "ВидыЗапасов"));
		
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаСерия",
		ПолучитьТекстЗапросаСерии(КомпонентыОбмена, "ВидыЗапасов"));
	
	// Заказ клиента берем из табличной части "Расшифровка платежа"
	// Если там указано несколько заказов или в качестве заказа указан документ, не являющийся заказом, то ничего не делаем
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначения.РазделительПакетаЗапросов() + 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВЫБОР
		|		КОГДА ВозвратТоваровОтКлиента.ДокументРеализации ССЫЛКА Документ.РеализацияТоваровУслуг
		|			ТОГДА ВЫРАЗИТЬ(ВозвратТоваровОтКлиента.ДокументРеализации КАК Документ.РеализацияТоваровУслуг).ЗаказКлиента
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК Заказ
		|ИЗ
		|	Документ.ВозвратТоваровОтКлиента КАК ВозвратТоваровОтКлиента
		|ГДЕ
		|	ВозвратТоваровОтКлиента.Ссылка = &Ссылка";
	
	#Область МаркировкиУпаковки
	
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначения.РазделительПакетаЗапросов() +
		"ВЫБРАТЬ
		|	ВозвратТоваровОтКлиентаШтрихкодыУпаковок.НомерСтроки КАК НомерСтрокиДокумента,
		|	ВозвратТоваровОтКлиентаШтрихкодыУпаковок.ШтрихкодУпаковки КАК ШтрихкодУпаковки,
		|	ШтрихкодыУпаковокТоваров.ТипУпаковки КАК ТипУпаковки,
		|	ШтрихкодыУпаковокТоваров.Номенклатура КАК Номенклатура,
		|	ШтрихкодыУпаковокТоваров.Характеристика КАК Характеристика,
		|	ШтрихкодыУпаковокТоваров.Серия КАК Серия,
		|	ШтрихкодыУпаковокТоваров.Упаковка КАК Упаковка,
		|	ШтрихкодыУпаковокТоваров.ЗначениеШтрихкода КАК ЗначениеШтрихкода
		|ПОМЕСТИТЬ втШтрихкодыУпаковок
		|ИЗ
		|	Документ.ВозвратТоваровОтКлиента.ШтрихкодыУпаковок КАК ВозвратТоваровОтКлиентаШтрихкодыУпаковок
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ШтрихкодыУпаковокТоваров КАК ШтрихкодыУпаковокТоваров
		|		ПО ВозвратТоваровОтКлиентаШтрихкодыУпаковок.ШтрихкодУпаковки = ШтрихкодыУпаковокТоваров.Ссылка
		|ГДЕ
		|	ВозвратТоваровОтКлиентаШтрихкодыУпаковок.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втШтрихкодыУпаковок.НомерСтрокиДокумента КАК НомерСтрокиДокумента,
		|	втШтрихкодыУпаковок.Номенклатура КАК Номенклатура,
		|	&ТипАкцизнойМарки,
		|	&ТекстЗапросаХарактеристикаАкцизныеМарки,
		|	&ТекстЗапросаСерияАкцизныеМарки,
		|	&ТекстЗапросаУпаковкаАкцизныеМарки,
		|	втШтрихкодыУпаковок.ЗначениеШтрихкода КАК НомерАкцизнойМарки
		|ИЗ
		|	втШтрихкодыУпаковок КАК втШтрихкодыУпаковок
		|ГДЕ
		|	втШтрихкодыУпаковок.ТипУпаковки = ЗНАЧЕНИЕ(Перечисление.ТипыУпаковок.МаркированныйТовар)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втШтрихкодыУпаковок.НомерСтрокиДокумента КАК НомерСтрокиДокумента,
		|	втШтрихкодыУпаковок.ШтрихкодУпаковки КАК МаркировкаУпаковки
		|ИЗ
		|	втШтрихкодыУпаковок КАК втШтрихкодыУпаковок
		|ГДЕ
		|	НЕ втШтрихкодыУпаковок.ТипУпаковки = ЗНАЧЕНИЕ(Перечисление.ТипыУпаковок.МаркированныйТовар)
		|	И НЕ втШтрихкодыУпаковок.ТипУпаковки = ЗНАЧЕНИЕ(Перечисление.ТипыУпаковок.МультитоварнаяУпаковка)";
	
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаХарактеристикаАкцизныеМарки",
			ПолучитьТекстЗапросаХарактеристики(КомпонентыОбмена, "втШтрихкодыУпаковок"));
			
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаСерияАкцизныеМарки",
			ПолучитьТекстЗапросаСерии(КомпонентыОбмена, "втШтрихкодыУпаковок"));
			
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаУпаковкаАкцизныеМарки",
			ПолучитьТекстЗапросаУпаковки(КомпонентыОбмена, "втШтрихкодыУпаковок", Ложь));
			
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТипАкцизнойМарки",
			ПолучитьТекстЗапросаТипАкцизнойМарки(КомпонентыОбмена, "втШтрихкодыУпаковок"));
			
	#КонецОбласти
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Ссылка", ДанныеИБ.Ссылка);
	Если ДанныеИБ.Валюта = ВалютаРегламентированногоУчета(КомпонентыОбмена) Тогда
		Запрос.УстановитьПараметр("Курс", 1);
		Запрос.УстановитьПараметр("Кратность", 1);
	Иначе
		КурсВалюты = РаботаСКурсамиВалют.ПолучитьКурсВалюты(ДанныеИБ.Валюта, ДанныеИБ.Дата);
		Запрос.УстановитьПараметр("Курс", КурсВалюты.Курс);
		Запрос.УстановитьПараметр("Кратность", КурсВалюты.Кратность);
	КонецЕсли;
	Результат = Запрос.ВыполнитьПакет();
	
	Товары = Результат[Результат.Количество() - 5].Выгрузить();
	
	ЗаполнитьХарактеристикуУпаковкуВТЧ(КомпонентыОбмена, Товары, Истина, Ложь);
	ЗаполнитьВидДеятельностиНДСВТЧ(КомпонентыОбмена, ДанныеИБ, Товары, "ВозвратПодДеятельность");
	
	ДанныеXDTO.Вставить("Товары", Товары);
	
	// Дополнительные свойства для договора
	Если ЗначениеЗаполнено(ДанныеИБ.Договор) Тогда
		ДанныеXDTO.Вставить("Договор", ДанныеИБ.Договор);
	Иначе
		
		ПараметрыПоУмолчанию = Новый Структура(ДоговорФиксированнаяСтруктураКлючей());
		ПараметрыПоУмолчанию.Контрагент               = КонтрагентДокумента;
		ПараметрыПоУмолчанию.Организация              = ДанныеИБ.Организация;
		ПараметрыПоУмолчанию.ВалютаВзаиморасчетов     = ВалютаВзаиморасчетов;
		ПараметрыПоУмолчанию.Дата                     = ДанныеИБ.Дата;
		ПараметрыПоУмолчанию.Номер                    = ДанныеИБ.Номер;
		ПараметрыПоУмолчанию.РасчетыВУсловныхЕдиницах = РасчетыВУсловныхЕдиницах;
	
		ПараметрыПоУмолчанию.ВидДоговора = ?(ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратОтКомиссионера,
			"СКомиссионером", "СПокупателем");
		
		ЗаказКлиента = Неопределено;
		
		Выборка = Результат[Результат.Количество() - 4].Выбрать();
		Если Выборка.Следующий() Тогда
			ЗаказКлиента = Выборка.Заказ;
		КонецЕсли;
		
		Заказ = Новый Структура("Заказ, Соглашение, Сделка", ЗаказКлиента, ДанныеИБ.Соглашение, ДанныеИБ.Сделка);
		
		Договор = ДоговорИнструкцияКонвертацииПоДаннымВзаиморасчетов(ПараметрыПоУмолчанию, КомпонентыОбмена, Заказ, ДанныеИБ);
		ДанныеXDTO.Вставить("Договор", Договор);
		
	КонецЕсли;
	
	Если ВерсияФорматаЧислом(КомпонентыОбмена.ВерсияФорматаОбмена) >= ВерсияФорматаЧислом("1.7") Тогда
		АкцизныеМарки = Результат[Результат.Количество() - 2].Выгрузить();
		ЗаполнитьХарактеристикуУпаковкуВТЧ(КомпонентыОбмена, АкцизныеМарки);
		ДанныеXDTO.Вставить("АкцизныеМарки", АкцизныеМарки);
	КонецЕсли;
	
	Если ВерсияФорматаЧислом(КомпонентыОбмена.ВерсияФорматаОбмена) >= ВерсияФорматаЧислом("1.7") Тогда
		МаркировкиУпаковок = Результат[Результат.Количество() - 1].Выгрузить();
		ДанныеXDTO.Вставить("МаркировкиУпаковок", МаркировкиУпаковок);
	КонецЕсли;
	
	ВыгрузитьДополнительныеРеквизитыИСведения(КомпонентыОбмена, ДанныеИБ, ДанныеXDTO);
КонецПроцедуры
