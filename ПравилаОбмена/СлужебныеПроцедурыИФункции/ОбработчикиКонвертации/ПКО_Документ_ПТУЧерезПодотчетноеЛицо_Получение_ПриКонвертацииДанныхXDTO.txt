Процедура ПКО_Документ_ПТУЧерезПодотчетноеЛицо_Получение_ПриКонвертацииДанныхXDTO(ДанныеXDTO, ПолученныеДанные, КомпонентыОбмена)
	ПолученныеДанные.Проведен               = Истина;
	ПолученныеДанные.Согласован             = Истина;
	ПолученныеДанные.ХозяйственнаяОперация  = Перечисления.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо;
	ПолученныеДанные.СпособДоставки 		= Перечисления.СпособыДоставки.СиламиПоставщикаДоНашегоСклада;
	
	ДобавитьВалютуВДопСвойства(ПолученныеДанные, ДанныеXDTO);
	
	ЗагрузитьДополнительныеРеквизиты(ПолученныеДанные, ДанныеXDTO, КомпонентыОбмена);
	
	ПолученныеДанные.ДополнительныеСвойства.Вставить("СуммаВключаетНДС", ДанныеXDTO.СуммаВключаетНДС);
	
	Если ДанныеXDTO.Свойство("АвансовыйОтчет") Тогда
		ПолученныеДанные.ДополнительныеСвойства.Вставить("АвансовыйОтчет", ДанныеXDTO.АвансовыйОтчет);
	КонецЕсли;
	
	ПравилаЗаполнения = Новый Соответствие;
	ПравилаЗаполнения.Вставить("Номенклатура",        "Номенклатура");
	ПравилаЗаполнения.Вставить("Количество",          "Количество");
	ПравилаЗаполнения.Вставить("Сумма",               "Сумма");
	ПравилаЗаполнения.Вставить("Цена",                "Цена");
	ПравилаЗаполнения.Вставить("СтавкаНДС",           "СтавкаНДС");
	ПравилаЗаполнения.Вставить("СуммаНДС",            "СуммаНДС");
	ПравилаЗаполнения.Вставить("ИдентификаторСтроки", "ИдентификаторСтроки");
	Если ЗначениеФО("ИспользоватьХарактеристикиНоменклатуры", КомпонентыОбмена) Тогда
		ПравилаЗаполнения.Вставить("Характеристика",     "Характеристика");
	КонецЕсли;
	Если ЗначениеФО("ИспользоватьУпаковкиНоменклатуры", КомпонентыОбмена) Тогда
		ПравилаЗаполнения.Вставить("Упаковка",           "Упаковка");
		ПравилаЗаполнения.Вставить("КоличествоУпаковок", "КоличествоУпаковок");
	КонецЕсли;
	Если ЗначениеФО("ИспользоватьСерииНоменклатуры", КомпонентыОбмена) Тогда
		ПравилаЗаполнения.Вставить("Серия",              "Серия");
	КонецЕсли;
	
	//Товары 
	СуммаДокумента = 0;
	МассивСтрокТовары = Новый Массив;
	Если ДанныеXDTO.Свойство("Товары")
		И ЗначениеЗаполнено(ДанныеXDTO.Товары) Тогда
		
		Если ДанныеXDTO.Товары.Колонки.Найти("ЗакупкаПодДеятельность") <> Неопределено Тогда
			ПолученныеДанные.ДополнительныеСвойства.Вставить("ЗакупкаПодДеятельность",
				ДанныеXDTO.Товары[0].ЗакупкаПодДеятельность);
		КонецЕсли;
		
		Для Каждого Строка Из ДанныеXDTO.Товары Цикл
			СтруктураДанныхСтроки = ДанныеКоллекцииВВидеСтруктуры(Строка, ПравилаЗаполнения);
			Если ЗначениеЗаполнено(Строка.НомерГТД) Тогда
				КлючевыеСвойстваГТД = Новый Структура("Код, СтранаПроисхождения", Строка.НомерГТД, Строка.СтранаПроисхождения);
				СтруктураДанныхСтроки.Вставить("НомерГТД", Новый Структура("Значение, ИмяПКО", КлючевыеСвойстваГТД, "Справочник_НомерГТД_Получение"));
			КонецЕсли;
			Если ДанныеXDTO.Свойство("Склад") Тогда
				СтруктураДанныхСтроки.Вставить("Склад", ДанныеXDTO.Склад);
			КонецЕсли;
			
			СуммаСНДС = Строка.Сумма;
			Если СтруктураДанныхСтроки.Свойство("СуммаНДС") И НЕ ПолученныеДанные.ЦенаВключаетНДС Тогда
				СуммаСНДС = СуммаСНДС + СтруктураДанныхСтроки.СуммаНДС
			КонецЕсли;
			СуммаДокумента = СуммаДокумента + СуммаСНДС;
			СтруктураДанныхСтроки.Вставить("СуммаСНДС", СуммаСНДС);
				
			Если СтруктураДанныхСтроки.Свойство("КоличествоУпаковок") Тогда
			    Если СтруктураДанныхСтроки.Свойство("Упаковка") Тогда
					СтруктураДанныхСтроки.Вставить("Цена", СтруктураДанныхСтроки.Сумма / СтруктураДанныхСтроки.КоличествоУпаковок);
				КонецЕсли;
			Иначе
				СтруктураДанныхСтроки.Вставить("КоличествоУпаковок", СтруктураДанныхСтроки.Количество);
			КонецЕсли;
	
			Если ЗначениеЗаполнено(Строка.Поставщик) И НЕ ПолученныеДанные.ДополнительныеСвойства.Свойство("Контрагент") Тогда
				ПолученныеДанные.ДополнительныеСвойства.Вставить("Контрагент", Строка.Поставщик);
			КонецЕсли;
			Если ЗначениеЗаполнено(Строка.НомерВходящегоДокумента) И НЕ ЗначениеЗаполнено(ПолученныеДанные.НомерВходящегоДокумента) Тогда
				ПолученныеДанные.НомерВходящегоДокумента = Строка.НомерВходящегоДокумента;
			КонецЕсли;
			Если ЗначениеЗаполнено(Строка.ДатаВходящегоДокумента) И НЕ ЗначениеЗаполнено(ПолученныеДанные.ДатаВходящегоДокумента) Тогда
				ПолученныеДанные.ДатаВходящегоДокумента = Строка.ДатаВходящегоДокумента;
			КонецЕсли;
			Если ЗначениеЗаполнено(Строка.ПредъявленСФ) 
				И ЗначениеЗаполнено(Строка.ДатаСФ)
				И ЗначениеЗаполнено (Строка.НомерСФ) 
				И НЕ ПолученныеДанные.ДополнительныеСвойства.Свойство("ДатаСФ") Тогда
				ПолученныеДанные.ДополнительныеСвойства.Вставить("ДатаСФ", Строка.ДатаСФ);
				ПолученныеДанные.ДополнительныеСвойства.Вставить("НомерСФ", Строка.НомерСФ);
			КонецЕсли;
	
			МассивСтрокТовары.Добавить(СтруктураДанныхСтроки);
		КонецЦикла;
	КонецЕсли;
	ПолученныеДанные.СуммаДокумента = СуммаДокумента;
	ПолученныеДанные.СуммаВзаиморасчетов = СуммаДокумента;
	Если МассивСтрокТовары.Количество() > 0 Тогда
		ПолученныеДанные.ДополнительныеСвойства.Вставить("Товары", МассивСтрокТовары);
	КонецЕсли;
	
	// Штрихкоды упаковок.
	ЗагрузитьШтрихкодыУпаковок(ДанныеXDTO, ПолученныеДанные, КомпонентыОбмена);
КонецПроцедуры
