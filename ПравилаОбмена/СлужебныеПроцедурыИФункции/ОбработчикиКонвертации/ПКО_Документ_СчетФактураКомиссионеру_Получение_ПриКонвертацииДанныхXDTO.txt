Процедура ПКО_Документ_СчетФактураКомиссионеру_Получение_ПриКонвертацииДанныхXDTO(ДанныеXDTO, ПолученныеДанные, КомпонентыОбмена)
	ПолученныеДанные.Проведен = Истина;
	ПолученныеДанные.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОтчетКомиссионера;
	ДобавитьВалютуВДопСвойства(ПолученныеДанные, ДанныеXDTO);
	ДобавитьДоговорВДопСвойства(ПолученныеДанные, ДанныеXDTO, КомпонентыОбмена);
	
	Если НЕ ЗначениеЗаполнено(ПолученныеДанные.КодВидаОперации) Тогда
		ПолученныеДанные.КодВидаОперации = "01";
	КонецЕсли;
	
	Если ДанныеXDTO.Свойство("СпособВыставления") И ЗначениеЗаполнено(ДанныеXDTO.СпособВыставления) Тогда
		ПолученныеДанные.ВыставленВЭлектронномВиде = (ДанныеXDTO.СпособВыставления.Значение = "ВЭлектронномВиде");
	КонецЕсли;
	
	// В Счет-фактуре комиссионеру может быть указан только один документ - основание.
	// В противном случае загружается счет-фактура выданный.
	Если ДанныеXDTO.Свойство("ДокументыОснования") И ЗначениеЗаполнено(ДанныеXDTO.ДокументыОснования) Тогда
		СтрокаТаблицы = ДанныеXDTO.ДокументыОснования[0];
		
		ПолученныеДанные.ДополнительныеСвойства.Вставить("ДокументОснование", СтрокаТаблицы.ДокументОснование);
		Если НЕ ЗначениеЗаполнено(ПолученныеДанные.СчетФактураОснование) И ЗначениеЗаполнено(СтрокаТаблицы.ИсходныйДокумент) Тогда
			ПолученныеДанные.ДополнительныеСвойства.Вставить("СчетФактураОснование", СтрокаТаблицы.ИсходныйДокумент);
		КонецЕсли;
	КонецЕсли;
	
	Покупатели = Новый Массив;
	СтруктураДанныхСтроки = Новый Структура("НомерСчетаФактуры, Покупатель");
	Если ДанныеXDTO.Свойство("НомерСчетаФактурыКомиссионера") Тогда
		СтруктураДанныхСтроки.НомерСчетаФактуры = ДанныеXDTO.НомерСчетаФактурыКомиссионера;
	КонецЕсли;
	Если ДанныеXDTO.Свойство("КонечныйПокупатель")
		И ЗначениеЗаполнено(ДанныеXDTO.КонечныйПокупатель) Тогда
		СтруктураДанныхСтроки.Покупатель = ДанныеXDTO.КонечныйПокупатель;
	Иначе
		СтруктураДанныхСтроки.Покупатель = ДанныеXDTO.Контрагент;
	КонецЕсли;
	Покупатели.Добавить(СтруктураДанныхСтроки);
	ПолученныеДанные.ДополнительныеСвойства.Вставить("Покупатели", Покупатели);		
	
	ЗагрузитьДополнительныеРеквизиты(ПолученныеДанные, ДанныеXDTO, КомпонентыОбмена);
КонецПроцедуры
