Процедура ПКО_Документ_ПБДСПрочееПоступление_Отправка_ПриОтправкеДанных(ДанныеИБ, ДанныеXDTO, КомпонентыОбмена, СтекВыгрузки)
	Если СтекВыгрузки.Количество() > 1 Тогда
		Возврат;
	КонецЕсли;
	
	Если СокрЛП(ДанныеИБ.НомерВходящегоДокумента) = "-" Тогда
		// Символ "-" проставляем искусственно если из БП загружен документ с пустым номером.
		ДанныеXDTO.НомерВходящегоДокумента = "";
	КонецЕсли;
	
	ДанныеШапки = ДанныеИБ.ДополнительныеСвойства.ДанныеШапки;
	
	ВыгрузитьПодразделениеИзРеквизитаДокумента(КомпонентыОбмена, ДанныеИБ, ДанныеXDTO, "БанковскийСчет");
	ВыгрузитьДополнительныеРеквизитыИСведения(КомпонентыОбмена, ДанныеИБ, ДанныеXDTO, Истина);
	
	Если ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВнутренняяПередачаДенежныхСредств Тогда
		СчетУчетаРасчетов = "79.02";
	ИначеЕсли ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзКассыНаРасчетныйСчет Тогда
		СчетУчетаРасчетов = "57.01";
	ИначеЕсли ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствСДругогоСчета Тогда
		СчетУчетаРасчетов = "51";
	ИначеЕсли ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратНеперечисленныхДС Тогда
		СчетУчетаРасчетов = "70";
	Иначе
		// Счет учета расчетов определяется из данных табличной части
		СчетУчетаРасчетов = "";
	КонецЕсли;
	
	РасшифровкаПлатежа = ДанныеИБ.ДополнительныеСвойства.РасшифровкаПлатежаРасширенная.Выгрузить();
	
	// Расшифровка платежа
	ТабРасшифровка = Новый ТаблицаЗначений;
	ТабРасшифровка.Колонки.Добавить("СтатьяДДС");
	ТабРасшифровка.Колонки.Добавить("Сумма");
	ТабРасшифровка.Колонки.Добавить("СчетУчетаРасчетов");
	
	Если РасшифровкаПлатежа.Количество() > 0 Тогда
		Для Каждого Строка Из РасшифровкаПлатежа Цикл
			НоваяСтрока = ТабРасшифровка.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка, "Сумма, СтатьяДДС");
			
			Если ЗначениеЗаполнено(Строка.СчетУчетаРасчетовСКонтрагентом) Тогда
				НоваяСтрока.СчетУчетаРасчетов = Строка.СчетУчетаРасчетовСКонтрагентом;
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(НоваяСтрока.СчетУчетаРасчетов) Тогда
				Если ЗначениеЗаполнено(СчетУчетаРасчетов) Тогда
					НоваяСтрока.СчетУчетаРасчетов = СчетУчетаРасчетов;
				ИначеЕсли ЭтоУТ() Тогда
					НоваяСтрока.СчетУчетаРасчетов = СчетУчетаРасчетовПрочихОперацийДДС(Новый Структура("СтатьяДДС", Строка.СтатьяДДС));
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	Иначе
		НоваяСтрока = ТабРасшифровка.Добавить();
		НоваяСтрока.Сумма             = ДанныеИБ.СуммаДокумента;
		НоваяСтрока.СтатьяДДС         = ДанныеИБ.СтатьяДвиженияДенежныхСредств;
		Если ЗначениеЗаполнено(СчетУчетаРасчетов) Тогда
			НоваяСтрока.СчетУчетаРасчетов = СчетУчетаРасчетов;
		ИначеЕсли ЭтоУТ() Тогда
			НоваяСтрока.СчетУчетаРасчетов = СчетУчетаРасчетовПрочихОперацийДДС(Новый Структура("СтатьяДДС", ДанныеИБ.СтатьяДвиженияДенежныхСредств));
		КонецЕсли;
	КонецЕсли;	
	
	ДанныеXDTO.Вставить("РасшифровкаПлатежа", ТабРасшифровка);
КонецПроцедуры
