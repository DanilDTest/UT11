Процедура ПОД_Документ_ПриходныйКассовыйОрдер_ПриОбработке(ДанныеИБ, ИспользованиеПКО, КомпонентыОбмена)
	УстановитьИспользованиеПКО(ИспользованиеПКО, Ложь);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
		|	ЕСТЬNULL(Шапка.СтатьяДвиженияДенежныхСредств.КорреспондирующийСчет, """") КАК КорреспондирующийСчет,
		|	Шапка.БанковскийСчет.ВалютаДенежныхСредств КАК ВалютаБанковскогоСчета,
		|	Шапка.Валюта КАК Валюта,
		|	Шапка.ВалютаКонвертации
		|ПОМЕСТИТЬ Шапка
		|ИЗ
		|	Документ.ПриходныйКассовыйОрдер КАК Шапка
		|ГДЕ
		|	Шапка.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ДанныеДокумента.СтатьяДоходов.КорреспондирующийСчет КАК КорреспондирующийСчет,
		|	ДанныеДокумента.СтатьяДоходов КАК СтатьяДоходов
		|ПОМЕСТИТЬ СтрокаСоСтатьейДоходовСКорСчетом
		|ИЗ
		|	Документ.ПриходныйКассовыйОрдер.РасшифровкаПлатежа КАК ДанныеДокумента
		|ГДЕ
		|	ДанныеДокумента.Ссылка = &Ссылка
		|	И (ДанныеДокумента.Ссылка.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрочиеДоходы)
		|			ИЛИ ДанныеДокумента.Ссылка.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрочееПоступлениеДенежныхСредств))
		|	И ДанныеДокумента.СтатьяДоходов.КорреспондирующийСчет ЕСТЬ НЕ NULL 
		|	И ДанныеДокумента.СтатьяДоходов.КорреспондирующийСчет <> """"
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДанныеДокумента.НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ПерваяСтрокаРасшифровки.ДоговорКредитаДепозита.СтатьяДДСКомиссии КАК ПерваяСтрРП_ДоговорКредитаДепозита_СДДСКомиссии,
		|	ПерваяСтрокаРасшифровки.ДоговорКредитаДепозита.СтатьяДДСПроцентов КАК ПерваяСтрРП_ДоговорКредитаДепозита_СДДСПроцентов,
		|	ПерваяСтрокаРасшифровки.СтатьяДвиженияДенежныхСредств КАК ПерваяСтрРП_СДДС
		|ПОМЕСТИТЬ ПерваяСтрокаРасшифровки
		|ИЗ
		|	Документ.ПриходныйКассовыйОрдер.РасшифровкаПлатежа КАК ПерваяСтрокаРасшифровки
		|ГДЕ
		|	ПерваяСтрокаРасшифровки.Ссылка = &Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПерваяСтрокаРасшифровки.НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(СтрокаСоСтатьейДоходовСКорСчетом.КорреспондирующийСчет, Шапка.КорреспондирующийСчет) КАК КорреспондирующийСчет,
		|	ЕСТЬNULL(СтрокаСоСтатьейДоходовСКорСчетом.СтатьяДоходов, НЕОПРЕДЕЛЕНО) КАК СтатьяДоходов,
		|	Шапка.ВалютаБанковскогоСчета,
		|	ЕСТЬNULL(ПерваяСтрокаРасшифровки.ПерваяСтрРП_ДоговорКредитаДепозита_СДДСКомиссии, НЕОПРЕДЕЛЕНО) КАК ПерваяСтрРП_ДоговорКредитаДепозита_СДДСКомиссии,
		|	ЕСТЬNULL(ПерваяСтрокаРасшифровки.ПерваяСтрРП_ДоговорКредитаДепозита_СДДСПроцентов, НЕОПРЕДЕЛЕНО) КАК ПерваяСтрРП_ДоговорКредитаДепозита_СДДСПроцентов,
		|	ЕСТЬNULL(ПерваяСтрокаРасшифровки.ПерваяСтрРП_СДДС, НЕОПРЕДЕЛЕНО) КАК ПерваяСтрРП_СДДС,
		|	ЕСТЬNULL(КурсыВалют.Курс, 1) КАК Валюта_Курс,
		|	ЕСТЬNULL(КурсыВалют.Кратность, 1) КАК Валюта_Кратность
		|ИЗ
		|	Шапка КАК Шапка
		|		ЛЕВОЕ СОЕДИНЕНИЕ СтрокаСоСтатьейДоходовСКорСчетом КАК СтрокаСоСтатьейДоходовСКорСчетом
		|		ПО (ИСТИНА)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПерваяСтрокаРасшифровки КАК ПерваяСтрокаРасшифровки
		|		ПО (ИСТИНА)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&Дата, ) КАК КурсыВалют
		|		ПО Шапка.Валюта = КурсыВалют.Валюта
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств КАК СтатьяДДС,
		|	РасшифровкаПлатежа.Сумма КАК Сумма,
		|	РасшифровкаПлатежа.СуммаНДС КАК СуммаНДС,
		|	РасшифровкаПлатежа.СтавкаНДС КАК СтавкаНДС,
		|	РасшифровкаПлатежа.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
		|	РасшифровкаПлатежа.ДоговорКредитаДепозита КАК ДоговорКредитаДепозита,
		|	РасшифровкаПлатежа.Заказ КАК Заказ,
		|	РасшифровкаПлатежа.ОснованиеПлатежа КАК ОснованиеПлатежа,
		|	РасшифровкаПлатежа.Партнер КАК Партнер,
		|	ВЫРАЗИТЬ(РасшифровкаПлатежа.Заказ КАК Справочник.ДоговорыКонтрагентов) КАК Договор,
		|	ЕСТЬNULL(КурсыВалют.Курс, 1) КАК КурсВзаиморасчетов,
		|	ЕСТЬNULL(КурсыВалют.Кратность, 1) КАК КратностьВзаиморасчетов,
		|	РасшифровкаПлатежа.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
		|	ВЫБОР
		|		КОГДА РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговораКредитовИДепозитов.КредитВБанке)
		|			ТОГДА ВЫБОР
		|					КОГДА РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипСрочности = ЗНАЧЕНИЕ(Перечисление.ТипыСрочностиКредитовИДепозитов.Краткосрочный)
		|						ТОГДА ""КраткосрочныеКредиты""
		|					КОГДА РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипСрочности = ЗНАЧЕНИЕ(Перечисление.ТипыСрочностиКредитовИДепозитов.Долгосрочный)
		|						ТОГДА ""ДолгосрочныеКредиты""
		|				КОНЕЦ
		|		КОГДА РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговораКредитовИДепозитов.ВнутреннийЗайм)
		|				ИЛИ РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговораКредитовИДепозитов.ВнешнийЗайм)
		|				ИЛИ РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговораКредитовИДепозитов.ВкладСотрудника)
		|			ТОГДА ВЫБОР
		|					КОГДА РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипСрочности = ЗНАЧЕНИЕ(Перечисление.ТипыСрочностиКредитовИДепозитов.Краткосрочный)
		|						ТОГДА ВЫБОР
		|								КОГДА РасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств = РасшифровкаПлатежа.ДоговорКредитаДепозита.СтатьяДДСПоступленияВыдачи
		|									ТОГДА ""КраткосрочныеЗаймы""
		|							КОНЕЦ
		|					КОГДА РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипСрочности = ЗНАЧЕНИЕ(Перечисление.ТипыСрочностиКредитовИДепозитов.Долгосрочный)
		|						ТОГДА ВЫБОР
		|								КОГДА РасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств = РасшифровкаПлатежа.ДоговорКредитаДепозита.СтатьяДДСПоступленияВыдачи
		|									ТОГДА ""ДолгосрочныеЗаймы""
		|							КОНЕЦ
		|				КОНЕЦ
		|		ИНАЧЕ NULL
		|	КОНЕЦ КАК ВидРасчетовРасширенный,
		|	ВЫБОР
		|		КОГДА РасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств.КорреспондирующийСчет <> """"
		|			ТОГДА РасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств.КорреспондирующийСчет
		|		ИНАЧЕ ВЫБОР
		|				КОГДА (РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговораКредитовИДепозитов.ВнутреннийЗайм)
		|						ИЛИ РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговораКредитовИДепозитов.ВнешнийЗайм))
		|						И (РасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств = РасшифровкаПлатежа.ДоговорКредитаДепозита.СтатьяДДСОсновногоДолга
		|							ИЛИ РасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств = РасшифровкаПлатежа.ДоговорКредитаДепозита.СтатьяДДСПроцентов)
		|					ТОГДА ""58.03""
		|				КОГДА (РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговораКредитовИДепозитов.ВнутреннийЗайм)
		|						ИЛИ РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговораКредитовИДепозитов.ВнешнийЗайм)
		|						ИЛИ РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговораКредитовИДепозитов.ВкладСотрудника))
		|						И РасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств = РасшифровкаПлатежа.ДоговорКредитаДепозита.СтатьяДДСПоступленияВыдачи
		|					ТОГДА ВЫБОР
		|							КОГДА РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипСрочности = ЗНАЧЕНИЕ(Перечисление.ТипыСрочностиКредитовИДепозитов.Краткосрочный)
		|								ТОГДА ВЫБОР
		|										КОГДА РасшифровкаПлатежа.ДоговорКредитаДепозита.ВалютаВзаиморасчетов = &ВалютаРегУчета
		|											ТОГДА ""66.03""
		|										ИНАЧЕ ""66.23""
		|									КОНЕЦ
		|							КОГДА РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипСрочности = ЗНАЧЕНИЕ(Перечисление.ТипыСрочностиКредитовИДепозитов.Долгосрочный)
		|								ТОГДА ВЫБОР
		|										КОГДА РасшифровкаПлатежа.ДоговорКредитаДепозита.ВалютаВзаиморасчетов = &ВалютаРегУчета
		|											ТОГДА ""67.03""
		|										ИНАЧЕ ""67.23""
		|									КОНЕЦ
		|						КОНЕЦ
		|				КОГДА РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговораКредитовИДепозитов.КредитВБанке)
		|					ТОГДА ВЫБОР
		|							КОГДА РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипСрочности = ЗНАЧЕНИЕ(Перечисление.ТипыСрочностиКредитовИДепозитов.Краткосрочный)
		|								ТОГДА ВЫБОР
		|										КОГДА РасшифровкаПлатежа.ДоговорКредитаДепозита.ВалютаВзаиморасчетов = &ВалютаРегУчета
		|											ТОГДА ""66.01""
		|										ИНАЧЕ ""66.21""
		|									КОНЕЦ
		|							КОГДА РасшифровкаПлатежа.ДоговорКредитаДепозита.ТипСрочности = ЗНАЧЕНИЕ(Перечисление.ТипыСрочностиКредитовИДепозитов.Долгосрочный)
		|								ТОГДА ВЫБОР
		|										КОГДА РасшифровкаПлатежа.ДоговорКредитаДепозита.ВалютаВзаиморасчетов = &ВалютаРегУчета
		|											ТОГДА ""67.01""
		|										ИНАЧЕ ""67.21""
		|									КОНЕЦ
		|						КОНЕЦ
		|			КОНЕЦ
		|	КОНЕЦ КАК СчетУчетаРасчетовСКонтрагентом
		|ИЗ
		|	Документ.ПриходныйКассовыйОрдер.РасшифровкаПлатежа КАК РасшифровкаПлатежа
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&Дата, ) КАК КурсыВалют
		|		ПО РасшифровкаПлатежа.ВалютаВзаиморасчетов = КурсыВалют.Валюта
		|ГДЕ
		|	РасшифровкаПлатежа.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ДанныеДокумента.СтатьяДоходов КАК СтатьяДоходов
		|ИЗ
		|	Документ.ПриходныйКассовыйОрдер.РасшифровкаПлатежа КАК ДанныеДокумента
		|ГДЕ
		|	ДанныеДокумента.Ссылка = &Ссылка
		|	И (ДанныеДокумента.Ссылка.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрочиеДоходы)
		|			ИЛИ ДанныеДокумента.Ссылка.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрочееПоступлениеДенежныхСредств))
		|	И (ДанныеДокумента.СтатьяДоходов ССЫЛКА ПланВидовХарактеристик.СтатьиДоходов
		|				И ДанныеДокумента.СтатьяДоходов <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.ПустаяСсылка)
		|			ИЛИ ДанныеДокумента.СтатьяДоходов ССЫЛКА ПланВидовХарактеристик.СтатьиРасходов
		|				И ДанныеДокумента.СтатьяДоходов <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка))
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДанныеДокумента.НомерСтроки";
	
	Запрос.УстановитьПараметр("Ссылка", ДанныеИБ.Ссылка);
	Запрос.УстановитьПараметр("Дата", ДанныеИБ.Дата);
	Запрос.УстановитьПараметр("ВалютаРегУчета", Константы.ВалютаРегламентированногоУчета.Получить());
	ДополнительныеДанные = Запрос.ВыполнитьПакет();
	
	ДанныеШапки = ДополнительныеДанные[3].Выгрузить()[0];
	ДанныеИБ.ДополнительныеСвойства.Вставить("ДанныеШапки",                   ДанныеШапки);
	ДанныеИБ.ДополнительныеСвойства.Вставить("РасшифровкаПлатежаРасширенная", ДополнительныеДанные[4]);
	
	Если ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВнутренняяПередачаДенежныхСредств Тогда
		ИспользованиеПКО.Документ_ПКОВнутренняяПередача_Отправка = Истина;
	ИначеЕсли ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.КонвертацияВалюты Тогда
		ИспользованиеПКО.Документ_ПКОКонвертацияВалюты_Отправка = Истина;
	ИначеЕсли ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзБанка Тогда
		ИспользованиеПКО.Документ_ПКОПолучениеНаличныхВБанке_Отправка = Истина;
	ИначеЕсли ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ИнкассацияДенежныхСредствИзБанка Тогда
		ИспользованиеПКО.Документ_ПКОПолучениеНаличныхВБанке_Отправка = Истина;
	ИначеЕсли ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПрочиеДоходы
		Или ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.СторнированиеПрочихРасходов Тогда
		// Хоз операции ПрочиеДоходы и СторнированиеПрочихРасходов больше не будет использоваться.
		// Оставлено на переходный период.
		ИспользованиеПКО.Документ_ПКОПрочиеДоходы_Отправка = Истина;
	ИначеЕсли ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПрочееПоступлениеДенежныхСредств Тогда
		ДанныеДоходов = ДополнительныеДанные[5].Выгрузить();
		Если ДанныеДоходов.Количество() > 0 Тогда
			ИспользованиеПКО.Документ_ПКОПрочиеДоходы_Отправка = Истина;
		Иначе
			ИспользованиеПКО.Документ_ПКОПрочееПоступление_Отправка = Истина;
		КонецЕсли;
	ИначеЕсли ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойОрганизации
		ИЛИ ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента Тогда
		ИспользованиеПКО.Документ_ПКОРасчетыСКонтрагентами_Отправка = Истина;
	ИначеЕсли ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтПоставщика
		ИЛИ ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтДругойОрганизации Тогда
		ИспользованиеПКО.Документ_ПКОРасчетыСКонтрагентами_Отправка = Истина;
	ИначеЕсли ЭтоХозяйственнаяОперацияРасчетыПоКредитамЗаймам(ДанныеИБ.ХозяйственнаяОперация)
		Или ЭтоХозяйственнаяОперацияРасчетыПоДепозитам(ДанныеИБ.ХозяйственнаяОперация) Тогда
		ИспользованиеПКО.Документ_ПКОРасчетыСКонтрагентами_Отправка = Истина;
	ИначеЕсли ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтПодотчетника
		Или ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПогашениеЗаймаСотрудником Тогда
		ИспользованиеПКО.Документ_ПКОРасчетыССотрудниками_Отправка = Истина;
	ИначеЕсли ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзКассыККМ Тогда
		Если ВерсияФорматаЧислом(КомпонентыОбмена.ВерсияФорматаОбмена) < ВерсияФорматаЧислом("1.4") Тогда
			ИспользованиеПКО.Удалить("Документ_ПКОПоступлениеИзКассыККМ_Отправка");
		Иначе
			ИспользованиеПКО.Документ_ПКОПоступлениеИзКассыККМ_Отправка = Истина;
		КонецЕсли;
	ИначеЕсли ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойКассы Тогда
		Если ВерсияФорматаЧислом(КомпонентыОбмена.ВерсияФорматаОбмена) < ВерсияФорматаЧислом("1.6") Тогда
			ИспользованиеПКО.Удалить("Документ_ПКОПоступлениеИзДругойКассы_Отправка");
		Иначе
			ИспользованиеПКО.Документ_ПКОПоступлениеИзДругойКассы_Отправка = Истина;
		КонецЕсли;
	КонецЕсли;
	
	ПроверкаЗаполненияРеквизитовОбъектаИБ(ДанныеИБ, КомпонентыОбмена, ИспользованиеПКО);
КонецПроцедуры
