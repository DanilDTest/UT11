Процедура ПКО_Справочник_Номенклатура_Получение_ПередЗаписьюПолученныхДанных(ПолученныеДанные, ДанныеИБ, КонвертацияСвойств, КомпонентыОбмена)
	Если НЕ ДанныеИБ = Неопределено Тогда
		ЗаполнитьРеквизиты(ПолученныеДанные, ДанныеИБ,
			"СтавкаНДС, ГруппаАналитическогоУчета");
	КонецЕсли;
	
	ДанныеДляЗаписиВИБ = ?(ДанныеИБ = Неопределено, ПолученныеДанные, ДанныеИБ);
	Если ПолученныеДанные.АлкогольнаяПродукция Тогда
		ДанныеДляЗаписиВИБ.ОсобенностьУчета = Перечисления.ОсобенностиУчетаНоменклатуры.АлкогольнаяПродукция;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(ДанныеДляЗаписиВИБ.СтавкаНДС) Тогда
		ДанныеДляЗаписиВИБ.СтавкаНДС = Перечисления.СтавкиНДС.НДС20;
	КонецЕсли;
	Если ДанныеДляЗаписиВИБ.ВесИспользовать И НЕ ЗначениеЗаполнено(ДанныеДляЗаписиВИБ.ВесЗнаменатель) Тогда
		ДанныеДляЗаписиВИБ.ВесЗнаменатель      = 1;
		ДанныеДляЗаписиВИБ.ВесЕдиницаИзмерения = НоменклатураСервер.ЕдиницаИзмеренияПоУмолчанию("Вес");
	КонецЕсли;
	Если Не ЗначениеЗаполнено(ДанныеДляЗаписиВИБ.ТипНоменклатуры) Тогда
		ДанныеДляЗаписиВИБ.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Товар;
	КонецЕсли;
	
	ЗагрузитьДополнительныеСведения(?(ДанныеИБ = Неопределено, ПолученныеДанные.ПолучитьСсылкуНового(), ДанныеИБ.Ссылка), 
								ПолученныеДанные.ДополнительныеСвойства, КомпонентыОбмена);
КонецПроцедуры
