Процедура ПКО_Справочник_ХарактеристикиНоменклатуры_ПриКонвертацииДанныхXDTO(ДанныеXDTO, ПолученныеДанные, КомпонентыОбмена)
	ИнструкцияВладелец = Новый Структура;
	Если ДанныеXDTO.КлючевыеСвойства.Свойство("ВидНоменклатуры") Тогда
		ИнструкцияВладелец.Вставить("Значение", ДанныеXDTO.КлючевыеСвойства.ВидНоменклатуры);
		ИнструкцияВладелец.Вставить("ИмяПКО",   "Справочник_ВидыНоменклатуры");
	ИначеЕсли ДанныеXDTO.КлючевыеСвойства.Свойство("Номенклатура") Тогда
	
		Если ВерсияФорматаЧислом(КомпонентыОбмена.ВерсияФорматаОбмена) < ВерсияФорматаЧислом("1.7") Тогда
			// Характеристика для вида номенклатуры выгружается как "Структура"
			// с подстановкой в качестве владельца - Номенклатуры из строки документа.
			// Проверим существование номенклатуры на момент загрузки.
			Номенклатура = RM_ОбменДаннымиXDTOСервер.СтруктураОбъектаXDTOВДанныеИБ(
					КомпонентыОбмена,
					ДанныеXDTO.КлючевыеСвойства.Номенклатура,
					RM_ОбменДаннымиXDTOСервер.ПКОПоИмени(КомпонентыОбмена, "Справочник_Номенклатура_Получение"), 
					"ПолучитьСсылку");
			
			Если ТипЗнч(Номенклатура) = Тип("СправочникСсылка.Номенклатура") Тогда
				РеквизитыНоменклатуры = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Номенклатура,
					"ВидНоменклатуры, ВладелецХарактеристик, ИспользованиеХарактеристик");
					
				Если ЗначениеЗаполнено(РеквизитыНоменклатуры.ИспользованиеХарактеристик) Тогда
					Если РеквизитыНоменклатуры.ИспользованиеХарактеристик = Перечисления.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеДляВидаНоменклатуры
						И ЗначениеЗаполнено(РеквизитыНоменклатуры.ВидНоменклатуры) Тогда
						ПолученныеДанные.Владелец = РеквизитыНоменклатуры.ВидНоменклатуры;
					ИначеЕсли РеквизитыНоменклатуры.ИспользованиеХарактеристик = Перечисления.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеСДругимВидомНоменклатуры
						И ЗначениеЗаполнено(РеквизитыНоменклатуры.ВладелецХарактеристик) Тогда
						ПолученныеДанные.Владелец = РеквизитыНоменклатуры.ВладелецХарактеристик;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;		
				
		Если НЕ ЗначениеЗаполнено(ПолученныеДанные.Владелец) Тогда
			ИнструкцияВладелец.Вставить("Значение", ДанныеXDTO.КлючевыеСвойства.Номенклатура);
			ИнструкцияВладелец.Вставить("ИмяПКО",   "Справочник_Номенклатура_Получение");
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИнструкцияВладелец) Тогда
		ПолученныеДанные.ДополнительныеСвойства.Вставить("Владелец", ИнструкцияВладелец);
	КонецЕсли;
КонецПроцедуры
