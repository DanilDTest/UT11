Процедура ПКО_Документ_СБДСПрочееСписание_Отправка_ПриОтправкеДанных(ДанныеИБ, ДанныеXDTO, КомпонентыОбмена, СтекВыгрузки)
	Если СтекВыгрузки.Количество() > 1 Тогда
		Возврат;
	КонецЕсли;
	
	ВыгрузитьПодразделениеИзРеквизитаДокумента(КомпонентыОбмена, ДанныеИБ, ДанныеXDTO, "БанковскийСчет");
	
	ЗаполнитьДанныеВходящегоДокументаСБДС(ДанныеИБ, ДанныеXDTO);
	ДанныеШапки = ДанныеИБ.ДополнительныеСвойства.ДанныеШапки;
	ДанныеXDTO.Вставить("ВидОперации", ДанныеШапки.ВидОперацииПрочее);
	ВыгрузитьДополнительныеРеквизитыИСведения(КомпонентыОбмена, ДанныеИБ, ДанныеXDTO);
	
	Если ВерсияФорматаЧислом(КомпонентыОбмена.ВерсияФорматаОбмена) >= ВерсияФорматаЧислом("1.7")
		И ЗначениеЗаполнено(ДанныеИБ.ЗаявкаНаРасходованиеДенежныхСредств) Тогда
		ДанныеXDTO.Вставить("ЗаявкаНаРасходДенежныхСредств", ДанныеИБ.ЗаявкаНаРасходованиеДенежныхСредств);
	КонецЕсли;
	
	ТабРасшифровка = Новый ТаблицаЗначений;
	ТабРасшифровка.Колонки.Добавить("СтатьяДДС");
	ТабРасшифровка.Колонки.Добавить("Сумма");
	ТабРасшифровка.Колонки.Добавить("СчетУчетаРасчетов");
	
	СчетРасчетов = "";
	
	Если ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.СнятиеНаличныхДенежныхСредств Тогда
		СчетРасчетов = "57.01";
	ИначеЕсли ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПеречислениеНаДепозиты Тогда
		Если ДанныеИБ.Валюта = Константы.ВалютаРегламентированногоУчета.Получить() Тогда
			СчетРасчетов = "55.03";
		Иначе
			СчетРасчетов = "55.23";
		КонецЕсли; 	
	
	ИначеЕсли ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыдачаЗаймаСотруднику Тогда
		СчетРасчетов = "73.01";
	КонецЕсли;
	
	Если ДанныеИБ.РасшифровкаПлатежа.Количество() > 0 Тогда
		Для Каждого Строка Из ДанныеИБ.РасшифровкаПлатежа Цикл
			
			НоваяСтрока = ТабРасшифровка.Добавить();
			НоваяСтрока.СтатьяДДС         = Строка.СтатьяДвиженияДенежныхСредств;
			НоваяСтрока.Сумма             = Строка.Сумма;
			// Выгружается документ с операцией "ПрочаяВыдачаДенежныхСредств",
			// в котором указаны только статьи активов/пассивов.
			Если ЗначениеЗаполнено(СчетРасчетов) Тогда
				НоваяСтрока.СчетУчетаРасчетов = СчетРасчетов;
			ИначеЕсли ЭтоУТ() Тогда
				НоваяСтрока.СчетУчетаРасчетов = СчетУчетаРасчетовПрочихОперацийДДС(Новый Структура("СтатьяДДС", Строка.СтатьяДвиженияДенежныхСредств));
	        КонецЕсли;
			
		КонецЦикла;
	Иначе
	
		НоваяСтрока = ТабРасшифровка.Добавить();
		НоваяСтрока.СтатьяДДС         = ДанныеИБ.СтатьяДвиженияДенежныхСредств;
		НоваяСтрока.Сумма             = ДанныеИБ.СуммаДокумента;
		Если ЗначениеЗаполнено(СчетРасчетов) Тогда
			НоваяСтрока.СчетУчетаРасчетов = СчетРасчетов;
		ИначеЕсли ЭтоУТ() Тогда
			НоваяСтрока.СчетУчетаРасчетов = СчетУчетаРасчетовПрочихОперацийДДС(Новый Структура("СтатьяДДС", ДанныеИБ.СтатьяДвиженияДенежныхСредств));
		КонецЕсли;
	КонецЕсли;
	
	ДанныеXDTO.Вставить("РасшифровкаПлатежа", ТабРасшифровка);
	
	Если ВерсияФорматаЧислом(КомпонентыОбмена.ВерсияФорматаОбмена) >= ВерсияФорматаЧислом("1.5")
		И ЗначениеЗаполнено(ДанныеИБ.КодВидаДохода) Тогда
		ДанныеXDTO.Вставить("КодВидаДохода", ДанныеИБ.КодВидаДохода);
	КонецЕсли;
КонецПроцедуры
