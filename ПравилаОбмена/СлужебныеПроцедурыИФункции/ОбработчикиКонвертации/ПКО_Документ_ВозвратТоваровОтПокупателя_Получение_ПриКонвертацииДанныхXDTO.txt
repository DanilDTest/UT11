Процедура ПКО_Документ_ВозвратТоваровОтПокупателя_Получение_ПриКонвертацииДанныхXDTO(ДанныеXDTO, ПолученныеДанные, КомпонентыОбмена)
	ДобавитьВалютуВДопСвойства(ПолученныеДанные, ДанныеXDTO);
	//ДобавитьДоговорВДопСвойства(ПолученныеДанные, ДанныеXDTO, КомпонентыОбмена);
	ЗагрузитьДополнительныеРеквизиты(ПолученныеДанные, ДанныеXDTO, КомпонентыОбмена);
	ПризнакНалогообложенияНДС(КомпонентыОбмена, ПолученныеДанные);
	
	ТипДокументаРеализации = Неопределено;
	Если ДанныеXDTO.Свойство("ДокументРеализации")
		И ТипЗнч(ДанныеXDTO.ДокументРеализации) = Тип("Структура")
		И ДанныеXDTO.ДокументРеализации.Свойство("ТипЗначения", ТипДокументаРеализации) Тогда
		
		ИмяПКО = "";
		Если ТипДокументаРеализации = "ОтчетОРозничныхПродажах" Тогда
			ИмяПКО = "Документ_ОтчетОРозничныхПродажах_Получение";
		ИначеЕсли ТипДокументаРеализации = "РеализацияТоваровУслуг" Тогда
			ИмяПКО = "Документ_РеализацияТоваровУслуг_Получение";
		КонецЕсли;
		
		Если НЕ ИмяПКО = "" Тогда
			Инструкция = Новый Структура("Значение, ИмяПКО", ДанныеXDTO.ДокументРеализации, ИмяПКО);
			ПолученныеДанные.ДополнительныеСвойства.Вставить("ДокументРеализации", Инструкция);
		КонецЕсли;
	КонецЕсли;	
		
	ПравилаЗаполнения = Новый Соответствие;
	ПравилаЗаполнения.Вставить("Номенклатура", "Номенклатура");
	ПравилаЗаполнения.Вставить("Количество",   "Количество");
	ПравилаЗаполнения.Вставить("ИдентификаторСтроки",     "ИдентификаторСтроки");
	ПравилаЗаполнения.Вставить("Сумма",        "Сумма");
	ПравилаЗаполнения.Вставить("Цена",         "Цена");
	ПравилаЗаполнения.Вставить("СтавкаНДС",    "СтавкаНДС");
	ПравилаЗаполнения.Вставить("СуммаНДС",     "СуммаНДС");
	Если ЗначениеФО("ИспользоватьХарактеристикиНоменклатуры", КомпонентыОбмена) Тогда
		ПравилаЗаполнения.Вставить("Характеристика",     "Характеристика");
	КонецЕсли;
	Если ЗначениеФО("ИспользоватьУпаковкиНоменклатуры", КомпонентыОбмена) Тогда
		ПравилаЗаполнения.Вставить("Упаковка",           "Упаковка");
		ПравилаЗаполнения.Вставить("КоличествоУпаковок", "КоличествоУпаковок");
	КонецЕсли;
	Если ЗначениеФО("ИспользоватьСерииНоменклатуры", КомпонентыОбмена) Тогда
		ПравилаЗаполнения.Вставить("Серия",              "Серия");
	КонецЕсли;
	
	// Товары 
	МассивСтрокТовары = Новый Массив;
	Если ДанныеXDTO.Свойство("Товары")
		И ЗначениеЗаполнено(ДанныеXDTO.Товары) Тогда
		
		Для Каждого Строка Из ДанныеXDTO.Товары Цикл
			СтруктураДанныхСтроки = ДанныеКоллекцииВВидеСтруктуры(Строка, ПравилаЗаполнения);
			
			Если ЗначениеЗаполнено(Строка.НомерГТД) Тогда
				СтруктураДанныхСтроки.Вставить("НомерГТД", Новый Структура("Код, СтранаПроисхождения", Строка.НомерГТД, Строка.СтранаПроисхождения));
			КонецЕсли;
			
			СуммаСНДС = Строка.Сумма;
			Если СтруктураДанныхСтроки.Свойство("СуммаНДС") И НЕ ПолученныеДанные.ЦенаВключаетНДС Тогда
				СуммаСНДС = СуммаСНДС + СтруктураДанныхСтроки.СуммаНДС
			КонецЕсли;
			
			СтруктураДанныхСтроки.Вставить("СуммаСНДС", СуммаСНДС);
			Если СтруктураДанныхСтроки.Свойство("КоличествоУпаковок") Тогда
			    Если СтруктураДанныхСтроки.Свойство("Упаковка") И СтруктураДанныхСтроки.Свойство("Сумма") Тогда
					СтруктураДанныхСтроки.Вставить("Цена", СтруктураДанныхСтроки.Сумма / СтруктураДанныхСтроки.КоличествоУпаковок);
				КонецЕсли;
			Иначе
				СтруктураДанныхСтроки.Вставить("КоличествоУпаковок", СтруктураДанныхСтроки.Количество);
			КонецЕсли;
			
			МассивСтрокТовары.Добавить(СтруктураДанныхСтроки);
		КонецЦикла;
		
	КонецЕсли;
	
	Если МассивСтрокТовары.Количество() > 0 Тогда
		ПолученныеДанные.ДополнительныеСвойства.Вставить("Товары", МассивСтрокТовары);
	КонецЕсли;
	
	//Серии
	МассивСерии = Новый Массив;
	
	ПравилаЗаполнения = Новый Соответствие;
	ПравилаЗаполнения.Вставить("Серия",  "Серия");
	ПравилаЗаполнения.Вставить("Количество",    "Количество");
	ПравилаЗаполнения.Вставить("Номенклатура",         "Номенклатура");
	
	Если ДанныеXDTO.Свойство("Серии")
		И ЗначениеЗаполнено(ДанныеXDTO.Серии) Тогда
		
		Для каждого Строка Из ДанныеXDTO.Серии Цикл
		
		СтруктураДанныхСтроки = ДанныеКоллекцииВВидеСтруктуры(Строка, ПравилаЗаполнения);
			
		МассивСерии.Добавить(СтруктураДанныхСтроки);
		КонецЦикла;
		
		ПолученныеДанные.ДополнительныеСвойства.Вставить("Серии", МассивСерии);	
	КонецЕсли;
	
	
	
	// Штрихкоды упаковок.
	ЗагрузитьШтрихкодыУпаковок(ДанныеXDTO, ПолученныеДанные, КомпонентыОбмена);
КонецПроцедуры
