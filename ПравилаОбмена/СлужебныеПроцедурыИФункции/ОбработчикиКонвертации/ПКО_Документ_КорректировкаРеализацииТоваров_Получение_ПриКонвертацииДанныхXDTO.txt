Процедура ПКО_Документ_КорректировкаРеализацииТоваров_Получение_ПриКонвертацииДанныхXDTO(ДанныеXDTO, ПолученныеДанные, КомпонентыОбмена)
	ПолученныеДанные.Проведен    = Истина;
	ПолученныеДанные.Согласован  = Истина;
	ПолученныеДанные.ДатаПлатежа = ПолученныеДанные.Дата;
	
	ДобавитьВалютуВДопСвойства(ПолученныеДанные, ДанныеXDTO);
	ДобавитьДоговорВДопСвойства(ПолученныеДанные, ДанныеXDTO, КомпонентыОбмена);
	
	ДокументРеализацииДанные = Неопределено;
	ИмяПКО = "";
	ДанныеXDTO.Свойство("ИсправляемыйДокументРеализации", ДокументРеализацииДанные);
	Если ДокументРеализацииДанные = Неопределено Тогда
		ДанныеXDTO.Свойство("ДокументРеализации", ДокументРеализацииДанные);
	КонецЕсли;
	Если ДокументРеализацииДанные <> Неопределено И ТипЗнч(ДокументРеализацииДанные) = Тип("Структура") Тогда
		ТипЗначенияДокумент = ДокументРеализацииДанные.ТипЗначения;
		Если Найти(ТипЗначенияДокумент, "РеализацияТоваровУслуг") Тогда
			ИмяПКО = "Документ_РеализацияТоваровУслуг_Получение";
		КонецЕсли; 
	КонецЕсли;
	Если ИмяПКО <> "" Тогда
		ПолученныеДанные.ДополнительныеСвойства.Вставить("ДокументОснование", Новый Структура("Значение, ИмяПКО",ДокументРеализацииДанные,ИмяПКО));
	КонецЕсли;
	Если ДанныеXDTO.ВидОперации = "ИсправлениеОшибки" Тогда
		ПолученныеДанные.ВидКорректировки = Перечисления.ХозяйственныеОперации.ИсправлениеОшибок;
	Иначе
		ПолученныеДанные.ВидКорректировки = Перечисления.ХозяйственныеОперации.КорректировкаПоСогласованиюСторон;
	КонецЕсли;
	
	#Область Товары
	
	ПравилаЗаполнения = Новый Соответствие;
	ПравилаЗаполнения.Вставить("Номенклатура", "Номенклатура");
	ПравилаЗаполнения.Вставить("Количество",   "Количество");
	ПравилаЗаполнения.Вставить("Сумма",        "Сумма");
	ПравилаЗаполнения.Вставить("Цена",         "Цена");
	ПравилаЗаполнения.Вставить("СтавкаНДС",    "СтавкаНДС");
	ПравилаЗаполнения.Вставить("СуммаНДС",     "СуммаНДС");
	Если ЗначениеФО("ИспользоватьХарактеристикиНоменклатуры", КомпонентыОбмена) Тогда
		ПравилаЗаполнения.Вставить("Характеристика",     "Характеристика");
	КонецЕсли;
	Если ЗначениеФО("ИспользоватьСерииНоменклатуры", КомпонентыОбмена) Тогда
		ПравилаЗаполнения.Вставить("Серия",              "Серия");
	КонецЕсли;
	
	МассивСтрокТовары = Новый Массив;
	Если ДанныеXDTO.Свойство("Товары")
		И ЗначениеЗаполнено(ДанныеXDTO.Товары) Тогда
		
		Для Каждого Строка Из ДанныеXDTO.Товары Цикл
			СтруктураДанныхСтроки = ДанныеКоллекцииВВидеСтруктуры(Строка, ПравилаЗаполнения);
			Если СтруктураДанныхСтроки.Свойство("Количество") И СтруктураДанныхСтроки.Количество <= 0 Тогда
				Продолжить;
			КонецЕсли;
			СуммаСНДС = Строка.Сумма;
			Если СтруктураДанныхСтроки.Свойство("СуммаНДС") И НЕ ПолученныеДанные.ЦенаВключаетНДС Тогда
				СуммаСНДС = СуммаСНДС + СтруктураДанныхСтроки.СуммаНДС;
			КонецЕсли;
			
			СтруктураДанныхСтроки.Вставить("СуммаСНДС", СуммаСНДС);
			СтруктураДанныхСтроки.Вставить("КоличествоУпаковок",Строка.Количество);
			ОбработатьСтавкуНДСПриПолучении(Строка, СтруктураДанныхСтроки, ПолученныеДанные, КомпонентыОбмена.ПараметрыКонвертации);
			СтруктураДанныхСтроки.Вставить("СуммаВзаиморасчетов", Строка.Сумма);
			Если СтруктураДанныхСтроки.Свойство("СуммаНДС") Тогда
				СтруктураДанныхСтроки.Вставить("СуммаНДСВзаиморасчетов", СтруктураДанныхСтроки.СуммаНДС);
			КонецЕсли;
			СтруктураДанныхСтроки.Вставить("Склад", ПолученныеДанные.Склад);
	
			МассивСтрокТовары.Добавить(СтруктураДанныхСтроки);
		КонецЦикла;
	КонецЕсли;
	
	// Услуги добавляем в товары.
	Если ДанныеXDTO.Свойство("Услуги")
		И ЗначениеЗаполнено(ДанныеXDTO.Услуги) Тогда
		
		Для Каждого Строка Из ДанныеXDTO.Услуги Цикл
			СтруктураДанныхСтроки = ДанныеКоллекцииВВидеСтруктуры(Строка, ПравилаЗаполнения);
			Если СтруктураДанныхСтроки.Свойство("Количество") И СтруктураДанныхСтроки.Количество <= 0 Тогда
				Продолжить;
			КонецЕсли;
			СуммаСНДС = Строка.Сумма;
			Если СтруктураДанныхСтроки.Свойство("СуммаНДС") И НЕ ПолученныеДанные.ЦенаВключаетНДС Тогда
				СуммаСНДС = СуммаСНДС + СтруктураДанныхСтроки.СуммаНДС
			КонецЕсли;
			
			СтруктураДанныхСтроки.Вставить("СуммаСНДС", СуммаСНДС);
			ОбработатьСтавкуНДСПриПолучении(Строка, СтруктураДанныхСтроки, ПолученныеДанные, КомпонентыОбмена.ПараметрыКонвертации);
			Если ЗначениеЗаполнено(Строка.Содержание) Тогда
				СтруктураДанныхСтроки.Вставить("Содержание", Строка.Содержание);
			КонецЕсли;
			СтруктураДанныхСтроки.Вставить("СуммаВзаиморасчетов", Строка.Сумма);
			Если СтруктураДанныхСтроки.Свойство("СуммаНДС") Тогда
				СтруктураДанныхСтроки.Вставить("СуммаНДСВзаиморасчетов", СтруктураДанныхСтроки.СуммаНДС);
			КонецЕсли;
	
			МассивСтрокТовары.Добавить(СтруктураДанныхСтроки);
		КонецЦикла;
	КонецЕсли;
	
	Если МассивСтрокТовары.Количество() > 0 Тогда
		ПолученныеДанные.ДополнительныеСвойства.Вставить("Товары", МассивСтрокТовары);
	КонецЕсли;
	
	#КонецОбласти
КонецПроцедуры
