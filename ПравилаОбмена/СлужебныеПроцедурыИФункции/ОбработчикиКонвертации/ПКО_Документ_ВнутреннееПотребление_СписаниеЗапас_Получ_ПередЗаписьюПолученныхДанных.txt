Процедура ПКО_Документ_ВнутреннееПотребление_СписаниеЗапас_Получ_ПередЗаписьюПолученныхДанных(ПолученныеДанные, ДанныеИБ, КонвертацияСвойств, КомпонентыОбмена)
	ЗагрузитьДополнительныеСведения(?(ДанныеИБ = Неопределено, ПолученныеДанные.ПолучитьСсылкуНового(), ДанныеИБ.Ссылка), 
		                            ПолученныеДанные.ДополнительныеСвойства, КомпонентыОбмена);
	
	ЗаполнитьСкладПоУмолчанию(ПолученныеДанные, ДанныеИБ, КомпонентыОбмена);
	ЗаполнитьПодразделениеПоУмолчанию(ПолученныеДанные, ДанныеИБ, КомпонентыОбмена);
	
	Если НЕ ДанныеИБ = Неопределено Тогда
		// Переносим те свойства, которые указаны в ПКС
		ЗаполнитьСвойстваШапкиОбъекта(КонвертацияСвойств, ПолученныеДанные, ДанныеИБ);
	
		МассивКлючевыхПолей = Новый Массив;
		МассивКлючевыхПолей.Добавить("Номенклатура");
		МассивКлючевыхПолей.Добавить("Характеристика");
		МассивКлючевыхПолей.Добавить("Серия");
		RM_ОбменДаннымиXDTOСервер.ЗаполнитьТабличнуюЧастьОбъектаНачальнымиДанными(
			ПолученныеДанные.Товары,
			ДанныеИБ.Товары,
			МассивКлючевыхПолей,
			Неопределено,
			"Количество, Упаковка, КоличествоУпаковок");  
		ДанныеИБ.Товары.Загрузить(ПолученныеДанные.Товары.Выгрузить());
		ПолученныеДанные = Неопределено;
	КонецЕсли;
	
	ДанныеДляЗаписиВИБ = ?(ДанныеИБ = Неопределено, ПолученныеДанные, ДанныеИБ);
	
	Если НЕ ЗначениеЗаполнено(ДанныеДляЗаписиВИБ.ДатаРаспоряжения) Тогда
		ДанныеДляЗаписиВИБ.ДатаРаспоряжения = ДанныеДляЗаписиВИБ.Дата;
	КонецЕсли;
	
	Для Каждого Строка Из ДанныеДляЗаписиВИБ.Товары Цикл
		Если Не ЗначениеЗаполнено(Строка.ИдентификаторСтроки) Тогда
			Строка.ИдентификаторСтроки = Новый УникальныйИдентификатор;
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(Строка.СтатьяРасходов) Тогда
			Строка.СтатьяРасходов = ЭлементПоУмолчанию(КомпонентыОбмена, "СтатьяРасходовСписаниеТоваров");
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры
