Процедура ПКО_Документ_ВозвратПС_Получение_ПриКонвертацииДанныхXDTO(ДанныеXDTO, ПолученныеДанные, КомпонентыОбмена)
	ПолученныеДанные.Статус   = Перечисления.СтатусыЧековККМ.Пробит;
	
	ДобавитьВалютуВДопСвойства(ПолученныеДанные, ДанныеXDTO);
	ЗагрузитьДополнительныеРеквизиты(ПолученныеДанные, ДанныеXDTO, КомпонентыОбмена);
	
	ОтчетОРозничныхПродажах = Неопределено;
	Если ДанныеXDTO.Свойство("ОтчетОРозничныхПродажах") Тогда
		ОтчетОРозничныхПродажах = RM_ОбменДаннымиXDTOСервер.СтруктураОбъектаXDTOВДанныеИБ(
			КомпонентыОбмена,
			ДанныеXDTO.ОтчетОРозничныхПродажах,
			RM_ОбменДаннымиXDTOСервер.ПКОПоИмени(КомпонентыОбмена, "Документ_ОтчетОРозничныхПродажах_Получение"), 
			"ПолучитьСсылку");
	КонецЕсли;
	ПолученныеДанные.ДополнительныеСвойства.Вставить("ОтчетОРозничныхПродажах", ОтчетОРозничныхПродажах);
	
	Склад = Справочники.Склады.ПустаяСсылка();
	Если ДанныеXDTO.Свойство("Склад") Тогда
		Склад = RM_ОбменДаннымиXDTOСервер.СтруктураОбъектаXDTOВДанныеИБ(
			КомпонентыОбмена,
			ДанныеXDTO.Склад,
			RM_ОбменДаннымиXDTOСервер.ПКОПоИмени(КомпонентыОбмена, "Справочник_Склады_Получение"), 
			"ПолучитьСсылку");
	КонецЕсли;
	ПолученныеДанные.ДополнительныеСвойства.Вставить("Склад", Склад);
	
	// ПодарочныеСертификаты
	МассивСтрокПС = Новый Массив;
	Если ДанныеXDTO.Свойство("ПодарочныеСертификаты")
		И ЗначениеЗаполнено(ДанныеXDTO.ПодарочныеСертификаты) Тогда
		
		Для Каждого Строка Из ДанныеXDTO.ПодарочныеСертификаты Цикл  
		
			СтруктураДанныхСтроки = Новый Структура;
			СтруктураДанныхСтроки.Вставить("Сумма", Строка.Сумма);
			СтруктураДанныхСтроки.Вставить("ПодарочныйСертификат", Строка.СертификатСсылка);
							
			МассивСтрокПС.Добавить(СтруктураДанныхСтроки);
			
		КонецЦикла;
	КонецЕсли;
	ПолученныеДанные.ДополнительныеСвойства.Вставить("ПодарочныеСертификаты", МассивСтрокПС);
	
	//Оплата платежными картами
	МассивСтрокОплатаПК = Новый Массив;
	Если ДанныеXDTO.Свойство("ОплатаПлатежнымиКартами")
		И ЗначениеЗаполнено(ДанныеXDTO.ОплатаПлатежнымиКартами) Тогда
		
		Для Каждого Строка Из ДанныеXDTO.ОплатаПлатежнымиКартами Цикл
			СтруктураДанныхСтроки = Новый Структура;
			СтруктураДанныхСтроки.Вставить("Сумма", Строка.СуммаОплаты);
			СтруктураДанныхСтроки.Вставить("НомерПлатежнойКарты", Строка.НомерКарты);
		 
			СтруктураЭТ = ?(ТипЗнч(Строка.ЭквайринговыйТерминал) = Тип("Структура"), Строка.ЭквайринговыйТерминал, Новый Структура("Номер, Ссылка"));
			СтруктураЭТ.Вставить("БанковскийСчетОрганизации", Строка.БанковскийСчетОрганизации);
			СтруктураЭТ.Вставить("Эквайрер", Строка.Эквайрер);
			
			// Готовим инструкцию для конвертации Эквайрингового терминала.
			Инструкция = Новый Структура;
			Инструкция.Вставить("Значение", СтруктураЭТ);
			
			Если ЗначениеЗаполнено(СтруктураЭТ.Номер) Тогда
				Инструкция.Вставить("ИмяПКО", "Справочник_ЭквайринговыеТерминалы_Получение");
			ИначеЕсли ЗначениеЗаполнено(СтруктураЭТ.БанковскийСчетОрганизации) И ЗначениеЗаполнено(СтруктураЭТ.Эквайрер) Тогда
				Инструкция.Вставить("ИмяПКО", "Справочник_ЭквайрингТермБезНомера_Получение_КлючСв");
			Иначе
				Инструкция = Неопределено;
			КонецЕсли;
			
			СтруктураДанныхСтроки.Вставить("ЭквайринговыйТерминал", Инструкция);
			
			МассивСтрокОплатаПК.Добавить(СтруктураДанныхСтроки);
		
		КонецЦикла;
		
		ПолученныеДанные.ДополнительныеСвойства.Вставить("ОплатаПлатежнымиКартами", МассивСтрокОплатаПК);	
	КонецЕсли;
КонецПроцедуры
