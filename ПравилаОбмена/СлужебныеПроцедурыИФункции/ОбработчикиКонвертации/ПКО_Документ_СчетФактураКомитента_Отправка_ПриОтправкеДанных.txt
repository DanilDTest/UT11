Процедура ПКО_Документ_СчетФактураКомитента_Отправка_ПриОтправкеДанных(ДанныеИБ, ДанныеXDTO, КомпонентыОбмена, СтекВыгрузки)
	Если СтекВыгрузки.Количество() > 1 Тогда
		Возврат;
	КонецЕсли;
	ДанныеXDTO.Вставить("ВидСчетаФактуры", "НаПоступление");
	Если ДанныеИБ.ПолученВЭлектронномВиде Тогда
		ДанныеXDTO.Вставить("СпособВыставления", "ВЭлектронномВиде");
	Иначе
		ДанныеXDTO.Вставить("СпособВыставления", "НаБумажномНосителе");
	КонецЕсли;
	
	Если ВерсияФорматаЧислом(КомпонентыОбмена.ВерсияФорматаОбмена) >= ВерсияФорматаЧислом("1.7") Тогда
		ДанныеXDTO.Вставить("ИННКонтрагента", ДанныеИБ.ИННКомитента);
		ДанныеXDTO.Вставить("КППКонтрагента", ДанныеИБ.КППКомитента);
	КонецЕсли;
	
	ВыгрузитьДополнительныеРеквизитыИСведения(КомпонентыОбмена, ДанныеИБ, ДанныеXDTO);
	
	// Сведения о счетах фактурах выставленных
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ 
		|	Субкомиссионер КАК СубкомиссионерСсылка,
		|	Покупатель КАК Покупатель,
		|	СчетФактураВыданный КАК СчетФактураВыданный,
		|	СуммаСНДС КАК Сумма,
		|	СуммаНДС КАК СуммаНДС
		|ИЗ Документ.СчетФактураКомитента.Покупатели
		|ГДЕ Ссылка = &Ссылка
		|";
	
	Запрос.УстановитьПараметр("Ссылка", ДанныеИБ.Ссылка);
	СчетаФактурыВыданные = Запрос.Выполнить().Выгрузить();
	СчетаФактурыВыданные.Колонки.Добавить("Субкомиссионер");
	Для Каждого СтрокаСФ Из СчетаФактурыВыданные Цикл
		Если ЗначениеЗаполнено(СтрокаСФ.СубкомиссионерСсылка) Тогда
			Если ТипЗнч(СтрокаСФ.СубкомиссионерСсылка) = Тип("СправочникСсылка.Контрагенты") Тогда
				СтрокаСФ.Субкомиссионер = СтрокаСФ.СубкомиссионерСсылка;
			ИначеЕсли ТипЗнч(СтрокаСФ.СубкомиссионерСсылка) = Тип("СправочникСсылка.Организации") Тогда
				СтрокаСФ.Субкомиссионер = КонтрагентИзОрганизации(СтрокаСФ.СубкомиссионерСсылка, КомпонентыОбмена);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла; 
	ДанныеXDTO.Вставить("СчетаФактурыВыданныеПокупателям", СчетаФактурыВыданные);
	
	// Счет фактура выставленный в шапке
	ДанныеXDTO.Вставить("ДанныеРеализацииСубкомиссионер", Неопределено);
	ДанныеXDTO.Вставить("ДанныеРеализацииКонечныйПокупатель", Неопределено);
	ДанныеXDTO.Вставить("ДанныеРеализацииСчетФактураВыставленный", Неопределено);
	Если СчетаФактурыВыданные.Количество() > 0 Тогда
		ДанныеXDTO.ДанныеРеализацииСубкомиссионер = СчетаФактурыВыданные[0].Субкомиссионер;
		ДанныеXDTO.ДанныеРеализацииКонечныйПокупатель = СчетаФактурыВыданные[0].Покупатель;
		ДанныеXDTO.ДанныеРеализацииСчетФактураВыставленный = СчетаФактурыВыданные[0].СчетФактураВыданный;
	КонецЕсли;
	
	// Документы основания
	МассивСФ = СчетаФактурыВыданные.ВыгрузитьКолонку("СчетФактураВыданный");
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Ссылка КАК ДокументОснование,
		|	Ссылка.Договор КАК Договор
		|ИЗ Документ.ОтчетПоКомиссииМеждуОрганизациями.Товары
		|	ГДЕ СчетФактураВыставленныйКомиссионера В (&МассивСФ)
		|	И Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар)
		|	И Ссылка.СуммаДокумента > 0
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Ссылка КАК ДокументОснование,
		|	Ссылка.Договор КАК Договор
		|ИЗ Документ.ОтчетКомитенту.Товары
		|	ГДЕ СчетФактураВыставленный В (&МассивСФ)
		|	И Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар)
		|	И Ссылка.СуммаДокумента > 0
		|";
	
	Запрос.УстановитьПараметр("МассивСФ", МассивСФ);
	ВыборкаТЧ = Запрос.Выполнить().Выбрать();
	
	ДокументыОснования = Новый ТаблицаЗначений();
	ДокументыОснования.Колонки.Добавить("ДокументОснование");
	
	Пока ВыборкаТЧ.Следующий() Цикл
		ДокументОснование = ВыборкаТЧ.ДокументОснование;
		Если НЕ ЗначениеЗаполнено(ДокументОснование) Тогда
			Продолжить;
		КонецЕсли;
	
		Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ОтчетКомитенту") Тогда
			ИмяПКО = "Документ_ОтчетКомитенту_Отправка";
		Иначе
			ИмяПКО = "ОтчетКомиссияМеждуОрганизациями_Комитент_Отправка";
		КонецЕсли;
		СтрокаОснование = ДокументыОснования.Добавить();
		СтрокаОснование.ДокументОснование = Новый Структура("Значение, ИмяПКО",ДокументОснование,ИмяПКО);
		
		Договор = ВыборкаТЧ.Договор;
		Если ЗначениеЗаполнено(Договор) Тогда
			Если ТипЗнч(Договор) = Тип("СправочникСсылка.ДоговорыМеждуОрганизациями") Тогда
				ДанныеXDTO.Вставить("Договор", Неопределено);
			Иначе
				ДанныеXDTO.Вставить("Договор", Новый Структура("Значение, ИмяПКО", Договор, "Справочник_ДоговорыКонтрагентов"));
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	ДанныеXDTO.Вставить("ДокументыОснования", ДокументыОснования);
	Если НЕ ДанныеXDTO.Свойство("Договор") Тогда
		ПараметрыПоУмолчанию = Новый Структура(ДоговорФиксированнаяСтруктураКлючей());
		ПараметрыПоУмолчанию.Контрагент = ДанныеИБ.Комитент;
		ПараметрыПоУмолчанию.Организация = ДанныеИБ.Организация;
		ПараметрыПоУмолчанию.ВалютаВзаиморасчетов = ДанныеИБ.Валюта;
		ПараметрыПоУмолчанию.ВидДоговора = "СКомитентом";
		ПараметрыПоУмолчанию.РасчетыВУсловныхЕдиницах = Ложь;
		
		Договор = ДоговорИнструкцияКонвертацииПоДаннымВзаиморасчетов(ПараметрыПоУмолчанию, КомпонентыОбмена);
		ДанныеXDTO.Вставить("Договор", Договор);
	КонецЕсли;
КонецПроцедуры
