Процедура ПКО_Документ_РКОПрочаяВыдача_Отправка_ПриОтправкеДанных(ДанныеИБ, ДанныеXDTO, КомпонентыОбмена, СтекВыгрузки)
	Если СтекВыгрузки.Количество() > 1 Тогда
		Возврат;
	КонецЕсли;
	
	ВыгрузитьПодразделениеИзРеквизитаДокумента(КомпонентыОбмена, ДанныеИБ, ДанныеXDTO, "Касса");
	ВыгрузитьОтветственноеЛицо(КомпонентыОбмена, ДанныеИБ, ДанныеXDTO, "ГлавныйБухгалтер");
	ВыгрузитьОтветственноеЛицо(КомпонентыОбмена, ДанныеИБ, ДанныеXDTO, "Руководитель");
	ВыгрузитьДополнительныеРеквизитыИСведения(КомпонентыОбмена, ДанныеИБ, ДанныеXDTO);
	
	Если ВерсияФорматаЧислом(КомпонентыОбмена.ВерсияФорматаОбмена) >= ВерсияФорматаЧислом("1.7")
		И ЗначениеЗаполнено(ДанныеИБ.ЗаявкаНаРасходованиеДенежныхСредств) Тогда
		ДанныеXDTO.Вставить("ЗаявкаНаРасходДенежныхСредств", ДанныеИБ.ЗаявкаНаРасходованиеДенежныхСредств);
	КонецЕсли;
	
	ТабРасшифровка = Новый ТаблицаЗначений;
	ТабРасшифровка.Колонки.Добавить("СтатьяДДС");
	ТабРасшифровка.Колонки.Добавить("Сумма");
	ТабРасшифровка.Колонки.Добавить("СчетУчетаРасчетов");
	
	Если ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ИнкассацияДенежныхСредствВБанк
		ИЛИ ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыдачаДенежныхСредствВКассуККМ Тогда
		
		// Документ с операцией "Выдача денежных средств в кассу ККМ" выгружается как прочая выдача.
		// Табличная часть "РасшифровкаПлатежа" для данной операции не заполняется,
		// поэтому выгружаем с одной строчкой и предустановленным счетом учета.
		НоваяСтрока = ТабРасшифровка.Добавить();
		НоваяСтрока.СтатьяДДС         = ДанныеИБ.СтатьяДвиженияДенежныхСредств;
		НоваяСтрока.Сумма             = ДанныеИБ.СуммаДокумента;
		Если ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыдачаДенежныхСредствВКассуККМ Тогда
			НоваяСтрока.СчетУчетаРасчетов = "50.02";
		Иначе
			НоваяСтрока.СчетУчетаРасчетов = "57.01";
		КонецЕсли;
	Иначе
		// Выгружается документ с операцией "Прочая выдача денежных средств", 
		// в табличной части "РасшифровкаПлатежа" которого, не указано ни одной статьи расходов.
		Если ДанныеИБ.РасшифровкаПлатежа.Количество() > 0 Тогда
			Для Каждого Строка Из ДанныеИБ.РасшифровкаПлатежа Цикл
				Если ЭтоУТ() Тогда
					СчетРасчетов = СчетУчетаРасчетовПрочихОперацийДДС(Новый Структура("СтатьяДДС", Строка.СтатьяДвиженияДенежныхСредств));
				КонецЕсли;
				
				НоваяСтрока = ТабРасшифровка.Добавить();
				НоваяСтрока.СтатьяДДС         = Строка.СтатьяДвиженияДенежныхСредств;
				НоваяСтрока.Сумма             = Строка.Сумма;
				НоваяСтрока.СчетУчетаРасчетов = СчетРасчетов;
				
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	ДанныеXDTO.Вставить("РасшифровкаПлатежа", ТабРасшифровка);
КонецПроцедуры
