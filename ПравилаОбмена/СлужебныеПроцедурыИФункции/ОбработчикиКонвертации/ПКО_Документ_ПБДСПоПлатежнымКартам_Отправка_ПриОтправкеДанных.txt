Процедура ПКО_Документ_ПБДСПоПлатежнымКартам_Отправка_ПриОтправкеДанных(ДанныеИБ, ДанныеXDTO, КомпонентыОбмена, СтекВыгрузки)
	Если СтекВыгрузки.Количество() > 1 Тогда
		Возврат;
	КонецЕсли;	
	
	Если СокрЛП(ДанныеИБ.НомерВходящегоДокумента) = "-" Тогда
		// Символ "-" проставляем искусственно, если из БП загружен документ с пустым номером.
		ДанныеXDTO.НомерВходящегоДокумента = "";
	КонецЕсли;
	
	ВыгрузитьПодразделениеИзРеквизитаДокумента(КомпонентыОбмена, ДанныеИБ, ДанныеXDTO, "БанковскийСчет");
	ВыгрузитьДополнительныеРеквизитыИСведения(КомпонентыОбмена, ДанныеИБ, ДанныеXDTO, Истина);
	
	ДанныеШапки = ДанныеИБ.ДополнительныеСвойства.ДанныеШапки;
	ВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();
	ТабРП = Новый ТаблицаЗначений;
	ТабРП.Колонки.Добавить("Договор");
	ТабРП.Колонки.Добавить("СтатьяДДС");
	ТабРП.Колонки.Добавить("Сумма");
	ТабРП.Колонки.Добавить("СуммаВзаиморасчетов");
	ТабРП.Колонки.Добавить("СуммаНДС");
	ТабРП.Колонки.Добавить("КурсВзаиморасчетов");
	ТабРП.Колонки.Добавить("КратностьВзаиморасчетов");
	ТабРП.Колонки.Добавить("СтавкаНДС");
	// Однострочная расшифровка платежа
	СтрокаРП = ТабРП.Добавить();
	СтрокаРП.СтатьяДДС = ДанныеИБ.СтатьяДвиженияДенежныхСредств;
	СтрокаРП.Сумма = ДанныеИБ.СуммаДокумента;
	СтрокаРП.КурсВзаиморасчетов = ДанныеШапки.Валюта_Курс;
	СтрокаРП.КратностьВзаиморасчетов = ДанныеШапки.Валюта_Кратность;
	СтрокаРП.СуммаВзаиморасчетов = ДанныеИБ.СуммаДокумента;
	
	ПараметрыПоУмолчанию = Новый Структура(ДоговорФиксированнаяСтруктураКлючей());
	ПараметрыПоУмолчанию.Контрагент = ДанныеИБ.Контрагент;
	ПараметрыПоУмолчанию.Организация = ДанныеИБ.Организация;
	ПараметрыПоУмолчанию.ВалютаВзаиморасчетов = ДанныеИБ.Валюта;
	ПараметрыПоУмолчанию.ВидДоговора = "Прочее";
	ПараметрыПоУмолчанию.Дата = ДанныеИБ.Дата;
	ПараметрыПоУмолчанию.Номер = ДанныеИБ.Номер;
	ПараметрыПоУмолчанию.РасчетыВУсловныхЕдиницах = Ложь;
	
	СтрокаРП.Договор = ДоговорИнструкцияКонвертацииПоДаннымВзаиморасчетов(ПараметрыПоУмолчанию, КомпонентыОбмена);
	ДанныеXDTO.Вставить("РасшифровкаПлатежа", ТабРП);
КонецПроцедуры
