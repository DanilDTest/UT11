Процедура ПКО_Документ_СборкаТоваров_Получение_ПриКонвертацииДанныхXDTO(ДанныеXDTO, ПолученныеДанные, КомпонентыОбмена)
	ЗагрузитьДополнительныеРеквизиты(ПолученныеДанные, ДанныеXDTO, КомпонентыОбмена);   
	ПолученныеДанные.Проведен = ДанныеXDTO.Проведен;
	
	Если ДанныеXDTO.Свойство("Характеристика") И ЗначениеЗаполнено(ДанныеXDTO.Характеристика)
		И ЗначениеФО("ИспользоватьХарактеристикиНоменклатуры", КомпонентыОбмена) Тогда
		ПолученныеДанные.ДополнительныеСвойства.Вставить("Характеристика", ДанныеXDTO.Характеристика);
	КонецЕсли;
	Если ДанныеXDTO.Свойство("Упаковка") И ЗначениеЗаполнено(ДанныеXDTO.Упаковка)
		И ЗначениеФО("ИспользоватьУпаковкиНоменклатуры", КомпонентыОбмена) Тогда
		ПолученныеДанные.ДополнительныеСвойства.Вставить("Упаковка", ДанныеXDTO.Упаковка);
		
		Если ДанныеXDTO.Свойство("КоличествоУпаковок") И ЗначениеЗаполнено(ДанныеXDTO.КоличествоУпаковок) Тогда
			ПолученныеДанные.КоличествоУпаковок = ДанныеXDTO.КоличествоУпаковок;
		КонецЕсли;	
	КонецЕсли;	
	
	#Область Товары
	
	ПравилаЗаполнения = Новый Соответствие;
	ПравилаЗаполнения.Вставить("Номенклатура",        	"Номенклатура");
	ПравилаЗаполнения.Вставить("Количество",          	"Количество");
	ПравилаЗаполнения.Вставить("ДоляСтоимости",       	"ДоляСтоимости");   
	ПравилаЗаполнения.Вставить("КлючСвязи",				"КодСтроки");
	
	Если ЗначениеФО("ИспользоватьХарактеристикиНоменклатуры", КомпонентыОбмена) Тогда
		ПравилаЗаполнения.Вставить("Характеристика",     "Характеристика");
	КонецЕсли;
	Если ЗначениеФО("ИспользоватьУпаковкиНоменклатуры", КомпонентыОбмена) Тогда
		ПравилаЗаполнения.Вставить("Упаковка",           "Упаковка");
		ПравилаЗаполнения.Вставить("КоличествоУпаковок", "КоличествоУпаковок");
	КонецЕсли;
	Если ЗначениеФО("ИспользоватьСерииНоменклатуры", КомпонентыОбмена) Тогда
		ПравилаЗаполнения.Вставить("Серия",              "Серия");
	КонецЕсли;
	
	МассивСтрокТовары = Новый Массив;           
	Если  ДанныеXDTO.Свойство("Товары") Тогда
		Для Каждого Строка Из ДанныеXDTO.Товары Цикл
		
			СтруктураДанныхСтроки = ДанныеКоллекцииВВидеСтруктуры(Строка, ПравилаЗаполнения);
		
			Если НЕ (СтруктураДанныхСтроки.Свойство("КоличествоУпаковок") И СтруктураДанныхСтроки.КоличествоУпаковок <> 0) Тогда
				СтруктураДанныхСтроки.Вставить("КоличествоУпаковок", СтруктураДанныхСтроки.Количество);
			КонецЕсли;
		
			МассивСтрокТовары.Добавить(СтруктураДанныхСтроки);
		
		КонецЦикла;
	КонецЕсли;
	
	ПолученныеДанные.ДополнительныеСвойства.Вставить("Товары", МассивСтрокТовары);
	
	#КонецОбласти  
	
	
	#Область Серии
	
	ПравилаЗаполнения = Новый Соответствие;
	ПравилаЗаполнения.Вставить("Номенклатура",        	"Номенклатура");
	ПравилаЗаполнения.Вставить("Количество",          	"Количество");
	ПравилаЗаполнения.Вставить("Характеристика",        "Характеристика");
	ПравилаЗаполнения.Вставить("Серия",          		"Серия");
	
	МассивСтрокСерии = Новый Массив;
	Если  ДанныеXDTO.Свойство("Серии") Тогда
		Для Каждого Строка Из ДанныеXDTO.Серии Цикл
		
			СтруктураДанныхСтроки = ДанныеКоллекцииВВидеСтруктуры(Строка, ПравилаЗаполнения);	
			МассивСтрокСерии.Добавить(СтруктураДанныхСтроки);
		
		КонецЦикла;
	КонецЕсли;
	
	
	ПолученныеДанные.ДополнительныеСвойства.Вставить("Серии", МассивСтрокСерии);
	
	#КонецОбласти      
	
	#Область ШтрихкодыУпаковок
	
	Если ДанныеXDTO.Свойство("АкцизныеМарки") Тогда
		ПравилаЗаполнения = Новый Соответствие;
		ПравилаЗаполнения.Вставить("ШтрихкодУпаковки", "ШтрихкодУпаковки");
	
		МассивСтрокШтрихкодыУпаковок = Новый Массив;
		Для Каждого Строка Из ДанныеXDTO.АкцизныеМарки Цикл
			СтруктураДанныхСтроки = ДанныеКоллекцииВВидеСтруктуры(Строка, ПравилаЗаполнения);
			МассивСтрокШтрихкодыУпаковок.Добавить(СтруктураДанныхСтроки);
		КонецЦикла;  
		
		ПолученныеДанные.ДополнительныеСвойства.Вставить("RM_ШтрихкодыУпаковок", МассивСтрокШтрихкодыУпаковок);
	КонецЕсли;
	#КонецОбласти
КонецПроцедуры
