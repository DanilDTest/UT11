Процедура ПКО_Документ_СчетФактураПолученный_Получение_ПриКонвертацииДанныхXDTO(ДанныеXDTO, ПолученныеДанные, КомпонентыОбмена)
	ПолученныеДанные.Проведен = Истина;
	ДобавитьДоговорВДопСвойства(ПолученныеДанные, ДанныеXDTO, КомпонентыОбмена);
	
	Если НЕ ЗначениеЗаполнено(ПолученныеДанные.КодВидаОперации) Тогда
		ПолученныеДанные.КодВидаОперации = "01";
	КонецЕсли;
	
	Если ДанныеXDTO.Свойство("ВидСчетаФактуры") И ЗначениеЗаполнено(ДанныеXDTO.ВидСчетаФактуры) Тогда
	    ПолученныеДанные.Корректировочный = (ДанныеXDTO.ВидСчетаФактуры.Значение = "Корректировочный");
	КонецЕсли;
	
	Если ДанныеXDTO.Свойство("СпособВыставления") И ЗначениеЗаполнено(ДанныеXDTO.СпособВыставления) Тогда
		ПолученныеДанные.ПолученВЭлектронномВиде = (ДанныеXDTO.СпособВыставления.Значение = "ВЭлектронномВиде");
	КонецЕсли;
	
	Если ДанныеXDTO.Свойство("ИсправляемыйСчетФактура")
		И ЗначениеЗаполнено(ДанныеXDTO.ИсправляемыйСчетФактура) Тогда
		ПолученныеДанные.Исправление = Истина;
	КонецЕсли;   
	
	ЗагрузитьДополнительныеРеквизиты(ПолученныеДанные, ДанныеXDTO, КомпонентыОбмена);
	
	ПравилаЗаполнения = Новый Соответствие;
	ПравилаЗаполнения.Вставить("ИсходныйДокумент", "ИсходныйДокумент");
	
	МассивСтрокДокументы = Новый Массив;
	Если ДанныеXDTO.Свойство("ДокументыОснования") И ЗначениеЗаполнено(ДанныеXDTO.ДокументыОснования) Тогда
		Для Каждого Строка Из ДанныеXDTO.ДокументыОснования Цикл
			СтруктураДанныхСтроки = ДанныеКоллекцииВВидеСтруктуры(Строка, ПравилаЗаполнения);	
		
			Если ЗначениеЗаполнено(Строка.ДокументОснование) И ТипЗнч(Строка.ДокументОснование) = Тип("Структура") Тогда
				ДокОснование = ЗначениеВИнструкциюПКО(КомпонентыОбмена, Строка.ДокументОснование);
				Если ДокОснование <> Неопределено Тогда
					СтруктураДанныхСтроки.Вставить("ДокументОснование", ДокОснование);
				Иначе
					// строка с пустым документом основанием не имеет смысла.
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			МассивСтрокДокументы.Добавить(СтруктураДанныхСтроки);
		КонецЦикла;
	
	КонецЕсли;
	Если МассивСтрокДокументы.Количество() > 0 Тогда
		ПолученныеДанные.ДополнительныеСвойства.Вставить("ДокументыОснования", МассивСтрокДокументы);
	КонецЕсли;
	
	Если ДанныеXDTO.Свойство("Продавцы") И ЗначениеЗаполнено(ДанныеXDTO.Продавцы) Тогда
		ПолученныеДанные.СоставленКомиссионеромОтИмениПродавца = Истина;
	КонецЕсли;
КонецПроцедуры
