Процедура ПКО_Документ_ОстаткиТоваровПереданныхНаКомис_Получение_ПередЗаписьюПолученныхДанных(ПолученныеДанные, ДанныеИБ, КонвертацияСвойств, КомпонентыОбмена)
	ЗаполнитьРазделУчетаВводаОстатков(ПолученныеДанные);
	
	#Область Соглашение_с_комиссионером
	Если ДанныеИБ = Неопределено Или Не ЗначениеЗаполнено(ДанныеИБ.СоглашениеСКомиссионером) Тогда
		
		Если Не ЗначениеЗаполнено(ПолученныеДанные.СоглашениеСКомиссионером) Тогда
	
			Запрос = Новый Запрос;
			Запрос.Текст = "
				|	ВЫБРАТЬ ПЕРВЫЕ 1
				|		СоглашенияСКлиентами.Ссылка
				|	ИЗ
				|		Справочник.СоглашенияСКлиентами КАК СоглашенияСКлиентами
				|	ГДЕ
				|		СоглашенияСКлиентами.ХозяйственнаяОперация = &ХозяйственнаяОперация
				|		И НЕ СоглашенияСКлиентами.Типовое
				|		И НЕ СоглашенияСКлиентами.ПометкаУдаления
				|		И СоглашенияСКлиентами.Контрагент = &Контрагент
				|		И СоглашенияСКлиентами.Организация = &Организация
				|		И СоглашенияСКлиентами.Валюта = &Валюта
				|	
				|	ОБЪЕДИНИТЬ ВСЕ
				|	
				|	ВЫБРАТЬ ПЕРВЫЕ 1
				|		СоглашенияСКлиентами.Ссылка
				|	ИЗ
				|		Справочник.СоглашенияСКлиентами КАК СоглашенияСКлиентами
				|	ГДЕ
				|		СоглашенияСКлиентами.ХозяйственнаяОперация = &ХозяйственнаяОперация
				|		И СоглашенияСКлиентами.Типовое
				|		И НЕ СоглашенияСКлиентами.ПометкаУдаления
				|		И СоглашенияСКлиентами.Валюта = &Валюта";
				
			Запрос.УстановитьПараметр("ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ПередачаНаКомиссию);
			Запрос.УстановитьПараметр("Контрагент", ПолученныеДанные.Контрагент);
			Запрос.УстановитьПараметр("Организация", ПолученныеДанные.Организация);
			Запрос.УстановитьПараметр("Валюта", ПолученныеДанные.Валюта);
			
			СоглашениеСКомиссионером = Справочники.СоглашенияСКлиентами.ПустаяСсылка();
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				СоглашениеСКомиссионером = Выборка.Ссылка;
			Иначе
				Соглашение = Справочники.СоглашенияСКлиентами.СоздатьЭлемент();
				Соглашение.Наименование          = НСтр("ru = 'С комиссионером (по умолчанию)';
														|en = 'With consignee (default)'");
				Соглашение.Организация           = ПолученныеДанные.Организация;
				Соглашение.Контрагент            = ПолученныеДанные.Контрагент;
				Соглашение.Партнер               = ДенежныеСредстваСервер.ПолучитьПартнераПоКонтрагенту(ПолученныеДанные.Контрагент);
				Соглашение.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаНаКомиссию;
				Соглашение.Валюта                = ПолученныеДанные.Валюта;
				
				Если Соглашение.Валюта = Константы.ВалютаРегламентированногоУчета.Получить() Тогда
					Соглашение.ПорядокОплаты = Перечисления.ПорядокОплатыПоСоглашениям.РасчетыВРубляхОплатаВРублях;
				Иначе
					Соглашение.ПорядокОплаты = Перечисления.ПорядокОплатыПоСоглашениям.РасчетыВВалютеОплатаВРублях;
				КонецЕсли;
				Соглашение.Статус = Перечисления.СтатусыСоглашенийСКлиентами.Действует;
				Соглашение.Согласован = Истина;
				Соглашение.Комментарий = НСтр("ru = 'БП -> ERP 2.0';
												|en = 'EA -> ERP 2.0'");
				Соглашение.Записать();
				
				// Синхронизация значения соответствующей функциональной опции.
				ТекущееЗначениеКонстанты = Константы.ИспользованиеСоглашенийСКлиентами.Получить();
				НовоеЗначениеКонстанты = Неопределено;
				Если ТекущееЗначениеКонстанты = Перечисления.ИспользованиеСоглашенийСКлиентами.НеИспользовать Тогда
					НовоеЗначениеКонстанты = Перечисления.ИспользованиеСоглашенийСКлиентами.ТолькоИндивидуальныеСоглашения;
				ИначеЕсли ТекущееЗначениеКонстанты = Перечисления.ИспользованиеСоглашенийСКлиентами.ТолькоТиповыеСоглашения Тогда
					НовоеЗначениеКонстанты = Перечисления.ИспользованиеСоглашенийСКлиентами.ТиповыеИИндивидуальныеСоглашения;
				КонецЕсли;
				Если НовоеЗначениеКонстанты <> Неопределено Тогда
					Константы.ИспользованиеСоглашенийСКлиентами.Установить(НовоеЗначениеКонстанты);
				КонецЕсли;				
				
				СоглашениеСКомиссионером = Соглашение.Ссылка;
			КонецЕсли;
			
			ПолученныеДанные.СоглашениеСКомиссионером = СоглашениеСКомиссионером;
			Если ДанныеИБ <> Неопределено Тогда
				ДанныеИБ.СоглашениеСКомиссионером = СоглашениеСКомиссионером;
			КонецЕсли;
		КонецЕсли;
			
	КонецЕсли;	
	#КонецОбласти
КонецПроцедуры
