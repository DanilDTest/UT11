Процедура ПКО_Документ_НачисленияКредитовИДепозитов_Отправка_ПриОтправкеДанных(ДанныеИБ, ДанныеXDTO, КомпонентыОбмена, СтекВыгрузки)
	Если СтекВыгрузки.Количество() > 1 Тогда
		Возврат;
	КонецЕсли;
	
	Если ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.НачисленияПоКредитам Тогда
		ДанныеXDTO.Вставить("ВидОперации", "КредитыПолученные");
	ИначеЕсли ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.НачисленияПоДепозитам Тогда
		ДанныеXDTO.Вставить("ВидОперации", "Депозиты");
	ИначеЕсли ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.НачисленияПоЗаймамВыданным Тогда
		ДанныеXDTO.Вставить("ВидОперации", "ЗаймыПолученные");
	КонецЕсли;
	
	Запрос = Новый Запрос("ВЫБРАТЬ
		|	ДокументТаблицаНачислений.НомерСтроки КАК НомерСтроки,
		|	МАКСИМУМ(КурсыВалют.Период) КАК Период
		|ПОМЕСТИТЬ ПериодыКурсовВалют
		|ИЗ
		|	Документ.НачисленияКредитовИДепозитов.Начисления КАК ДокументТаблицаНачислений
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют КАК КурсыВалют
		|		ПО ДокументТаблицаНачислений.Дата >= КурсыВалют.Период
		|			И ДокументТаблицаНачислений.ВалютаВзаиморасчетов = КурсыВалют.Валюта
		|ГДЕ
		|	ДокументТаблицаНачислений.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ДокументТаблицаНачислений.НомерСтроки
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА Документ.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НачисленияПоКредитам)
		|			ТОГДА ВЫБОР
		|					КОГДА ДокументТаблицаНачислений.ТипСуммыГрафика = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.Комиссия)
		|						ТОГДА ВЫБОР
		|								КОГДА ДокументТаблицаНачислений.ВалютаВзаиморасчетов = &ВалютаРегУчета
		|									ТОГДА ""76.09""
		|								ИНАЧЕ ""76.29""
		|							КОНЕЦ
		|					КОГДА ДокументТаблицаНачислений.ТипСуммыГрафика = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.Проценты)
		|						ТОГДА ВЫБОР
		|								КОГДА ДокументТаблицаНачислений.Договор.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговораКредитовИДепозитов.КредитВБанке)
		|									ТОГДА ВЫБОР
		|											КОГДА ДокументТаблицаНачислений.Договор.ТипСрочности = ЗНАЧЕНИЕ(Перечисление.ТипыСрочностиКредитовИДепозитов.Краткосрочный)
		|												ТОГДА ВЫБОР
		|														КОГДА ДокументТаблицаНачислений.ВалютаВзаиморасчетов = &ВалютаРегУчета
		|															ТОГДА ""66.02""
		|														ИНАЧЕ ""66.22""
		|													КОНЕЦ
		|											КОГДА ДокументТаблицаНачислений.Договор.ТипСрочности = ЗНАЧЕНИЕ(Перечисление.ТипыСрочностиКредитовИДепозитов.Долгосрочный)
		|												ТОГДА ВЫБОР
		|														КОГДА ДокументТаблицаНачислений.ВалютаВзаиморасчетов = &ВалютаРегУчета
		|															ТОГДА ""67.02""
		|														ИНАЧЕ ""67.22""
		|													КОНЕЦ
		|										КОНЕЦ
		|								КОГДА ДокументТаблицаНачислений.Договор.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговораКредитовИДепозитов.ВкладСотрудника)
		|										ИЛИ ДокументТаблицаНачислений.Договор.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговораКредитовИДепозитов.ВнутреннийЗайм)
		|										ИЛИ ДокументТаблицаНачислений.Договор.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговораКредитовИДепозитов.ВнешнийЗайм)
		|									ТОГДА ВЫБОР
		|											КОГДА ДокументТаблицаНачислений.Договор.ТипСрочности = ЗНАЧЕНИЕ(Перечисление.ТипыСрочностиКредитовИДепозитов.Краткосрочный)
		|												ТОГДА ВЫБОР
		|														КОГДА ДокументТаблицаНачислений.ВалютаВзаиморасчетов = &ВалютаРегУчета
		|															ТОГДА ""66.04""
		|														ИНАЧЕ ""66.24""
		|													КОНЕЦ
		|											КОГДА ДокументТаблицаНачислений.Договор.ТипСрочности = ЗНАЧЕНИЕ(Перечисление.ТипыСрочностиКредитовИДепозитов.Долгосрочный)
		|												ТОГДА ВЫБОР
		|														КОГДА ДокументТаблицаНачислений.ВалютаВзаиморасчетов = &ВалютаРегУчета
		|															ТОГДА ""67.04""
		|														ИНАЧЕ ""67.24""
		|													КОНЕЦ
		|										КОНЕЦ
		|							КОНЕЦ
		|				КОНЕЦ
		|		КОГДА Документ.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НачисленияПоЗаймамВыданным)
		|				ИЛИ Документ.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НачисленияПоДепозитам)
		|			ТОГДА ""91.01""
		|	КОНЕЦ КАК СчетКтВычисл,
		|	ВЫБОР
		|		КОГДА Документ.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НачисленияПоКредитам)
		|			ТОГДА ""91.02""
		|		КОГДА Документ.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НачисленияПоЗаймамВыданным)
		|			ТОГДА ""76.09""
		|		КОГДА Документ.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НачисленияПоДепозитам)
		|			ТОГДА ВЫБОР
		|					КОГДА ДокументТаблицаНачислений.ВалютаВзаиморасчетов = &ВалютаРегУчета
		|						ТОГДА ""55.03""
		|					ИНАЧЕ ""55.23""
		|				КОНЕЦ
		|	КОНЕЦ КАК СчетДтВычисл,
		|	ВЫБОР
		|		КОГДА Документ.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НачисленияПоКредитам)
		|			ТОГДА ВЫБОР
		|					КОГДА ДокументТаблицаНачислений.ТипСуммыГрафика = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.Комиссия)
		|						ТОГДА ДокументТаблицаНачислений.Договор.СтатьяДДСКомиссии.КорреспондирующийСчет
		|					КОГДА ДокументТаблицаНачислений.ТипСуммыГрафика = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.Проценты)
		|						ТОГДА ДокументТаблицаНачислений.Договор.СтатьяДДСПроцентов.КорреспондирующийСчет
		|				КОНЕЦ
		|		КОГДА Документ.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НачисленияПоЗаймамВыданным)
		|				ИЛИ Документ.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НачисленияПоДепозитам)
		|			ТОГДА ""91.01""
		|	КОНЕЦ КАК СчетКтПоДоговору,
		|	ВЫБОР
		|		КОГДА Документ.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НачисленияПоКредитам)
		|			ТОГДА ""91.02""
		|		КОГДА Документ.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НачисленияПоЗаймамВыданным)
		|				ИЛИ Документ.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НачисленияПоДепозитам)
		|			ТОГДА ВЫБОР
		|					КОГДА ДокументТаблицаНачислений.ТипСуммыГрафика = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.Комиссия)
		|						ТОГДА ДокументТаблицаНачислений.Договор.СтатьяДДСКомиссии.КорреспондирующийСчет
		|					КОГДА ДокументТаблицаНачислений.ТипСуммыГрафика = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.Проценты)
		|						ТОГДА ДокументТаблицаНачислений.Договор.СтатьяДДСПроцентов.КорреспондирующийСчет
		|				КОНЕЦ
		|	КОНЕЦ КАК СчетДтПоДоговору,
		|	Документ.Дата КАК Дата,
		|	ДокументТаблицаНачислений.Контрагент КАК Контрагент,
		|	ДокументТаблицаНачислений.Договор КАК Договор,
		|	ДокументТаблицаНачислений.СуммаВзаиморасчетов КАК Сумма,
		|	ДокументТаблицаНачислений.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
		|	ДокументТаблицаНачислений.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
		|	ДокументТаблицаНачислений.ТипСуммыГрафика КАК ТипСуммыГрафика,
		|	ВЫБОР
		|		КОГДА ДокументТаблицаНачислений.Договор.ТипСрочности = ЗНАЧЕНИЕ(Перечисление.ТипыСрочностиКредитовИДепозитов.Долгосрочный)
		|			ТОГДА ""Долгосрочный""
		|		ИНАЧЕ ""Краткосрочный""
		|	КОНЕЦ КАК СрочностьКредитаЗайма
		|ПОМЕСТИТЬ ВычислениеСчетаУчета
		|ИЗ
		|	Документ.НачисленияКредитовИДепозитов КАК Документ
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.НачисленияКредитовИДепозитов.Начисления КАК ДокументТаблицаНачислений
		|		ПО Документ.Ссылка = ДокументТаблицаНачислений.Ссылка
		|			И (Документ.Ссылка = &Ссылка)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПериодыКурсовВалют КАК ПериодыКурсовВалют
		|		ПО (ДокументТаблицаНачислений.НомерСтроки = ПериодыКурсовВалют.НомерСтроки)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют КАК КурсыВалют
		|		ПО (ПериодыКурсовВалют.Период = КурсыВалют.Период)
		|			И (ДокументТаблицаНачислений.ВалютаВзаиморасчетов = КурсыВалют.Валюта)
		|ГДЕ
		|	Документ.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА ТаблицаНачислений.СчетКтПоДоговору ЕСТЬ NULL
		|				ИЛИ ТаблицаНачислений.СчетКтПоДоговору = """"
		|			ТОГДА ТаблицаНачислений.СчетКтВычисл
		|		ИНАЧЕ ТаблицаНачислений.СчетКтПоДоговору
		|	КОНЕЦ КАК СчетКт,
		|	ВЫБОР
		|		КОГДА ТаблицаНачислений.СчетДтПоДоговору ЕСТЬ NULL
		|				ИЛИ ТаблицаНачислений.СчетДтПоДоговору = """"
		|			ТОГДА ТаблицаНачислений.СчетДтВычисл
		|		ИНАЧЕ ТаблицаНачислений.СчетДтПоДоговору
		|	КОНЕЦ КАК СчетДт,
		|	ТаблицаНачислений.Контрагент КАК КонтрагентСсылка,
		|	ТаблицаНачислений.Договор КАК Договор,
		|	ВЫБОР
		|		КОГДА ТаблицаНачислений.ТипСуммыГрафика = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.Проценты)
		|				ИЛИ ТаблицаНачислений.ТипСуммыГрафика = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.ОсновнойДолг)
		|			ТОГДА ""Проценты""
		|		ИНАЧЕ ""Комиссия""
		|	КОНЕЦ КАК ТипСуммыГрафика,
		|	ТаблицаНачислений.Дата КАК Дата,
		|	ТаблицаНачислений.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
		|	ТаблицаНачислений.Сумма КАК Сумма,
		|	ТаблицаНачислений.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов
		|ИЗ
		|	ВычислениеСчетаУчета КАК ТаблицаНачислений");
	
	Запрос.УстановитьПараметр("Ссылка",         ДанныеИБ.Ссылка);
	Запрос.УстановитьПараметр("ВалютаРегУчета", Константы.ВалютаРегламентированногоУчета.Получить());
	
	ТаблицаНачислений = Запрос.Выполнить().Выгрузить();
	ТаблицаНачислений.Колонки.Добавить("Контрагент");
	
	Для Каждого Строка Из ТаблицаНачислений Цикл
		Если ЗначениеЗаполнено(Строка.КонтрагентСсылка) Тогда
	
			Если ТипЗнч(Строка.КонтрагентСсылка) = Тип("СправочникСсылка.Контрагенты") Тогда
				Строка.Контрагент = Новый Структура("Значение, ИмяПКО", Строка.КонтрагентСсылка, "Справочник_Контрагенты");
			Иначе
				Строка.Контрагент = Новый Структура("Значение, ИмяПКО", Строка.КонтрагентСсылка, "Справочник_ФизическиеЛица_Отправка");
			КонецЕсли;
		
		КонецЕсли;
	КонецЦикла;
	
	ДанныеXDTO.Вставить("Начисления", ТаблицаНачислений);
КонецПроцедуры
