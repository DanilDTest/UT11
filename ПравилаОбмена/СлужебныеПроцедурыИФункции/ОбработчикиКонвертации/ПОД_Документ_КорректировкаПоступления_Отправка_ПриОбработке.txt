Процедура ПОД_Документ_КорректировкаПоступления_Отправка_ПриОбработке(ДанныеИБ, ИспользованиеПКО, КомпонентыОбмена)
	УстановитьИспользованиеПКО(ИспользованиеПКО, Ложь);
		
	ИспользованиеПКО.Документ_КорректировкаПоступления_Отправка = Истина;
	
	Если ДанныеИБ.Расхождения.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	// Списание, Оприходование
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Док.Номенклатура КАК Номенклатура,
	|	Док.Характеристика КАК Характеристика,
	|	ВЫБОР
	|		КОГДА Док.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|			ТОГДА ВЫРАЗИТЬ(""КомиссионныеТовары"" КАК СТРОКА(18))
	|		ИНАЧЕ ВЫРАЗИТЬ(""СобственныеТовары"" КАК СТРОКА(18))
	|	КОНЕЦ КАК ТипЗапасов,
	|	СУММА(Док.Количество) КАК Количество,
	|	СУММА(Док.СуммаСНДС - Док.СуммаНДС) КАК Сумма,
	|	Док.НомерГТД КАК НомерГТД,
	|	Док.Серия КАК Серия,
	|	Док.Упаковка КАК Упаковка,
	|	Док.КоличествоУпаковок КАК КоличествоУпаковок
	|ПОМЕСТИТЬ ТоварыКоличественныхРасхождений
	|ИЗ
	|	Документ.КорректировкаПриобретения.Расхождения КАК Док
	|ГДЕ
	|	Док.Ссылка = &КорректировкаПоступления
	|	И (Док.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар)
	|			ИЛИ Док.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|				И НЕ ЕСТЬNULL(ВЫРАЗИТЬ(Док.Ссылка.ДокументОснование КАК Документ.ПриобретениеТоваровУслуг).ВернутьМногооборотнуюТару, ЛОЖЬ))
	|	И Док.ВариантОтражения В (ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.ОтразитьНаПрочихДоходах), ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.СписатьНаРасходы))
	|
	|СГРУППИРОВАТЬ ПО
	|	Док.Номенклатура,
	|	Док.Характеристика,
	|	Док.НомерГТД,
	|	ВЫБОР
	|		КОГДА Док.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|			ТОГДА ВЫРАЗИТЬ(""КомиссионныеТовары"" КАК СТРОКА(18))
	|		ИНАЧЕ ВЫРАЗИТЬ(""СобственныеТовары"" КАК СТРОКА(18))
	|	КОНЕЦ,
	|	Док.Серия,
	|	Док.Упаковка,
	|	Док.КоличествоУпаковок
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Количество
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура КАК Номенклатура,
	|	&ТекстЗапросаХарактеристика,
	|	&ТекстЗапросаСерия,
	|	&ТекстЗапросаУпаковка,
	|	Товары.ТипЗапасов КАК ТипЗапасов,
	|	Товары.НомерГТД.Код КАК НомерГТД,
	|	Товары.НомерГТД.СтранаПроисхождения КАК СтранаПроисхождения,
	|	Товары.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	Товары.Количество КАК Количество,
	|	Товары.Сумма КАК Сумма,
	|	ВЫРАЗИТЬ(0 КАК ЧИСЛО(15, 2)) КАК Цена
	|ИЗ
	|	ТоварыКоличественныхРасхождений КАК Товары
	|ГДЕ
	|	Товары.Количество > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура КАК Номенклатура,
	|	&ТекстЗапросаХарактеристика,
	|	&ТекстЗапросаСерия,
	|	&ТекстЗапросаУпаковка,
	|	Товары.ТипЗапасов КАК ТипЗапасов,
	|	Товары.НомерГТД.Код КАК НомерГТД,
	|	Товары.НомерГТД.СтранаПроисхождения КАК СтранаПроисхождения,
	|	Товары.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	-Товары.Количество КАК Количество,
	|	-Товары.Сумма КАК Сумма,
	|	ВЫРАЗИТЬ(0 КАК ЧИСЛО(15, 2)) КАК Цена
	|ИЗ
	|	ТоварыКоличественныхРасхождений КАК Товары
	|ГДЕ
	|	Товары.Количество < 0";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаХарактеристика",
		ПолучитьТекстЗапросаХарактеристики(КомпонентыОбмена, "Товары"));
		
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаУпаковка",
		ПолучитьТекстЗапросаУпаковки(КомпонентыОбмена, "Товары"));
		
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаСерия",
		ПолучитьТекстЗапросаСерии(КомпонентыОбмена, "Товары"));
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("КорректировкаПоступления", ДанныеИБ.Ссылка);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ТоварыКСписаниюВБухгалтерии      = РезультатЗапроса[1].Выгрузить();
	ТоварыКОприходованиюВБухгалтерии = РезультатЗапроса[2].Выгрузить();
	
	Если ТоварыКСписаниюВБухгалтерии.Количество() > 0 Тогда
		ЗаполнитьХарактеристикуУпаковкуВТЧ(КомпонентыОбмена, ТоварыКСписаниюВБухгалтерии);
		ЗаполнитьЦенуСписаниеОприходование(ТоварыКСписаниюВБухгалтерии, КомпонентыОбмена);
		
		ИспользованиеПКО.Документ_КорректировкаПоступления_СписаниеЗапасов = Истина;
		ДанныеИБ.ДополнительныеСвойства.Вставить("ТоварыКСписанию", ТоварыКСписаниюВБухгалтерии);
	КонецЕсли;
	
	Если ТоварыКОприходованиюВБухгалтерии.Количество() > 0 Тогда
		ЗаполнитьХарактеристикуУпаковкуВТЧ(КомпонентыОбмена, ТоварыКОприходованиюВБухгалтерии);
		ЗаполнитьЦенуСписаниеОприходование(ТоварыКОприходованиюВБухгалтерии, КомпонентыОбмена);
		
		Если ВерсияФорматаЧислом(КомпонентыОбмена.ВерсияФорматаОбмена) >= ВерсияФорматаЧислом("1.6") Тогда
			ВидДеятельностиНДС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеИБ.ДокументОснование, "ЗакупкаПодДеятельность");
			
			ТоварыКОприходованиюВБухгалтерии.Колонки.Добавить("ОприходованиеПодДеятельность");
			ТоварыКОприходованиюВБухгалтерии.ЗаполнитьЗначения(ВидДеятельностиНДС, "ОприходованиеПодДеятельность");
		КонецЕсли;
		
		ИспользованиеПКО.Документ_КорректировкаПоступления_Оприходование = Истина;
		ДанныеИБ.ДополнительныеСвойства.Вставить("ТоварыКОприходованию", ТоварыКОприходованиюВБухгалтерии);
	КонецЕсли;
	
	ПроверкаЗаполненияРеквизитовОбъектаИБ(ДанныеИБ, КомпонентыОбмена, ИспользованиеПКО);
КонецПроцедуры
