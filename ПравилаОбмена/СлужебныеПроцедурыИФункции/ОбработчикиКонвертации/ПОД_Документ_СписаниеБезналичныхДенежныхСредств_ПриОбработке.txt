Процедура ПОД_Документ_СписаниеБезналичныхДенежныхСредств_ПриОбработке(ДанныеИБ, ИспользованиеПКО, КомпонентыОбмена)
	УстановитьИспользованиеПКО(ИспользованиеПКО, Ложь);
		
	ДанныеШапки       = Новый Структура;
	ВидОперацииПрочее = "";
	ВидРасчетов       = "";
	
	Если ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.КонвертацияВалюты Тогда
		
		ИспользованиеПКО.Документ_СБДСКонвертацияВалюты_Отправка = Истина;
	
	ИначеЕсли ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПеречислениеДенежныхСредствНаДругойСчет Тогда
		
		ИспользованиеПКО.Документ_СБДСПереводНаДругойСчет_Отправка = Истина;
	
	ИначеЕсли ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыплатаЗарплатыПоЗарплатномуПроекту Тогда
		
		ИспользованиеПКО.Документ_СБДСПеречислениеЗаработнойПлаты_Отправка = Истина;
	
	ИначеЕсли ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыплатаЗарплатыНаЛицевыеСчета Тогда
		
		ИспользованиеПКО.Документ_СБДСПеречислениеЗПРаботнику_Отправка = Истина;
	
	ИначеЕсли ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПеречислениеВБюджет Тогда
		
		ИспользованиеПКО.Документ_СБДСПлатежиВБюджет_Отправка = Истина;
	
	ИначеЕсли ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПеречислениеТаможне Тогда
		
		ИспользованиеПКО.Документ_СБДСРасчетыСКонтрагентами_Отправка = Истина;
		ВидРасчетов = "Прочее";
	
	ИначеЕсли ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВнутренняяПередачаДенежныхСредств Тогда
		
		ВидОперацииПрочее = "ВнутренняяПередача";
	
	ИначеЕсли ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПрочиеРасходы 
		Или ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПрочаяВыдачаДенежныхСредств Тогда
		
		// Хоз. операция ПрочиеРасходы больше не будет использоваться. Оставлено на переходный период.
		// Прочий расход отражается видом операции ПрочаяВыдачаДенежныхСредств.
		ВидОперацииПрочее = "ПрочееСписание";
		Для Каждого СтрокаТЧ Из ДанныеИБ.РасшифровкаПлатежа Цикл
			
			// Если заполнена статья расходов - то это прочий расход.
			Если ЗначениеЗаполнено(СтрокаТЧ.СтатьяРасходов)
				И ТипЗнч(СтрокаТЧ.СтатьяРасходов) = Тип("ПланВидовХарактеристикСсылка.СтатьиРасходов") Тогда
				
				ИспользованиеПКО.Документ_СБДСПрочиеРасходы_Отправка = Истина;
				// При операции ПрочиеРасходы ВидОперацииПрочее не должен быть заполнен.
				ВидОперацииПрочее = "";
				Прервать;
				
			КонецЕсли;
			
		КонецЦикла;
		
	ИначеЕсли ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОплатаПоставщику
		Или ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОплатаДенежныхСредствВДругуюОрганизацию Тогда
	
		ИспользованиеПКО.Документ_СБДСРасчетыСКонтрагентами_Отправка = Истина;
		ВидРасчетов = "СПоставщиком";
	
	ИначеЕсли ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратОплатыКлиенту
		Или ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствВДругуюОрганизацию Тогда
	
		ИспользованиеПКО.Документ_СБДСРасчетыСКонтрагентами_Отправка = Истина;
		ВидРасчетов = "СПокупателем";
	
	ИначеЕсли ЭтоХозяйственнаяОперацияРасчетыПоКредитамЗаймам(ДанныеИБ.ХозяйственнаяОперация) Тогда
	
		ИспользованиеПКО.Документ_СБДСРасчетыСКонтрагентами_Отправка = Истина;
		ВидРасчетов = "КредитыИЗаймы";
		
	ИначеЕсли ЭтоХозяйственнаяОперацияРасчетыПоДепозитам(ДанныеИБ.ХозяйственнаяОперация) Тогда
	
		ВидОперацииПрочее = "ПрочееСписание";
		
	ИначеЕсли ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОплатаЛизингодателю Тогда
	
		ИспользованиеПКО.Документ_СБДСРасчетыСКонтрагентами_Отправка = Истина;
		ВидРасчетов = "Прочее";
	
	ИначеЕсли ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыдачаДенежныхСредствПодотчетнику Тогда
	
		ИспользованиеПКО.Документ_СБДСРасчетыССотрудниками_Отправка = Истина;
		ВидРасчетов = "Подотчет";
		
	ИначеЕсли ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыдачаЗаймаСотруднику Тогда
	
		ИспользованиеПКО.Документ_СБДСРасчетыССотрудниками_Отправка = Истина;
		ВидРасчетов = "Займы";
	
	ИначеЕсли ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.СнятиеНаличныхДенежныхСредств Тогда
		
		Если ВерсияФорматаЧислом(КомпонентыОбмена.ВерсияФорматаОбмена) < ВерсияФорматаЧислом("1.6") Тогда
			ВидОперацииПрочее = "ПрочееСписание";
		Иначе
			ИспользованиеПКО.Документ_СБДССнятиеНаличных_Отправка = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВидОперацииПрочее) Тогда
	
		ДанныеШапки.Вставить("ВидОперацииПрочее", ВидОперацииПрочее);
		ИспользованиеПКО.Документ_СБДСПрочееСписание_Отправка = Истина;
	
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВидРасчетов) Тогда
		
		ДанныеШапки.Вставить("ВидРасчетов", ВидРасчетов);
		
	КонецЕсли;
	
	ДанныеИБ.ДополнительныеСвойства.Вставить("ДанныеШапки", ДанныеШапки);
	
	ПроверкаЗаполненияРеквизитовОбъектаИБ(ДанныеИБ, КомпонентыОбмена, ИспользованиеПКО);
КонецПроцедуры
