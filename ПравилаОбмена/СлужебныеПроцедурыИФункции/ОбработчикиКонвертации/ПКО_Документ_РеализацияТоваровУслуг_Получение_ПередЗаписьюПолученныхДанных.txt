Процедура ПКО_Документ_РеализацияТоваровУслуг_Получение_ПередЗаписьюПолученныхДанных(ПолученныеДанные, ДанныеИБ, КонвертацияСвойств, КомпонентыОбмена)
	ЗаполнитьСкладПоУмолчанию(ПолученныеДанные, ДанныеИБ, КомпонентыОбмена);
	Если ЗначениеЗаполнено(ПолученныеДанные.Контрагент) И НЕ ЗначениеЗаполнено(ПолученныеДанные.Партнер) Тогда
		ПолученныеДанные.Партнер = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПолученныеДанные.Контрагент, "Партнер");
	КонецЕсли;
	
	// Подразделение        
	ПолученныеДанные.Подразделение = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПолученныеДанные.Склад, "Подразделение");
	//ЗаполнитьПодразделениеПоУмолчанию(ПолученныеДанные, ДанныеИБ, КомпонентыОбмена);
	
	ПолученныеДанные.ДатаРаспоряжения = ПолученныеДанные.Дата;
	ЗагрузитьДополнительныеСведения(?(ДанныеИБ = Неопределено, ПолученныеДанные.ПолучитьСсылкуНового(), ДанныеИБ.Ссылка), 
								ПолученныеДанные.ДополнительныеСвойства, КомпонентыОбмена);
	Если ДанныеИБ <> Неопределено Тогда
	
		ДанныеИБСклад = ДанныеИБ.Склад;
	
		// Переносим те свойства, которые указаны в ПКС.
		ЗаполнитьСвойстваШапкиОбъекта(КонвертацияСвойств, ПолученныеДанные, ДанныеИБ);
	
		Если НЕ КомпонентыОбмена.ПараметрыКонвертации.ВыгружатьАналитикуПоСкладам Тогда
			// Восстанавливаем склад из первоначального документа.
			ДанныеИБ.Склад = ДанныеИБСклад;
		КонецЕсли;
	
		// Товары
		МассивКлючевыхПолей = Новый Массив;
		МассивКлючевыхПолей.Добавить("Номенклатура");
		МассивКлючевыхПолей.Добавить("Характеристика");
		МассивКлючевыхПолей.Добавить("Серия");
		МассивКлючевыхПолей.Добавить("Склад");
		МассивКлючевыхПолей.Добавить("СтавкаНДС");
	
		RM_ОбменДаннымиXDTOСервер.ЗаполнитьТабличнуюЧастьОбъектаНачальнымиДанными(
			ПолученныеДанные.Товары,
			ДанныеИБ.Товары,
			МассивКлючевыхПолей,
			Неопределено,
			"Количество, Упаковка, КоличествоУпаковок, Цена, Сумма, СуммаНДС, СуммаСНДС, КодСтроки, КлючСвязи");
			
		ДанныеИБ.Товары.Загрузить(ПолученныеДанные.Товары.Выгрузить()); 
		ДанныеИБ.ШтрихкодыУпаковок.Загрузить(ПолученныеДанные.ШтрихкодыУпаковок.Выгрузить());
	
	
		Для Каждого Строка Из ДанныеИБ.Товары Цикл
			Строка.Склад = ДанныеИБ.Склад;
		КонецЦикла;
	
		ПолученныеДанные = Неопределено;
		
	КонецЕсли;
	
	ДанныеДляЗаписиВИБ = ?(ДанныеИБ = Неопределено, ПолученныеДанные, ДанныеИБ);
	Для Каждого Строка Из ДанныеДляЗаписиВИБ.Товары Цикл
		Если Не ЗначениеЗаполнено(Строка.Номенклатура) Тогда
			Строка.Номенклатура = ЭлементПоУмолчанию(КомпонентыОбмена, "НоменклатураУслуга");
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры
