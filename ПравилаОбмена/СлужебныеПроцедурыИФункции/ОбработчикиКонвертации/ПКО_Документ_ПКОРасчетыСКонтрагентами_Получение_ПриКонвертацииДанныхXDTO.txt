Процедура ПКО_Документ_ПКОРасчетыСКонтрагентами_Получение_ПриКонвертацииДанныхXDTO(ДанныеXDTO, ПолученныеДанные, КомпонентыОбмена)
	// Заполнение данных шапки
	ПолученныеДанные.Проведен = Истина;
	
	ВидРасчетов = "СПокупателем";
	Если ДанныеXDTO.Свойство("ВидРасчетов") Тогда
		ВидРасчетов = ДанныеXDTO.ВидРасчетов.Значение;
	КонецЕсли;
	
	ПолученныеДанные.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПрочееПоступлениеДенежныхСредств;
	Если ВидРасчетов = "СПокупателем" Тогда
		ПолученныеДанные.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента;
	ИначеЕсли ВидРасчетов = "СПоставщиком" Тогда
		ПолученныеДанные.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтПоставщика;
	КонецЕсли;
	ЗагрузитьОтветственноеЛицо(ПолученныеДанные, ДанныеXDTO, КомпонентыОбмена, "ГлавныйБухгалтер");
	ДобавитьВалютуВДопСвойства(ПолученныеДанные, ДанныеXDTO);
	
	
	ЗагрузитьДополнительныеРеквизиты(ПолученныеДанные, ДанныеXDTO, КомпонентыОбмена);
	
	// Расшифровка платежа
	ЗаполнятьДоговор = (ВидРасчетов = "СПокупателем" И КомпонентыОбмена.ПараметрыКонвертации.ИспользуютсяДоговорыСКлиентами)
					ИЛИ (ВидРасчетов = "СПоставщиком" И КомпонентыОбмена.ПараметрыКонвертации.ИспользуютсяДоговорыСПоставщиками);
	
	МассивСтрокРасшифровкаПлатежа = Новый Массив;
	Если ЗначениеЗаполнено(ДанныеXDTO.РасшифровкаПлатежа) Тогда
	
		Т_РП = Новый Соответствие;
		Т_РП.Вставить("Сумма", "Сумма");
		Т_РП.Вставить("СуммаВзаиморасчетов", "Сумма");
		Т_РП.Вставить("СтатьяДвиженияДенежныхСредств", "СтатьяДДС");
		Т_РП.Вставить("СуммаНДС", "СуммаНДС");
		Т_РП.Вставить("СтавкаНДС", "СтавкаНДС");
		
		Для Каждого СтрокаXDTO Из ДанныеXDTO.РасшифровкаПлатежа Цикл
			СтруктураДанныхСтроки = Новый Структура;
			ЗаполнитьСтруктуруДанныхСтроки(Т_РП, СтрокаXDTO, СтруктураДанныхСтроки);
			ДобавитьВалютуВРасшифровкуПлатежа(СтруктураДанныхСтроки, СтрокаXDTO);
					
			МассивСтрокРасшифровкаПлатежа.Добавить(СтруктураДанныхСтроки);
			ВидРасчетовРасширенный = СтрокаXDTO.ВидРасчетовРасширенный;
			Если Найти(НРЕГ(ВидРасчетовРасширенный), "займ") > 0 И ВидРасчетов = "КредитыИЗаймы" Тогда
				ПолученныеДанные.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоЗаймамВыданным;
			КонецЕсли;
		КонецЦикла;
		
		ПолученныеДанные.ДополнительныеСвойства.Вставить("РасшифровкаПлатежа", МассивСтрокРасшифровкаПлатежа);
	
	КонецЕсли;
КонецПроцедуры
