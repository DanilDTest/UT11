Процедура ПКО_Документ_СчетФактураПолученный_Отправка_ПриОтправкеДанных(ДанныеИБ, ДанныеXDTO, КомпонентыОбмена, СтекВыгрузки)
	Если СтекВыгрузки.Количество() > 1 Тогда
		Возврат;
	КонецЕсли;
	
	Если ВерсияФорматаЧислом(КомпонентыОбмена.ВерсияФорматаОбмена) >= ВерсияФорматаЧислом("1.7") Тогда
		ДанныеXDTO.Вставить("ИННКонтрагента", ДанныеИБ.ИННКонтрагента);
		ДанныеXDTO.Вставить("КППКонтрагента", ДанныеИБ.КППКонтрагента);
	КонецЕсли;
	
	// Документы-основания.
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДокументОснование,
		|	ИсходныйДокумент
		|ИЗ 
		|	Документ.СчетФактураПолученный.ДокументыОснования
		|ГДЕ 
		|	Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ДанныеИБ.Ссылка);
	ВыборкаТЧ = Запрос.Выполнить().Выбрать();
	
	ДокументыОснования = Новый ТаблицаЗначений();
	ДокументыОснования.Колонки.Добавить("ДокументОснование");
	ДокументыОснования.Колонки.Добавить("ИсходныйДокумент");
	
	Пока ВыборкаТЧ.Следующий() Цикл
		
		ДокументОснование = ВыборкаТЧ.ДокументОснование;
	
		ИмяПКО = ИмяПКОДляОбъектаДанных(КомпонентыОбмена, ДокументОснование, ДанныеИБ.Организация);
		
		Если НЕ (ЗначениеЗаполнено(ДокументОснование) И ЗначениеЗаполнено(ИмяПКО)) Тогда
			Продолжить;
		КонецЕсли;
			
		СтрокаОснование = ДокументыОснования.Добавить();
		СтрокаОснование.ДокументОснование = Новый Структура("Значение, ИмяПКО", ДокументОснование, ИмяПКО);
		Если ЗначениеЗаполнено(ВыборкаТЧ.ИсходныйДокумент) Тогда
			СтрокаОснование.ИсходныйДокумент = ВыборкаТЧ.ИсходныйДокумент;
		КонецЕсли;
	
	КонецЦикла;
	
	ДанныеXDTO.Вставить("ДокументыОснования", ДокументыОснования);
	ДанныеXDTO.Вставить("ВидСчетаФактуры",    "НаПоступление");
	
	Если ДанныеИБ.Корректировочный Тогда
		ДанныеXDTO.Вставить("ВидСчетаФактуры", "Корректировочный");
	КонецЕсли;
	
	ВыгрузитьСпособВыставленияСчетаФактуры(ДанныеXDTO, ДанныеИБ.ПолученВЭлектронномВиде);
	
	#Область ВычислениеИтоговыхСумм
	
	СуммаДокумента = 0;
	СуммаНДС       = 0;
	
	МассивДокументовОснований = ДанныеИБ.ДокументыОснования.ВыгрузитьКолонку("ДокументОснование");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивДокументовОснований", МассивДокументовОснований);
	Запрос.Текст = "ВЫБРАТЬ
		|	Док.СуммаНДСВознаграждения КАК СуммаНДСДокумента,
		|	Док.СуммаВознаграждения КАК СуммаДокумента
		|ПОМЕСТИТЬ ТаблицаСумм
		|ИЗ Документ.ОтчетКомиссионера КАК Док
		|ГДЕ Док.Ссылка В (&МассивДокументовОснований)
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ
		|	Док.СуммаНДС КАК СуммаНДСДокумента,
		|	Док.СуммаСНДС КАК СуммаДокумента
		|ИЗ Документ.ОтчетКомитенту.Товары КАК Док
		|ГДЕ Док.Ссылка В (&МассивДокументовОснований)
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ
		|	Док.СуммаНДС,
		|	Док.СуммаСНДС
		|ИЗ Документ.ВозвратТоваровПоставщику.Товары КАК Док
		|ГДЕ Док.Ссылка В (&МассивДокументовОснований)
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ
		|	Док.СуммаНДС,
		|	Док.СуммаСНДС
		|ИЗ Документ.ПриобретениеТоваровУслуг.Товары КАК Док
		|ГДЕ Док.Ссылка В (&МассивДокументовОснований)
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ
		|	Док.СуммаНДС,
		|	Док.СуммаСНДС
		|ИЗ Документ.АктВыполненныхРабот.Услуги КАК Док
		|ГДЕ Док.Ссылка В (&МассивДокументовОснований)
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ
		|	Док.СуммаНДС,
		|	Док.СуммаСНДС
		|ИЗ Документ.ВозвратТоваровОтКлиента.Товары КАК Док
		|ГДЕ Док.Ссылка В (&МассивДокументовОснований)
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ
		|	Док.СуммаНДС,
		|	Док.СуммаСНДС
		|ИЗ Документ.ПриобретениеУслугПрочихАктивов.Расходы КАК Док
		|ГДЕ Док.Ссылка В (&МассивДокументовОснований)
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ
		|	Док.СуммаНДС,
		|	Док.СуммаСНДС
		|ИЗ Документ.КорректировкаПриобретения.Товары КАК Док
		|ГДЕ Док.Ссылка В (&МассивДокументовОснований)
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ
		|	Док.СуммаНДС,
		|	Док.СуммаСНДС
		|ИЗ Документ.ПередачаТоваровМеждуОрганизациями.Товары КАК Док
		|ГДЕ Док.Ссылка В (&МассивДокументовОснований)
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ
		|	Док.СуммаНДС,
		|	Док.СуммаСНДС
		|ИЗ Документ.ВозвратТоваровМеждуОрганизациями.Товары КАК Док
		|ГДЕ Док.Ссылка В (&МассивДокументовОснований)
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ
		|	Док.СуммаНДС,
		|	Док.СуммаСНДС
		|ИЗ Документ.ОтчетПоКомиссииМеждуОрганизациями.Товары КАК Док
		|ГДЕ Док.Ссылка В (&МассивДокументовОснований)
		|;
		|ВЫБРАТЬ
		|СУММА(СуммаНДСДокумента) КАК СуммаНДСДокумента,
		|СУММА(СуммаДокумента) КАК СуммаДокумента
		|ИЗ ТаблицаСумм
		|ИТОГИ ПО Общие";
	
	Выборка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Если Выборка.Следующий() Тогда
		СуммаДокумента = Выборка.СуммаДокумента;
		СуммаНДС       = Выборка.СуммаНДСДокумента;
	КонецЕсли;
	
	#КонецОбласти
	
	ДанныеXDTO.Вставить("Сумма",    СуммаДокумента);
	ДанныеXDTO.Вставить("СуммаНДС", СуммаНДС);
	
	Если ДанныеИБ.СоставленКомиссионеромОтИмениПродавца
		И ВерсияФорматаЧислом(КомпонентыОбмена.ВерсияФорматаОбмена) >= ВерсияФорматаЧислом("1.6") Тогда
		ДанныеXDTO.Вставить("Продавцы", ДанныеИБ.Продавцы.Выгрузить());	
	КонецЕсли;
	
	ВыгрузитьДополнительныеРеквизитыИСведения(КомпонентыОбмена, ДанныеИБ, ДанныеXDTO);
КонецПроцедуры
