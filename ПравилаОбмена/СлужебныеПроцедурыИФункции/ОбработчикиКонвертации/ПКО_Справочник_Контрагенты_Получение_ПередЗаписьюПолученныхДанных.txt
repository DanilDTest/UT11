Процедура ПКО_Справочник_Контрагенты_Получение_ПередЗаписьюПолученныхДанных(ПолученныеДанные, ДанныеИБ, КонвертацияСвойств, КомпонентыОбмена)
	ИсходныеИсторическиеДанные = Неопределено;
	Если ПолученныеДанные.ДополнительныеСвойства.Свойство("ИсходныеИсторическиеДанные", ИсходныеИсторическиеДанные) Тогда
		
		Если ДанныеИБ = Неопределено Тогда
			// Принимаем данные из обмена.
			Для Каждого ЭлементДанных Из ИсходныеИсторическиеДанные Цикл
				ПолученныеДанные[ЭлементДанных.Ключ] = ЭлементДанных.Значение;
			КонецЦикла;
		Иначе
			// Восстанавливаем актуальные исторические значения реквизитов шапки из объекта ИБ.
			АктуальныеДанныеКонтрагента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеИБ.Ссылка, ИсходныеИсторическиеДанные);
			ЗаполнитьЗначенияСвойств(ПолученныеДанные, АктуальныеДанныеКонтрагента);
		КонецЕсли;
		
	КонецЕсли;
	
	// Актуализация "среза последних" для исторических данных (при наличии).
	Если ДанныеИБ <> Неопределено Тогда
		
		ИсторическиеДанные = Новый Структура; // Ключ - имя ТЧ, значение - имя реквизита ТЧ
		ИсторическиеДанные.Вставить("ИсторияКПП",          "КПП");
		ИсторическиеДанные.Вставить("ИсторияНаименований", "НаименованиеПолное");
		
		Для Каждого ЭлементДанных Из ИсторическиеДанные Цикл
			ПолученныеДанные[ЭлементДанных.Ключ].Загрузить(ДанныеИБ[ЭлементДанных.Ключ].Выгрузить());
			
			АктуальнаяЗапись     = Неопределено;
			ЗначениеЕстьВИстории = Ложь;
			
			Для Каждого ИсторияЗначения Из ПолученныеДанные[ЭлементДанных.Ключ] Цикл
				Если Не ЗначениеЕстьВИстории
					И ИсторияЗначения[ЭлементДанных.Значение] = ПолученныеДанные[ЭлементДанных.Значение] Тогда
					ЗначениеЕстьВИстории = Истина;
				КонецЕсли;
				
				Если АктуальнаяЗапись = Неопределено Тогда
					АктуальнаяЗапись = ИсторияЗначения;
				ИначеЕсли АктуальнаяЗапись.Период < ИсторияЗначения.Период Тогда
					АктуальнаяЗапись = ИсторияЗначения;
				КонецЕсли;
			КонецЦикла;
			
			Если Не АктуальнаяЗапись = Неопределено
				И Не ЗначениеЕстьВИстории Тогда
				АктуальнаяЗапись[ЭлементДанных.Значение] = ПолученныеДанные[ЭлементДанных.Значение];
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	
	
	Если ДанныеИБ <> Неопределено Тогда
		Если НЕ ЗначениеЗаполнено(ПолученныеДанные.ГоловнойКонтрагент) Тогда
			ПолученныеДанные.ГоловнойКонтрагент = ДанныеИБ.ГоловнойКонтрагент;
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(ПолученныеДанные.СтранаРегистрации) Тогда
			ПолученныеДанные.СтранаРегистрации = ДанныеИБ.СтранаРегистрации;
		КонецЕсли;
		Если ПолученныеДанные.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо
			и ДанныеИБ.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ИндивидуальныйПредприниматель 
			или ПолученныеДанные.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо
			и ДанныеИБ.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицоНеРезидент Тогда
			
			ПолученныеДанные.ЮрФизЛицо = ДанныеИБ.ЮрФизЛицо; 
		КонецЕсли;	
	КонецЕсли;
	
	ЗагрузитьДополнительныеСведения(?(ДанныеИБ = Неопределено, ПолученныеДанные.ПолучитьСсылкуНового(), ДанныеИБ.Ссылка), 
								ПолученныеДанные.ДополнительныеСвойства, КомпонентыОбмена);
КонецПроцедуры
