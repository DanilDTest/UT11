Процедура ПКО_Документ_АктОРасхожденияхПослеПеремещения_Получени_ПередЗаписьюПолученныхДанных(ПолученныеДанные, ДанныеИБ, КонвертацияСвойств, КомпонентыОбмена)
	ЭтоНовый = Истина; 
	Проведен = ПолученныеДанные.Проведен;    
	                  
	ЭтоЗагрузкаОтСкладаОтправителя = RM_ОбменДаннымиПовтИсп.СкладПринадлежитУзлуОбмена(ПолученныеДанные.СкладОтправитель, КомпонентыОбмена.ПараметрыКонвертации.УзелОтправительESB); 
	                  
	ЭтоДокументТекущегоУзла = RM_ОбменДаннымиПовтИсп.СкладПринадлежитУзлуОбмена(ПолученныеДанные.СкладОтправитель, КомпонентыОбмена.ПараметрыКонвертации.ТекущийУзелESB)  
		ИЛИ RM_ОбменДаннымиПовтИсп.СкладПринадлежитУзлуОбмена(ПолученныеДанные.СкладПолучатель, КомпонентыОбмена.ПараметрыКонвертации.ТекущийУзелESB);
		
	Если ДанныеИБ <> Неопределено Тогда     
		
		// Восстанавливаем "затертые" данные 
		Если НЕ ЭтоЗагрузкаОтСкладаОтправителя Тогда
			//Товары
			МассивКлючевыхПолей = Новый Массив;
			МассивКлючевыхПолей.Добавить("Номенклатура");
			МассивКлючевыхПолей.Добавить("Упаковка");
			МассивКлючевыхПолей.Добавить("КоличествоУпаковок");
			МассивКлючевыхПолей.Добавить("КоличествоУпаковокПоДокументу");
			
			RM_ОбменДаннымиXDTOСервер.ЗаполнитьТабличнуюЧастьОбъектаНачальнымиДанными(
				ПолученныеДанные.Товары,
				ДанныеИБ.Товары,
				МассивКлючевыхПолей,
				"RM_СтатусОшибки, RM_ДатаРезолюции");  
		КонецЕсли;     
		
		// Переносим те свойства, которые указаны в ПКС.
		ЗаполнитьСвойстваШапкиОбъекта(КонвертацияСвойств, ПолученныеДанные, ДанныеИБ);
		
		// Табличные части
		ДанныеИБ.Товары.Загрузить(ПолученныеДанные.Товары.Выгрузить());
		ДанныеИБ.RM_ШтрихкодыУпаковок.Загрузить(ПолученныеДанные.RM_ШтрихкодыУпаковок.Выгрузить());
		
		ЭтоНовый = Ложь;
		ПолученныеДанные = Неопределено;
		
	КонецЕсли;                             
	                                       
	
	ДанныеДляЗаписиВИБ = ?(ДанныеИБ = Неопределено, ПолученныеДанные, ДанныеИБ);  
	РежимЗаписи = ?(Проведен, РежимЗаписиДокумента.Проведение, РежимЗаписиДокумента.Запись); 
	 
	
	Если ЭтоДокументТекущегоУзла Тогда
		ПравилаПроверкиМодифицированности = Документы.АктОРасхожденияхПослеПеремещения.RM_ПравилаПроверкиМодифицированности(); 
		ОбъектМодифицирован = RM_ОбщийМодуль.ОбъектМодифицирован(ДанныеДляЗаписиВИБ, ПравилаПроверкиМодифицированности, РежимЗаписи); 
		
		ДанныеДляЗаписиВИБ.ДополнительныеСвойства.Вставить("ОбъектМодифицирован", ОбъектМодифицирован); 
		ДанныеДляЗаписиВИБ.ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтоНовый); 
		ДанныеДляЗаписиВИБ.ДополнительныеСвойства.Вставить("ЭтоДокументТекущегоУзла", ЭтоДокументТекущегоУзла);
		
		ДанныеДляЗаписиВИБ.ОбменДанными.Загрузка = Истина; 
		
		РегистрыСведений.RM_СостояниеОтработкиНесоответствий.ЗафиксироватьСостояниеОбъекта(ДанныеДляЗаписиВИБ);   
	КонецЕсли;
КонецПроцедуры
