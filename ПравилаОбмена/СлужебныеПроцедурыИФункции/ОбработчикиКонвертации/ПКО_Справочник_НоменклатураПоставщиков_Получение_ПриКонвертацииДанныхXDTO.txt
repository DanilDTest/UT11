Процедура ПКО_Справочник_НоменклатураПоставщиков_Получение_ПриКонвертацииДанныхXDTO(ДанныеXDTO, ПолученныеДанные, КомпонентыОбмена)
	Владелец = Справочники.Партнеры.НеизвестныйПартнер;
	Если ДанныеXDTO.КлючевыеСвойства.Свойство("Владелец") 
		И ЗначениеЗаполнено(ДанныеXDTO.КлючевыеСвойства.Владелец) Тогда
		
		Контрагент = RM_ОбменДаннымиXDTOСервер.СтруктураОбъектаXDTOВДанныеИБ(КомпонентыОбмена,
							ДанныеXDTO.КлючевыеСвойства.Владелец,
							RM_ОбменДаннымиXDTOСервер.ПКОПоИмени(КомпонентыОбмена, "Справочник_Контрагенты_Получение"), 
							"ПолучитьСсылку");
							
		Если ЗначениеЗаполнено(Контрагент) И ОбщегоНазначения.СсылкаСуществует(Контрагент) Тогда
			Владелец = ДенежныеСредстваСервер.ПолучитьПартнераПоКонтрагенту(Контрагент);
		КонецЕсли;
	КонецЕсли; 
	
	ПолученныеДанные.ДополнительныеСвойства.Вставить("Владелец", Владелец);
						
	Если ДанныеXDTO.КлючевыеСвойства.Свойство("УпаковкаНоменклатуры") 
		И ЗначениеЗаполнено(ДанныеXDTO.КлючевыеСвойства.УпаковкаНоменклатуры) Тогда
		
		ПравилаЗаполнения = Новый Соответствие;
		ПравилаЗаполнения.Вставить("ЕдиницаИзмерения", "ЕдиницаИзмерения");
		ПравилаЗаполнения.Вставить("Коэффициент", "Коэффициент");
		ПравилаЗаполнения.Вставить("Наименование", "НаименованиеУпаковки");
		ДанныеУпаковки = ДанныеКоллекцииВВидеСтруктуры(ДанныеXDTO.КлючевыеСвойства.УпаковкаНоменклатуры, ПравилаЗаполнения);
			
		ПолученныеДанные.НаименованиеУпаковки = ДанныеУпаковки.НаименованиеУпаковки;
		ПолученныеДанные.НаименованиеНоменклатуры = ДанныеXDTO.КлючевыеСвойства.Наименование;
		
		Если Не ЗначениеЗаполнено(ДанныеУпаковки.Коэффициент) Тогда
			ПолученныеДанные.КоличествоУпаковок = 1;
			ПолученныеДанные.КоличествоБазовойЕдиницыИзмерения = 1;
		Иначе
			ПолученныеДанные.КоличествоУпаковок = 1;
			ПолученныеДанные.КоличествоБазовойЕдиницыИзмерения = ДанныеУпаковки.Коэффициент;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ДанныеУпаковки.ЕдиницаИзмерения) Тогда
			ПравилаЗаполнения = Новый Соответствие;
			ПравилаЗаполнения.Вставить("Код", "КодОКЕИБазовойЕдиницыИзмерения");
			ПравилаЗаполнения.Вставить("Наименование", "НаименованиеБазовойЕдиницыИзмерения");
			ДанныеЕдиницыИзмерения = ДанныеКоллекцииВВидеСтруктуры(ДанныеУпаковки.ЕдиницаИзмерения, ПравилаЗаполнения);
		
			ЗаполнитьЗначенияСвойств(ПолученныеДанные, ДанныеЕдиницыИзмерения);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры
