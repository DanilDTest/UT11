Процедура ПКО_Документ_ОстаткиТоваровПринятыхНаКомис_Отправка_ПриОтправкеДанных(ДанныеИБ, ДанныеXDTO, КомпонентыОбмена, СтекВыгрузки)
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ВводОстатковТовары.Номенклатура КАК Номенклатура,
		|	ВводОстатковТовары.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	&ТекстЗапросаХарактеристика,
		|	&ТекстЗапросаУпаковка,
		|	&ТекстЗапросаСерия,
		|	ВводОстатковТовары.Количество КАК Количество,
		|	ВводОстатковТовары.СтавкаНДС КАК СтавкаНДС,
		|	ВводОстатковТовары.СуммаБезНДС КАК СуммаБезНДС,
		|	ВводОстатковТовары.СуммаНДС КАК СуммаНДС,
		|	ВводОстатковТовары.НомерГТД.Код КАК НомерГТД,
		|	ВводОстатковТовары.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	ВводОстатковТовары.НомерГТД.СтранаПроисхождения КАК СтранаПроисхождения
		|ИЗ
		|	Документ.ВводОстатков.Товары КАК ВводОстатковТовары
		|ГДЕ
		|	ВводОстатковТовары.Ссылка = &Ссылка";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаХарактеристика",
		ПолучитьТекстЗапросаХарактеристики(КомпонентыОбмена, "ВводОстатковТовары"));
		
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаУпаковка",
		ПолучитьТекстЗапросаУпаковки(КомпонентыОбмена, "ВводОстатковТовары"));
		
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаСерия",
		ПолучитьТекстЗапросаСерии(КомпонентыОбмена, "ВводОстатковТовары"));
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Ссылка", ДанныеИБ.Ссылка);
	Товары = Запрос.Выполнить().Выгрузить();
	ЗаполнитьХарактеристикуУпаковкуВТЧ(КомпонентыОбмена, Товары);
	
	ДанныеXDTO.Вставить("Товары", Товары);
КонецПроцедуры
