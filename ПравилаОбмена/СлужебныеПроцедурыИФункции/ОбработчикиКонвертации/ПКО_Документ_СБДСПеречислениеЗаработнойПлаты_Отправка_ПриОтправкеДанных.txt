Процедура ПКО_Документ_СБДСПеречислениеЗаработнойПлаты_Отправка_ПриОтправкеДанных(ДанныеИБ, ДанныеXDTO, КомпонентыОбмена, СтекВыгрузки)
	Если СтекВыгрузки.Количество() > 1 Тогда
		Возврат;
	КонецЕсли;
	
	ВыгрузитьПодразделениеИзРеквизитаДокумента(КомпонентыОбмена, ДанныеИБ, ДанныеXDTO, "БанковскийСчет");
	
	ЗаполнитьДанныеВходящегоДокументаСБДС(ДанныеИБ, ДанныеXDTO);
	ДанныеШапки = ДанныеИБ.ДополнительныеСвойства.ДанныеШапки;
	
	ДатаВедомости = ДанныеИБ.ДатаВедомостиНаВыплатуЗарплаты;
	НомерВедомости = ДанныеИБ.НомерВедомостиНаВыплатуЗарплаты;
	Если (Не ЗначениеЗаполнено(ДатаВедомости)
			Или Не ЗначениеЗаполнено(НомерВедомости))
		И (ДанныеИБ.РасшифровкаПлатежа.Количество() > 0
			И ЗначениеЗаполнено(ДанныеИБ.РасшифровкаПлатежа[0].Ведомость)) Тогда
		
		НомерДатаВедомости = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеИБ.РасшифровкаПлатежа[0].Ведомость, "Номер, Дата");
		
		ДатаВедомости = НомерДатаВедомости.Дата;
		НомерВедомости = НомерДатаВедомости.Номер;		
	КонецЕсли;
	ВыгрузитьДополнительныеРеквизитыИСведения(КомпонентыОбмена, ДанныеИБ, ДанныеXDTO);
	
	Если ВерсияФорматаЧислом(КомпонентыОбмена.ВерсияФорматаОбмена) >= ВерсияФорматаЧислом("1.7")
		И ЗначениеЗаполнено(ДанныеИБ.ЗаявкаНаРасходованиеДенежныхСредств) Тогда
		ДанныеXDTO.Вставить("ЗаявкаНаРасходДенежныхСредств", ДанныеИБ.ЗаявкаНаРасходованиеДенежныхСредств);
	КонецЕсли;
	
	ТабРасшифровка = Новый ТаблицаЗначений;
	ТабРасшифровка.Колонки.Добавить("СтатьяДДС");
	ТабРасшифровка.Колонки.Добавить("Сумма");
	ТабРасшифровка.Колонки.Добавить("ДатаВедомостиНаВыплатуЗарплаты");
	ТабРасшифровка.Колонки.Добавить("НомерВедомостиНаВыплатуЗарплаты");
	
	НоваяСтрока = ТабРасшифровка.Добавить();
	НоваяСтрока.СтатьяДДС = ДанныеИБ.СтатьяДвиженияДенежныхСредств;
	НоваяСтрока.Сумма = ДанныеИБ.СуммаДокумента;
	НоваяСтрока.ДатаВедомостиНаВыплатуЗарплаты = ДатаВедомости;
	НоваяСтрока.НомерВедомостиНаВыплатуЗарплаты = НомерВедомости;
	ДанныеXDTO.Вставить("РасшифровкаПлатежа", ТабРасшифровка);
	
	Если ВерсияФорматаЧислом(КомпонентыОбмена.ВерсияФорматаОбмена) >= ВерсияФорматаЧислом("1.5")
		И ЗначениеЗаполнено(ДанныеИБ.КодВидаДохода) Тогда
		ДанныеXDTO.Вставить("КодВидаДохода", ДанныеИБ.КодВидаДохода);
	КонецЕсли;
КонецПроцедуры
