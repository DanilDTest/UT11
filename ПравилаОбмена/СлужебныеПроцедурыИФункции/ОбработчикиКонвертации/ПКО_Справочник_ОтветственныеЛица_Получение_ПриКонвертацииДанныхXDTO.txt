Процедура ПКО_Справочник_ОтветственныеЛица_Получение_ПриКонвертацииДанныхXDTO(ДанныеXDTO, ПолученныеДанные, КомпонентыОбмена)
	ПравилаЗаполнения = Новый Соответствие;
	ПравилаЗаполнения.Вставить("ФизическоеЛицо",             "ФизическоеЛицо");
	ПравилаЗаполнения.Вставить("Должность",                  "ДолжностьСсылка");
	ПравилаЗаполнения.Вставить("ВидОтветственногоЛица",      "ОтветственноеЛицо");
	ПравилаЗаполнения.Вставить("УполномоченноеЛицо",         "УполномоченноеЛицо");
	ПравилаЗаполнения.Вставить("ДатаНачала",                 "ДатаНачала");
	ПравилаЗаполнения.Вставить("ДатаОкончания",              "ДатаОкончания");
	ПравилаЗаполнения.Вставить("ВидДокументаПравоПодписи",   "ДокументПраваПодписи");
	ПравилаЗаполнения.Вставить("ДатаДокументаПраваПодписи",  "ДатаДокументаПраваПодписи");
	ПравилаЗаполнения.Вставить("НомерДокументаПраваПодписи", "НомерДокументаПраваПодписи");
	
	Если ДанныеXDTO.Свойство("Записи")
		И ЗначениеЗаполнено(ДанныеXDTO.Записи) Тогда
		
		ДатаНачала = Неопределено;
		Для Каждого Строка Из ДанныеXDTO.Записи Цикл
		
			Если ДатаНачала <> Неопределено И Строка.ДатаНачала < ДатаНачала Тогда
				Продолжить;
			КонецЕсли;
			
			ДатаНачала = Строка.ДатаНачала;
			СтруктураДанныхСтроки = ДанныеКоллекцииВВидеСтруктуры(Строка, ПравилаЗаполнения);
			
		КонецЦикла;
		
		КлючиСтруктуры = Новый Структура("ДатаНачала, ДатаОкончания, НомерДокументаПраваПодписи, ДатаДокументаПраваПодписи, ДокументПраваПодписи, ФизическоеЛицо, ДолжностьСсылка, ОтветственноеЛицо");
		Для Каждого КлючЗначение Из КлючиСтруктуры Цикл
			Если Не СтруктураДанныхСтроки.Свойство(КлючЗначение.Ключ) Тогда
				СтруктураДанныхСтроки.Вставить(КлючЗначение.Ключ);
			КонецЕсли;
		КонецЦикла;
		Если ТипЗнч(СтруктураДанныхСтроки.ДокументПраваПодписи) = Тип("Структура")
			И СтруктураДанныхСтроки.ДокументПраваПодписи.Свойство("Значение") Тогда
			
			СтруктураДанныхСтроки.ДокументПраваПодписи = СтруктураДанныхСтроки.ДокументПраваПодписи.Значение;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(ПолученныеДанные, СтруктураДанныхСтроки,
			"ДатаНачала, ДатаОкончания, НомерДокументаПраваПодписи, ДатаДокументаПраваПодписи, ДокументПраваПодписи");
		ПолученныеДанные.ДополнительныеСвойства.Вставить("ФизическоеЛицо",    СтруктураДанныхСтроки.ФизическоеЛицо);
		ПолученныеДанные.ДополнительныеСвойства.Вставить("ДолжностьСсылка",   СтруктураДанныхСтроки.ДолжностьСсылка);
		ПолученныеДанные.ДополнительныеСвойства.Вставить("ОтветственноеЛицо", СтруктураДанныхСтроки.ОтветственноеЛицо);
		ОснованиеПраваПодписи = НСтр("ru = '%1 №%2 от %3';
									|en = '%1 No%2 from %3'");
		ПолученныеДанные.ОснованиеПраваПодписи = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
													ОснованиеПраваПодписи,
													ПолученныеДанные.ДокументПраваПодписи,
													ПолученныеДанные.НомерДокументаПраваПодписи,
													ПолученныеДанные.ДатаДокументаПраваПодписи);
	КонецЕсли;
КонецПроцедуры
