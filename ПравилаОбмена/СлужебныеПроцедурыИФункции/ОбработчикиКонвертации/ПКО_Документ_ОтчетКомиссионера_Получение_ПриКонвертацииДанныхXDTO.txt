Процедура ПКО_Документ_ОтчетКомиссионера_Получение_ПриКонвертацииДанныхXDTO(ДанныеXDTO, ПолученныеДанные, КомпонентыОбмена)
	ПолученныеДанные.Проведен    = Истина;
	ПолученныеДанные.ДатаПлатежа = ПолученныеДанные.Дата;
	ДобавитьВалютуВДопСвойства(ПолученныеДанные, ДанныеXDTO);
	ДобавитьДоговорВДопСвойства(ПолученныеДанные, ДанныеXDTO, КомпонентыОбмена);
	
	ПолученныеДанные.Услуга = ЭлементПоУмолчанию(КомпонентыОбмена, "НоменклатураУслуга");
	
	ЗагрузитьОтветственноеЛицо(ПолученныеДанные, ДанныеXDTO, КомпонентыОбмена, "Руководитель");
	ЗагрузитьОтветственноеЛицо(ПолученныеДанные, ДанныеXDTO, КомпонентыОбмена, "ГлавныйБухгалтер");
	
	#Область Товары
	
	ПолученныеДанные.СуммаНДСВознаграждения = ДанныеXDTO.Товары.Итог("СуммаНДСВознаграждения");
	
	ПравилаЗаполнения = Новый Соответствие;
	ПравилаЗаполнения.Вставить("Номенклатура",           "Номенклатура");
	ПравилаЗаполнения.Вставить("Количество",             "Количество");
	ПравилаЗаполнения.Вставить("Покупатель",             "Покупатель");
	ПравилаЗаполнения.Вставить("Сумма",                  "Сумма");
	ПравилаЗаполнения.Вставить("СуммаВознаграждения",    "СуммаВознаграждения");
	ПравилаЗаполнения.Вставить("СуммаПередачи",          "СуммаПродажи");
	ПравилаЗаполнения.Вставить("Цена",                   "Цена");
	ПравилаЗаполнения.Вставить("ЦенаПередачи",           "ЦенаПродажи");
	ПравилаЗаполнения.Вставить("СтавкаНДС",              "СтавкаНДС");
	ПравилаЗаполнения.Вставить("СуммаНДС",               "СуммаНДС");
	ПравилаЗаполнения.Вставить("СуммаНДСВознаграждения", "СуммаНДСВознаграждения");
	Если ЗначениеФО("ИспользоватьХарактеристикиНоменклатуры", КомпонентыОбмена) Тогда
		ПравилаЗаполнения.Вставить("Характеристика",     "Характеристика");
	КонецЕсли;
	Если ЗначениеФО("ИспользоватьУпаковкиНоменклатуры", КомпонентыОбмена) Тогда
		ПравилаЗаполнения.Вставить("Упаковка",           "Упаковка");
		ПравилаЗаполнения.Вставить("КоличествоУпаковок", "КоличествоУпаковок");
	КонецЕсли;
	
	МассивСтрокТовары = Новый Массив;
	Для Каждого Строка Из ДанныеXDTO.Товары Цикл
		
		СтруктураДанныхСтроки = ДанныеКоллекцииВВидеСтруктуры(Строка, ПравилаЗаполнения);
		
		Если НЕ СтруктураДанныхСтроки.Свойство("КоличествоУпаковок") Тогда
			СтруктураДанныхСтроки.Вставить("КоличествоУпаковок", СтруктураДанныхСтроки.Количество);
		КонецЕсли;
		Если СтруктураДанныхСтроки.Свойство("Сумма") Тогда
			СтруктураДанныхСтроки.Вставить("Цена", СтруктураДанныхСтроки.Сумма / СтруктураДанныхСтроки.КоличествоУпаковок);
		КонецЕсли;
		Если СтруктураДанныхСтроки.Свойство("СуммаПродажи") Тогда
			СтруктураДанныхСтроки.Вставить("ЦенаПродажи", СтруктураДанныхСтроки.СуммаПродажи / СтруктураДанныхСтроки.КоличествоУпаковок);
		КонецЕсли;
			
		Если ДанныеXDTO.Свойство("Покупатели") И ЗначениеЗаполнено(ДанныеXDTO.Покупатели) Тогда
			СтруктураДанныхСтроки.Вставить("Покупатель");
			СтруктураДанныхСтроки.Вставить("ДатаСчетаФактурыКомиссионера");
			СтруктураДанныхСтроки.Вставить("НомерСчетаФактурыКомиссионера");
			
			ПравилаЗаполненияСФ = Новый Соответствие;
			ПравилаЗаполненияСФ.Вставить("Покупатель",  "Покупатель");
			ПравилаЗаполненияСФ.Вставить("ДатаСФ",      "ДатаСчетаФактурыКомиссионера");
			ПравилаЗаполненияСФ.Вставить("НомерСФ",     "НомерСчетаФактурыКомиссионера");
			ПравилаЗаполненияСФ.Вставить("СчетФактура", "СчетФактура");
		
			СтрокаПокупатель = ДанныеXDTO.Покупатели.Найти(Строка.КлючСтроки, "КлючСтроки");
			Если НЕ СтрокаПокупатель = Неопределено Тогда
				СтруктураДанныхСтрокиСФ = ДанныеКоллекцииВВидеСтруктуры(СтрокаПокупатель, ПравилаЗаполненияСФ);
				ЗаполнитьЗначенияСвойств(СтруктураДанныхСтроки, СтруктураДанныхСтрокиСФ);
				
				Если СтруктураДанныхСтрокиСФ.Свойство("СчетФактура")
					И (НЕ СтруктураДанныхСтрокиСФ.Свойство("ДатаСчетаФактурыКомиссионера")
					ИЛИ НЕ СтруктураДанныхСтрокиСФ.Свойство("НомерСчетаФактурыКомиссионера")) Тогда
					Если СтруктураДанныхСтрокиСФ.СчетФактура.Свойство("Номер") Тогда
						СтруктураДанныхСтроки.Вставить("НомерСчетаФактурыКомиссионера", СтруктураДанныхСтрокиСФ.СчетФактура.Номер);
					КонецЕсли;
					Если СтруктураДанныхСтрокиСФ.СчетФактура.Свойство("Дата") Тогда
						СтруктураДанныхСтроки.Вставить("ДатаСчетаФактурыКомиссионера",  СтруктураДанныхСтрокиСФ.СчетФактура.Дата);
					КонецЕсли;
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;	
		
		МассивСтрокТовары.Добавить(СтруктураДанныхСтроки);
		
	КонецЦикла;
	
	ПолученныеДанные.ДополнительныеСвойства.Вставить("Товары", МассивСтрокТовары);
	
	#КонецОбласти
КонецПроцедуры
