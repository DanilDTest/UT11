Процедура ПКО_Документ_ПКОРасчетыССотрудниками_Получение_ПриКонвертацииДанныхXDTO(ДанныеXDTO, ПолученныеДанные, КомпонентыОбмена)
	ВидРасчетов = "Подотчет";
	Если ДанныеXDTO.Свойство("ВидРасчетов") Тогда
		ВидРасчетов = ДанныеXDTO.ВидРасчетов.Значение;
	КонецЕсли;
	
	Если ВидРасчетов = "Подотчет" Тогда
		ПолученныеДанные.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтПодотчетника;
	Иначе
		ПолученныеДанные.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПогашениеЗаймаСотрудником;
	КонецЕсли;
	
	ДобавитьВалютуВДопСвойства(ПолученныеДанные, ДанныеXDTO);
	ЗагрузитьОтветственноеЛицо(ПолученныеДанные, ДанныеXDTO, КомпонентыОбмена, "ГлавныйБухгалтер");
	ПолученныеДанные.Проведен = Истина;
	ЗагрузитьДополнительныеРеквизиты(ПолученныеДанные, ДанныеXDTO, КомпонентыОбмена);
	
	// Расшифровка платежа
	МассивСтрокРасшифровкаПлатежа = Новый Массив;
	Если ЗначениеЗаполнено(ДанныеXDTO.РасшифровкаПлатежа) Тогда
		ПолученныеДанные.ДополнительныеСвойства.Вставить("СтатьяДвиженияДенежныхСредств", ДанныеXDTO.РасшифровкаПлатежа[0].СтатьяДДС);
	
		Т_РП = Новый Соответствие;
		Т_РП.Вставить("Сумма", "Сумма");
		Т_РП.Вставить("СуммаВзаиморасчетов", "Сумма");
		Т_РП.Вставить("СтатьяДвиженияДенежныхСредств", "СтатьяДДС");
		
		Для Каждого СтрокаXDTO Из ДанныеXDTO.РасшифровкаПлатежа Цикл
			СтруктураДанныхСтроки = Новый Структура;
			ЗаполнитьСтруктуруДанныхСтроки(Т_РП, СтрокаXDTO, СтруктураДанныхСтроки);
			МассивСтрокРасшифровкаПлатежа.Добавить(СтруктураДанныхСтроки);
		КонецЦикла;
		
		ПолученныеДанные.ДополнительныеСвойства.Вставить("РасшифровкаПлатежа", МассивСтрокРасшифровкаПлатежа);
	
	КонецЕсли;
КонецПроцедуры
