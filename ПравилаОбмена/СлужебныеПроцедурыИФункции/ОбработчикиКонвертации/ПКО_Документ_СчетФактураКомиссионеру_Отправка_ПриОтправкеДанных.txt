Процедура ПКО_Документ_СчетФактураКомиссионеру_Отправка_ПриОтправкеДанных(ДанныеИБ, ДанныеXDTO, КомпонентыОбмена, СтекВыгрузки)
	Если ДанныеИБ.Организация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеИБ.ДокументОснование, "Организация") Тогда
		ДанныеXDTO.КлючевыеСвойства.Вставить("Дата", НачалоДня(ДанныеИБ.Дата));
	КонецЕсли;
	
	Если СтекВыгрузки.Количество() > 1 Тогда
		Возврат;
	КонецЕсли;
	ДанныеXDTO.Вставить("ВидСчетаФактуры", "НаРеализацию");
	ДанныеXDTO.Вставить("Выставлен", ЗначениеЗаполнено(ДанныеИБ.ДатаВыставления));
	
	ДанныеXDTO.Вставить("Руководитель", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеИБ.Руководитель, "ФизическоеЛицо"));
	ДанныеXDTO.Вставить("ГлавныйБухгалтер", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеИБ.ГлавныйБухгалтер, "ФизическоеЛицо"));
	
	ДанныеXDTO.Вставить("СпособВыставления", ?(ДанныеИБ.ВыставленВЭлектронномВиде,"ВЭлектронномВиде", "НаБумажномНосителе"));
	
	
	ВыгрузитьДополнительныеРеквизитыИСведения(КомпонентыОбмена, ДанныеИБ, ДанныеXDTO);
	
	ДанныеXDTO.Вставить("НомерСчетаФактурыКомиссионера", Неопределено);
	Если ДанныеИБ.Покупатели.Количество() > 0 Тогда
		ДанныеXDTO.НомерСчетаФактурыКомиссионера = ДанныеИБ.Покупатели[0].НомерСчетаФактуры;
		КонтрагентСсылка = ДанныеИБ.Покупатели[0].Покупатель;
	Иначе
		КонтрагентСсылка = Неопределено;
		Если ТипЗнч(ДанныеИБ.Комиссионер) = Тип("СправочникСсылка.Контрагенты") Тогда
			КонтрагентСсылка = ДанныеИБ.Комиссионер;
		ИначеЕсли ТипЗнч(ДанныеИБ.ДокументОснование) = Тип("ДокументСсылка.ОтчетПоКомиссииМеждуОрганизациями") 
				И ДанныеИБ.ДокументОснование.РасчетыЧерезОтдельногоКонтрагента Тогда
			КонтрагентСсылка = ДанныеИБ.ДокументОснование.Контрагент;
		ИначеЕсли ТипЗнч(ДанныеИБ.Комиссионер) = Тип("СправочникСсылка.Организации") Тогда 
			КонтрагентСсылка = КонтрагентИзОрганизации(ДанныеИБ.Комиссионер, КомпонентыОбмена);
		КонецЕсли;
	КонецЕсли;
	ДанныеXDTO.Вставить("Контрагент", КонтрагентСсылка);
	
	ДокументыОснования = Новый ТаблицаЗначений;
	ДокументыОснования.Колонки.Добавить("ДокументОснование");
	СтрокаОснование = ДокументыОснования.Добавить();
	ИмяПКО = "Документ_ОтчетКомиссионера_Отправка";
	Если ТипЗнч(ДанныеИБ.ДокументОснование) = Тип("ДокументСсылка.ОтчетПоКомиссииМеждуОрганизациями") Тогда
		ИмяПКО = "ОтчетКомиссияМеждуОрганизациями_Комиссионер_Отправ";
	КонецЕсли;
	СтрокаОснование.ДокументОснование = Новый Структура("Значение, ИмяПКО",ДанныеИБ.ДокументОснование,ИмяПКО);
	ДанныеXDTO.Вставить("ДокументыОснования", ДокументыОснования);
	
	ПлатРасчДокументы = Новый ТаблицаЗначений;
	ПлатРасчДокументы.Колонки.Добавить("ДатаДокумента");
	ПлатРасчДокументы.Колонки.Добавить("НомерДокумента");
	Для Каждого СтрокаДокумента Из ДанныеИБ.ПлатежноРасчетныеДокументы Цикл
		СтрокаПлатРасчДокумент = ПлатРасчДокументы.Добавить();
		СтрокаПлатРасчДокумент.ДатаДокумента = СтрокаДокумента.ДатаПлатежноРасчетногоДокумента;
		СтрокаПлатРасчДокумент.НомерДокумента = СтрокаДокумента.НомерПлатежноРасчетногоДокумента;
	КонецЦикла;
	Если ПлатРасчДокументы.Количество() > 0 Тогда
		ДанныеXDTO.Вставить("ПлатежноРасчетныеДокументы", ПлатРасчДокументы);
	КонецЕсли;
КонецПроцедуры
