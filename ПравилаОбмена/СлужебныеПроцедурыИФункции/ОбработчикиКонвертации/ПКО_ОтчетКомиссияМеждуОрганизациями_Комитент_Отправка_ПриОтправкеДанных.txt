Процедура ПКО_ОтчетКомиссияМеждуОрганизациями_Комитент_Отправка_ПриОтправкеДанных(ДанныеИБ, ДанныеXDTO, КомпонентыОбмена, СтекВыгрузки)
	НомерДок = Лев(ДанныеИБ.Номер, 5) + "И" + Сред(ДанныеИБ.Номер, 7);
	ДанныеXDTO.КлючевыеСвойства.Вставить("Номер", НомерДок);
	
	Если СтекВыгрузки.Количество() > 1 Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДанныеИБ.СтавкаНДСВознаграждения) Тогда
		ДанныеXDTO.Вставить("СтавкаНДСВознаграждения", "БезНДС");
	КонецЕсли;
	
	ВыгрузитьПодразделение(ДанныеИБ, ДанныеXDTO);
	
	Если ДанныеИБ.РасчетыЧерезОтдельногоКонтрагента Тогда
		КонтрагентСсылка = ДанныеИБ.Контрагент;
	Иначе
		КонтрагентСсылка = КонтрагентИзОрганизации(ДанныеИБ.Организация, КомпонентыОбмена);
	КонецЕсли;
	ДанныеXDTO.Вставить("Контрагент", КонтрагентСсылка);
	
	#Область Договор
	
	ИнструкцияДоговор = Неопределено;
	Если ДанныеИБ.РасчетыЧерезОтдельногоКонтрагента
		И ЗначениеЗаполнено(ДанныеИБ.ДоговорПродажи)
		И ОбщегоНазначения.СсылкаСуществует(ДанныеИБ.ДоговорПродажи) Тогда
		
		ИнструкцияДоговор = Новый Структура;
		ИнструкцияДоговор.Вставить("ИмяПКО",   "Справочник_ДоговорыКонтрагентов");
		ИнструкцияДоговор.Вставить("Значение", ДанныеИБ.ДоговорПродажи);
		
	ИначеЕсли ЗначениеЗаполнено(ДанныеИБ.Договор) Тогда
		
		ДанныеДоговора = ДанныеДоговораМеждуОрганизациями(КомпонентыОбмена, ДанныеИБ.Договор);
		ИнструкцияДоговор = Новый Структура("Значение, ИмяПКО", ДанныеДоговора, "Справочник_ДоговорыКонтрагентов_ИзСтруктуры");
		
	Иначе
		ПараметрыПоУмолчанию = Новый Структура(ДоговорФиксированнаяСтруктураКлючей());
		ПараметрыПоУмолчанию.ВидДоговора              = "СКомитентом";
		ПараметрыПоУмолчанию.Организация              = ДанныеИБ.Комиссионер;
		ПараметрыПоУмолчанию.Контрагент               = КонтрагентСсылка;
		ПараметрыПоУмолчанию.ВалютаВзаиморасчетов     = ДанныеИБ.Валюта;
		ПараметрыПоУмолчанию.РасчетыВУсловныхЕдиницах = Ложь;
		
		ИнструкцияДоговор = ДоговорИнструкцияКонвертацииПоДаннымВзаиморасчетов(ПараметрыПоУмолчанию,
			КомпонентыОбмена,
			ДанныеИБ.Договор);
	КонецЕсли;
	ДанныеXDTO.Вставить("Договор", ИнструкцияДоговор);
	
	#КонецОбласти
	
	ДанныеXDTO.Вставить("СуммаВключаетНДС", Истина);
	
	ВыгрузитьКурсИКратностьВзаиморасчетов(ДанныеXDTO, ДанныеИБ.Валюта, ДанныеИБ.Дата);
	
	// Табличная часть
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ОтчетПоКомиссииМеждуОрганизациямиТовары.НомерСтроки КАК НомерСтрокиДокумента,
		|	ОтчетПоКомиссииМеждуОрганизациямиТовары.Номенклатура КАК Номенклатура,
		|	&ТекстЗапросаХарактеристика,
		|	&ТекстЗапросаУпаковка,
		|	ОтчетПоКомиссииМеждуОрганизациямиТовары.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ОтчетПоКомиссииМеждуОрганизациямиТовары.Количество КАК Количество,
		|	ВЫБОР
		|		КОГДА ОтчетПоКомиссииМеждуОрганизациямиТовары.Количество = 0
		|			ТОГДА 0
		|		ИНАЧЕ ВЫБОР
		|				КОГДА ОтчетПоКомиссииМеждуОрганизациямиТовары.СуммаСНДС / ОтчетПоКомиссииМеждуОрганизациямиТовары.Количество < 0.01
		|					ТОГДА 0.01
		|				ИНАЧЕ ВЫРАЗИТЬ(ОтчетПоКомиссииМеждуОрганизациямиТовары.СуммаСНДС / ОтчетПоКомиссииМеждуОрганизациямиТовары.Количество КАК ЧИСЛО(15, 2))
		|			КОНЕЦ
		|	КОНЕЦ КАК ЦенаПоступления,
		|	ОтчетПоКомиссииМеждуОрганизациямиТовары.СуммаСНДС КАК СуммаПоступления,
		|	ВЫБОР
		|		КОГДА ОтчетПоКомиссииМеждуОрганизациямиТовары.Количество = 0
		|			ТОГДА 0
		|		ИНАЧЕ ВЫБОР
		|				КОГДА ОтчетПоКомиссииМеждуОрганизациямиТовары.СуммаПродажи / ОтчетПоКомиссииМеждуОрганизациямиТовары.Количество < 0.01
		|					ТОГДА 0.01
		|				ИНАЧЕ ВЫРАЗИТЬ(ОтчетПоКомиссииМеждуОрганизациямиТовары.СуммаПродажи / ОтчетПоКомиссииМеждуОрганизациямиТовары.Количество КАК ЧИСЛО(15, 2))
		|			КОНЕЦ
		|	КОНЕЦ КАК Цена,
		|	ОтчетПоКомиссииМеждуОрганизациямиТовары.СуммаПродажи КАК Сумма,
		|	ОтчетПоКомиссииМеждуОрганизациямиТовары.СуммаПродажиНДС КАК СуммаНДС,
		|	ОтчетПоКомиссииМеждуОрганизациямиТовары.СтавкаНДС КАК СтавкаНДС,
		|	ОтчетПоКомиссииМеждуОрганизациямиТовары.СуммаВознаграждения КАК СуммаВознаграждения,
		|	ОтчетПоКомиссииМеждуОрганизациямиТовары.СуммаНДСВознаграждения КАК СуммаНДСВознаграждения,
		|	ОтчетПоКомиссииМеждуОрганизациямиТовары.ДатаСчетаФактурыКомиссионера КАК ДатаРеализации,
		|	ОтчетПоКомиссииМеждуОрганизациямиТовары.Покупатель КАК Покупатель
		|ИЗ
		|	Документ.ОтчетПоКомиссииМеждуОрганизациями.Товары КАК ОтчетПоКомиссииМеждуОрганизациямиТовары
		|ГДЕ
		|	ОтчетПоКомиссииМеждуОрганизациямиТовары.Ссылка = &Ссылка";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаХарактеристика",
		ПолучитьТекстЗапросаХарактеристики(КомпонентыОбмена, "ОтчетПоКомиссииМеждуОрганизациямиТовары"));
		
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаУпаковка",
		ПолучитьТекстЗапросаУпаковки(КомпонентыОбмена, "ОтчетПоКомиссииМеждуОрганизациямиТовары"));
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Ссылка", ДанныеИБ.Ссылка);
	
	Товары = Запрос.Выполнить().Выгрузить();
	
	ЗаполнитьХарактеристикуУпаковкуВТЧ(КомпонентыОбмена, Товары);
	
	Если Товары.Итог("СуммаНДСВознаграждения") <> ДанныеИБ.СуммаНДСВознаграждения Тогда
		
		БазаРаспределенияПоСуммеВознаграждения = Товары.ВыгрузитьКолонку("СуммаВознаграждения");
		РаспределениеНДСВознаграждения = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(ДанныеИБ.СуммаНДСВознаграждения, 
			БазаРаспределенияПоСуммеВознаграждения);
			
		Если ЗначениеЗаполнено(РаспределениеНДСВознаграждения) Тогда
			Товары.ЗагрузитьКолонку(РаспределениеНДСВознаграждения, "СуммаНДСВознаграждения");
		КонецЕсли;
			
	КонецЕсли;
	
	ДанныеXDTO.Вставить("Товары", Товары);
	
	ВыгрузитьДополнительныеРеквизитыИСведения(КомпонентыОбмена, ДанныеИБ, ДанныеXDTO);
КонецПроцедуры
