Процедура ПКО_Документ_РеализацияПС_Получение_ПередЗаписьюПолученныхДанных(ПолученныеДанные, ДанныеИБ, КонвертацияСвойств, КомпонентыОбмена)
	ЗагрузитьДополнительныеСведения(?(ДанныеИБ = Неопределено, ПолученныеДанные.ПолучитьСсылкуНового(), ДанныеИБ.Ссылка), 
		ПолученныеДанные.ДополнительныеСвойства, КомпонентыОбмена);
	
	
	// В случае наличия объекта в базе он и будет записан (а не ПолученныеДанные).
	// Шапка и табличная часть предварительно переносятся из ПолученныеДанные в ДанныеИБ:
	// шапка - в составе свойств из ПКС, табличная часть - полностью берется из полученных данных.
	Если ДанныеИБ <> Неопределено Тогда
	
		// Переносим те свойства, которые указаны в ПКС.
		ЗаполнитьСвойстваШапкиОбъекта(КонвертацияСвойств, ПолученныеДанные, ДанныеИБ);
	
		// Табличные части
		ДанныеИБ.ПодарочныеСертификаты.Загрузить(ПолученныеДанные.ПодарочныеСертификаты.Выгрузить());
		Если ДанныеИБ.ОплатаПлатежнымиКартами.Итог("Сумма") <> ПолученныеДанные.ОплатаПлатежнымиКартами.Итог("Сумма")
			Или ПолученныеДанные.ОплатаПлатежнымиКартами.Количество() <> ДанныеИБ.ОплатаПлатежнымиКартами.Количество() Тогда
			ДанныеИБ.ОплатаПлатежнымиКартами.Загрузить(ПолученныеДанные.ОплатаПлатежнымиКартами.Выгрузить());
		КонецЕсли;
	
		ПолученныеДанные   = Неопределено;
		ДанныеДляЗаписиВИБ = ДанныеИБ;
		
	Иначе
		ДанныеДляЗаписиВИБ = ПолученныеДанные;
	КонецЕсли;
	
	
	Для Каждого СтрокаОплаты Из ДанныеДляЗаписиВИБ.ОплатаПлатежнымиКартами Цикл
		Если Не ЗначениеЗаполнено(СтрокаОплаты.ЭквайринговыйТерминал) Тогда
			СтрокаОплаты.ЭквайринговыйТерминал = Справочники.ЭквайринговыеТерминалы.ПолучитьЭквайринговыйТерминалПоУмолчанию(ДанныеДляЗаписиВИБ.Организация);
		КонецЕсли;
	КонецЦикла;
	
	ДанныеДляЗаписиВИБ.Валюта = КомпонентыОбмена.ПараметрыКонвертации.ВалютаРегламентированногоУчета;
	ДанныеДляЗаписиВИБ.СуммаДокумента = ДанныеДляЗаписиВИБ.ПодарочныеСертификаты.Итог("Сумма");
КонецПроцедуры
