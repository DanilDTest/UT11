Процедура ПОД_Справочник_ДисконтныеКарты_Отправка_ПриОбработке(ДанныеИБ, ИспользованиеПКО, КомпонентыОбмена)
	ДанныеВидКарты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
					ДанныеИБ.Владелец, "ТипКарты, БонуснаяПрограммаЛояльности, Персонализирована");
	
	НомерКарты = "";				
	Если ДанныеВидКарты.ТипКарты = Перечисления.ТипыКарт.Штриховая Тогда
		НомерКарты = ДанныеИБ.Штрихкод;
	ИначеЕсли ДанныеВидКарты.ТипКарты = Перечисления.ТипыКарт.Магнитная Тогда
		НомерКарты = ДанныеИБ.МагнитныйКод;
	Иначе
		Если ЗначениеЗаполнено(ДанныеИБ.Штрихкод) Тогда
			НомерКарты = ДанныеИБ.Штрихкод;
		ИначеЕсли ЗначениеЗаполнено(ДанныеИБ.МагнитныйКод) Тогда
			НомерКарты = ДанныеИБ.МагнитныйКод;
		КонецЕсли;
	КонецЕсли;
	
	Если ВерсияФорматаЧислом(КомпонентыОбмена.ВерсияФорматаОбмена) < ВерсияФорматаЧислом("1.6")
		ИЛИ НЕ ЗначениеЗаполнено(НомерКарты) ИЛИ НЕ ДанныеВидКарты.Персонализирована Тогда
		ИспользованиеПКО.Очистить();
		Возврат;
	КонецЕсли;
	
	ДанныеИБ.ДополнительныеСвойства.Вставить("НомерКарты", НомерКарты);
	
	Если ЗначениеЗаполнено(ДанныеВидКарты.БонуснаяПрограммаЛояльности) Тогда
		ДанныеИБ.ДополнительныеСвойства.Вставить("ПрограммаЛояльности", ДанныеВидКарты.БонуснаяПрограммаЛояльности);
	КонецЕсли;
КонецПроцедуры
