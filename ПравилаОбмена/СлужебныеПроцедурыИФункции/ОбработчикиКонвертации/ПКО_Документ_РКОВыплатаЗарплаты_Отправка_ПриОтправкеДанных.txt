Процедура ПКО_Документ_РКОВыплатаЗарплаты_Отправка_ПриОтправкеДанных(ДанныеИБ, ДанныеXDTO, КомпонентыОбмена, СтекВыгрузки)
	Если СтекВыгрузки.Количество() > 1 Тогда
		Возврат;
	КонецЕсли;
	
	ВыгрузитьПодразделениеИзРеквизитаДокумента(КомпонентыОбмена, ДанныеИБ, ДанныеXDTO, "Касса");
	ВыгрузитьОтветственноеЛицо(КомпонентыОбмена, ДанныеИБ, ДанныеXDTO, "ГлавныйБухгалтер");
	ВыгрузитьОтветственноеЛицо(КомпонентыОбмена, ДанныеИБ, ДанныеXDTO, "Руководитель");
	ВыгрузитьДополнительныеРеквизитыИСведения(КомпонентыОбмена, ДанныеИБ, ДанныеXDTO);
	
	Если ВерсияФорматаЧислом(КомпонентыОбмена.ВерсияФорматаОбмена) >= ВерсияФорматаЧислом("1.7")
		И ЗначениеЗаполнено(ДанныеИБ.ЗаявкаНаРасходованиеДенежныхСредств) Тогда
		ДанныеXDTO.Вставить("ЗаявкаНаРасходДенежныхСредств", ДанныеИБ.ЗаявкаНаРасходованиеДенежныхСредств);
	КонецЕсли;
	
	ТабРасшифровка = Новый ТаблицаЗначений;
	ТабРасшифровка.Колонки.Добавить("СтатьяДДС");
	ТабРасшифровка.Колонки.Добавить("Сумма");
	ТабРасшифровка.Колонки.Добавить("ДатаВедомостиНаВыплатуЗарплаты");
	ТабРасшифровка.Колонки.Добавить("НомерВедомостиНаВыплатуЗарплаты");
	ТабРасшифровка.Колонки.Добавить("Сотрудник");
	
	НоваяСтрока = ТабРасшифровка.Добавить();
	НоваяСтрока.СтатьяДДС = ДанныеИБ.СтатьяДвиженияДенежныхСредств;
	НоваяСтрока.Сумма = ДанныеИБ.СуммаДокумента;
	
	НоваяСтрока.ДатаВедомостиНаВыплатуЗарплаты = ДанныеИБ.ДатаВедомостиНаВыплатуЗарплаты;
	НоваяСтрока.НомерВедомостиНаВыплатуЗарплаты = ДанныеИБ.НомерВедомостиНаВыплатуЗарплаты;
		
	Если ЗначениеЗаполнено(ДанныеИБ.Ведомость)
		И (НЕ ЗначениеЗаполнено(НоваяСтрока.ДатаВедомостиНаВыплатуЗарплаты) ИЛИ НЕ ЗначениеЗаполнено(НоваяСтрока.НомерВедомостиНаВыплатуЗарплаты)) Тогда
		ДанныеВедомость = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеИБ.Ведомость, "Дата, Номер");
		НоваяСтрока.НомерВедомостиНаВыплатуЗарплаты = ДанныеВедомость.Номер;
		НоваяСтрока.ДатаВедомостиНаВыплатуЗарплаты = ДанныеВедомость.Дата;
	КонецЕсли;
		
	Если ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыплатаЗарплатыРаботнику
		ИЛИ ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыплатаЗарплатыРаздатчиком Тогда
		НоваяСтрока.Сотрудник = ДанныеИБ.ПодотчетноеЛицо;
	КонецЕсли;	
	
	ДанныеXDTO.Вставить("РасшифровкаПлатежа", ТабРасшифровка);
КонецПроцедуры
