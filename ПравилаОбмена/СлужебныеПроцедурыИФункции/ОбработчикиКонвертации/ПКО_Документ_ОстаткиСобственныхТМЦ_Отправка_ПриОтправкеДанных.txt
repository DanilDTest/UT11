Процедура ПКО_Документ_ОстаткиСобственныхТМЦ_Отправка_ПриОтправкеДанных(ДанныеИБ, ДанныеXDTO, КомпонентыОбмена, СтекВыгрузки)
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ВводОстатковТовары.Номенклатура КАК Номенклатура,
		|	ВводОстатковТовары.ВидЗапасов.ТипЗапасов КАК ВидЗапасов,
		|	&ТекстЗапросаХарактеристика,
		|	&ТекстЗапросаУпаковка,
		|	&ТекстЗапросаСерия,
		|	ВЫБОР
		|		КОГДА УпаковкиЕдиницыИзмерения.Ссылка ЕСТЬ NULL
		|				ИЛИ УпаковкиЕдиницыИзмерения.Знаменатель = 0
		|			ТОГДА ВводОстатковТовары.Количество
		|		ИНАЧЕ ВЫРАЗИТЬ(ВводОстатковТовары.Количество * (УпаковкиЕдиницыИзмерения.Числитель / УпаковкиЕдиницыИзмерения.Знаменатель) КАК ЧИСЛО(18, 2))
		|	КОНЕЦ КАК Количество,
		|	ВводОстатковТовары.СтавкаНДС КАК СтавкаНДС,
		|	ВЫБОР
		|		КОГДА НЕ ВводОстатковТовары.СуммаБезНДС = 0
		|			ТОГДА ВводОстатковТовары.СуммаБезНДС
		|		ИНАЧЕ ВЫБОР
		|				КОГДА ВводОстатковТовары.Количество > 0
		|					ТОГДА -0.01
		|				ИНАЧЕ 0.01
		|			КОНЕЦ
		|	КОНЕЦ КАК СуммаБезНДС,
		|	ВводОстатковТовары.СуммаНДС КАК СуммаНДС,
		|	ВводОстатковТовары.НомерГТД.Код КАК НомерГТД,
		|	ВводОстатковТовары.НомерГТД.СтранаПроисхождения КАК СтранаПроисхождения,
		|	ВводОстатковТовары.ИдентификаторСтроки,
		|	СпрНоменклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения
		|ИЗ
		|	Документ.ВводОстатков.Товары КАК ВводОстатковТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
		|		ПО ВводОстатковТовары.Номенклатура = СпрНоменклатура.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УпаковкиЕдиницыИзмерения КАК УпаковкиЕдиницыИзмерения
		|		ПО ВводОстатковТовары.Упаковка = УпаковкиЕдиницыИзмерения.Ссылка
		|ГДЕ
		|	ВводОстатковТовары.Ссылка = &Ссылка";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаХарактеристика",
		ПолучитьТекстЗапросаХарактеристики(КомпонентыОбмена, "ВводОстатковТовары"));
		
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаУпаковка",
		ПолучитьТекстЗапросаУпаковки(КомпонентыОбмена, "ВводОстатковТовары"));
		
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаСерия",
		ПолучитьТекстЗапросаСерии(КомпонентыОбмена, "ВводОстатковТовары"));
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Ссылка", ДанныеИБ.Ссылка);
	Товары = Запрос.Выполнить().Выгрузить();
	ЗаполнитьХарактеристикуУпаковкуВТЧ(КомпонентыОбмена, Товары);
	ДанныеXDTO.Вставить("ТМЦ", Товары);
КонецПроцедуры
