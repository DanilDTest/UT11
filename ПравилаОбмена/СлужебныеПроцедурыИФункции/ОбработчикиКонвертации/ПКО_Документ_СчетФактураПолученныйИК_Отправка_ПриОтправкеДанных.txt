Процедура ПКО_Документ_СчетФактураПолученныйИК_Отправка_ПриОтправкеДанных(ДанныеИБ, ДанныеXDTO, КомпонентыОбмена, СтекВыгрузки)
	ДанныеXDTO.КлючевыеСвойства.Вставить("НомерВходящегоДокумента", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДанныеИБ.Номер, Ложь, Истина));
	
	Если ВерсияФорматаЧислом(КомпонентыОбмена.ВерсияФорматаОбмена) >= ВерсияФорматаЧислом("1.7") Тогда
		
		РеквизитыОрганизации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			ДанныеИБ.Организация,
			"ИНН, КПП, РегистрацияВНалоговомОргане");
			
		Если Не ЗначениеЗаполнено(РеквизитыОрганизации.КПП) 
			И ЗначениеЗаполнено(РеквизитыОрганизации.РегистрацияВНалоговомОргане) Тогда
			
			РеквизитыОрганизации.Вставить(
				"КПП", 
				ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РеквизитыОрганизации.РегистрацияВНалоговомОргане, "КПП"));
				
		КонецЕсли;
			
		ДанныеXDTO.Вставить("ИННКонтрагента", РеквизитыОрганизации.ИНН);
		ДанныеXDTO.Вставить("КППКонтрагента", РеквизитыОрганизации.КПП);
		
	КонецЕсли;
	
	ДокументОснование = Неопределено;
	Для Каждого СтрокаОснование Из ДанныеИБ.ДокументыОснования Цикл
		Если НЕ ЗначениеЗаполнено(СтрокаОснование.ДокументОснование) Тогда
			Продолжить;
		КонецЕсли;
		ТипДокументаОснования = ТипЗнч(СтрокаОснование.ДокументОснование);
		Если (ТипДокументаОснования = Тип("ДокументСсылка.ПередачаТоваровМеждуОрганизациями")
			Или ТипДокументаОснования = Тип("ДокументСсылка.ВозвратТоваровМеждуОрганизациями")
			Или ТипДокументаОснования = Тип("ДокументСсылка.ОтчетПоКомиссииМеждуОрганизациями"))
			И Не ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаОснование.ДокументОснование, "РасчетыЧерезОтдельногоКонтрагента") Тогда
			ДокументОснование = СтрокаОснование.ДокументОснование;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ТипЗнч(ДанныеИБ.Контрагент) = Тип("СправочникСсылка.Организации") Тогда
		Организация = ДанныеИБ.Контрагент;
		РеквизитыДокументаОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			ДокументОснование, "РасчетыЧерезОтдельногоКонтрагента, Контрагент, Организация");
	ИначеЕсли ТипДокументаОснования = Тип("ДокументСсылка.ОтчетПоКомиссииМеждуОрганизациями") Тогда
		РеквизитыДокументаОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			ДокументОснование, "РасчетыЧерезОтдельногоКонтрагента, Контрагент, Организация, Комиссионер");
		Если ДанныеИБ.Организация = РеквизитыДокументаОснования.Организация Тогда
			Организация = РеквизитыДокументаОснования.Комиссионер;
		Иначе
			Организация = РеквизитыДокументаОснования.Организация;
		КонецЕсли;
	Иначе
		РеквизитыДокументаОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			ДокументОснование, "РасчетыЧерезОтдельногоКонтрагента, Контрагент, Организация, ОрганизацияПолучатель");
	
		Организация = РеквизитыДокументаОснования.ОрганизацияПолучатель;
	КонецЕсли;
	ДанныеXDTO.КлючевыеСвойства.Вставить("Организация", Организация);
	
	
	// Контрагент
	Если РеквизитыДокументаОснования.РасчетыЧерезОтдельногоКонтрагента Тогда
		КонтрагентСсылка = РеквизитыДокументаОснования.Контрагент;
	Иначе
		КонтрагентСсылка = КонтрагентИзОрганизации(РеквизитыДокументаОснования.Организация, КомпонентыОбмена);
	КонецЕсли;
	КонтрагентИнструкция = Новый Структура("Значение, ИмяПКО", КонтрагентСсылка, "Справочник_Контрагенты");
	ДанныеXDTO.КлючевыеСвойства.Вставить("Контрагент", КонтрагентИнструкция);
	
	Если СтекВыгрузки.Количество() > 1 Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеИБ.Договор) Тогда
		Если ТипЗнч(ДанныеИБ.Договор) = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
			ДоговорИнструкция = Новый Структура("Значение, ИмяПКО",
				ДанныеИБ.Договор,
				"Справочник_ДоговорыКонтрагентов");	
		
		ИначеЕсли ТипЗнч(ДанныеИБ.Договор) = Тип("СправочникСсылка.ДоговорыМеждуОрганизациями") Тогда
			
			ДанныеДоговора = ДанныеДоговораМеждуОрганизациями(КомпонентыОбмена, ДанныеИБ.Договор);
			ДоговорИнструкция = Новый Структура("Значение, ИмяПКО", ДанныеДоговора, "Справочник_ДоговорыКонтрагентов_ИзСтруктуры");
	
		КонецЕсли;
		
		ДанныеXDTO.Вставить("Договор", ДоговорИнструкция);	
	КонецЕсли;
	
	ВыгрузитьДополнительныеРеквизитыИСведения(КомпонентыОбмена, ДанныеИБ, ДанныеXDTO);
	
	ДанныеXDTO.Вставить("ДатаПолучения", КонецДня(ДанныеИБ.Дата));
	ДанныеXDTO.Вставить("ВидСчетаФактуры", "НаПоступление");
	
	Если ДанныеИБ.ВыставленВЭлектронномВиде Тогда
		ДанныеXDTO.Вставить("СпособВыставления", "ВЭлектронномВиде");
	Иначе
		ДанныеXDTO.Вставить("СпособВыставления", "НаБумажномНосителе");
	КонецЕсли;
	
	// Суммы
	СуммаДокумента = 0;
	СуммаНДСДокумента = 0;
	
	Если ТипДокументаОснования = Тип("ДокументСсылка.ОтчетПоКомиссииМеждуОрганизациями")
		И ДанныеИБ.Организация = РеквизитыДокументаОснования.Организация Тогда
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	СУММА(ДокТовары.СуммаПродажи) КАК СуммаДокумента,
			|	СУММА(ДокТовары.СуммаПродажиНДС) КАК СуммаНДСДокумента
			|ИЗ
			|	Документ.ОтчетПоКомиссииМеждуОрганизациями.Товары КАК ДокТовары
			|ГДЕ
			|	ДокТовары.Ссылка = &Ссылка
			|	И ДокТовары.ДатаСчетаФактурыКомиссионера = &Дата
			|	И ДокТовары.Покупатель = &Покупатель
			|
			|СГРУППИРОВАТЬ ПО
			|	ДокТовары.Покупатель");
		Запрос.УстановитьПараметр("Ссылка", ДокументОснование);
		Запрос.УстановитьПараметр("Дата", НачалоДня(ДанныеИБ.Дата));
		Запрос.УстановитьПараметр("Покупатель", КонтрагентСсылка);
		
		Результат = Запрос.Выполнить().Выбрать();
		Если Результат.Следующий() Тогда
			СуммаДокумента    = Результат.СуммаДокумента;
			СуммаНДСДокумента = Результат.СуммаНДСДокумента;
		КонецЕсли;
	КонецЕсли;
	ДанныеXDTO.Вставить("Сумма", СуммаДокумента);	
	ДанныеXDTO.Вставить("СуммаНДС", СуммаНДСДокумента);
	
	// Документы основания
	ДокументыОснования = Новый ТаблицаЗначений();
	ДокументыОснования.Колонки.Добавить("ДокументОснование");
	
	ИмяПКО = "";
	Если ТипДокументаОснования = Тип("ДокументСсылка.ПередачаТоваровМеждуОрганизациями") Тогда
		ИмяПКО = "ПередачаМеждуОрганизациями_ПТУ_Отправка";
	ИначеЕсли ТипДокументаОснования = Тип("ДокументСсылка.ВозвратТоваровМеждуОрганизациями") Тогда
		ИмяПКО = "ВозвратМеждуОрганизациями_ОтПокупателя_Отправка";
	ИначеЕсли ТипДокументаОснования = Тип("ДокументСсылка.ОтчетПоКомиссииМеждуОрганизациями") Тогда
		Если ДанныеИБ.Организация = РеквизитыДокументаОснования.Организация Тогда
			ИмяПКО = "ОтчетКомиссияМеждуОрганизациями_Комитент_Отправка";
		Иначе
			ИмяПКО = "ОтчетКомиссияМеждуОрганизациями_Комиссионер_Отправ";
		КонецЕсли;
	КонецЕсли;
	СтрокаОснование = ДокументыОснования.Добавить();
	
	СтрокаОснование.ДокументОснование = Новый Структура("Значение, ИмяПКО",ДокументОснование,ИмяПКО);
	ДанныеXDTO.Вставить("ДокументыОснования", ДокументыОснования);
КонецПроцедуры
