Процедура ПКО_Документ_ЗаказНаПеремещение_Отправка_ПриОтправкеДанных(ДанныеИБ, ДанныеXDTO, КомпонентыОбмена, СтекВыгрузки)
	Если СтекВыгрузки.Количество() > 1 Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДанныеИБ.Подразделение) Тогда
		ДанныеXDTO.Вставить("ПодразделениеОтправитель",
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеИБ.СкладОтправитель, "Подразделение"));
	КонецЕсли;
	
	ДанныеXDTO.Вставить("ПодразделениеПолучатель",
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеИБ.СкладПолучатель, "Подразделение"));
			
	
	ВыгрузитьНалогообложениеНДС(КомпонентыОбмена, ДанныеИБ, ДанныеXDTO, Ложь, Ложь);
	
	ВыгрузитьДополнительныеРеквизитыИСведения(КомпонентыОбмена, ДанныеИБ, ДанныеXDTO, Истина);
	
	#Область Товары
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ЗаказНаПеремещениеТовары.НомерСтроки КАК НомерСтрокиДокумента,
		|	ЗаказНаПеремещениеТовары.Номенклатура КАК Номенклатура, 
		|	ЗаказНаПеремещениеТовары.ВариантОбеспечения КАК ВариантОбеспечения,
		|	&ТекстЗапросаХарактеристика,
		|	&ТекстЗапросаУпаковка,
		|	&ТекстЗапросаСерия,
		|	ЗаказНаПеремещениеТовары.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ЗаказНаПеремещениеТовары.Количество КАК Количество,
		|	ЗаказНаПеремещениеТовары.Отменено КАК Отменено, 
		|	ЗаказНаПеремещениеТовары.КодСтроки КАК КодСтроки
		|ИЗ
		|	Документ.ЗаказНаПеремещение.Товары КАК ЗаказНаПеремещениеТовары
		|ГДЕ
		|	ЗаказНаПеремещениеТовары.Ссылка = &Ссылка";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаХарактеристика",
		ПолучитьТекстЗапросаХарактеристики(КомпонентыОбмена, "ЗаказНаПеремещениеТовары"));
		
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаУпаковка",
		ПолучитьТекстЗапросаУпаковки(КомпонентыОбмена, "ЗаказНаПеремещениеТовары"));
		
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаСерия",
		ПолучитьТекстЗапросаСерии(КомпонентыОбмена, "ЗаказНаПеремещениеТовары"));
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Ссылка", ДанныеИБ.Ссылка);
	
	Товары = Запрос.Выполнить().Выгрузить();
	ЗаполнитьХарактеристикуУпаковкуВТЧ(КомпонентыОбмена, Товары);
		
	ДанныеXDTO.Вставить("Товары", Товары);
	
	#КонецОбласти        
	
	Если ЗначениеЗаполнено(ДанныеИБ.ДокументОснование) Тогда
		Если ТипЗнч(ДанныеИБ.ДокументОснование) = Тип("ДокументСсылка.ЗаказКлиента") Тогда
			ДанныеXDTO.Вставить("ДокументОснование",  Новый Структура("Значение, ИмяПКО", ДанныеИБ.ДокументОснование, "Документ_ЗаказКлиента_Отправка"));	
		КонецЕсли;
	КонецЕсли;
	
	СтруктураДополнительныхРеквизитов = Новый Структура;
	СтруктураДополнительныхРеквизитов.Вставить("ДлительностьПеремещения", ДанныеИБ.ДлительностьПеремещения);
	СтруктураДополнительныхРеквизитов.Вставить("ЖелаемаяДатаПоступления", ДанныеИБ.ЖелаемаяДатаПоступления);
	СтруктураДополнительныхРеквизитов.Вставить("МаксимальныйКодСтроки", ДанныеИБ.МаксимальныйКодСтроки);
	СтруктураДополнительныхРеквизитов.Вставить("ВремяДоставкиПо", ДанныеИБ.ВремяДоставкиПо);
	СтруктураДополнительныхРеквизитов.Вставить("АдресДоставкиПеревозчика", ДанныеИБ.АдресДоставкиПеревозчика);
	СтруктураДополнительныхРеквизитов.Вставить("АдресДоставкиЗначенияПолей", ДанныеИБ.АдресДоставкиЗначенияПолей);
	СтруктураДополнительныхРеквизитов.Вставить("АдресДоставкиПеревозчикаЗначенияПолей", ДанныеИБ.АдресДоставкиПеревозчикаЗначенияПолей);
	СтруктураДополнительныхРеквизитов.Вставить("ВремяДоставкиС", ДанныеИБ.ВремяДоставкиС);
	СтруктураДополнительныхРеквизитов.Вставить("ДополнительнаяИнформацияПоДоставке", ДанныеИБ.ДополнительнаяИнформацияПоДоставке);
	СтруктураДополнительныхРеквизитов.Вставить("АдресДоставки", ДанныеИБ.АдресДоставки);
	СтруктураДополнительныхРеквизитов.Вставить("ОсобыеУсловияПеревозки", ДанныеИБ.ОсобыеУсловияПеревозки);
	СтруктураДополнительныхРеквизитов.Вставить("ОсобыеУсловияПеревозкиОписание", ДанныеИБ.ОсобыеУсловияПеревозкиОписание);
	СтруктураДополнительныхРеквизитов.Вставить("RM_ПлановаяДатаОтгрузки", ДанныеИБ.RM_ПлановаяДатаОтгрузки);
	СтруктураДополнительныхРеквизитов.Вставить("АдресДоставкиЗначение", ДанныеИБ.АдресДоставкиЗначение);
	СтруктураДополнительныхРеквизитов.Вставить("АдресДоставкиПеревозчикаЗначение", ДанныеИБ.АдресДоставкиПеревозчикаЗначение);
	СтруктураДополнительныхРеквизитов.Вставить("RM_ДатаАвтоматическогоСозданияВозврата", ДанныеИБ.RM_ДатаАвтоматическогоСозданияВозврата);
	СтруктураДополнительныхРеквизитов.Вставить("RM_СодержитХозПродукцию", ДанныеИБ.RM_СодержитХозПродукцию);      
	
	ЗаполнитьЗначенияСвойств(СтруктураДополнительныхРеквизитов, ДанныеИБ);  
	ВыгрузитьДополнительныеРеквизитыСвойствоСтрокой(СтруктураДополнительныхРеквизитов, ДанныеXDTO);
КонецПроцедуры
