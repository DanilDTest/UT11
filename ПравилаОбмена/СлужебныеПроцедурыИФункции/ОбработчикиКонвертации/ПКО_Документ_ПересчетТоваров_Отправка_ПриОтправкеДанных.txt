Процедура ПКО_Документ_ПересчетТоваров_Отправка_ПриОтправкеДанных(ДанныеИБ, ДанныеXDTO, КомпонентыОбмена, СтекВыгрузки)
	Если СтекВыгрузки.Количество() > 1 Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеXDTO.Вставить("Подразделение", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеИБ.Склад, "Подразделение"));
	
	ВыгрузитьДополнительныеРеквизитыИСведения(КомпонентыОбмена, ДанныеИБ, ДанныеXDTO);
	
	#Область Товары
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ПересчетТоваровТовары.НомерСтроки КАК НомерСтрокиДокумента,
		|	ПересчетТоваровТовары.Номенклатура КАК Номенклатура,
		|	&ТекстЗапросаХарактеристика,
		|	&ТекстЗапросаУпаковка,
		|	&ТекстЗапросаСерия,
		|	ПересчетТоваровТовары.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ПересчетТоваровТовары.Количество КАК Количество,
		|	ПересчетТоваровТовары.КоличествоФакт КАК КоличествоФакт,
		|	ПересчетТоваровТовары.КоличествоУпаковокФакт КАК КоличествоУпаковокФакт
		|ИЗ
		|	Документ.ПересчетТоваров.Товары КАК ПересчетТоваровТовары
		|ГДЕ
		|	ПересчетТоваровТовары.Ссылка = &Ссылка";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаХарактеристика",
		ПолучитьТекстЗапросаХарактеристики(КомпонентыОбмена, "ПересчетТоваровТовары"));
		
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаУпаковка",
		ПолучитьТекстЗапросаУпаковки(КомпонентыОбмена, "ПересчетТоваровТовары"));
		
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаСерия",
		ПолучитьТекстЗапросаСерии(КомпонентыОбмена, "ПересчетТоваровТовары"));
		
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Ссылка", ДанныеИБ.Ссылка);
	
	Товары = Запрос.Выполнить().Выгрузить();
	
	ЗаполнитьХарактеристикуУпаковкуВТЧ(КомпонентыОбмена, Товары);
	
	Для Каждого Стр Из Товары Цикл
		Если ВерсияФорматаЧислом(КомпонентыОбмена.ВерсияФорматаОбмена) < ВерсияФорматаЧислом("1.6") Тогда
			Стр.КоличествоУпаковокФакт = Стр.КоличествоФакт;
		КонецЕсли;
	КонецЦикла;
	
	ДанныеXDTO.Вставить("Товары", Товары);
	
	#КонецОбласти
КонецПроцедуры
