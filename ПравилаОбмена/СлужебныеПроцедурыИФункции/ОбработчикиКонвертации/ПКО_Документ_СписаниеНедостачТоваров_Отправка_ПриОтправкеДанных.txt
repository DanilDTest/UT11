Процедура ПКО_Документ_СписаниеНедостачТоваров_Отправка_ПриОтправкеДанных(ДанныеИБ, ДанныеXDTO, КомпонентыОбмена, СтекВыгрузки)
	Если СтекВыгрузки.Количество() > 1 Тогда
		Возврат;
	КонецЕсли;
	
	СкладДляВыгрузки = СкладДляВыгрузки(ДанныеИБ.Склад, КомпонентыОбмена.ПараметрыКонвертации);
	ДанныеXDTO.Вставить("Склад", СкладДляВыгрузки);
	
	ВыгрузитьПодразделениеИзРеквизитаДокумента(КомпонентыОбмена, ДанныеИБ, ДанныеXDTO, "Склад");
	ВыгрузитьНалогообложениеНДС(КомпонентыОбмена, ДанныеИБ, ДанныеXDTO, Ложь, Ложь);
	ВыгрузитьДополнительныеРеквизитыИСведения(КомпонентыОбмена, ДанныеИБ, ДанныеXDTO);
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
		|	ВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ВидыЗапасов.Количество КАК Количество,
		|	ВидыЗапасов.НомерГТД.Код КАК НомерГТД,
		|	ВидыЗапасов.НомерГТД.СтранаПроисхождения КАК СтранаПроисхождения,
		|	ВЫБОР
		|		КОГДА ВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
		|			ТОГДА ""ВозвратнаяТара""
		|		КОГДА ВидыЗапасов.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
		|			ТОГДА ВЫРАЗИТЬ(""КомиссионныеТовары"" КАК СТРОКА(18))
		|		ИНАЧЕ ВЫРАЗИТЬ(""СобственныеТовары"" КАК СТРОКА(18))
		|	КОНЕЦ КАК ТипЗапасов,
		|	ВидыЗапасов.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
		|	ВидыЗапасов.АналитикаУчетаНоменклатуры.Серия КАК Серия
		|ПОМЕСТИТЬ ВидыЗапасов
		|ИЗ
		|	Документ.СписаниеНедостачТоваров.ВидыЗапасов КАК ВидыЗапасов
		|ГДЕ
		|	ВидыЗапасов.Ссылка = &Ссылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Товары.Номенклатура КАК Номенклатура,
		|	Товары.Характеристика КАК Характеристика,
		|	Товары.Серия КАК Серия,
		|	МАКСИМУМ(Товары.НомерСтроки) КАК НомерСтроки
		|ПОМЕСТИТЬ ТаблицаТоварыПоМаксСтроке
		|ИЗ
		|	Документ.СписаниеНедостачТоваров.Товары КАК Товары
		|ГДЕ
		|	Товары.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	Товары.Номенклатура,
		|	Товары.Характеристика,
		|	Товары.Серия
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВидыЗапасов.Номенклатура КАК Номенклатура,
		|	ВидыЗапасов.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ВидыЗапасов.НомерГТД КАК НомерГТД,
		|	ВидыЗапасов.СтранаПроисхождения КАК СтранаПроисхождения,
		|	ВидыЗапасов.ТипЗапасов КАК ТипЗапасов,
		|	ЕСТЬNULL(ПорядокСтрокТаблицыТовары.НомерСтроки, 0) КАК НомерСтрокиДокумента,
		|	ВидыЗапасов.Количество КАК Количество,
		|	0 КАК Сумма,
		|	0 КАК Цена,
		|	&ТекстЗапросаХарактеристика,
		|	&ТекстЗапросаСерия
		|ИЗ
		|	ВидыЗапасов КАК ВидыЗапасов
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаТоварыПоМаксСтроке КАК ПорядокСтрокТаблицыТовары
		|		ПО ВидыЗапасов.Номенклатура = ПорядокСтрокТаблицыТовары.Номенклатура
		|			И ВидыЗапасов.Характеристика = ПорядокСтрокТаблицыТовары.Характеристика
		|			И ВидыЗапасов.Серия = ПорядокСтрокТаблицыТовары.Серия
		|
		|УПОРЯДОЧИТЬ ПО
		|	ЕСТЬNULL(ПорядокСтрокТаблицыТовары.НомерСтроки, 0)";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаХарактеристика",
		ПолучитьТекстЗапросаХарактеристики(КомпонентыОбмена, "ВидыЗапасов"));
		
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаСерия",
		ПолучитьТекстЗапросаСерии(КомпонентыОбмена, "ВидыЗапасов"));
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Ссылка", ДанныеИБ.Ссылка);
	Товары = Запрос.Выполнить().Выгрузить();
	ЗаполнитьХарактеристикуУпаковкуВТЧ(КомпонентыОбмена, Товары, Истина, Ложь);
	
	ДанныеXDTO.Вставить("Товары", Товары);
КонецПроцедуры
