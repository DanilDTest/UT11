Процедура ПОД_Справочник_СостоянияЭД_Отправка_ПриОбработке(ДанныеИБ, ИспользованиеПКО, КомпонентыОбмена)
	Если ВерсияФорматаЧислом(КомпонентыОбмена.ВерсияФорматаОбмена) < ВерсияФорматаЧислом("1.7")
		ИЛИ ДанныеИБ.Количество() = 0  Тогда
		ИспользованиеПКО.Очистить();
		Возврат;	
	КонецЕсли;
	
	СсылкаНаОбъект = ДанныеИБ.Отбор.СсылкаНаОбъект.Значение;
	ИмяПКООснованиеЭД = ИмяПКОДляОбъектаДанных(КомпонентыОбмена, СсылкаНаОбъект, Неопределено);
	
	ТаблицаЗаписи = ДанныеИБ.Выгрузить();
	СостояниеЭлектронногоДокумента        = ТаблицаЗаписи[0].СостояниеВерсииЭД;
	СостояниеОбменаЭДНаНашейСтороне       = ТаблицаЗаписи[0].ДействияСНашейСтороны;
	СостояниеОбменаЭДНаСторонеКонтрагента = ТаблицаЗаписи[0].ДействияСоСтороныДругогоУчастника;
	
	Если НЕ ЗначениеЗаполнено(ИмяПКООснованиеЭД) Или НЕ ЗначениеЗаполнено(СостояниеЭлектронногоДокумента)
		Или НЕ ЗначениеЗаполнено(СостояниеОбменаЭДНаНашейСтороне)
		Или НЕ ЗначениеЗаполнено(СостояниеОбменаЭДНаСторонеКонтрагента)
		Или СостояниеОбменаЭДНаНашейСтороне = Перечисления.СводныеСостоянияЭД.ПригласитьКОбмену
		Или СостояниеОбменаЭДНаСторонеКонтрагента = Перечисления.СводныеСостоянияЭД.ПригласитьКОбмену
		Или СостояниеЭлектронногоДокумента = Перечисления.СостоянияВерсийЭД.ОжидаетсяСогласиеКонтрагента
		Или СостояниеЭлектронногоДокумента = Перечисления.СостоянияВерсийЭД.ПолученОтказОтКонтрагента Тогда
		ИспользованиеПКО.Очистить();
	Иначе
		ДанныеИБ.ДополнительныеСвойства.Вставить("ОснованиеЭлектронногоДокумента",
					Новый Структура("Значение, ИмяПКО", СсылкаНаОбъект, ИмяПКООснованиеЭД));
		ДанныеИБ.ДополнительныеСвойства.Вставить("СостояниеЭлектронногоДокумента", СостояниеЭлектронногоДокумента);
		ДанныеИБ.ДополнительныеСвойства.Вставить("СостояниеОбменаЭДНаНашейСтороне", СостояниеОбменаЭДНаНашейСтороне);
		ДанныеИБ.ДополнительныеСвойства.Вставить("СостояниеОбменаЭДНаСторонеКонтрагента", СостояниеОбменаЭДНаСторонеКонтрагента);
	КонецЕсли;
КонецПроцедуры
