Процедура ПКО_Документ_ЗаказНаПеремещение_Получение_ПередЗаписьюПолученныхДанных(ПолученныеДанные, ДанныеИБ, КонвертацияСвойств, КомпонентыОбмена)
	ЗаполнитьПодразделениеПоУмолчанию(ПолученныеДанные, ДанныеИБ, КомпонентыОбмена);
	
	ЗагрузитьДополнительныеСведения(?(ДанныеИБ = Неопределено, ПолученныеДанные.ПолучитьСсылкуНового(), ДанныеИБ.Ссылка), 
	                            ПолученныеДанные.ДополнительныеСвойства, КомпонентыОбмена);
	                                                       
	Если ДанныеИБ <> Неопределено Тогда
		// Переносим те свойства, которые указаны в ПКС
		ЗаполнитьСвойстваШапкиОбъекта(КонвертацияСвойств, ПолученныеДанные, ДанныеИБ);   
		                                                    
		
		ПоляТЧДляОбновления = "Упаковка, Количество, КоличествоУпаковок, Отменено, ВариантОбеспечения, НачалоОтгрузки, ОкончаниеПоступления";                                                   
		Если ПолученныеДанные.Товары.Количество() > 0 Тогда   
			Если НЕ ЗначениеЗаполнено(ПолученныеДанные.Товары[0].ВариантОбеспечения) Тогда  
				//Грузим из ВМС ОАЭ, от туда данные по обеспечению и отгрузке не передаются
				ПоляТЧДляОбновления = "Упаковка, Количество, КоличествоУпаковок, Отменено";
			КонецЕсли;
		
		КонецЕсли;
		
		// Товары
		МассивКлючевыхПолей = Новый Массив;
		МассивКлючевыхПолей.Добавить("Номенклатура");
		МассивКлючевыхПолей.Добавить("Характеристика");
		МассивКлючевыхПолей.Добавить("Серия");
		
		RM_ОбменДаннымиXDTOСервер.ЗаполнитьТабличнуюЧастьОбъектаНачальнымиДанными(
			ПолученныеДанные.Товары,
			ДанныеИБ.Товары,
			МассивКлючевыхПолей,
			Неопределено,
			ПоляТЧДляОбновления);
			
		ДанныеИБ.Товары.Загрузить(ПолученныеДанные.Товары.Выгрузить());
		
		ПолученныеДанные = Неопределено;
	КонецЕсли;
	
	ДанныеДляЗаписиВИБ = ?(ДанныеИБ = Неопределено, ПолученныеДанные, ДанныеИБ);
	Если НЕ ЗначениеЗаполнено(ДанныеДляЗаписиВИБ.Статус) Тогда
		ДанныеДляЗаписиВИБ.Статус = Перечисления.СтатусыВнутреннихЗаказов.КВыполнению;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(ДанныеДляЗаписиВИБ.СпособДоставки) Тогда
		ДанныеДляЗаписиВИБ.СпособДоставки = Перечисления.СпособыДоставки.Самовывоз;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(ДанныеДляЗаписиВИБ.ВариантПриемкиТоваров) Тогда
		ДанныеДляЗаписиВИБ.ВариантПриемкиТоваров = Перечисления.ВариантыПриемкиТоваров.РазделенаПоЗаказамИНакладным;
	КонецЕсли;
КонецПроцедуры
