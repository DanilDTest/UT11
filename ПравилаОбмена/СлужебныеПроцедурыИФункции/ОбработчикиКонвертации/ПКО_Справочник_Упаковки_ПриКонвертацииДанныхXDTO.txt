Процедура ПКО_Справочник_Упаковки_ПриКонвертацииДанныхXDTO(ДанныеXDTO, ПолученныеДанные, КомпонентыОбмена)
	Если ДанныеXDTO.КлючевыеСвойства.Свойство("Номенклатура") Тогда
	
		// Упаковки для набора упаковок выгружается как "Структура"
		// с подстановкой в качестве владельца - Номенклатуры из строки документа.
		// Проверим существование номенклатуры на момент загрузки.
		Номенклатура = RM_ОбменДаннымиXDTOСервер.СтруктураОбъектаXDTOВДанныеИБ(
				КомпонентыОбмена,
				ДанныеXDTO.КлючевыеСвойства.Номенклатура,
				RM_ОбменДаннымиXDTOСервер.ПКОПоИмени(КомпонентыОбмена, "Справочник_Номенклатура_Получение"), 
				"ПолучитьСсылку");
		
		Если ТипЗнч(Номенклатура) = Тип("СправочникСсылка.Номенклатура") Тогда
			НаборУпаковок = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "НаборУпаковок");
			
			Если ЗначениеЗаполнено(НаборУпаковок) И НЕ НаборУпаковок = Справочники.НаборыУпаковок.ИндивидуальныйДляНоменклатуры Тогда	
		    	ПолученныеДанные.Владелец = НаборУпаковок;
		    КонецЕсли;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ПолученныеДанные.Владелец) Тогда
			ПолученныеДанные.ДополнительныеСвойства.Вставить("Владелец", ДанныеXDTO.КлючевыеСвойства.Номенклатура);
		КонецЕсли;
	КонецЕсли;  
	
	
	Если ДанныеXDTO.КлючевыеСвойства.Свойство("Коэффициент")
		И ЗначениеЗаполнено(ДанныеXDTO.КлючевыеСвойства.Свойство("Коэффициент")) Тогда
		ПолученныеДанные.Числитель = ДанныеXDTO.КлючевыеСвойства.Коэффициент;
		ПолученныеДанные.Знаменатель = 1;
	КонецЕсли;
	
	Если ПолученныеДанные.Числитель = 0 Тогда
		ПолученныеДанные.Числитель = 1;
	КонецЕсли;    
	
	Если ПолученныеДанные.Знаменатель = 0 Тогда
		ПолученныеДанные.Знаменатель = 1 
	КонецЕсли;
	
	
	ЗаполнитьРеквизитыОбъектаПоДопРеквизитам(ДанныеXDTO, ПолученныеДанные); 
	Если ПолученныеДанные.Числитель > 1 Тогда
		ПолученныеДанные.ТипУпаковки = Перечисления.ТипыУпаковокНоменклатуры.Составная;    
	Иначе
		ПолученныеДанные.ТипУпаковки = Перечисления.ТипыУпаковокНоменклатуры.Конечная; 
	КонецЕсли;
КонецПроцедуры
