Процедура ПКО_Документ_ТаможеннаяДекларация_Получение_ПриКонвертацииДанныхXDTO(ДанныеXDTO, ПолученныеДанные, КомпонентыОбмена)
	ПолученныеДанные.Проведен = Истина;
	ПолученныеДанные.ВариантОформления = Перечисления.ХозяйственныеОперации.ОформлениеГТДСамостоятельно;
	ПолученныеДанные.Статус = Перечисления.СтатусыТаможенныхДеклараций.ВыпущеноСТаможни;
	ДобавитьВалютуВДопСвойства(ПолученныеДанные, ДанныеXDTO);
	ДобавитьВалютуВДопСвойства(ПолученныеДанные, ДанныеXDTO,,"ВалютаВзаиморасчетов");
	
	Если ДанныеXDTO.Свойство("ДоговорРегл") И ЗначениеЗаполнено(ДанныеXDTO.ДоговорРегл) Тогда
		ДобавитьДоговорВДопСвойства(ПолученныеДанные, ДанныеXDTO, КомпонентыОбмена, "ДоговорРегл");
	Иначе
		ДобавитьДоговорВДопСвойства(ПолученныеДанные, ДанныеXDTO, КомпонентыОбмена);
	КонецЕсли;
	
	ЗагрузитьДополнительныеРеквизиты(ПолученныеДанные, ДанныеXDTO, КомпонентыОбмена);
	
	// Товары 
	ПравилаЗаполнения = Новый Соответствие;
	ПравилаЗаполнения.Вставить("НомерРаздела",        "НомерРаздела");
	ПравилаЗаполнения.Вставить("Номенклатура",        "Номенклатура");
	ПравилаЗаполнения.Вставить("Количество",          "Количество");
	ПравилаЗаполнения.Вставить("ТаможеннаяСтоимость", "ТаможеннаяСтоимость");
	ПравилаЗаполнения.Вставить("СуммаНДС",            "СуммаНДС");
	ПравилаЗаполнения.Вставить("СуммаПошлины",        "СуммаПошлины");
	ПравилаЗаполнения.Вставить("СтранаПроисхождения", "СтранаПроисхождения");
	ПравилаЗаполнения.Вставить("ДокументПоступления", "ДокументПоступления");
	Если ЗначениеФО("ИспользоватьХарактеристикиНоменклатуры", КомпонентыОбмена) Тогда
		ПравилаЗаполнения.Вставить("Характеристика",     "Характеристика");
	КонецЕсли;
	Если ЗначениеФО("ИспользоватьУпаковкиНоменклатуры", КомпонентыОбмена) Тогда
		ПравилаЗаполнения.Вставить("Упаковка",           "Упаковка");
		ПравилаЗаполнения.Вставить("КоличествоУпаковок", "КоличествоУпаковок");
	КонецЕсли;
	Если ЗначениеФО("ИспользоватьСерииНоменклатуры", КомпонентыОбмена) Тогда
		ПравилаЗаполнения.Вставить("Серия",              "Серия");
	КонецЕсли;
	
	СуммаДокумента = 0;
	
	#Область Товары
	
	МассивСтрокТовары = Новый Массив;
	
	Если ДанныеXDTO.Свойство("Товары")
		И ЗначениеЗаполнено(ДанныеXDTO.Товары) Тогда
		
		Для Каждого Строка Из ДанныеXDTO.Товары Цикл
			СтруктураДанныхСтроки = ДанныеКоллекцииВВидеСтруктуры(Строка, ПравилаЗаполнения);
			
			Если НЕ СтруктураДанныхСтроки.Свойство("КоличествоУпаковок") Тогда
				СтруктураДанныхСтроки.Вставить("КоличествоУпаковок", СтруктураДанныхСтроки.Количество);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ДанныеXDTO.НомерДекларации) Тогда
				СтруктураДанныхСтроки.Вставить("НомерГТД", Новый Структура("Код, СтранаПроисхождения", ДанныеXDTO.НомерДекларации, Строка.СтранаПроисхождения));
			КонецЕсли;
			МассивСтрокТовары.Добавить(СтруктураДанныхСтроки);
			Если Не СтруктураДанныхСтроки.Свойство("СуммаПошлины") Тогда
				СтруктураДанныхСтроки.Вставить("СуммаПошлины", 0);
			КонецЕсли;
			Если Не СтруктураДанныхСтроки.Свойство("СуммаНДС") Тогда
				СтруктураДанныхСтроки.Вставить("СуммаНДС", 0);
			КонецЕсли;
	
			СуммаДокумента = СуммаДокумента + СтруктураДанныхСтроки.СуммаПошлины + СтруктураДанныхСтроки.СуммаНДС;
		КонецЦикла;
	КонецЕсли;
	Если МассивСтрокТовары.Количество() > 0 Тогда
		ПолученныеДанные.ДополнительныеСвойства.Вставить("Товары", МассивСтрокТовары);
	КонецЕсли;
	
	#КонецОбласти
	
	
	#Область Разделы
	
	// Разделы. 
	ПравилаЗаполнения = Новый Соответствие;
	ПравилаЗаполнения.Вставить("НомерРаздела",        "НомерРаздела");
	ПравилаЗаполнения.Вставить("СтавкаПошлины",       "СтавкаПошлины");
	ПравилаЗаполнения.Вставить("СтавкаНДС",           "СтавкаНДС");
	ПравилаЗаполнения.Вставить("ТаможеннаяСтоимость", "ТаможеннаяСтоимость");
	ПравилаЗаполнения.Вставить("СуммаНДС",            "СуммаНДС");
	ПравилаЗаполнения.Вставить("СуммаПошлины",        "СуммаПошлины");
	МассивСтрокРазделы = Новый Массив;
	Если ДанныеXDTO.Свойство("Разделы")
		И ЗначениеЗаполнено(ДанныеXDTO.Разделы) Тогда
		
		Для Каждого Строка Из ДанныеXDTO.Разделы Цикл
			СтруктураДанныхСтроки = ДанныеКоллекцииВВидеСтруктуры(Строка, ПравилаЗаполнения);
			
			Если СтруктураДанныхСтроки.Свойство("СуммаПошлины")
				И НЕ СтруктураДанныхСтроки.Свойство("СтавкаПошлины")
				И НЕ СтруктураДанныхСтроки.ТаможеннаяСтоимость = 0 Тогда
				СтавкаПошлины = 100 * СтруктураДанныхСтроки.СуммаПошлины / СтруктураДанныхСтроки.ТаможеннаяСтоимость;
				СтруктураДанныхСтроки.Вставить("СтавкаПошлины", СтавкаПошлины);
			КонецЕсли;
	
			// Страна происхождения - из раздела Товары
			Для Каждого СтрокаТовары Из ДанныеXDTO.Товары Цикл
				Если СтрокаТовары.НомерРаздела = Строка.НомерРаздела И ЗначениеЗаполнено(СтрокаТовары.СтранаПроисхождения) Тогда
					СтруктураДанныхСтроки.Вставить("СтранаПроисхождения", СтрокаТовары.СтранаПроисхождения);
					Прервать;
				КонецЕсли;
			КонецЦикла;
			МассивСтрокРазделы.Добавить(СтруктураДанныхСтроки);
		КонецЦикла;
	КонецЕсли;
	Если МассивСтрокРазделы.Количество() > 0 Тогда
		ПолученныеДанные.ДополнительныеСвойства.Вставить("Разделы", МассивСтрокРазделы);
	КонецЕсли;
	
	#КонецОбласти
	
	ПолученныеДанные.СуммаДокумента = СуммаДокумента + ПолученныеДанные.ТаможенныйСбор + ПолученныеДанные.ТаможенныйШтраф;
КонецПроцедуры
