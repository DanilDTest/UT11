Процедура ПКО_Документ_СчетФактураВыданныйАванс_Получение_ПриКонвертацииДанныхXDTO(ДанныеXDTO, ПолученныеДанные, КомпонентыОбмена)
	ПолученныеДанные.Проведен = Истина;
	Если Не ЗначениеЗаполнено(ПолученныеДанные.КодВидаОперации) Тогда
		ПолученныеДанные.КодВидаОперации = "02";
	КонецЕсли;
	
	Если ДанныеXDTO.Свойство("СпособВыставления") И ЗначениеЗаполнено(ДанныеXDTO.СпособВыставления) Тогда
		ПолученныеДанные.ВыставленВЭлектронномВиде = (ДанныеXDTO.СпособВыставления.Значение = "ВЭлектронномВиде");
	КонецЕсли;
	
	Если ДанныеXDTO.Свойство("ДокументыОснования") И ЗначениеЗаполнено(ДанныеXDTO.ДокументыОснования) Тогда
		Для Каждого Строка Из ДанныеXDTO.ДокументыОснования Цикл
	
			Если ЗначениеЗаполнено(Строка.ДокументОснование) И ТипЗнч(Строка.ДокументОснование) = Тип("Структура") Тогда
				ДокОснование = ЗначениеВИнструкциюПКО(КомпонентыОбмена, Строка.ДокументОснование);
				Если ДокОснование <> Неопределено Тогда
					ПолученныеДанные.ДополнительныеСвойства.Вставить("ДокументОснование", ДокОснование);
					Прервать;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	#Область Авансы
	
	Если ДанныеXDTO.Свойство("Авансы") И ЗначениеЗаполнено(ДанныеXDTO.Авансы) Тогда
		
		ПравилаЗаполнения = Новый Соответствие;
		ПравилаЗаполнения.Вставить("Номенклатура", "Номенклатура");
		ПравилаЗаполнения.Вставить("Содержание",   "Содержание");
		ПравилаЗаполнения.Вставить("СтавкаНДС",    "СтавкаНДС");
		ПравилаЗаполнения.Вставить("Сумма",        "Сумма");
		ПравилаЗаполнения.Вставить("СуммаНДС",     "СуммаНДС");
		Если ЗначениеФО("ИспользоватьХарактеристикиНоменклатуры", КомпонентыОбмена) Тогда
			ПравилаЗаполнения.Вставить("Характеристика",     "Характеристика");
		КонецЕсли;
	
		МассивСтрокАвансы = Новый Массив;
		Для Каждого Строка Из ДанныеXDTO.Авансы Цикл
			
			СтруктураДанныхСтроки = ДанныеКоллекцииВВидеСтруктуры(Строка, ПравилаЗаполнения);
					
			МассивСтрокАвансы.Добавить(СтруктураДанныхСтроки);
			
		КонецЦикла;
	
		ПолученныеДанные.ДополнительныеСвойства.Вставить("Авансы", МассивСтрокАвансы);
	КонецЕсли;
	
	#КонецОбласти
КонецПроцедуры
