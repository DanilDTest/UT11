Процедура ПКО_Документ_ПБДСРасчетыСКонтрагентами_Получение_ПриКонвертацииДанныхXDTO(ДанныеXDTO, ПолученныеДанные, КомпонентыОбмена)
	ПолученныеДанные.Проведен = Истина;
	ЗаполнитьДатуИНомерВходящегоДокумента(ПолученныеДанные);
	ДобавитьВалютуВДопСвойства(ПолученныеДанные, ДанныеXDTO);
	
	ПолученныеДанные.ПроведеноБанком = ЗначениеЗаполнено(ПолученныеДанные.ДатаПроведенияБанком);
	// ХозяйственнаяОперация
	СоответствиеЗначений = Новый Соответствие;
	СоответствиеЗначений.Вставить("СПокупателем",  Перечисления.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента);
	СоответствиеЗначений.Вставить("СПоставщиком",  Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтПоставщика);
	СоответствиеЗначений.Вставить("КредитыИЗаймы", Перечисления.ХозяйственныеОперации.ПрочееПоступлениеДенежныхСредств);
	СоответствиеЗначений.Вставить("Прочее",        Перечисления.ХозяйственныеОперации.ПрочееПоступлениеДенежныхСредств);
	
	ПолученныеДанные.ХозяйственнаяОперация = СоответствиеЗначений.Получить(ДанныеXDTO.ВидРасчетов.Значение);
	ЗагрузитьДополнительныеРеквизиты(ПолученныеДанные, ДанныеXDTO, КомпонентыОбмена);
	
	// РасшифровкаПлатежа
	ЗаполнятьДоговор = (ДанныеXDTO.ВидРасчетов.Значение = "СПокупателем" И КомпонентыОбмена.ПараметрыКонвертации.ИспользуютсяДоговорыСКлиентами)
					ИЛИ (ДанныеXDTO.ВидРасчетов.Значение = "СПоставщиком" И КомпонентыОбмена.ПараметрыКонвертации.ИспользуютсяДоговорыСПоставщиками);
	
	МассивСтрокРасшифровка = Новый Массив;
	Для Каждого Строка Из ДанныеXDTO.РасшифровкаПлатежа Цикл
		
		ПравилаЗаполнения = Новый Соответствие;
		ПравилаЗаполнения.Вставить("Сумма", "Сумма");
		ПравилаЗаполнения.Вставить("СуммаВзаиморасчетов", "СуммаВзаиморасчетов");
		ПравилаЗаполнения.Вставить("СтатьяДДС", "СтатьяДвиженияДенежныхСредств");
		ПравилаЗаполнения.Вставить("СтавкаНДС", "СтавкаНДС");
		ПравилаЗаполнения.Вставить("СуммаНДС", "СуммаНДС");
		
		СтруктураДанныхСтроки = ДанныеКоллекцииВВидеСтруктуры(Строка, ПравилаЗаполнения);
		ДобавитьВалютуВРасшифровкуПлатежа(СтруктураДанныхСтроки, Строка);
		
		ИнструкцияЗаказ = Неопределено;
		Если ЗначениеЗаполнено(Строка.ДокументРасчетов) Тогда
			ИнструкцияЗаказ = ЗначениеВИнструкциюПКО(КомпонентыОбмена, Строка.ДокументРасчетов);
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(ИнструкцияЗаказ) И ЗначениеЗаполнено(Строка.Заказ) Тогда
			ИнструкцияЗаказ = ЗначениеВИнструкциюПКО(КомпонентыОбмена, Строка.Заказ);
		КонецЕсли; 
		Если ЗначениеЗаполнено(ИнструкцияЗаказ) Тогда
		    СтруктураДанныхСтроки.Вставить("ОснованиеПлатежа", ИнструкцияЗаказ);
		    СтруктураДанныхСтроки.Вставить("Заказ", ИнструкцияЗаказ);	
		Иначе
			ДобавитьДоговорВРасшифровкуПлатежа(ЗаполнятьДоговор, СтруктураДанныхСтроки, Строка, "ОснованиеПлатежа");
		КонецЕсли;
		
		МассивСтрокРасшифровка.Добавить(СтруктураДанныхСтроки);
		
	КонецЦикла;
	ПолученныеДанные.ДополнительныеСвойства.Вставить("РасшифровкаПлатежа", МассивСтрокРасшифровка);
КонецПроцедуры
