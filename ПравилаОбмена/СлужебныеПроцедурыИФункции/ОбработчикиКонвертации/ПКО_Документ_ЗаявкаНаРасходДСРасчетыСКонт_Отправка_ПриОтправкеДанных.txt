Процедура ПКО_Документ_ЗаявкаНаРасходДСРасчетыСКонт_Отправка_ПриОтправкеДанных(ДанныеИБ, ДанныеXDTO, КомпонентыОбмена, СтекВыгрузки)
	Если СтекВыгрузки.Количество() > 1 Тогда
		Возврат;
	КонецЕсли;
	
	ВыгрузитьДополнительныеРеквизитыИСведения(КомпонентыОбмена, ДанныеИБ, ДанныеXDTO);
	
	Если ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОплатаДенежныхСредствВДругуюОрганизацию
		Или ДанныеИБ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствВДругуюОрганизацию Тогда
		
		ДанныеКонтрагента = ПолучитьДанныеКонтрагентаПоБанкСчетуОрганизации(ДанныеИБ.БанковскийСчетПолучатель, КомпонентыОбмена);
	
		ДанныеXDTO.Вставить("Контрагент",     ДанныеКонтрагента.Контрагент);
		ДанныеXDTO.Вставить("БанковскийСчет", ДанныеКонтрагента.БанковскийСчетКонтрагента);
		
		КонтрагентСсылка = ДанныеКонтрагента.Контрагент;
		
	Иначе
		
		ДанныеXDTO.Вставить("БанковскийСчет", ДанныеИБ.БанковскийСчетКонтрагента);
		ДанныеXDTO.Вставить("Контрагент",     ДанныеИБ.Контрагент);
		
		КонтрагентСсылка = ДанныеИБ.Контрагент;
		
	КонецЕсли;
		
	ВидРасчетов = "";
	ХозОперацияДокумента = ДанныеИБ.ХозяйственнаяОперация;
	
	Если ХозОперацияДокумента = Перечисления.ХозяйственныеОперации.ОплатаПоставщику
		Или ХозОперацияДокумента = Перечисления.ХозяйственныеОперации.ОплатаДенежныхСредствВДругуюОрганизацию Тогда
		ВидРасчетов = "СПоставщиком";
	ИначеЕсли ХозОперацияДокумента = Перечисления.ХозяйственныеОперации.ВозвратОплатыКлиенту
		Или ХозОперацияДокумента = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствВДругуюОрганизацию Тогда
		ВидРасчетов = "СПокупателем";
	ИначеЕсли ХозОперацияДокумента = Перечисления.ХозяйственныеОперации.ОплатаПоКредитам Тогда
		ВидРасчетов = "КредитыИЗаймы";
	КонецЕсли;
	
	ДанныеXDTO.Вставить("ВидРасчетов", ВидРасчетов);
	
	Запрос = Новый Запрос("ВЫБРАТЬ
		|	РасшифровкаПлатежа.Сумма КАК Сумма,
		|	РасшифровкаПлатежа.Заказ,
		|	РасшифровкаПлатежа.ВалютаВзаиморасчетов,
		|	РасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств Как СтатьяДДС,
		|	РасшифровкаПлатежа.СуммаВзаиморасчетов,
		|	РасшифровкаПлатежа.СтавкаНДС,
		|	РасшифровкаПлатежа.СуммаНДС
		|ИЗ
		|	Документ.ЗаявкаНаРасходованиеДенежныхСредств.РасшифровкаПлатежа КАК РасшифровкаПлатежа
		|ГДЕ
		|	РасшифровкаПлатежа.Ссылка = &Ссылка");
	
	Запрос.УстановитьПараметр("Ссылка", ДанныеИБ.Ссылка);
	РасшифровкаПлатежа = Запрос.Выполнить().Выгрузить();
	
	РасшифровкаПлатежа.Колонки.Добавить("Договор");
	РасшифровкаПлатежа.Колонки.Добавить("ДокументРасчетов");
	
	Для Каждого СтрокаТаблицы Из РасшифровкаПлатежа Цикл
		
		Если ЗначениеЗаполнено(СтрокаТаблицы.Заказ) Тогда
			
			ТипЗаказа = ТипЗнч(СтрокаТаблицы.Заказ);
			
			Если ТипЗаказа = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
				СтрокаТаблицы.Договор = СтрокаТаблицы.Заказ;
			Иначе
				
				Если СтрокаТаблицы.Заказ.Метаданные().Реквизиты.Найти("Договор") <> Неопределено Тогда
				
					ТекДоговор = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаТаблицы.Заказ, "Договор");
					
					Если ТипЗнч(ТекДоговор) = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
						СтрокаТаблицы.Договор = ТекДоговор;
					КонецЕсли;
					
				КонецЕсли;
				
				ИмяПКО = "";
				ТипРасчетов = Перечисления.ТипыРасчетовСПартнерами.РасчетыСКлиентом;
				
				Если ТипЗаказа = Тип("ДокументСсылка.ВозвратТоваровОтКлиента") Тогда
					ИмяПКО = "Документ_ВозвратТоваровОтКлиента_Отправка";
				ИначеЕсли ТипЗаказа = Тип("ДокументСсылка.ТаможеннаяДекларацияИмпорт") Тогда
					ИмяПКО = "Документ_ТаможеннаяДекларация_Отправка";
				ИначеЕсли ТипЗаказа = Тип("ДокументСсылка.ПриобретениеТоваровУслуг") Тогда
					ИмяПКО = "Документ_ПоступлениеТоваровУслуг_Отправка";
					ТипРасчетов = Перечисления.ТипыРасчетовСПартнерами.РасчетыСПоставщиком;
				ИначеЕсли ТипЗаказа = Тип("ДокументСсылка.ОтчетКомиссионера") Тогда
					ИмяПКО = "Документ_ОтчетКомиссионера_Отправка";	
				ИначеЕсли ТипЗаказа = Тип("ДокументСсылка.ПервичныйДокумент")
					И ВерсияФорматаЧислом(КомпонентыОбмена.ВерсияФорматаОбмена) >= ВерсияФорматаЧислом("1.7") Тогда
					ИмяПКО = "Документ_ПервичныйДокумент_Отправка";
				ИначеЕсли ТипЗаказа = Тип("ДокументСсылка.ПриобретениеУслугПрочихАктивов") Тогда
					ИмяПКО = "Документ_ПоступлениеУслугПрочихАктивов_Отправка";
				ИначеЕсли ТипЗаказа = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
					ИмяПКО = "Документ_РеализацияТоваровУслуг_Отправка";
				ИначеЕсли ТипЗаказа = Тип("ДокументСсылка.ВозвратТоваровПоставщику") Тогда
					ИмяПКО = "Документ_ВозвратТоваровПоставщику_Отправка";
					ТипРасчетов = Перечисления.ТипыРасчетовСПартнерами.РасчетыСПоставщиком;
					
				
						
				ИначеЕсли ТипЗаказа = Тип("ДокументСсылка.ЗаказПоставщику") Тогда
					ИмяПКО = "Документ_ЗаказПоставщику_Отправка";
					ТипРасчетов = Перечисления.ТипыРасчетовСПартнерами.РасчетыСПоставщиком;
				ИначеЕсли ТипЗаказа = Тип("ДокументСсылка.ОтчетКомитенту") Тогда
					ИмяПКО = "Документ_ОтчетКомитенту_Отправка";
					ТипРасчетов = Перечисления.ТипыРасчетовСПартнерами.РасчетыСПоставщиком;
				ИначеЕсли ТипЗаказа = Тип("ДокументСсылка.РеализацияУслугПрочихАктивов") Тогда
					ИмяПКО = "Документ_РеализацияУслугПрочихАктивов_Отправка";
				ИначеЕсли ТипЗаказа = Тип("ДокументСсылка.ОтчетКомитентуОСписании") Тогда
					ИмяПКО = "Документ_ОтчетКомитентуОСписании_Отправка";
					ТипРасчетов = Перечисления.ТипыРасчетовСПартнерами.РасчетыСПоставщиком;
				ИначеЕсли ТипЗаказа = Тип("ДокументСсылка.АктВыполненныхРабот") Тогда
					Если СвойствоФорматаОбмена(КомпонентыОбмена, "Документ.АктВыполненныхРабот") Тогда
						ИмяПКО = "Документ_АктВыполненныхРабот_Отправка";
					Иначе
						ИмяПКО = "Документ_АктВыполненныхРаботРТУ_Отправка";
					КонецЕсли;
				ИначеЕсли ТипЗаказа = Тип("ДокументСсылка.ОтчетКомиссионераОСписании") Тогда
					ИмяПКО = "Документ_ОтчетКомиссионераОСписании_Отправка";
				ИначеЕсли ТипЗаказа = Тип("ДокументСсылка.ЗаказКлиента") Тогда
					ИмяПКО = "Документ_ЗаказКлиента_Отправка";
				ИначеЕсли ТипЗаказа = Тип("ДокументСсылка.ПередачаТоваровМеждуОрганизациями") Тогда
					ОрганизацииИнтеркампани = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СтрокаТаблицы.Заказ, "Организация, ОрганизацияПолучатель");
					Если ОрганизацииИнтеркампани.Организация = КонтрагентСсылка Тогда
						ИмяПКО = "ПередачаМеждуОрганизациями_РТУ_Отправка";
					ИначеЕсли ОрганизацииИнтеркампани.ОрганизацияПолучатель = КонтрагентСсылка Тогда
						ИмяПКО = "ПередачаМеждуОрганизациями_ПТУ_Отправка";
						ТипРасчетов = Перечисления.ТипыРасчетовСПартнерами.РасчетыСПоставщиком;
					КонецЕсли;
				ИначеЕсли ТипЗаказа = Тип("ДокументСсылка.ОтчетПоКомиссииМеждуОрганизациямиОСписании") Тогда
					ОрганизацииИнтеркампани = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СтрокаТаблицы.Заказ, "Организация, Комиссионер");
					Если ОрганизацииИнтеркампани.Организация = КонтрагентСсылка Тогда
						ИмяПКО = "Документ_ОтчетКомиссияОрганизСписаниеКомитент";
						ТипРасчетов = Перечисления.ТипыРасчетовСПартнерами.РасчетыСПоставщиком;
					ИначеЕсли ОрганизацииИнтеркампани.Комиссионер = КонтрагентСсылка Тогда
						ИмяПКО = "Документ_ОтчетКомиссияОрганизСписаниеКомиссионер";
					КонецЕсли;
				ИначеЕсли ТипЗаказа = Тип("ДокументСсылка.ОтчетПоКомиссииМеждуОрганизациями") Тогда
					ОрганизацииИнтеркампани = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СтрокаТаблицы.Заказ, "Организация, Комиссионер");
					Если ОрганизацииИнтеркампани.Организация = КонтрагентСсылка Тогда
						ИмяПКО = "ОтчетКомиссияМеждуОрганизациями_Комиссионер_Отправ";
					ИначеЕсли ОрганизацииИнтеркампани.Комиссионер = КонтрагентСсылка Тогда
						ИмяПКО = "ОтчетКомиссияМеждуОрганизациями_Комитент_Отправка";
						ТипРасчетов = Перечисления.ТипыРасчетовСПартнерами.РасчетыСПоставщиком;
					КонецЕсли;
				КонецЕсли;
				
				Если Не ПустаяСтрока(ИмяПКО) Тогда
					СтрокаТаблицы.ДокументРасчетов = Новый Структура("Значение, ИмяПКО", СтрокаТаблицы.Заказ, ИмяПКО);
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаТаблицы.Договор) Тогда
			Продолжить;
		КонецЕсли;
		
		РасчетыВУсловныхЕдиницах = Ложь;
		ВидДоговора = "";
	
		ПолучитьПараметрыДоговораПоЗаказу(СтрокаТаблицы.Заказ, ТипРасчетов, ВидДоговора, РасчетыВУсловныхЕдиницах);
		
		ПараметрыПоУмолчанию = Новый Структура(ДоговорФиксированнаяСтруктураКлючей());
		ПараметрыПоУмолчанию.Организация              = ДанныеИБ.Организация;
		ПараметрыПоУмолчанию.ВалютаВзаиморасчетов     = СтрокаТаблицы.ВалютаВзаиморасчетов;
		ПараметрыПоУмолчанию.РасчетыВУсловныхЕдиницах = РасчетыВУсловныхЕдиницах;
		ПараметрыПоУмолчанию.ВидДоговора              = ВидДоговора; 
		ПараметрыПоУмолчанию.Контрагент               = КонтрагентСсылка;
		
		СтрокаТаблицы.Договор = ДоговорИнструкцияКонвертацииПоДаннымВзаиморасчетов(ПараметрыПоУмолчанию, КомпонентыОбмена, СтрокаТаблицы.Заказ, ДанныеИБ.Ссылка);
		
	КонецЦикла;
	
	ДанныеXDTO.Вставить("РасшифровкаПлатежа", РасшифровкаПлатежа);
КонецПроцедуры
