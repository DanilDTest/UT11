Процедура ПКО_Документ_ВЗ_ОрганизацияКредитор_ПриОтправкеДанных(ДанныеИБ, ДанныеXDTO, КомпонентыОбмена, СтекВыгрузки)
	ЭтоВзаимозачетДвухОрганизаций = ДанныеИБ.ДополнительныеСвойства.Свойство("ВзаимозачетСУчастиемДвухОрганизаций");
		
	Если ЭтоВзаимозачетДвухОрганизаций Тогда
		СсылкаОрганизация = ДанныеИБ.Организация;
		СсылкаДебитор     = ДанныеИБ.КонтрагентДебитор;
		СсылкаКредитор    = ДанныеИБ.ОрганизацияКредитор;
		ВидОперации       = "ПроведениеВзаимозачета";
	Иначе
		СсылкаОрганизация = ДанныеИБ.КонтрагентКредитор;
		СсылкаДебитор     = ДанныеИБ.Организация;
		СсылкаКредитор    = ДанныеИБ.КонтрагентДебитор;
		ВидОперации       = "ПереносЗадолженности";
	КонецЕсли;
	
	Если СтекВыгрузки.Количество() > 1 Тогда
		ДанныеXDTO.Вставить("Номер", Лев(ДанныеИБ.Номер, 5) + "ВК" + Сред(ДанныеИБ.Номер, 8));
		ДанныеXDTO.Вставить("Организация", СсылкаОрганизация);
		Возврат;
	КонецЕсли;
	
	ДанныеXDTO.Вставить("ВидОперации", ВидОперации);
	
	ДанныеXDTO.КлючевыеСвойства.Вставить("Номер", Лев(ДанныеИБ.Номер, 5) + "ВК" + Сред(ДанныеИБ.Номер, 8));
	ДанныеXDTO.КлючевыеСвойства.Вставить("Организация", СсылкаОрганизация);
	
	ВалютаРеглУчета = ВалютаРегламентированногоУчета(КомпонентыОбмена);
	ДанныеXDTO.Вставить("Валюта", ВалютаРеглУчета);
	ВыгрузитьКурсИКратностьДокумента(ДанныеXDTO, ВалютаРеглУчета, ДанныеИБ.Дата);
	
	СуммаДебиторскойЗадолженности  = ДанныеИБ.ДебиторскаяЗадолженность.Итог("СуммаРегл");
	СуммаКредиторскойЗадолженности = ДанныеИБ.КредиторскаяЗадолженность.Итог("СуммаРегл");
	ДанныеXDTO.Вставить("СуммаДебиторскойЗадолженности", СуммаДебиторскойЗадолженности);
	ДанныеXDTO.Вставить("СуммаКредиторскойЗадолженности", СуммаКредиторскойЗадолженности);
	
	ВыгрузитьПодразделение(ДанныеИБ, ДанныеXDTO);
	
	Если ТипЗнч(СсылкаДебитор) = Тип("СправочникСсылка.Организации") Тогда
		СсылкаНаКонтрагента = КонтрагентИзОрганизации(СсылкаДебитор, КомпонентыОбмена);
		ДанныеXDTO.Вставить("КонтрагентДебитор", СсылкаНаКонтрагента);
	Иначе
		ДанныеXDTO.Вставить("КонтрагентДебитор", СсылкаДебитор);
	КонецЕсли;
	
	Если ТипЗнч(СсылкаКредитор) = Тип("СправочникСсылка.Организации") Тогда
		СсылкаНаКонтрагента = КонтрагентИзОрганизации(СсылкаКредитор, КомпонентыОбмена);
		ДанныеXDTO.Вставить("КонтрагентКредитор", СсылкаНаКонтрагента);
	Иначе
		ДанныеXDTO.Вставить("КонтрагентКредитор", СсылкаКредитор);
	КонецЕсли;
	
	ДебиторскаяЗадолженность = ВыгрузитьТаблицуЗадолженности(
		?(ЭтоВзаимозачетДвухОрганизаций, "ДебиторскаяЗадолженность", "КредиторскаяЗадолженность"),
		ДанныеИБ.Ссылка,
		ДанныеXDTO.КонтрагентДебитор,
		СсылкаОрганизация,
		?(ЭтоВзаимозачетДвухОрганизаций, Неопределено, СсылкаДебитор),
		КомпонентыОбмена);
	ДанныеXDTO.Вставить("ДебиторскаяЗадолженность", ДебиторскаяЗадолженность);
	
	ПараметрыПоУмолчанию = Новый Структура(ДоговорФиксированнаяСтруктураКлючей());
	ПараметрыПоУмолчанию.Организация              = СсылкаОрганизация;
	ПараметрыПоУмолчанию.ВалютаВзаиморасчетов     = ВалютаРеглУчета;
	ПараметрыПоУмолчанию.РасчетыВУсловныхЕдиницах = Ложь;
	ПараметрыПоУмолчанию.ВидДоговора              = "Прочее"; 
	ПараметрыПоУмолчанию.Контрагент               = ДанныеXDTO.КонтрагентКредитор;
	
	Если ЭтоВзаимозачетДвухОрганизаций Тогда
		ПараметрыПоУмолчанию.ВидДоговора = ВидДоговораПоОбъектуРасчетовИнтеркампани(СсылкаОрганизация,
			ДанныеИБ.ОбъектРасчетовИнтеркампани, "СПоставщиком");
	КонецЕсли;
	
	КредиторскаяЗадолженность = Новый ТаблицаЗначений;
	КредиторскаяЗадолженность.Колонки.Добавить("Договор");
	КредиторскаяЗадолженность.Колонки.Добавить("КратностьВзаиморасчетов");
	КредиторскаяЗадолженность.Колонки.Добавить("КурсВзаиморасчетов");
	КредиторскаяЗадолженность.Колонки.Добавить("Сумма");
	КредиторскаяЗадолженность.Колонки.Добавить("СуммаВзаиморасчетов");
	
	КредиторскаяЗадолженностьСтрока = КредиторскаяЗадолженность.Добавить();
	КредиторскаяЗадолженностьСтрока.Договор = ДоговорИнструкцияКонвертацииПоДаннымВзаиморасчетов(ПараметрыПоУмолчанию, КомпонентыОбмена);
	КредиторскаяЗадолженностьСтрока.КратностьВзаиморасчетов = 1;
	КредиторскаяЗадолженностьСтрока.КурсВзаиморасчетов = 1;
	КредиторскаяЗадолженностьСтрока.Сумма = СуммаКредиторскойЗадолженности;
	КредиторскаяЗадолженностьСтрока.СуммаВзаиморасчетов = СуммаКредиторскойЗадолженности;
	ДанныеXDTO.Вставить("КредиторскаяЗадолженность", КредиторскаяЗадолженность);
	
	ВыгрузитьДополнительныеРеквизитыИСведения(КомпонентыОбмена, ДанныеИБ, ДанныеXDTO);
КонецПроцедуры
